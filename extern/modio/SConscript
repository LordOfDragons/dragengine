import os
import sys
import SCons

from SConsCommon import *
from SConsExternCommon import *

Import('parent_env parent_targets parent_report')

if parent_env['platform_webwasm']:
	Return()

if parent_env['OSBeOS']:
	Return()

envLibrary = parent_env.Clone()

# parameters
libVersion = '2025_10'

srcdir = 'modio-sdk'

libLinkNames = ['modioStatic', 'mbedx509', 'mbedtls', 'mbedcrypto']

installLibDir = 'install/static/lib'

if envLibrary['OSWindows']:
	staticFileNames = ['{}/lib{}.a'.format(installLibDir, x) for x in libLinkNames]
	
else:
	staticFileNames = [envLibrary.subst('{}/${{LIBPREFIX}}{}${{LIBSUFFIX}}'.format(installLibDir, x)) for x in libLinkNames]

depUring = None
if not envLibrary['OSWindows'] and envLibrary['platform_android'] == 'no':
	depUring = parent_targets['lib_liburing']

libs = libLinkNames[:]
if depUring:
	libs.extend(depUring['libs'])

nodeArtifacts = envLibrary.DownloadArtifactHelper(
	['{}-{}.tar.xz'.format(srcdir, libVersion)], 'modio', 'lib_modio_fetch')

def modioBuild(target, source, env):
	buildDir = target[0].get_dir().up().up().up()
	untarArchive(buildDir.abspath, source[0].abspath)
	
	cmakeSourceDir = buildDir.Dir(srcdir).abspath
	
	cmakeBuildDir = buildDir.Dir('build').abspath
	if not os.path.isdir(cmakeBuildDir):
		os.mkdir(cmakeBuildDir)
	
	cmakeInstallDir = buildDir.Dir('install').abspath
	if not os.path.isdir(cmakeInstallDir):
		os.mkdir(cmakeInstallDir)
	
	deps = []
	if depUring:
		deps.append(depUring)
	
	cppflags = []
	cppflags.append('-fPIC')
	cppflags.append('-DMODIO_SEPARATE_COMPILATION')
	if env['platform_android'] != 'no':
		cppflags.append('-Wno-error=unused-but-set-variable')
		cppflags.append('-Wno-error=documentation')
	
	script = createScriptHeader(env, deps=deps, cppflags=cppflags, cflags=cppflags)
	
	script.append('export CMAKE_POLICY_VERSION_MINIMUM=3.5\n')
	
	# modio cmake scripts seem to only look at CXXFLAGS not CPPFLAGS
	script.append('export CXXFLAGS="$CPPFLAGS"')
	
	script.append('cd "{}" || exit 1'.format(cmakeSourceDir))
	for p in env['PATCHES']:
		script.append('patch -p1 <{} || exit 1'.format(p.srcnode().abspath))
	
	script.append('cd "{}" || exit 1'.format(cmakeBuildDir))
	
	cmdline = []
	cmdline.append('cmake')
	cmdline.append('-G "Unix Makefiles"')
	cmdline.append('-S "{}"'.format(cmakeSourceDir))
	cmdline.append('-B "{}"'.format(cmakeBuildDir))
	
	for x in env.Flatten(env['EXTERN_CMAKE_FLAGS']):
		cmdline.append(x)
	
	cmakeCFlags = env.Flatten(env['EXTERN_CMAKE_C_FLAGS'])
	if cmakeCFlags:
		cmdline.append('-DCMAKE_C_FLAGS="{}"'.format(' '.join(cmakeCFlags)))
	
	if env['platform_android'] != 'no':
		cmdline.append('-DMODIO_PLATFORM="ANDROID"')
		cmdline.append('-DCMAKE_ANDROID_NDK="{}"'.format(env['ANDROID_NDKROOT']))
		cmdline.append('-DANDROID_ABI="{}"'.format(env['ANDROID_ABI']))
		cmdline.append('-DCMAKE_ANDROID_STL_TYPE="c++_shared"')
		cmdline.append('-DANDROID_PLATFORM="android-{}"'.format(env['ANDROID_APILEVEL']))
	else:
		if env['OSWindows']:
			cmdline.append('--preset=win')
		else:
			cmdline.append('--preset=linux64')
		
		if 'CROSSCOMPILE_HOST' in env:
			cmdline.append('-DCMAKE_SYSTEM_NAME="{}"'.format(env['CROSSCOMPILE_HOST']))
			cmdline.append('-DCMAKE_SYSTEM_PROCESSOR=AMD64')
	
	cmdline.append('-DCMAKE_BUILD_TYPE="Release"')
	cmdline.append('-DEXPECTED_BUILD_TESTS=Off')
	cmdline.append('-DFETCHCONTENT_FULLY_DISCONNECTED=On')
	
	script.append('{} || exit 1'.format(' '.join(cmdline)))
	
	script.append('cmake --build "{}" -- -j {} || exit 1'.format(cmakeBuildDir, env['with_threads']))
	
	# modio build script has a bug. if build and install directory are custom
	# the install script tries to access 'cmake_install.cmake' inside 'install dir'
	# instead of 'build dir'. copy it over to prevent the script from failing
	script.append('cp "{}/cmake_install.cmake" "{}" || exit 1'.format(cmakeBuildDir, cmakeInstallDir))
	
	script.append('cmake --install "{}" || exit 1'.format(cmakeInstallDir))
	
	# the bug mentioned above also copies the files into the totally wrong directory.
	# move them to where they belong
	script.append('cp -af "{}/out/install/linux64/"* "{}" || exit 1'.format(cmakeSourceDir, cmakeInstallDir))
	script.append('rm -rf "{}/out" || exit 1'.format(cmakeSourceDir))
	
	script.append('rm -f "{}/cmake_install.cmake" || exit 1'.format(cmakeInstallDir))
	
	if not env.RunExternalCommand(script):
		raise Exception('Failed building libmodio')

targets = staticFileNames[:]

# after building lib_modio run this from build directory to update header listing
# ./prepare_file_lists.sh

with open(envLibrary.File('header_file_list/modio').srcnode().abspath, 'r', encoding='utf-8') as f:
	targets.extend([x[:-1] for x in f.readlines()])

patches = [] # create with 'diff -rupN original_sources patches_sources'
envLibrary.Append(PATCHES = [])

globFiles(envLibrary, 'patches', '*.patch', patches, recursive=False)
globFiles(envLibrary, os.path.join('patches', libVersion), '*.patch', patches, recursive=False)
if envLibrary['platform_android'] != 'no':
	globFiles(envLibrary, os.path.join('patches', 'android', libVersion),
		'*.patch', patches, recursive=False)
envLibrary.Append(PATCHES = [envLibrary.File(x) for x in sorted(patches)])

library = envLibrary.Command(targets,
	[nodeArtifacts, patches, depUring['depends'] if depUring else []],
	envLibrary.Action(modioBuild, 'Build ModIO library'))

envLibrary.SideEffect('#notParallelCMake', library)

depends = parent_env.Alias('lib_modio', library)

envLibrary.Clean(library, ['install', 'build', srcdir])

cpppath = [
	envLibrary.Dir('install/static/inc'),
	envLibrary.Dir('install/header_only/linux'),
	envLibrary.Dir('install/header_only/common'),
	envLibrary.Dir('install/header_only/common/modio/core'),
	envLibrary.Dir('install/header_only/common/modio/core/entities'),
	envLibrary.Dir('install/header_only/common/modio/detail'),
	envLibrary.Dir('install/header_only/common/modio/detail/userdata'),
	envLibrary.Dir('install/header_only/common/modio/detail/compression/zip'),
	envLibrary.Dir('install/header_only/ext')]
if envLibrary['platform_android'] != 'no':
	cpppath.append(envLibrary.Dir('modio-sdk/platform/android'))
if depUring:
	cpppath.extend(depUring['cpppath'])

libpath = [envLibrary.Dir(installLibDir)]
if depUring:
	libpath.extend(depUring['libpath'])

libs = library[0:len(staticFileNames)]
if depUring:
	libs.extend(depUring['libs'])
runtimelibs = []

if depUring:
	runtimelibs.extend(depUring['runtimelibs'])

parent_targets['lib_modio'] = {
	'name' : 'Internal ModIO library',
	'cpppath' : cpppath,
	'libpath' : libpath,
	'libs' : libs,
	'runtimelibs' : runtimelibs,
	'depends' : depends}
