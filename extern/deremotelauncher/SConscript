import os
import SCons

from SConsCommon import *
from SConsExternCommon import *

Import('parent_env parent_targets parent_report')

envLibrary = parent_env.Clone()

srcVersion = 'nightly'
if envLibrary['platform_android'] != 'no':
	sysarch = 'android-{}'.format(envLibrary['ANDROID_ARCH_ID'])
else:
	sysarch = 'unix-x64'
srcdir = 'deremotelauncher-{}-{}'.format(sysarch, srcVersion)

libLinkName = 'deremotelauncher'

libFileName = envLibrary.subst('{}/lib/${{LIBPREFIX}}{}${{LIBSUFFIX}}'.format(srcdir, libLinkName))

baseUrl = 'https://github.com/LordOfDragons/deremotelauncher/releases/download'

nodeArtifacts = envLibrary.DownloadArtifactHelper('{}.tar.bz2'.format(srcdir),
	srcVersion, 'lib_deremotelauncher_fetch', baseUrl=baseUrl)

if 'with_deremotelauncher_inc' in envLibrary and envLibrary['with_deremotelauncher_inc']:
	envLibrary.Append(CPPPATH = [envLibrary['with_deremotelauncher_inc']])

if 'with_deremotelauncher_lib' in envLibrary and envLibrary['with_deremotelauncher_lib']:
	envLibrary.Append(LIBPATH = [envLibrary['with_deremotelauncher_lib']])

hasSystemLib = False
conf = envLibrary.Configure()
hasSystemLib = conf.CheckLibWithHeader(libLinkName, 'deremotelauncher/derlProtocol.h', 'c++')
conf.Finish()

targets = [libFileName]
library = []
cpppath = []
libpath = []

if hasSystemLib:
	if 'with_deremotelauncher_inc' in envLibrary and envLibrary['with_deremotelauncher_inc']:
		cpppath.append(envLibrary['with_deremotelauncher_inc'])
	if 'with_deremotelauncher_lib' in envLibrary and envLibrary['with_deremotelauncher_lib']:
		libpath.append(envLibrary['with_deremotelauncher_lib'])
	library.append(libLinkName)
else:
	def deremotelauncherUnpack(target, source, env):
		untarArchive(target[0].get_dir().up().up().abspath, source[0].abspath)

	with open(envLibrary.File('header_file_list/deremotelauncher').srcnode().abspath, 'r') as f:
		for x in f.readlines():
			x = x[:-1]
			x = x.replace('{version}', srcVersion)
			x = x.replace('{sysarch}', sysarch)
			targets.append(x)

	library = envLibrary.Command(targets, nodeArtifacts,
		envLibrary.Action(deremotelauncherUnpack, 'Unpack DERemoteLauncher'))[0:1]

	envLibrary.Clean(library, [srcdir])

	cpppath.append(envLibrary.Dir(os.path.join(srcdir, 'include')))
	libpath.append(envLibrary.Dir(os.path.join(srcdir, 'lib')))

parent_targets['lib_deremotelauncher'] = {
	'name' : 'Internal DERemoteLauncher library',
	'cpppath' : cpppath,
	'libpath' : libpath,
	'libs' : library,
	'runtimelibs' : [],
	'runtimedata' : [],
	'depends' : parent_env.Alias('lib_deremotelauncher', library)}
