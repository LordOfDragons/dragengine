import os
import sys
import SCons

from SConsCommon import *
from SConsExternCommon import *

Import( 'parent_env parent_targets parent_report' )

envLibrary = parent_env.Clone()

# switch
buildStaticLinking = True

# parameters
srcdir = 'libsigsegv-2.11'

libLinkVersion = '0'
libLinkName = 'sigsegv'

if envLibrary[ 'OSWindows' ]:
	libFileName = 'bin/lib{}-{}.dll'.format( libLinkName, libLinkVersion )
	staticFileName = 'lib/lib{}.a'.format( libLinkName )
else:
	libFileName = envLibrary.subst( 'lib/${{SHLIBPREFIX}}{}${{SHLIBSUFFIX}}.{}'.format( libLinkName, libLinkVersion ) )
	staticFileName = envLibrary.subst( 'lib/${{LIBPREFIX}}{}${{LIBSUFFIX}}'.format( libLinkName ) )

# check if system libsigsegv is present
hasSystemSigSegv = False
conf = envLibrary.Configure()
if envLibrary[ 'with_system_libsigsegv' ] != TernaryVariableNo:
	hasSystemSigSegv = conf.CheckLibWithHeader( libLinkName, 'sigsegv.h', 'c++', 'sigsegv_deinstall_handler();' )
conf.Finish()

if not hasSystemSigSegv and envLibrary[ 'with_system_libsigsegv' ] == TernaryVariableYes:
	Exit( 'Requested to use system libsigsegv but no usable libsigsegv found.' )

libs = [ libLinkName ]
libs.extend( parent_targets[ 'lib_zlib' ][ 'libs' ] )

parent_report[ 'use system libsigsegv' ] = 'yes' if hasSystemSigSegv else 'no'

if hasSystemSigSegv:
	parent_targets[ 'lib_libsigsegv' ] = {
		'name' : 'Internal libsigsegv library',
		'cpppath' : [],
		'libpath' : [],
		'libs' : libs,
		'runtimelibs' : [],
		'depends' : [] }
	Return()

# build libsigsegv
def libsigsegvBuild( target, source, env ):
	buildDir = target[ 0 ].get_dir().up()
	
	untarArchive( buildDir.abspath, source[ 0 ].abspath )
	
	script = createScriptHeader( env, deps=[ parent_targets[ 'lib_zlib' ] ] )
	
	script.append( 'cd "{}" || exit 1'.format( buildDir.Dir( srcdir ).abspath ) )
	
	cmdline = [ './configure --prefix="{}"'.format( buildDir.abspath ) ]
	cmdline.append( '--enable-silent-rules' )
	if buildStaticLinking:
		cmdline.append( '--enable-shared=no' )
		cmdline.append( '--enable-static=yes' )
		cmdline.append( '--with-pic=yes' )
	else:
		cmdline.append( '--enable-shared=yes' )
		cmdline.append( '--enable-static=no' )
	
	if 'CROSSCOMPILE_HOST' in env:
		cmdline.append('--host={}'.format(env['CROSSCOMPILE_HOST']))
	
	script.append( '{} || exit 1'.format( ' '.join( cmdline ) ) )
	
	script.append( 'make -j {} install || exit 1'.format(env['with_threads']) )
	
	if not env.RunExternalCommand(script):
		raise Exception('Failed building libsigsegv')

if buildStaticLinking:
	targets = [ staticFileName ]
else:
	targets = [ libFileName ]
targets.extend( [ 'include/sigsegv.h' ] )

library = envLibrary.Command( targets, '{}.tar.bz2'.format( srcdir ),
	envLibrary.Action(libsigsegvBuild, 'Build libsigsegv' ) )

if parent_targets[ 'lib_zlib' ][ 'depends' ]:
	envLibrary.Depends( library, parent_targets[ 'lib_zlib' ][ 'depends' ] )

envLibrary.SideEffect('#notParallelCMake', library)

depends = parent_env.Alias( 'lib_libsigsegv', library )

envLibrary.Clean( library, [ 'bin', 'include', 'lib', srcdir ] )

cpppath = [ envLibrary.Dir( 'include' ) ]
cpppath.extend( parent_targets[ 'lib_zlib' ][ 'cpppath' ] )

libpath = [ envLibrary.Dir( 'lib' ) ]
libpath.extend( parent_targets[ 'lib_zlib' ][ 'libpath' ] )

if buildStaticLinking:
	libs = [ library[ 0 ] ]
	libs.extend( parent_targets[ 'lib_zlib' ][ 'libs' ] )
	runtimelibs = []
else:
	runtimelibs = [ library[ 0 ] ]
runtimelibs.extend( parent_targets[ 'lib_zlib' ][ 'runtimelibs' ] )

parent_targets[ 'lib_libsigsegv' ] = {
	'name' : 'Internal libsigsegv library',
	'cpppath' : cpppath,
	'libpath' : libpath,
	'libs' : libs,
	'runtimelibs' : runtimelibs,
	'depends' : depends }
