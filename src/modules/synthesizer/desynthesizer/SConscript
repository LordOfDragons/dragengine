from SConsCommon import *

# contrib include
import sys
sys.path.append( Dir( '.' ).srcnode().abspath )

# import whatever is given by the parent builder
Import( 'parent_env parent_targets parent_report' )

# setup the environment matching the OS
envModule = parent_env.Clone()

libs = []

# do configure stuff
buildModule = True

# build module if requested
parent_report[ 'build Drag[en]gine Synthesizer Module' ] = 'yes' if buildModule else 'no'
if not buildModule:
	Return()

# fetch values in expanded form for later use
versionString = '1.3'
if envModule['force_version']:
	versionString = envModule['force_version']

pathConfig = '{}/modules/synthesizer/desynthesizer/{}'.format(
	envModule.subst( envModule[ 'path_de_config' ] ), versionString )
pathModule = '{}/modules/synthesizer/desynthesizer/{}'.format(
	envModule.subst( envModule[ 'path_de_data' ] ), versionString )

envModule.Append( CPPFLAGS = parent_env[ 'MODULE_CPPFLAGS' ] )
envModule.Append( LINKFLAGS = parent_env[ 'MODULE_LINKFLAGS' ] )

appendLibrary(envModule, parent_targets['dragengine'], libs)
appendLibrary(envModule, parent_targets['lib_soundtouch'], libs)

# setup the builders
sources = []
headers = []
globFiles( envModule, 'src', '*.cpp', sources )
globFiles( envModule, 'src', '*.h', headers )

objects = [ envModule.SharedObject( s ) for s in sources ]

#appendLibrary( envModule, parent_targets[ 'dragengine' ], libs )

library = envModule.LoadableModule( target='synthdesynthesizer', source=objects, LIBS=libs )
targetBuild = envModule.Alias( 'synth_desynthesizer_build', library )

install = []
archive = {}
for l in library:
	if l.name.endswith( envModule[ 'SHLIBSUFFIX' ] ):
		install.append( envModule.Install( pathModule, l ) )
		archive[ os.path.join( pathModule, l.name ) ] = l

if envModule['OSWindows'] and envModule['CROSSCOMPILE_CLANG'] and envModule['with_install_pdb']:
	pathPdb = os.path.join(envModule.subst(envModule['path_pdb']), 'EngineModules')
	install.append(envModule.Install(pathPdb, 'synthdesynthesizer.pdb'))
	archive['{}/synthdesynthesizer.pdb'.format(pathPdb)] = envModule.File('synthdesynthesizer.pdb')

manifestUpdates = [
	{ 'action': 'filename', 'name': library[ 0 ].name, 'keyword': 'FILENAME' },
	{ 'action': 'filesize', 'path': library[ 0 ].abspath, 'keyword': 'FILESIZE' },
	{ 'action': 'filehash', 'path': library[ 0 ].abspath, 'keyword': 'SHA1SUM' },
	{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]

for l in reversed(parent_targets['lib_soundtouch']['runtimelibs']):
	manifestUpdates.append({'action': 'preloadLibrary', 'path': l.name})
	install.append(envModule.Install(pathModule, l))
	archive[os.path.join(pathModule, l.name)] = l

manifest = envModule.File( 'src/module.xml' )
updateManifest = envModule.Command( manifest, manifest.srcnode(),
	Action( UpdateModuleManifest ), ManifestUpdates=manifestUpdates )
Depends(updateManifest, library)
Depends(updateManifest, 'SConscript')
Depends(updateManifest, parent_targets['lib_libpng']['runtimelibs'])

install.append( envModule.Install( pathModule, updateManifest ) )
archive[ os.path.join( pathModule, 'module.xml' ) ] = updateManifest[ 0 ]

targetInstall = envModule.Alias( 'synth_desynthesizer', install )

# source directory required for special commands
srcdir = Dir( '.' ).srcnode().abspath

# cloc
buildClocBare = BuildCLOC( envModule, [ '{}/src'.format( srcdir ) ], '{}/clocreport.csv'.format( srcdir ), [ 'doc' ] )
Depends( buildClocBare, sources )
Depends( buildClocBare, headers )
buildCloc = envModule.Alias( 'synth_desynthesizer_cloc', buildClocBare )

# add the targets to the targets list
parent_targets[ 'synth_desynthesizer' ] = {
	'name' : 'Drag[en]gine Synthesizer Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'install-engine-runtime' : targetInstall,
	'archive-engine' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format( srcdir ) }
