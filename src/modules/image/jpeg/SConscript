from SConsCommon import *

Import( 'parent_env parent_targets parent_report' )

envModule = parent_env.Clone()

buildModule = envModule[ 'build_image_jpeg' ] != TernaryVariableNo
internalModule = envModule['build_image_jpeg_internal']
parent_report['build JPEG Image Module'] = ('yes (internal)' if internalModule else 'yes') if buildModule else 'no'

if not buildModule:
	Return()

libs = []
appendLibrary(envModule, parent_targets['dragengine'], libs, internalModule=internalModule)
appendLibrary(envModule, parent_targets['lib_libjpeg'], libs)

envModule.Append( CPPFLAGS = parent_env[ 'MODULE_CPPFLAGS' ] )
envModule.Append( LINKFLAGS = parent_env[ 'MODULE_LINKFLAGS' ] )

# fetch values in expanded form for later use
versionString = '1.0'
if envModule['force_version']:
	versionString = envModule['force_version']

pathConfig = '{}/modules/image/jpeg/{}'.format( envModule.subst( envModule[ 'path_de_config' ] ), versionString )
pathModule = '{}/modules/image/jpeg/{}'.format( envModule.subst( envModule[ 'path_de_data' ] ), versionString )

# setup the builders
sources = []
headers = []
globFiles( envModule, 'src', '*.cpp', sources )
globFiles( envModule, 'src', '*.h', headers )

install = []
archive = {}

if internalModule:
	envModule.Append(CPPFLAGS = ['-DWITH_INTERNAL_MODULE'])
	envModule.Append(CPPFLAGS = ['-DMODULE_VERSION=\\"{}\\"'.format(versionString)])
	
	objects = [envModule.SharedObject(s) for s in sources]
	
	library = envModule.StaticLibrary(target='imgjpeg', source=objects, LIBS=libs)
	targetBuild = parent_env.Alias('img_jpeg_build', library)
	install.extend(targetBuild)
	
	parent_targets['dragengine']['add internal module'](
		envModule, library, ['lib_libjpeg'], 'deJpegRegisterInternalModule')
	
else:
	objects = [ envModule.SharedObject( s ) for s in sources ]
	
	library = envModule.LoadableModule( target='imgjpeg', source=objects, LIBS=libs )
	targetBuild = envModule.Alias( 'img_jpeg_build', library )
	
	for l in library:
		if l.name.endswith( envModule[ 'SHLIBSUFFIX' ] ):
			install.append( envModule.Install( pathModule, l ) )
			archive[ os.path.join( pathModule, l.name ) ] = l
	
	if envModule['OSWindows'] and envModule['CROSSCOMPILE_CLANG'] and envModule['with_install_pdb']:
		pathPdb = os.path.join(envModule.subst(envModule['path_pdb']), 'EngineModules')
		install.append(envModule.Install(pathPdb, 'imgjpeg.pdb'))
		archive['{}/imgjpeg.pdb'.format(pathPdb)] = envModule.File('imgjpeg.pdb')
	
	manifestUpdates = [
		{'action': 'filename', 'file': library[0], 'keyword': 'FILENAME'},
		{'action': 'filesize', 'file': library[0], 'keyword': 'FILESIZE'},
		{'action': 'filehash', 'file': library[0], 'keyword': 'SHA1SUM'},
		{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]
	
	for l in reversed(parent_targets['lib_libjpeg']['runtimelibs']):
		manifestUpdates.append({'action': 'preloadLibrary', 'library': l})
		install.append(envModule.Install(pathModule, l))
		archive[os.path.join(pathModule, l.name)] = l
	
	manifest = envModule.File( 'src/module.xml' )
	updateManifest = envModule.UpdateModuleManifest(manifest,
		manifest.srcnode(), ManifestUpdates=manifestUpdates)
	
	install.append( envModule.Install( pathModule, updateManifest ) )
	archive[ os.path.join( pathModule, 'module.xml' ) ] = updateManifest[ 0 ]

targetInstall = envModule.Alias( 'img_jpeg', install )

# source directory required for special commands
srcdir = Dir( '.' ).srcnode().abspath

# cloc
buildClocBare = BuildCLOC( envModule, [ '{}/src'.format( srcdir ) ], '{}/clocreport.csv'.format( srcdir ), [ 'doc' ] )
buildCloc = envModule.Alias( 'img_jpeg_cloc', buildClocBare )

# add the targets to the targets list
parent_targets[ 'img_jpeg' ] = {
	'name' : 'JPEG Image Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'install-engine-runtime' : targetInstall,
	'archive-engine' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format( srcdir ) }
