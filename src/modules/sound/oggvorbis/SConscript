from SConsCommon import *

Import( 'parent_env parent_targets parent_report' )

envModule = parent_env.Clone()

buildModule = envModule[ 'build_sound_ogg' ] != TernaryVariableNo
internalModule = envModule['build_sound_ogg_internal']
parent_report['build OGG Vorbis Sound Module'] = ('yes (internal)' if internalModule else 'yes') if buildModule else 'no'

if not buildModule:
	Return()

libs = []
appendLibrary(envModule, parent_targets['dragengine'], libs, internalModule=internalModule)
appendLibrary(envModule, parent_targets['lib_libvorbis'], libs)

# fetch values in expanded form for later use
versionString = '1.0'
if envModule['force_version']:
	versionString = envModule['force_version']

pathConfig = '{}/modules/sound/oggvorbis/{}'.format( envModule.subst( envModule[ 'path_de_config' ] ), versionString )
pathModule = '{}/modules/sound/oggvorbis/{}'.format( envModule.subst( envModule[ 'path_de_data' ] ), versionString )

envModule.Append( CPPFLAGS = parent_env[ 'MODULE_CPPFLAGS' ] )
envModule.Append( LINKFLAGS = parent_env[ 'MODULE_LINKFLAGS' ] )

# deal with header static includes causing compiler warnings because unused
envModule.Append( CXXFLAGS = '-DOV_EXCLUDE_STATIC_CALLBACKS' )

# determine the source files
sources = []
headers = []
globFiles( envModule, 'src', '*.cpp', sources )
globFiles( envModule, 'src', '*.h', headers )

# setup the builders
install = []
archive = {}

if internalModule:
	envModule.Append(CPPFLAGS = ['-DWITH_INTERNAL_MODULE'])
	envModule.Append(CPPFLAGS = ['-DMODULE_VERSION=\\"{}\\"'.format(versionString)])
	
	objects = [envModule.SharedObject(s) for s in sources]
	envModule.Depends(objects, parent_targets['dragengine']['headers'])
	
	library = envModule.StaticLibrary(target='sndoggvorbis', source=objects, LIBS=libs)
	targetBuild = parent_env.Alias('snd_ogg_build', library)
	install.extend(targetBuild)
	
	parent_targets['dragengine']['add internal module'](
		envModule, library, ['lib_libvorbis'], 'deoggRegisterInternalModule')
	
else:
	objects = [ envModule.SharedObject( s ) for s in sources ]
	
	library = envModule.LoadableModule( target='sndoggvorbis', source=objects, LIBS=libs )
	targetBuild = envModule.Alias( 'snd_ogg_build', library )
	
	for l in library:
		if l.name.endswith( envModule[ 'SHLIBSUFFIX' ] ):
			install.append( envModule.Install( pathModule, l ) )
			archive[ os.path.join( pathModule, l.name ) ] = l
	
	if envModule['OSWindows'] and envModule['CROSSCOMPILE_CLANG'] and envModule['with_install_pdb']:
		pathPdb = os.path.join(envModule.subst(envModule['path_pdb']), 'EngineModules')
		install.append(envModule.Install(pathPdb, 'sndoggvorbis.pdb'))
		archive['{}/sndoggvorbis.pdb'.format(pathPdb)] = envModule.File('sndoggvorbis.pdb')
	
	manifestUpdates = [
		{ 'action': 'filename', 'name': library[ 0 ].name, 'keyword': 'FILENAME' },
		{ 'action': 'filesize', 'path': library[ 0 ].abspath, 'keyword': 'FILESIZE' },
		{ 'action': 'filehash', 'path': library[ 0 ].abspath, 'keyword': 'SHA1SUM' },
		{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]
	
	for l in reversed( parent_targets[ 'lib_libvorbis' ][ 'runtimelibs' ] ):
		manifestUpdates.append( { 'action': 'preloadLibrary', 'path': l.name } )
		install.append( envModule.Install( pathModule, l ) )
		archive[ os.path.join( pathModule, l.name ) ] = l
	
	manifest = envModule.File( 'src/module.xml' )
	updateManifest = envModule.Command( manifest, manifest.srcnode(),
		Action( UpdateModuleManifest ), ManifestUpdates=manifestUpdates )
	Depends( updateManifest, library )
	Depends(updateManifest, 'SConscript')
	Depends( updateManifest, parent_targets[ 'lib_libvorbis' ][ 'runtimelibs' ] )
	
	install.append( envModule.Install( pathModule, updateManifest ) )
	archive[ os.path.join( pathModule, 'module.xml' ) ] = updateManifest[ 0 ]

targetInstall = envModule.Alias( 'snd_ogg', install )

# source directory required for special commands
srcdir = Dir( '.' ).srcnode().abspath

# cloc
buildClocBare = BuildCLOC( envModule, [ '{}/src'.format( srcdir ) ], '{}/clocreport.csv'.format( srcdir ), [ 'doc' ] )
Depends( buildClocBare, sources )
Depends( buildClocBare, headers )
buildCloc = envModule.Alias( 'snd_ogg_cloc', buildClocBare )

# add the targets to the targets list
parent_targets[ 'snd_ogg' ] = {
	'name' : 'Ogg Vorbis Sound Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'install-engine-runtime' : targetInstall,
	'archive-engine' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format( srcdir ) }
