from SConsCommon import *

# import whatever is given by the parent builder
Import( 'parent_env parent_targets parent_report' )

# setup the environment matching the OS
envModule = parent_env.Clone()

internalModule = envModule['build_audio_null_internal']
parent_report['build Null Audio Module'] = 'yes (internal)' if internalModule else 'yes'

# fetch values in expanded form for later use
versionString = '1.0'
if envModule['force_version']:
	versionString = envModule['force_version']

pathConfig = '{}/modules/audio/null/{}'.format( envModule.subst( envModule[ 'path_de_config' ] ), versionString )
pathModule = '{}/modules/audio/null/{}'.format( envModule.subst( envModule[ 'path_de_data' ] ), versionString )

envModule.Append( CPPFLAGS = parent_env[ 'MODULE_CPPFLAGS' ] )
envModule.Append( LINKFLAGS = parent_env[ 'MODULE_LINKFLAGS' ] )

# setup the builders
sources = []
headers = []
globFiles( envModule, 'src', '*.cpp', sources )
globFiles( envModule, 'src', '*.h', headers )

libs = []
appendLibrary(envModule, parent_targets['dragengine'], libs, internalModule=internalModule)

install = []
archive = {}

if internalModule:
	envModule.Append(CPPFLAGS = ['-DWITH_INTERNAL_MODULE'])
	envModule.Append(CPPFLAGS = ['-DMODULE_VERSION=\\"{}\\"'.format(versionString)])
	
	objects = [envModule.SharedObject(s) for s in sources]
	
	library = envModule.StaticLibrary(target='audnull', source=objects, LIBS=libs)
	targetBuild = parent_env.Alias('aud_null_build', library)
	install.extend(targetBuild)
	
	parent_targets['dragengine']['add internal module'](
		envModule, library, [], 'deanRegisterInternalModule')
	
else:
	objects = [ envModule.SharedObject( s ) for s in sources ]
	
	library = envModule.LoadableModule( target='audnull', source=objects, LIBS=libs )
	targetBuild = envModule.Alias( 'aud_null_build', library )
	
	for l in library:
		if l.name.endswith( envModule[ 'SHLIBSUFFIX' ] ):
			install.append( envModule.Install( pathModule, l ) )
			archive[ os.path.join( pathModule, l.name ) ] = l
	
	if envModule['OSWindows'] and envModule['CROSSCOMPILE_CLANG'] and envModule['with_install_pdb']:
		pathPdb = os.path.join(envModule.subst(envModule['path_pdb']), 'EngineModules')
		install.append(envModule.Install(pathPdb, 'audnull.pdb'))
		archive['{}/audnull.pdb'.format(pathPdb)] = envModule.File('audnull.pdb')
	
	manifestUpdates = [
		{ 'action': 'filename', 'name': library[ 0 ].name, 'keyword': 'FILENAME' },
		{ 'action': 'filesize', 'path': library[ 0 ].abspath, 'keyword': 'FILESIZE' },
		{ 'action': 'filehash', 'path': library[ 0 ].abspath, 'keyword': 'SHA1SUM' },
		{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]
	
	manifest = envModule.File( 'src/module.xml' )
	updateManifest = envModule.Command(manifest,
		[manifest.srcnode(), library, 'SConscript'],
		Action(UpdateModuleManifest), ManifestUpdates=manifestUpdates)
	
	install.append( envModule.Install( pathModule, updateManifest ) )
	archive[ os.path.join( pathModule, 'module.xml' ) ] = updateManifest[ 0 ]

targetInstall = envModule.Alias( 'aud_null', install )

# source directory required for special commands
srcdir = Dir( '.' ).srcnode().abspath

# cloc
buildClocBare = BuildCLOC( envModule, [ '{}/src'.format( srcdir ) ], '{}/clocreport.csv'.format( srcdir ), [ 'doc' ] )
buildCloc = envModule.Alias( 'aud_null_cloc', buildClocBare )

# add the targets to the targets list
parent_targets[ 'aud_null' ] = {
	'name' : 'Null Audio Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'install-engine-runtime' : targetInstall,
	'archive-engine' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format( srcdir ) }
