from SConsCommon import *

# import whatever is given by the parent builder
Import('parent_env parent_targets parent_report')

# setup the environment matching the OS
envModule = parent_env.Clone()

libs = []

# do configure stuff
buildModule = False

if envModule['platform_android'] != 'no' and envModule['build_input_android'] != TernaryVariableNo:
	buildModule = True

# build module if requested
internalModule = envModule['build_input_android_internal']
parent_report['build Android Input Module'] = ('yes (internal)' if internalModule else 'yes') if buildModule else 'no'
if not buildModule:
	Return()

# fetch values in expanded form for later use
versionString = '1.0'
if envModule['force_version']:
	versionString = envModule['force_version']

pathConfig = '{}/modules/input/androidinput/{}'.format(envModule.subst(envModule['path_de_config']), versionString)
pathModule = '{}/modules/input/androidinput/{}'.format(envModule.subst(envModule['path_de_data']), versionString)
pathShared = '{}/modules/input/androidinput/{}'.format(envModule.subst(envModule['path_de_share']), versionString)

envModule.Append(CPPFLAGS = parent_env['MODULE_CPPFLAGS'])
envModule.Append(LINKFLAGS = parent_env['MODULE_LINKFLAGS'])

# determine the source files
sources = []
headers = []
datafiles = []

globFiles(envModule, 'src', '*.cpp', sources)
globFiles(envModule, 'src', '*.h', headers)
globFiles(envModule, 'data', '*', datafiles)

# setup the builders
libs.extend(envModule['ANDROID_LIBS'])

appendLibrary(envModule, parent_targets['dragengine'], libs, internalModule=internalModule)
appendLibrary(envModule, parent_targets['lib_agdk'], libs)

install = []
archive = {}

if internalModule:
	envModule.Append(CPPFLAGS = ['-DWITH_INTERNAL_MODULE'])
	envModule.Append(CPPFLAGS = ['-DMODULE_VERSION=\\"{}\\"'.format(versionString)])
	
	objects = [envModule.SharedObject(s) for s in sources]
	envModule.Depends(objects, parent_targets['dragengine']['headers'])
	
	library = envModule.StaticLibrary(target='inpandroid', source=objects, LIBS=libs)
	targetBuild = parent_env.Alias('inp_android_build', library)
	install.extend(targetBuild)
	
	parent_targets['dragengine']['add internal module'](
		envModule, library, ['lib_agdk'], 'deaiRegisterInternalModule')
	
else:
	objects = [envModule.SharedObject(s) for s in sources]
	
	library = envModule.LoadableModule(target='inpandroid', source=objects, LIBS=libs)
	targetBuild = envModule.Alias('inp_android_build', library)
	
	for l in library:
		if l.name.endswith(envModule['SHLIBSUFFIX']):
			install.append(envModule.Install(pathModule, l))
			archive[os.path.join(pathModule, l.name)] = l
	
	manifestUpdates = [
		{'action': 'filename', 'name': library[0].name, 'keyword': 'FILENAME'},
		{'action': 'filesize', 'path': library[0].abspath, 'keyword': 'FILESIZE'},
		{'action': 'filehash', 'path': library[0].abspath, 'keyword': 'SHA1SUM'},
		{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]
	
	manifest = envModule.File('src/module.xml')
	updateManifest = envModule.Command(manifest, manifest.srcnode(),
		Action(UpdateModuleManifest), ManifestUpdates=manifestUpdates)
	Depends(updateManifest, library)
	Depends(updateManifest, 'SConscript')
	
	install.append(envModule.Install(pathModule, updateManifest))
	archive[os.path.join(pathModule, 'module.xml')] = updateManifest[0]

cutlen = len('data/')
for d in datafiles:
	install.append(envModule.InstallAs(os.path.join(pathShared, d[cutlen:]), d))
	archive[os.path.join(pathShared, d[cutlen:])] = envModule.File(d).srcnode()

targetInstall = envModule.Alias('inp_android', install)

# source directory required for special commands
srcdir =  Dir('.').srcnode().abspath

# cloc
buildClocBare = BuildCLOC(envModule, ['{}/src'.format(srcdir)], '{}/clocreport.csv'.format(srcdir), ['doc'])
Depends(buildClocBare, sources)
Depends(buildClocBare, headers)
buildCloc = envModule.Alias('inp_android_cloc', buildClocBare)

# add the targets to the targets list
parent_targets['inp_android'] = {
	'name' : 'Android Input Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'install-engine-runtime' : targetInstall,
	'archive-engine': archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format(srcdir)}
