from SConsCommon import *

# import whatever is given by the parent builder
Import('parent_env parent_targets parent_report')

# setup the environment matching the OS
envModule = parent_env.Clone()

libs = []

# do configure stuff
hasX = False
buildModule = False

if parent_env['OSPosix'] and envModule['build_input_x'] != TernaryVariableNo:
	conf = envModule.Configure()
	
	if envModule['with_x'] != TernaryVariableNo:
		hasX = conf.CheckLibWithHeader('X11', 'X11/Xlib.h', 'c++', 'XOpenDisplay(NULL);')
		if hasX:
			buildModule = True
			envModule.Append(CXXFLAGS = '-DHAS_LIB_X11')
			libs.append('X11')
		elif envModule['with_x'] == TernaryVariableYes:
			Exit('Requested to use X Window System but Xlib.h or libX11 could not be found or is not usable.')
		
		hasXi = conf.CheckLibWithHeader('Xi', 'X11/extensions/XInput.h', 'c++', ' XListInputDevices(0,0);')
		if hasXi:
			buildModule = buildModule and True
			envModule.Append(CXXFLAGS = '-DHAS_LIB_XI')
			libs.append('Xi')
		elif envModule['with_x'] == TernaryVariableYes:
			Exit('Requested to use X Window System but XInput.h or libXi could not be found or is not usable.')
		
		if parent_env['with_system_libevdev'] == TernaryVariableNo:
			buildModule = buildModule and True
			appendLibrary(envModule, parent_targets['lib_libevdev'], libs)
		else:
			hasEvdev = conf.CheckLibWithHeader('evdev', 'libevdev-1.0/libevdev/libevdev.h',
				'c++', ' libevdev_get_driver_version(0);')
			if hasEvdev:
				buildModule = buildModule and True
				envModule.Append(CXXFLAGS = '-DHAS_LIB_EVDEV')
				libs.append('evdev')
			elif envModule['with_x'] == TernaryVariableYes:
				Exit('Requested to use X Window System but libevdev.h or libevdev could not be found or is not usable.')
	
	conf.Finish()

if envModule['build_input_x'] == TernaryVariableYes and (not hasX or not hasXi):
	Exit('Requested to build X Input Module but X could not be found or is not usable.')

# build module if requested
parent_report['build X Input Module'] = 'yes' if buildModule else 'no'
if not buildModule:
	Return()

# fetch values in expanded form for later use
versionString = '1.0'
if envModule['force_module_versions']:
	versionString = envModule['force_module_versions']

pathConfig = '{}/modules/input/xsystem/{}'.format(envModule.subst(envModule['path_de_config']), versionString)
pathModule = '{}/modules/input/xsystem/{}'.format(envModule.subst(envModule['path_de_data']), versionString)
pathShared = '{}/modules/input/xsystem/{}'.format(envModule.subst(envModule['path_de_share']), versionString)

envModule.Append(CPPFLAGS = parent_env['MODULE_CPPFLAGS'])
envModule.Append(LINKFLAGS = parent_env['MODULE_LINKFLAGS'])

# setup the builders
sources = []
headers = []
globFiles(envModule, 'src', '*.cpp', sources)
globFiles(envModule, 'src', '*.h', headers)

data = []
globFiles(envModule, 'data', '*', data)

objects = [envModule.SharedObject(s) for s in sources]

appendLibrary(envModule, parent_targets['dragengine'], libs)
appendLibrary(envModule, parent_targets['lib_libevdev'], libs)

library = envModule.SharedLibrary(target='inpxsystem', source=objects, LIBS=libs)
targetBuild = envModule.Alias('inp_x_build', library)

install = []
archive = {}
for l in library:
	if l.name.endswith(envModule['SHLIBSUFFIX']):
		install.append(envModule.Install(pathModule, l))
		archive[os.path.join(pathModule, l.name)] = l

manifestUpdates = [
	{ 'action': 'filename', 'name': library[0].name, 'keyword': 'FILENAME' },
	{ 'action': 'filesize', 'path': library[0].abspath, 'keyword': 'FILESIZE' },
	{ 'action': 'filehash', 'path': library[0].abspath, 'keyword': 'SHA1SUM' },
	{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]

for l in reversed(parent_targets['lib_libevdev']['runtimelibs']):
	manifestUpdates.append({'action': 'preloadLibrary', 'path': l.name})
	install.append(envModule.Install(pathModule, l))
	archive[os.path.join(pathModule, l.name)] = l

manifest = envModule.File('src/module.xml')
updateManifest = envModule.Command(manifest, manifest.srcnode(),
	Action(UpdateModuleManifest), ManifestUpdates=manifestUpdates)
Depends(updateManifest, library)
Depends(updateManifest, parent_targets['lib_libevdev']['runtimelibs'])

install.append(envModule.Install(pathModule, updateManifest))
archive[os.path.join(pathModule, 'module.xml')] = updateManifest[0]

for d in data:
	filename = os.path.join(pathShared, d[5:])
	install.append(envModule.InstallAs(filename, d))
	archive[filename] = envModule.File(d).srcnode()

targetInstall = envModule.Alias('inp_x', install)

# source directory required for special commands
srcdir = Dir('.').srcnode().abspath

# cloc
buildClocBare = BuildCLOC(envModule, ['{}/src'.format(srcdir)], '{}/clocreport.csv'.format(srcdir), ['doc'])
Depends(buildClocBare, sources)
Depends(buildClocBare, headers)
buildCloc = envModule.Alias('inp_x_cloc', buildClocBare)

# add the targets to the targets list
parent_targets['inp_x'] = {
	'name' : 'X Input Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'archive-engine' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format(srcdir) }
