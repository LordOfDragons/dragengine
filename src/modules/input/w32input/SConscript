from SConsCommon import *

# import whatever is given by the parent builder
Import( 'parent_env parent_targets parent_report' )

# setup the environment matching the OS
envModule = parent_env.Clone()

libs = []

# do configure stuff
buildModule = False

if envModule['OSWindows'] and envModule[ 'build_input_w32' ] != TernaryVariableNo:
	buildModule = True

# build module if requested
parent_report[ 'build Windows Input Module' ] = 'yes' if buildModule else 'no'

if not buildModule:
	Return()

# fetch values in expanded form for later use
versionMajor = 1
versionMinor = 0
versionString = '{}.{}'.format( versionMajor, versionMinor )

pathConfig = '{}/modules/input/w32/{}'.format( envModule.subst( envModule[ 'path_de_config' ] ), versionString )
pathModule = '{}/modules/input/w32/{}'.format( envModule.subst( envModule[ 'path_de_data' ] ), versionString )
pathShared = '{}/modules/input/w32/{}'.format(envModule.subst(envModule['path_de_share']), versionString)

envModule.Append( CPPFLAGS = parent_env[ 'MODULE_CPPFLAGS' ] )
envModule.Append( LINKFLAGS = parent_env[ 'MODULE_LINKFLAGS' ] )

# setup the builders
sources = []
headers = []
globFiles( envModule, 'src', '*.cpp', sources )
globFiles( envModule, 'src', '*.h', headers )

data = []
globFiles(envModule, 'data', '*', data)

objects = [ envModule.SharedObject( s ) for s in sources ]

appendLibrary( envModule, parent_targets[ 'dragengine' ], libs )

libs.append( [ 'kernel32', 'user32', 'gdi32', 'winmm', 'wsock32' ] )

library = envModule.SharedLibrary( target='inpw32', source=objects, LIBS=libs )
targetBuild = envModule.Alias( 'inp_w32_build', library )

install = []
archive = {}
for l in library:
	if l.name.endswith( envModule[ 'SHLIBSUFFIX' ] ):
		install.append( envModule.Install( pathModule, l ) )
		archive[ os.path.join( pathModule, l.name ) ] = l

manifestUpdates = [
	{ 'action': 'filename', 'name': library[ 0 ].name, 'keyword': 'FILENAME' },
	{ 'action': 'filesize', 'path': library[ 0 ].abspath, 'keyword': 'FILESIZE' },
	{ 'action': 'filehash', 'path': library[ 0 ].abspath, 'keyword': 'SHA1SUM' },
	{'action': 'version', 'version': '1.0', 'keyword': 'VERSION'}]

manifest = envModule.File( 'src/module.xml' )
updateManifest = envModule.Command( manifest, manifest.srcnode(),
	Action( UpdateModuleManifest ), ManifestUpdates=manifestUpdates )
Depends( updateManifest, library )

install.append( envModule.Install( pathModule, updateManifest ) )
archive[ os.path.join( pathModule, 'module.xml' ) ] = updateManifest[ 0 ]

for d in data:
	filename = os.path.join(pathShared, d[5:])
	install.append(envModule.InstallAs(filename, d))
	archive[filename] = envModule.File(d).srcnode()

targetInstall = envModule.Alias( 'inp_w32', install )

# source directory required for special commands
srcdir = Dir( '.' ).srcnode().abspath

# cloc
buildClocBare = BuildCLOC( envModule, [ '{}/src'.format( srcdir ) ], '{}/clocreport.csv'.format( srcdir ), [ 'doc' ] )
Depends( buildClocBare, sources )
Depends( buildClocBare, headers )
buildCloc = envModule.Alias( 'inp_w32_cloc', buildClocBare )

# add the targets to the targets list
parent_targets[ 'inp_w32' ] = {
	'name' : 'Windows Input Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'archive-engine' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format( srcdir ) }
