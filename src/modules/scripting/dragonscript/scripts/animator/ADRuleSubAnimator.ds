/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Animators

pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Definition of sub animator animator rule.
 * 
 * Once defined a rule can be added to an animator using the stored definition.
 */
class ADRuleSubAnimator extends ADRule
	private var Animator pSubAnimator
	private var Array pConnections
	private var bool pEnablePosition
	private var bool pEnableOrientation
	private var bool pEnableSize
	private var bool pEnableVertexPositionSet
	
	
	
	/** \brief Create animation rule definition. */
	public func new()
		pConnections = Array.new()
		pEnablePosition = true
		pEnableOrientation = true
		pEnableSize = true
		pEnableVertexPositionSet = true
	end
	
	/**
	 * \brief Create copy of animation rule definition.
	 * \version 1.13
	 */
	public func new(AnimatorDefinition fromDef, AnimatorDefinition toDef,\
	ADRuleSubAnimator rule) super(fromDef, toDef, rule)
		pSubAnimator = rule.pSubAnimator
		pConnections = Array.new(rule.pConnections)
		pEnablePosition = rule.pEnablePosition
		pEnableOrientation = rule.pEnableOrientation
		pEnableSize = rule.pEnableSize
		pEnableVertexPositionSet = rule.pEnableVertexPositionSet
	end
	
	
	
	/** \brief Sub animator. */
	public func Animator getSubAnimator()
		return pSubAnimator
	end
	
	/** \brief Set sub animator. */
	public func void setSubAnimator(Animator subAnimator)
		pSubAnimator = subAnimator
	end
	
	/** \brief Count of connections. */
	public func int getConnectionCount()
		return pConnections.getCount()
	end
	
	/** \brief Connection. */
	public func int getConnectionAt(int index)
		return pConnections.getAt(index) cast int
	end
	
	/** \brief Add connection. */
	public func void addConnection(int subAnimatorController, int localController)
		if subAnimatorController < 0
			throw EInvalidParam.new("subAnimatorController < 0")
		end
		if subAnimatorController >= pSubAnimator.getControllerCount()
			throw EInvalidParam.new("subAnimatorController >= controllerCount")
		end
		
		while subAnimatorController >= pConnections.getCount()
			pConnections.add(-1)
		end
		
		pConnections.setAt(subAnimatorController, localController)
	end
	
	/** \brief Add connection. */
	public func void addConnection(String subAnimatorController, int localController)
		if pSubAnimator == null
			return
		end
		
		var int index = pSubAnimator.indexOfControllerNamed(subAnimatorController)
		if index != -1
			addConnection(index, localController)
		end
	end
	
	/** \brief Remove all connections. */
	public func void clearConnections()
		pConnections.removeAll()
	end
	
	
	
	/** \brief Position manipulation is enabled. */
	public func bool getEnablePosition()
		return pEnablePosition
	end
	
	/** \brief Set if position manipulation is enabled. */
	public func void setEnablePosition(bool enable)
		pEnablePosition = enable
	end
	
	/** \brief Orientation manipulation is enabled. */
	public func bool getEnableOrientation()
		return pEnableOrientation
	end
	
	/** \brief Set if orientation manipulation is enabled. */
	public func void setEnableOrientation(bool enable)
		pEnableOrientation = enable
	end
	
	/** \brief Size manipulation is enabled. */
	public func bool getEnableSize()
		return pEnableSize
	end
	
	/** \brief Set if size manipulation is enabled. */
	public func void setEnableSize(bool enable)
		pEnableSize = enable
	end
	
	/**
	 * \brief Vertex position set manipulation is enabled.
	 * \version 1.17
	 */
	public func bool getEnableVertexPositionSet()
		return pEnableVertexPositionSet
	end
	
	/**
	 * \brief Set if vertex position set manipulation is enabled.
	 * \version 1.17
	 */
	public func void setEnableVertexPositionSet(bool enable)
		pEnableVertexPositionSet = enable
	end
	
	
	
	/** \brief Build engine animator rule using the stored definition. */
	public func AnimatorRule buildRule(Animator animator)
		var ARSubAnimator rule = ARSubAnimator.new()
		
		buildRuleCommon(rule)
		
		forEachTarget(block ADRuleTargetLink each
			rule.targetAddLink(each.getTarget() cast ARSubAnimatorTarget, each.getLink().getIndex())
		end)
		
		if pSubAnimator != null
			rule.setSubAnimator(pSubAnimator)
		end
		
		var int connectionCount = pConnections.getCount()
		if connectionCount == 0
			rule.setMatchingConnections(animator)
			
		else
			var int i
			for i = 0 to connectionCount
				rule.setConnectionAt(i, pConnections.getAt(i) cast int)
			end
		end
		
		rule.setEnablePosition(pEnablePosition)
		rule.setEnableOrientation(pEnableOrientation)
		rule.setEnableSize(pEnableSize)
		rule.setEnableVertexPositionSet(pEnableVertexPositionSet)
		
		return rule
	end
	
	
	
	/**
	 * \brief Create copy of rule.
	 * \version 1.13
	 */
	public func ADRule copy(AnimatorDefinition fromDef, AnimatorDefinition toDef)
		return new(fromDef, toDef, this)
	end
	
	
	
	public func void debug(Console console)
		console.addMessage(debugCommon("Sub Animator") + " epos=" + pEnablePosition \
			+ " eorient=" + pEnableOrientation + " esize=" + pEnableSize \
			+ " evps=" + pEnableVertexPositionSet + ")")
	end
end
