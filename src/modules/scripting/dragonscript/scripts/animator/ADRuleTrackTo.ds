/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Animators

pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Definition of track to animator rule.
 * 
 * Once defined a rule can be added to an animator using the stored definition.
 */
class ADRuleTrackTo extends ADRule
	private var String pTrackBone
	private var ARTrackToAxis pTrackAxis
	private var ARTrackToAxis pUpAxis
	private var ARTrackToUp pUpTarget
	private var ARTrackToLocked pLockedAxis
	
	
	
	/** \brief Create animation rule definition. */
	public func new()
		pTrackBone = ""
		pTrackAxis = ARTrackToAxis.posZ
		pUpAxis = ARTrackToAxis.posY
		pUpTarget = ARTrackToUp.componentY
		pLockedAxis = ARTrackToLocked.none
	end
	
	/**
	 * \brief Create copy of animation rule definition.
	 * \version 1.13
	 */
	public func new(AnimatorDefinition fromDef, AnimatorDefinition toDef,\
	ADRuleTrackTo rule) super(fromDef, toDef, rule)
		pTrackBone = rule.pTrackBone
		pTrackAxis = rule.pTrackAxis
		pUpAxis = rule.pUpAxis
		pUpTarget = rule.pUpTarget
		pLockedAxis = rule.pLockedAxis
	end
	
	
	
	/** \brief Name of track bone or empty string to use none. */
	public func String getTrackBone()
		return pTrackBone
	end
	
	/** \brief Set name of track bone or empty string to use none. */
	public func void setTrackBone(String boneName)
		if boneName == null
			throw ENullPointer.new("boneName")
		end
		pTrackBone = boneName
	end
	
	/** \brief Ttrack axis. */
	public func ARTrackToAxis getTrackAxis()
		return pTrackAxis
	end
	
	/** \brief Set track axis. */
	public func void setTrackAxis(ARTrackToAxis axis)
		pTrackAxis = axis
	end
	
	/** \brief Up axis. */
	public func ARTrackToAxis getUpAxis()
		return pUpAxis
	end
	
	/** \brief Set up axis. */
	public func void setUpAxis(ARTrackToAxis axis)
		pUpAxis = axis
	end
	
	/** \brief Up target. */
	public func ARTrackToUp getUpTarget()
		return pUpTarget
	end
	
	/** \brief Set up target. */
	public func void setUpTarget(ARTrackToUp target)
		pUpTarget = target
	end
	
	/** \brief Locked axis. */
	public func ARTrackToLocked getLockedAxis()
		return pLockedAxis
	end
	
	/** \brief Set locked axis. */
	public func void setLockedAxis(ARTrackToLocked axis)
		pLockedAxis = axis
	end
	
	
	
	/** \brief Build engine animator rule using the stored definition. */
	public func AnimatorRule buildRule(Animator animator)
		var ARTrackTo rule = ARTrackTo.new()
		
		buildRuleCommon(rule)
		
		forEachTarget(block ADRuleTargetLink each
			rule.targetAddLink(each.getTarget() cast ARTrackToTarget, each.getLink().getIndex())
		end)
		
		rule.setTrackBone(pTrackBone)
		rule.setTrackAxis(pTrackAxis)
		rule.setUpAxis(pUpAxis)
		rule.setUpTarget(pUpTarget)
		rule.setLockedAxis(pLockedAxis)
		
		return rule
	end
	
	
	
	/**
	 * \brief Create copy of rule.
	 * \version 1.13
	 */
	public func ADRule copy(AnimatorDefinition fromDef, AnimatorDefinition toDef)
		return new(fromDef, toDef, this)
	end
	
	
	
	public func void debug(Console console)
		console.addMessage(debugCommon("Track To") + \
			" trackBone=" + pTrackBone + \
			" trackAxis=" + pTrackAxis + \
			" upAxis=" + pUpAxis + \
			" upTarget=" + pUpTarget + \
			" lockedAxis=" + pLockedAxis + ")")
	end
end
