/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Commands

pin Dragengine.Utils.Text


/**
 * \brief Command carrying out a specific task.
 * 
 * Can be bound to player input events or used in widgets.
 */
class Command
	private var String pName
	private var String pDescription
	private var String pTranslateDescription
	private var String pDisplayName
	private var String pTranslateDisplayName
	
	
	
	/** \brief Create command. */
	public func new()
		pName = "unnamed"
		pDescription = ""
	end
	
	/** \brief Create command. */
	public func new(String name, String description)
		setName(name)
		setDescription(description)
	end
	
	
	
	/** \brief Unique name. */
	public func String getName()
		return pName
	end
	
	/**
	 * \brief Set unique name of the command.
	 * 
	 * Allowed are only upper and lower case letters, numbers and underscores.
	 */
	public func void setName(String name)
		if name == null
			throw EInvalidParam.new()
		end
		pName = name
	end
	
	/** \brief Description. */
	public func String getDescription()
		return pDescription
	end
	
	/** \brief Set description. */
	public func void setDescription(String description)
		if description == null
			throw EInvalidParam.new()
		end
		pDescription = description
	end
	
	/**
	 * \brief Translation description or null.
	 * \version 1.16
	 * 
	 * If not null use TranslationManager to get description. If null or no translation
	 * can be found getDescription() has to be used.
	 */
	public func String getTranslateDescription()
		return pTranslateDescription
	end
	
	/**
	 * \brief Set translate description or null.
	 * \version 1.16
	 * 
	 * If not null use TranslationManager to get display name. If null or no translation
	 * can be found getDescription() has to be used.
	 */
	public func void setTranslateDescription(String translateName)
		pTranslateDescription = translateName
	end
	
	/**
	 * \brief Display name or null.
	 * \version 1.16
	 * 
	 * Display name is used for EditBindingsPanel and other placers where commands
	 * have to be shown to the user in a UI. If null getName() has to be used.
	 */
	public func String getDisplayName()
		return pDisplayName
	end
	
	/**
	 * \brief Set display name or null.
	 * \version 1.16
	 * 
	 * Display name is used for EditBindingsPanel and other placers where commands
	 * have to be shown to the user in a UI. If null getName() has to be used.
	 */
	public func void setDisplayName(String displayName)
		pDisplayName = displayName
	end
	
	/**
	 * \brief Translation display name or null.
	 * \version 1.16
	 * 
	 * If not null use TranslationManager to get display name. If null or no translation
	 * can be found getDisplayName() has to be used.
	 */
	public func String getTranslateDisplayName()
		return pTranslateDisplayName
	end
	
	/**
	 * \brief Set translate display name or null.
	 * \version 1.16
	 * 
	 * If not null use TranslationManager to get display name. If null or no translation
	 * can be found getDisplayName() has to be used.
	 */
	public func void setTranslateDisplayName(String translateName)
		pTranslateDisplayName = translateName
	end
	
	
	
	/**
	 * \brief Translated display name.
	 * \version 1.16
	 * 
	 * Tries first to translate using translationManager. If translationManager is null
	 * or the transation can not be found tries translate using EngineTranslations.
	 * If this does not provide a translation returns getDisplayName(). If getDisplayName()
	 * is null returns getName().
	 * 
	 * Returns UTF-8 string.
	 * 
	 * \param translationManager TranslationManager to use or null.
	 */
	public func String translateDisplayName(TranslationManager translationManager)
		var UnicodeString text
		
		if pTranslateDisplayName != null
			if translationManager != null
				text = translationManager.translate(pTranslateDisplayName, null)
			end
			if text == null
				text = EngineTranslations.get().translate(pTranslateDisplayName, null)
			end
		end
		
		if text != null
			return text.toUTF8()
			
		elif pDisplayName != null
			return pDisplayName
			
		else
			return pName
		end
	end
	
	/**
	 * \brief Translated display name using default TranslationManager.
	 * \version 1.16
	 * 
	 * Same as translateDisplayName(TranslationManager) using TranslationManager.get() .
	 */
	public func String translateDisplayName()
		return translateDisplayName(TranslationManager.get())
	end
	
	/**
	 * \brief Translated command description.
	 * \version 1.16
	 * 
	 * Tries first to translate using translationManager. If translationManager is null
	 * or no translation is found tries translate using EngineTranslations. If no translation
	 * is found returns getDescription().
	 * 
	 * Returns UTF-8 string.
	 * 
	 * \param translationManager TranslationManager to use or null.
	 */
	public func String translateDescription(TranslationManager translationManager)
		var UnicodeString t
		
		if pTranslateDescription != null
			if translationManager != null
				t = translationManager.translate(pTranslateDescription, null)
			end
			if t == null
				t = EngineTranslations.get().translate(pTranslateDescription, null)
			end
		end
		
		return t != null if t.toUTF8() else pDescription
	end
	
	/**
	 * \brief Translated command description using default TranslationManager.
	 * \version 1.16
	 * 
	 * Same as translateDescription(TranslationManager) using TranslationManager.get() .
	 */
	public func String translateDescription()
		return translateDescription(TranslationManager.get())
	end
	
	
	
	/** \brief Input device key or button has been pressed. */
	public func void start()
	end
	
	/** \brief Input device key or button has been released. */
	public func void stop()
	end
	
	/**
	 * \brief Convenience method calling start() and stop() in succession.
	 * 
	 * Used for example to run the command for different reasons than input handling.
	 */
	public func void pulse()
		start()
		stop()
	end
	
	/**
	 * \brief Relative input device axis position changed.
	 * 
	 * Value is the change in the relative axis. It is reset to 0 right after sending the
	 * event. No event is sent for the reset. A typical device is mouse movement resulting
	 * in this method to be called.
	 * 
	 * \param value Relative axis position change in axis units. Caller is responsible to
	 *              adjust the range to make different input devices match in magnitude.
	 */
	public func void analogRelative(float value)
	end
	
	/**
	 * \brief Absolute input device axis position changed.
	 * 
	 * Axis keeps position until next event. Gamepad events typically send this method
	 * when the stick is pushed to a new position.
	 * 
	 * \param value New axis position in the range -1 to 1.
	 */
	public func void analogAbsolute(float value)
	end
	
	/**
	 * \brief Input device button has been touched.
	 * \version 1.6
	 */
	public func void touch()
	end
	
	/**
	 * \brief Input device button has been untouched.
	 * \version 1.6
	 */
	public func void untouch()
	end
end
