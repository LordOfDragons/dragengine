/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Commands


/**
 * \brief Manage input bindings.
 * 
 * Bindings are identified by device and either axis modifier or button modifier. For
 * quicker access the device, axis and button index are used. Upon receiving
 * InputEventType.deviceAttached, InputEventType.deviceDetached or
 * InputEventType.deviceParamsChanged the internal data structures have to be updated.
 * Game scripts have to send received events to the binding manager using checkInputEvent().
 * This method knows what events are important avoiding the game scripts requiring to know.
 * 
 * After creating the binding manager has updated the internal data structures already.
 */
class BindingManager
	var Array pAxisBindings, pButtonBindings, pDevices, pListeners
	var InputDevice pMouse, pKeyboard, pVRHMD, pVRLeftHand, pVRRightHand
	
	
	/** \brief Create binding manager. */
	func new()
		pAxisBindings = Array.new()
		pButtonBindings = Array.new()
		pDevices = Array.new()
		pListeners = Array.new()
		
		reloadDevices()
	end
	
	/** \brief Create copy of binding manager. */
	func new(BindingManager bindingManager)
		pAxisBindings = bindingManager.pAxisBindings.map(block Binding each
			return Binding.new(each)
		end)
		pButtonBindings = bindingManager.pButtonBindings.map(block Binding each
			return Binding.new(each)
		end)
		pDevices = Array.new(bindingManager.pDevices)
	end
	
	
	/**
	 * \brief Check input event for device changes.
	 * 
	 * Call after receiving input events of any kind. Updates internal data structures
	 * if certain input events arrive.
	 */
	func void checkInputEvent(InputEvent event)
		select event.getType()
		case InputEventType.deviceAttachedDetached
			var Array oldDevices = Array.new(pDevices)
			
			reloadDevices()
			
			// notify listeners about removed devices
			oldDevices.forEach(block InputDevice oldDevice
				if pDevices.find(block InputDevice newDevice
					return newDevice.getID().equals(oldDevice.getID())
				end) == null
					pListeners.forEach(block BindingManagerListener each
						each.deviceDetached(this, oldDevice)
					end)
				end
			end)
			
			// notify listeners about added devices
			pDevices.forEach(block InputDevice newDevice
				if oldDevices.find(block InputDevice oldDevice
					return oldDevice.getID().equals(newDevice.getID())
				end) == null
					pListeners.forEach(block BindingManagerListener each
						each.deviceAttached(this, newDevice)
					end)
				end
			end)
			
		case InputEventType.deviceParamsChanged
			reloadDevices()
		end
	end
	
	
	
	/** \brief Number of axis bindings. */
	func int getAxisBindingCount()
		return pAxisBindings.getCount()
	end
	
	/** \brief Axis binding at index. */
	func Binding getAxisBindingAt(int index)
		return pAxisBindings.getAt(index) cast Binding
	end
	
	/** \brief Add axis binding. */
	func void addAxisBinding(Binding binding)
		if binding == null
			throw EInvalidParam.new()
		end
		
		pAxisBindings.add(binding)
		
		updateAxisBindingIndices(binding)
		
		pListeners.forEach(block BindingManagerListener each
			each.axisBindingAdded(this, binding)
		end)
	end
	
	/** \brief Remove axis binding. */
	func void removeAxisBinding(Binding binding)
		pAxisBindings.remove(binding)
		
		pListeners.forEach(block BindingManagerListener each
			each.axisBindingRemoved(this, binding)
		end)
	end
	
	/** \brief Remove all axis bindings. */
	func void removeAllAxisBindings()
		if pAxisBindings.getCount() == 0
			return
		end
		
		pAxisBindings.removeAll()
		
		pListeners.forEach(block BindingManagerListener each
			each.allAxisBindingsRemoved(this)
		end)
	end
	
	/** \brief Visit axis bindings with block with parameter Binding. */
	func void forEachAxisBinding(Block ablock)
		pAxisBindings.forEach(ablock)
	end
	
	/**
	 * \brief Find axis binding matching device and source.
	 * \deprecated Use findAxisBinding(InputEventSource,int,int).
	 */
	func Binding findAxisBinding(int device, int axis)
		return findAxisBinding(InputEventSource.input, device, axis)
	end
	
	/**
	 * \brief Find axis binding matching device and source.
	 * \version 1.6
	 */
	func Binding findAxisBinding(InputEventSource source, int device, int axis)
		return pAxisBindings.find(block Binding each
			return each.getSystem() == source and each.getDeviceIndex() == device \
				and each.getSourceIndex() == axis
		end) cast Binding
	end
	
	/**
	 * \brief Find axis binding matching device and source.
	 * \deprecated Use findAxisBinding(InputEventSource,int,int,bool)
	 */
	func Binding findAxisBinding(int device, int axis, bool inverseAxis)
		return findAxisBinding(InputEventSource.input, device, axis, inverseAxis)
	end
	
	/**
	 * \brief Find axis binding matching device and source.
	 * \version 1.6
	 */
	func Binding findAxisBinding(InputEventSource source, int device, int axis, bool inverseAxis)
		return pAxisBindings.find(block Binding each
			return each.getSystem() == source and each.getDeviceIndex() == device \
				and each.getSourceIndex() == axis and each.getInverseAxis() == inverseAxis
		end) cast Binding
	end
	
	/** \brief Find axis binding matching device and source. */
	func Binding findAxisBinding(String deviceID, String axisID)
		return pAxisBindings.find(block Binding each
			return each.getDeviceID().equals(deviceID) \
				and each.getSourceID().equals(axisID)
		end) cast Binding
	end
	
	/** \brief Find axis binding matching device and source. */
	func Binding findAxisBinding(String deviceID, String axisID, bool inverseAxis)
		return pAxisBindings.find(block Binding each
			return each.getDeviceID().equals(deviceID) \
				and each.getSourceID().equals(axisID) \
				and each.getInverseAxis() == inverseAxis
		end) cast Binding
	end
	
	/** \brief Find axis binding matching block with parameter Binding. */
	func Binding findAxisBinding(Block ablock)
		return pAxisBindings.find(ablock) cast Binding
	end
	
	/**
	 * \brief Collect axis binding matching block with parameter Binding as Array of Binding.
	 * \version 1.10
	 */
	func Array collectAxisBinding(Block ablock)
		return pAxisBindings.collect(ablock)
	end
	
	
	/** \brief Number of button bindings. */
	func int getButtonBindingCount()
		return pButtonBindings.getCount()
	end
	
	/** \brief Button binding at index. */
	func Binding getButtonBindingAt(int index)
		return pButtonBindings.getAt(index) cast Binding
	end
	
	/** \brief Add button binding. */
	func void addButtonBinding(Binding binding)
		if binding == null
			throw EInvalidParam.new()
		end
		
		pButtonBindings.add(binding)
		
		updateButtonBindingIndices(binding)
		
		pListeners.forEach(block BindingManagerListener each
			each.buttonBindingAdded(this, binding)
		end)
	end
	
	/** \brief Remove button binding. */
	func void removeButtonBinding(Binding binding)
		pButtonBindings.remove(binding)
		
		pListeners.forEach(block BindingManagerListener each
			each.buttonBindingRemoved(this, binding)
		end)
	end
	
	/** \brief Remove all button bindings. */
	func void removeAllButtonBindings()
		if pButtonBindings.getCount() == 0
			return
		end
		
		pButtonBindings.removeAll()
		
		pListeners.forEach(block BindingManagerListener each
			each.allButtonBindingsRemoved(this)
		end)
	end
	
	/** \brief Visit button bindings with block with parameter Binding. */
	func void forEachButtonBinding(Block ablock)
		pButtonBindings.forEach(ablock)
	end
	
	/**
	 * \brief Find button binding matching device and source.
	 * \deprecated Use findButtonBinding(InputEventSource,int,int)
	 */
	func Binding findButtonBinding(int device, int button)
		return findButtonBinding(InputEventSource.input, device, button)
	end
	
	/**
	 * \brief Find button binding matching device and source.
	 * \version 1.6
	 */
	func Binding findButtonBinding(InputEventSource source, int device, int button)
		return pButtonBindings.find(block Binding each
			return each.getSystem() == source and each.getDeviceIndex() == device \
				and each.getSourceIndex() == button
		end) cast Binding
	end
	
	/** \brief Find button binding matching device and source. */
	func Binding findButtonBinding(String deviceID, String buttonID)
		return pButtonBindings.find(block Binding each
			return each.getDeviceID().equals(deviceID) and each.getSourceID().equals(buttonID)
		end) cast Binding
	end
	
	/** \brief Find button binding matching block with parameter Binding. */
	func Binding findButtonBinding(Block ablock)
		return pButtonBindings.find(ablock) cast Binding
	end
	
	/**
	 * \brief Collect button binding matching block with parameter Binding as Array of Binding.
	 * \version 1.10
	 */
	func Array collectButtonBinding(Block ablock)
		return pButtonBindings.collect(ablock)
	end
	
	
	/** \brief Remove all bindings with command. */
	func void removeBindingsWithCommand(Command command)
		pAxisBindings.collect(block Binding each
			return each.getCommand() == command
		end).forEach(block Binding each
			removeAxisBinding(each)
		end)
		
		pButtonBindings.collect(block Binding each
			return each.getCommand() == command
		end).forEach(block Binding each
			removeButtonBinding(each)
		end)
	end
	
	
	/** \brief Number of input devices. */
	func int getDeviceCount()
		return pDevices.getCount()
	end
	
	/** \brief Device at index. */
	func InputDevice getDeviceAt(int index)
		return pDevices.getAt(index) cast InputDevice
	end
	
	/** \brief Visit devices with block with parameter InputDevice. */
	func void forEachDevice(Block ablock)
		pDevices.forEach(ablock)
	end
	
	/**
	 * \brief Find device with block with parameter InputDevice.
	 * 
	 * \returns \em null if no device matches.
	 */
	func InputDevice findDevice(Block ablock)
		return pDevices.find(ablock) cast InputDevice
	end
	
	/**
	 * \brief Collect devices with block with parameter InputDevice as Array of InputDevice.
	 * \version 1.10
	 */
	func Array collectDevices(Block ablock)
		return pDevices.collect(ablock)
	end
	
	
	/**
	 * \brief Update axis binding indices.
	 * 
	 * Call if you made changes to the binding affecting device and source id.
	 * You do not need to call this if you added or removed the binding.
	 */
	func void updateAxisBindingIndices(Binding binding)
		var int deviceIndex = -1
		var int sourceIndex = -1
		
		deviceIndex = InputSystem.indexOfDeviceWithID(binding.getDeviceID())
		if deviceIndex != -1
			sourceIndex = InputSystem.indexOfAxisWithID(deviceIndex, binding.getSourceID())
			if sourceIndex != -1
				binding.setSystem(InputEventSource.input)
				binding.setDeviceIndex(deviceIndex)
				binding.setSourceIndex(sourceIndex)
				return
			end
			
		else
			deviceIndex = VRSystem.indexOfDeviceWithID(binding.getDeviceID())
			if deviceIndex != -1
				sourceIndex = VRSystem.indexOfAxisWithID(deviceIndex, binding.getSourceID())
				if sourceIndex != -1
					binding.setSystem(InputEventSource.vr)
					binding.setDeviceIndex(deviceIndex)
					binding.setSourceIndex(sourceIndex)
					return
				end
			end
		end
		
		binding.setSystem(null)
		binding.setDeviceIndex(-1)
		binding.setSourceIndex(-1)
	end
	
	/**
	 * \brief Update button binding indices.
	 * 
	 * Call if you made changes to the binding affecting device and source id.
	 * You do not need to call this if you added or removed the binding.
	 */
	func void updateButtonBindingIndices(Binding binding)
		var int deviceIndex = -1
		var int sourceIndex = -1
		
		deviceIndex = InputSystem.indexOfDeviceWithID(binding.getDeviceID())
		if deviceIndex != -1
			sourceIndex = InputSystem.indexOfButtonWithID(deviceIndex, binding.getSourceID())
			if sourceIndex != -1
				binding.setSystem(InputEventSource.input)
				binding.setDeviceIndex(deviceIndex)
				binding.setSourceIndex(sourceIndex)
				return
			end
			
		else
			deviceIndex = VRSystem.indexOfDeviceWithID(binding.getDeviceID())
			if deviceIndex != -1
				sourceIndex = VRSystem.indexOfButtonWithID(deviceIndex, binding.getSourceID())
				if sourceIndex != -1
					binding.setSystem(InputEventSource.vr)
					binding.setDeviceIndex(deviceIndex)
					binding.setSourceIndex(sourceIndex)
					return
				end
			end
		end
		
		binding.setSystem(null)
		binding.setDeviceIndex(-1)
		binding.setSourceIndex(-1)
	end
	
	/**
	 * \brief Update all binding indices.
	 * 
	 * Call if you made changes to many bindings affecting device and source ids.
	 * You do not need to call this if you just added or removed bindings.
	 */
	func void updateAllBindingIndices()
		pAxisBindings.forEach(block Binding each
			updateAxisBindingIndices(each)
		end)
		
		pButtonBindings.forEach(block Binding each
			updateButtonBindingIndices(each)
		end)
	end
	
	/** \brief Update input devices and all binding indices. */
	func void reloadDevices()
		var int i, deviceCount
		
		pDevices.removeAll()
		
		deviceCount = InputSystem.getDeviceCount()
		for i = 0 to deviceCount
			pDevices.add(InputSystem.getDeviceAt(i))
		end
		
		deviceCount = VRSystem.getDeviceCount()
		for i = 0 to deviceCount
			pDevices.add(VRSystem.getDeviceAt(i))
		end
		
		findDefaultDevices()
		updateAllBindingIndices()
		
		pListeners.forEach(block BindingManagerListener each
			each.devicesReloaded(this)
		end)
	end
	
	
	/**
	 * \brief Default mouse device or null.
	 * \version 1.28
	 */
	func InputDevice getMouse()
		return pMouse
	end
	
	/**
	 * \brief Default keyboard device or null.
	 * \version 1.28
	 */
	func InputDevice getKeyboard()
		return pKeyboard
	end
	
	/**
	 * \brief Default VR HMD device or null.
	 * \version 1.28
	 */
	func InputDevice getVRHMD()
		return pVRHMD
	end
	
	/**
	 * \brief Default VR Right Hand device or null.
	 * \version 1.28
	 */
	func InputDevice getVRRightHand()
		return pVRRightHand
	end
	
	/**
	 * \brief Default VR Left Hand device or null.
	 * \version 1.28
	 */
	func InputDevice getVRLeftHand()
		return pVRLeftHand
	end
	
	/**
	 * \brief Find default devices for quick access.
	 * \version 1.28
	 */
	protected func void findDefaultDevices()
		pMouse = pKeyboard = pVRHMD = pVRRightHand = pVRLeftHand = null
		
		pDevices.forEach(block InputDevice each
			select each.getType()
			case InputDeviceType.mouse
				if pMouse == null
					pMouse = each
				end
				
			case InputDeviceType.keyboard
				if pKeyboard == null
					pKeyboard = each
				end
				
			case InputDeviceType.vrHMD
				if pVRHMD == null
					pVRHMD = each
				end
				
			case InputDeviceType.vrLeftHand
				if pVRLeftHand == null
					pVRLeftHand = each
				end
				
			case InputDeviceType.vrRightHand
				if pVRRightHand == null
					pVRRightHand = each
				end
			end
		end)
	end
	
	
	/**
	 * \brief Add listener.
	 * \version 1.6
	 */
	func void addListener(BindingManagerListener listener)
		if listener == null
			throw ENullPointer.new("listener")
		end
		pListeners.add(listener)
	end
	
	/**
	 * \brief Remove listener.
	 * \version 1.6
	 */
	func void removeListener(BindingManagerListener listener)
		pListeners.remove(listener)
	end
end
