/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.LoadSave

pin Dragengine.Gui.Designers
pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.XML



/**
 * \brief Load designer from xml file.
 */
class LoadWidgetDesigner extends BaseXmlLoader
	private var Dictionary pLoadedDesigners
	private var LoadCanvasCreator pCanvasCreatorLoader
	private var LoadBorder pBorderLoader
	private var LoadDecoration pDecorationLoader
	private var LoadMousePointer pMousePointerLoader
	private var WidgetDesigners pDesigners
	
	
	
	/** \brief Create loader. */
	public func new( Console console ) this( console, "LoadWidgetDesigner" )
	end
	
	/** \brief Create loader. */
	public func new( Console console, String name ) super( console, name )
		this.pLoadedDesigners = Dictionary.new()
		this.pDesigners = WidgetDesigners.new()
		this.pDesigners.addDefaultDesigners()
	end
	
	
	
	/** \brief Canvas creator loader. */
	public func LoadCanvasCreator getCanvasCreatorLoader()
		return this.pCanvasCreatorLoader
	end
	
	/** \brief Set canvas creator loader. */
	public func void setCanvasCreatorLoader( LoadCanvasCreator loader )
		this.pCanvasCreatorLoader = loader
	end
	
	/** \brief Border loader. */
	public func LoadBorder getBorderLoader()
		return this.pBorderLoader
	end
	
	/** \brief Set border loader. */
	public func void setBorderLoader( LoadBorder loader )
		this.pBorderLoader = loader
	end
	
	/** \brief Decoration loader. */
	public func LoadDecoration getDecorationLoader()
		return this.pDecorationLoader
	end
	
	/** \brief Set decoration loader. */
	public func void setDecorationLoader( LoadDecoration loader )
		this.pDecorationLoader = loader
	end
	
	/** \brief Mouse pointer loader. */
	public func LoadMousePointer getMousePointerLoader()
		return this.pMousePointerLoader
	end
	
	/** \brief Set mouse pointer loader. */
	public func void setMousePointerLoader( LoadMousePointer loader )
		this.pMousePointerLoader = loader
	end
	
	
	
	/** \brief Designers. */
	public func WidgetDesigners getDesigners()
		return this.pDesigners
	end
	
	/** \brief Set designers. */
	public func void setDesigners( WidgetDesigners designers )
		if designers == null
			throw EInvalidParam.new()
		end
		this.pDesigners = designers
	end
	
	
	
	/**
	 * \brief Load widget designer from xml file.
	 * 
	 * Widget designers are shared. Do not modify directly. If you need to modify them
	 * call copyDesigner() first.
	 */
	public func WidgetDesigner loadFromFile( String filename )
		var WidgetDesigner designer = this.pLoadedDesigners.getAt( \
			filename, null ) cast WidgetDesigner
		if designer!= null
			return designer
		end
		
		this.logLoadingFromFile( filename )
		
		var EasyXML document = EasyXML.new( filename )
		if document.hasParseFailed()
			this.logParsingFailed( document.getParseLog() )
			throw EInvalidParam.new()
		end
		
		var EasyXMLElement root = document.getRootElement()
		if not root.getTagName().equals( "designer" )
			this.logUnknownTag( filename, root )
			throw EInvalidParam.new()
		end
		
		var String basePath = File.new( filename ).getParent().getPath()
		
		designer = this.parseWidgetDesigner( filename, root, basePath )
		this.pLoadedDesigners.setAt( filename, designer )
		return designer
	end
	
	
	
	/**
	 * \brief Parse widget designer tag.
	 * \details This is public to be used by LoadGuiTheme.
	 */
	public func WidgetDesigner parseWidgetDesigner( String filename, \
	EasyXMLElement root, String basePath )
		return this.parseWidgetDesigner( filename, root, basePath, null )
	end
	
	/**
	 * \brief Parse widget designer tag.
	 * \details This is public to be used by LoadGuiTheme.
	 */
	public func WidgetDesigner parseWidgetDesigner( String filename, \
	EasyXMLElement root, String basePath, GuiTheme guitheme )
		var WidgetDesigner designer
		
		if root.hasAttributeNamed( "extend" )
			try
				designer = this.loadFromFile( File.absolutePath( \
					root.getAttributeNamed( "extend" ), basePath ).getPath() )
				
			catch Exception e
				this.printOnConsole( filename, root, "Failed loading widget designer '" \
					+ root.getAttributeNamed( "extend" ) + "'" )
				throw
			end
			
		elif root.hasAttributeNamed( "extendNamed" )
			if guitheme == null
				this.printOnConsole( filename, root, "extendNamed used with null guitheme." )
				throw EInvalidParam.new()
			end
			
			try
				designer = guitheme.getNamedWidgetDesigner( root.getAttributeNamed( "extendNamed" ) )
				
			catch Exception e
				this.printOnConsole( filename, root, "Designer '" \
					+ root.getAttributeNamed( "extendNamed" ) + "' not found." )
				throw
			end
			
		elif root.hasAttributeNamed( "type" )
			designer = this.pDesigners.getNamed( root.getAttributeNamed( "type" ) )
			if designer == null
				this.printOnConsole( filename, root, "Widget designer type '" \
					+ root.getAttributeNamed( "type" ) + "' not found" )
				throw EInvalidParam.new()
			end
			
		else
			this.printOnConsole( filename, root, "Source attribute missing" )
			throw EInvalidParam.new()
		end
		
		var int i, elementCount = root.getElementCount()
		var EasyXMLElement element
		var Object parameterValue
		var String parameterName
		var bool isShared = true
		
		for i = 0 to elementCount
			element = root.getElementAt( i )
			if element.getType() != EasyXMLElementType.tag
				continue
			end
			
			if isShared
				designer = designer.copyDesigner()
				isShared = false
			end
			
			parameterName = element.getAttributeNamed( "parameter" )
			parameterValue = this.parseParameter( filename, element, basePath, guitheme )
			
			try
				designer.setParameter( parameterName, parameterValue )
				
			catch Exception e
				this.printOnConsole( filename, element, designer.className() \
					+ " does not understand parameter '" + parameterName \
					+ "' with value '" + parameterValue + "'" )
			end
		end
		
		return designer
	end
	
	
	
	/**
	 * \brief Parse designer parameter.
	 * \details Overwrite in subclass to extend the supported parameter types.
	 */
	private func Object parseParameter( String filename, EasyXMLElement element, \
	String basePath, GuiTheme guitheme )
		var String tagName = element.getTagName()
		
		if tagName.equals( "integer" )
			return element.getFirstCDataInt()
			
		elif tagName.equals( "float" )
			return element.getFirstCDataFloat()
			
		elif tagName.equals( "boolean" )
			return element.getFirstCDataBool()
			
		elif tagName.equals( "string" )
			return element.getFirstCData()
			
		elif tagName.equals( "color" )
			return this.readColor( element )
			
		elif tagName.equals( "colorMatrix" )
			return this.readColorMatrix( element )
			
		elif tagName.equals( "font" )
			try
				return this.readFont( element, basePath )
				
			catch Exception e
				this.printOnConsole( filename, element, "Failed loading font '" \
					+ element.getFirstCData() + "'" )
				throw
			end
			
		elif tagName.equals( "image" )
			var String path = element.getFirstCData()
			if path.empty()
				return null
			end
			
			try
				return Image.new( File.absolutePath( path, basePath ).getPath() )
				
			catch Exception e
				this.printOnConsole( filename, element, "Failed loading image '" + path + "'" )
				throw
			end
			
		elif tagName.equals( "video" )
			var String path = element.getFirstCData()
			if path.empty()
				return null
			end
			
			try
				return Video.new( File.absolutePath( path, basePath ).getPath() )
				
			catch Exception e
				this.printOnConsole( filename, element, "Failed loading video '" + path + "'" )
				throw
			end
			
		elif tagName.equals( "borderSize" )
			return this.readBorderSize( element )
			
		elif tagName.equals( "point" )
			return this.readPoint( element )
			
		elif tagName.equals( "canvasCreator" )
			return this.pCanvasCreatorLoader.parseCanvasCreator( filename, element, basePath )
			
		elif tagName.equals( "border" )
			return this.pBorderLoader.parseBorder( filename, element, basePath )
			
		elif tagName.equals( "decoration" )
			return this.pDecorationLoader.parseDecoration( filename, element, basePath )
			
		elif tagName.equals( "mousePointer" )
			return this.pMousePointerLoader.parseMousePointer( filename, element, basePath )
			
		elif tagName.equals( "designer" )
			return this.parseWidgetDesigner( filename, element, basePath, guitheme )
			
		elif tagName.equals( "null" )
			return null
			
		else
			this.logUnknownTag( filename, element )
			throw EInvalidParam.new()
		end
	end
end
