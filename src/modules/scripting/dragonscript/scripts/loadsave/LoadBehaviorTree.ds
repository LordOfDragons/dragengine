/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.LoadSave

pin Dragengine.BehaviorTreeSystem
pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.XML



/**
 *\brief Load behavior tree from debehaviorTree file.
 * 
 * Supports caching behavior trees to avoid loading the same behavior tree multiple times.
 * Returned behavior tree can thus be shared so do not change it.
 */
class LoadBehaviorTree extends BaseXmlLoader
	/**\brief Dictionary of cached behavior trees (Dictionary: String -> BehaviorTree). */
	var Dictionary pBehaviorTrees
	
	
	
	/**\brief Create loader. */
	func new(Console console) this(console, "LoadBehaviorTree")
	end
	
	/**\brief Create new loader. */
	func new(Console console, String name) super(console, name)
		pBehaviorTrees = Dictionary.new()
	end
	
	
	
	/**\brief Load behaviorTree from file. */
	func BehaviorTree loadFromFile(String filename)
		var BehaviorTree behaviorTree = pBehaviorTrees.getAt(filename, null) cast BehaviorTree
		if behaviorTree != null
			return behaviorTree
		end
		
		logLoadingFromFile(filename)
		
		try
			var EasyXML document = EasyXML.new(filename)
			if document.hasParseFailed()
				logParsingFailed(document.getParseLog())
				throw EInvalidParam.new()
			end
			
			var EasyXMLElement root = document.getRootElement()
			if not root.getTagName().equals("behaviorTree")
				logUnknownTag(root)
				throw EInvalidParam.new()
			end
			
			behaviorTree = BehaviorTree.new()
			readBehaviorTree(root, behaviorTree, File.new(filename).getParent().getPath())
			
			pBehaviorTrees.setAt(filename, behaviorTree)
			return behaviorTree
			
		catch Exception e
			logLoadingFailed(filename)
			throw
		end
	end
	
	
	
	/**\brief Read behavior tree tag. */
	private func void readBehaviorTree(EasyXMLElement root, BehaviorTree behaviorTree, String basePath)
		var bool hasRootRule = false
		var BTRule rule
		
		root.forEachTag(block EasyXMLElement element, String tagName
			rule = readRule(element, behaviorTree, "", "", basePath)
			if rule == null
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
			
			if hasRootRule
				var String id = behaviorTree.getRootRule().getID()
				logDuplicateObject(element, "Root rule", id != null if id else "")
				throw EInvalidParam.new()
			end
			
			behaviorTree.setRootRule(rule)
			hasRootRule = true
			
			if rule.getID() != null
				behaviorTree.registerRule(rule)
			end
		end)
	end
	
	/**
	 *\brief Read rule tag.
	 * 
	 *\returns Parse rule or\em null if not a rule tag.
	 */
	private func BTRule readRule(EasyXMLElement element, BehaviorTree behaviorTree,\
	String idPrefix, String parentID, String basePath)
		var String tagName = element.getTagName()
		
		if tagName.equals("action")
			return readRuleAction(element, behaviorTree, idPrefix, parentID)
			
		elif tagName.equals("sequence")
			return readRuleSequence(element, behaviorTree, idPrefix, parentID, basePath)
			
		elif tagName.equals("choice")
			return readRuleChoice(element, behaviorTree, idPrefix, parentID, basePath)
			
		elif tagName.equals("success")
			return readRuleSuccess(element, behaviorTree, idPrefix, parentID)
			
		elif tagName.equals("failure")
			return readRuleFailure(element, behaviorTree, idPrefix, parentID)
			
		elif tagName.equals("running")
			return readRuleRunning(element, behaviorTree, idPrefix, parentID)
			
		elif tagName.equals("subtree") or tagName.equals("subTree")
			return readRuleSubTree(element, behaviorTree, idPrefix, parentID, basePath)
			
		else
			return null
		end
	end
	
	/**\brief Get rule identifier from element. */
	private func String pGetRuleID(EasyXMLElement element, BehaviorTree behaviorTree,\
	String idPrefix, String parentID)
		if not element.hasAttributeNamed("id")
			return null
		end
		
		var String id = element.getAttributeNamed("id")
		if id.empty()
			logInvalidValue(element, id)
			throw EInvalidParam.new()
		end
		
		if id.getAt(0) == '.'
			id = parentID + id
			
		else
			id = idPrefix + id
		end
		
		if behaviorTree.hasRuleWithID(id)
			logDuplicateObject(element, "rule", id)
			throw EInvalidParam.new()
		end
		
		return id
	end
	
	/**\brief Read rule common. */
	private func bool readRuleCommon(EasyXMLElement element, BTRule rule)
		var String tagName = element.getTagName()
		
		if tagName.equals("parameter")
			rule.getParameters().setAt(element.getAttributeNamed("name"), element.getFirstCData())
			
		elif tagName.equals("condition")
			rule.addCondition(element.getFirstCData())
			
		elif tagName.equals("conditionMode")
			var String value = element.getFirstCData()
			var BTRule.ConditionMode mode = BTRule.ConditionMode.named(value)
			if mode == null
				logInvalidValue(element, value)
				throw EInvalidParam.new()
			end
			rule.setConditionMode(mode)
			
		else
			return false
		end
		
		return true
	end
	
	/**\brief Read action rule. */
	private func BTRuleAction readRuleAction(EasyXMLElement root, BehaviorTree behaviorTree,\
	String idPrefix, String parentID)
		var BTRuleAction rule = BTRuleAction.new(pGetRuleID(root, behaviorTree, idPrefix, parentID),\
			root.getAttributeNamed("name"))
		if root.hasAttributeNamed("doNotFail")
			rule.setDoNotFail(root.getAttributeNamed("doNotFail").equals("true"))
		end
		
		root.forEachTag(block EasyXMLElement element, String tagName
			if not readRuleCommon(element, rule)
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
		end)
		
		return rule
	end
	
	/**\brief Read sequence rule. */
	private func BTRuleSequence readRuleSequence(EasyXMLElement root, BehaviorTree behaviorTree,\
	String idPrefix, String parentID, String basePath)
		var BTRuleSequence rule = BTRuleSequence.new(pGetRuleID(root, behaviorTree, idPrefix, parentID))
		var BTRule childRule
		
		if root.hasAttributeNamed("loop")
			rule.setLooping(root.getAttributeNamed("loop").equals("true"))
		end
		if root.hasAttributeNamed("doNotFail")
			rule.setDoNotFail(root.getAttributeNamed("doNotFail").equals("true"))
		end
		
		if rule.getID() != null
			parentID = rule.getID()
		end
		
		root.forEachTag(block EasyXMLElement element, String tagName
			childRule = readRule(element, behaviorTree, idPrefix, parentID, basePath)
			if childRule != null
				rule.addRule(childRule)
				if childRule.getID() != null
					behaviorTree.registerRule(childRule)
				end
				
			elif not readRuleCommon(element, rule)
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
		end)
		
		return rule
	end
	
	/**\brief Read choice rule. */
	private func BTRuleChoice readRuleChoice(EasyXMLElement root, BehaviorTree behaviorTree,\
	String idPrefix, String parentID, String basePath)
		var BTRuleChoice rule = BTRuleChoice.new(pGetRuleID(root, behaviorTree, idPrefix, parentID))
		var BTRule childRule
		
		if root.hasAttributeNamed("loop")
			rule.setLooping(root.getAttributeNamed("loop").equals("true"))
		end
		if root.hasAttributeNamed("doNotFail")
			rule.setDoNotFail(root.getAttributeNamed("doNotFail").equals("true"))
		end
		
		if rule.getID() != null
			parentID = rule.getID()
		end
		
		root.forEachTag(block EasyXMLElement element, String tagName
			childRule = readRule(element, behaviorTree, idPrefix, parentID, basePath)
			if childRule != null
				rule.addRule(childRule)
				if childRule.getID() != null
					behaviorTree.registerRule(childRule)
				end
				
			elif not readRuleCommon(element, rule)
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
		end)
		
		return rule
	end
	
	/**\brief Read success rule. */
	private func BTRuleSuccess readRuleSuccess(EasyXMLElement root, BehaviorTree behaviorTree,\
	String idPrefix, String parentID)
		var BTRuleSuccess rule = BTRuleSuccess.new(pGetRuleID(root, behaviorTree, idPrefix, parentID))
		
		root.forEachTag(block EasyXMLElement element, String tagName
			if not readRuleCommon(element, rule)
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
		end)
		
		return rule
	end
	
	/**\brief Read failure rule. */
	private func BTRuleFailure readRuleFailure(EasyXMLElement root, BehaviorTree behaviorTree,\
	String idPrefix, String parentID)
		var BTRuleFailure rule = BTRuleFailure.new(pGetRuleID(root, behaviorTree, idPrefix, parentID))
		
		root.forEachTag(block EasyXMLElement element, String tagName
			if not readRuleCommon(element, rule)
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
		end)
		
		return rule
	end
	
	/**\brief Read running rule. */
	private func BTRuleRunning readRuleRunning(EasyXMLElement root, BehaviorTree behaviorTree,\
	String idPrefix, String parentID)
		var BTRuleRunning rule = BTRuleRunning.new(pGetRuleID(root, behaviorTree, idPrefix, parentID))
		
		root.forEachTag(block EasyXMLElement element, String tagName
			if not readRuleCommon(element, rule)
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
		end)
		
		return rule
	end
	
	/**\brief Read sub tree rule. */
	private func BTRuleSequence readRuleSubTree(EasyXMLElement root, BehaviorTree behaviorTree,\
	String idPrefix, String parentID, String basePath)
		var BTRuleSequence rule = BTRuleSequence.new(pGetRuleID(root, behaviorTree, idPrefix, parentID))
		
		if root.hasAttributeNamed("loop")
			rule.setLooping(root.getAttributeNamed("loop").equals("true"))
		end
		if root.hasAttributeNamed("doNotFail")
			rule.setDoNotFail(root.getAttributeNamed("doNotFail").equals("true"))
		end
		
		var String filename = File.absolutePath(root.getFirstCData(), basePath).getPath()
		var String subBasePath = File.new(filename).getParent().getPath()
		var BTRule subRule
		try
			var EasyXML document = EasyXML.new(filename)
			if document.hasParseFailed()
				logParsingFailed(document.getParseLog())
				throw EInvalidParam.new()
			end
			
			var EasyXMLElement subRoot = document.getRootElement()
			if not subRoot.getTagName().equals("behaviorTree")
				logUnknownTag(subRoot)
				throw EInvalidParam.new()
			end
			
			if rule.getID() != null
				idPrefix = rule.getID() + "."
			end
			
			subRoot.forEachTag(block EasyXMLElement subElement, String subTagName
				subRule = readRule(subElement, behaviorTree, idPrefix, "", subBasePath)
				if subRule == null
					logUnknownTag(subElement)
					throw EInvalidParam.new()
				end
				
				rule.addRule(subRule)
				if subRule.getID() != null
					behaviorTree.registerRule(subRule)
				end
			end)
			
		catch Exception e
			logLoadingFailed(filename)
			throw
		end
		
		return rule
	end
end
