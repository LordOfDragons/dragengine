/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.LoadSave

pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.Gui
pin Dragengine.XML



/**
 * \brief Load ParameterTree from XML file.
 * 
 * Example file:
 * 
 * \code{.xml}
 * <?xml version='1.0' encoding='ISO-8859-1'?>
 * <parameterTree>
 *   <string path='ui/mainMenu/bgImage'>/content/mainMenuBG.png</string>
 *   <list path='ui/mainMenu/borderColors'>
 *     <color r='1' g='0' b='0'/>
 *     <color r='0' g='1' b='1'/>
 *   </list>
 * </parameterTree>
 * \endcode
 * 
 * The "parameterTree" tag can have the "pathPrefix" attribute. If used this prefix is prepended
 * to all "path" attributes as is. This avoids writing the same prefix for all keys.
 * 
 * Supported are these tags:
 * - <string></string>: UTF8 encoded string value
 * - <float></float>: Floating point value
 * - <int></int>: Integer value
 * - <bool></bool>: Boolean value ("true", "false")
 * - <point x='' y=''/>: 2-Component point (integer)
 * - <point3 x='' y='' z=''/>: 3-Component point (integer)
 * - <vector2 x='' y=''/>: 2-Component vector (float)
 * - <vector x='' y='' z=''/>: 3-Component vector (float)
 * - <color r='' g='' b='' a=''/>: Color (float)
 * - <borderSize left='' top='' right='' bottom=''/>: BorderSize (int)
 * - <rectArea x1='' y1='' x2='' y2=''/>: RectArea (int)
 * - <floatRectArea x1='' y1='' x2='' y2=''/>: FloatRectArea (float)
 * - <list></list>: List of values
 * - <map></map>: Map of values
 * - <tree></tree>: Inline parameter tree
 * - <treeFile>filename</treeFile>: Parameter tree loaded from file relative to this file
 * - <null/>: null value
 * 
 * Inside <map> tag values required the attribute "key='name'".
 */
class LoadParameterTree extends BaseXmlLoader
	/** \brief Create loader. */
	func new(Console console) this(console, "LoadParameterTree")
	end
	
	/** \brief Create loader. */
	func new(Console console, String name) super(console, name)
	end
	
	
	
	/**
	 * \brief Load element class from file.
	 * 
	 * Loaded content is added to \em parameterTree. It is not cleared first.
	 */
	func void loadFromFile(String filename, ParameterTree parameterTree)
		if parameterTree == null
			throw ENullPointer.new()
		end
		
		loadAndParseRootTag(filename, "parameterTree", block EasyXMLElement root
			readParameterTree(root, parameterTree, File.new(filename).getParent().getPath())
		end)
	end
	
	/**
	 * \brief Load element class from file into a new ParameterTree.
	 * \version 1.23
	 */
	func ParameterTree loadFromFile(String filename)
		var ParameterTree parameterTree = ParameterTree.new()
		loadFromFile(filename, parameterTree)
		return parameterTree
	end
	
	/** \brief Save parameter tree to file. */
	func void saveToFile(String filename, ParameterTree parameterTree)
		if parameterTree == null
			throw ENullPointer.new()
		end
		
		logSavingFromFile(filename)
		
		var EasyXML document = EasyXML.new()
		var EasyXMLElement root = document.getRootElement()
		
		root.setTagName("parameterTree")
		writeParameterTree(root, parameterTree)
		
		document.writeToFile(FileWriter.new(filename), false)
	end
	
	
	
	/** \brief Parse parameter tree tag. */
	protected func void readParameterTree(EasyXMLElement root, ParameterTree tree, String basePath)
		var String pathPrefix = ""
		if root.hasAttributeNamed("pathPrefix")
			pathPrefix = root.getAttributeNamed("pathPrefix")
		end
		
		root.forEachTag(block EasyXMLElement element, String tagName
			tree.setAt(pathPrefix + element.getAttributeNamed("path"), readValue(element, basePath))
		end)
	end
	
	/**
	 * \brief Parse value.
	 * \version 1.26
	 */
	protected func Object readValue(EasyXMLElement element, String basePath)
		var String tagName = element.getTagName()
		
		if tagName.equals("string")
			return element.getFirstCData()
			
		elif tagName.equals("float")
			return element.getFirstCDataFloat()
			
		elif tagName.equals("integer")
			return element.getFirstCDataInt()
			
		elif tagName.equals("boolean")
			return element.getFirstCDataBool()
			
		elif tagName.equals("vector")
			return readVector(element)
			
		elif tagName.equals("vector2")
			return readVector2(element)
			
		elif tagName.equals("point")
			return readPoint(element)
			
		elif tagName.equals("point3")
			return readPoint3(element)
			
		elif tagName.equals("borderSize")
			return readBorderSize(element)
			
		elif tagName.equals("rectArea")
			return readRectArea(element)
			
		elif tagName.equals("floatRectArea")
			return readFloatRectArea(element)
			
		elif tagName.equals("color")
			return readColor(element)
			
		elif tagName.equals("null")
			return null
			
		elif tagName.equals("list")
			return readList(element, basePath)
			
		elif tagName.equals("map")
			return readMap(element, basePath)
			
		elif tagName.equals("tree")
			var ParameterTree tree = ParameterTree.new()
			readParameterTree(element, tree, basePath)
			return tree
			
		elif tagName.equals("treeFile")
			return loadFromFile(File.absolutePath(element.getCDataContent(), basePath).getPath())
			
		else
			logUnknownTag(element)
			throw EInvalidParam.new()
		end
	end
	
	/** \brief Parse list tag. */
	protected func Array readList(EasyXMLElement root, String basePath)
		var Array list = Array.new()
		
		root.forEachTag(block EasyXMLElement element, String tagName
			list.add(readValue(element, basePath))
		end)
		
		return list
	end
	
	/**
	 * \brief Parse map tag.
	 * \version 1.26
	 */
	protected func Dictionary readMap(EasyXMLElement root, String basePath)
		var Dictionary map = Dictionary.new()
		
		root.forEachTag(block EasyXMLElement element, String tagName
			map.setAt(element.getAttributeNamed("key"), readValue(element, basePath))
		end)
		
		return map
	end
	
	
	
	/** \brief Write parameter tree tag. */
	protected func void writeParameterTree(EasyXMLElement root, ParameterTree tree)
		tree.forEach(block String path, Object value
			writeParameter(root, path, null, value)
		end)
	end
	
	/** \brief Write parameter tag. */
	protected func void writeParameter(EasyXMLElement root, String path, Object value)
		writeParameter(root, path, null, value)
	end
	
	/**
	 * \brief Write parameter tag.
	 * \version 1.26
	 */
	protected func void writeParameter(EasyXMLElement root, String path, String key, Object value)
		var EasyXMLElement tag
		
		if value typeof Array
			tag = root.addTag("list")
			tag.setAttribute("path", path)
			(value cast Array).forEach(block Object each
				writeParameter(tag, null, null, each)
			end)
			
		elif value typeof Dictionary
			tag = root.addTag("map")
			tag.setAttribute("path", path)
			(value cast Dictionary).forEach(block String key, Object value
				writeParameter(tag, null, key, value)
			end)
			
		elif value typeof ParameterTree
			tag = root.addTag("tree")
			tag.setAttribute("path", path)
			writeParameterTree(tag, value cast ParameterTree)
			
		elif value typeof String
			tag = root.addDataTag("string", value cast String)
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			
		elif value typeof float
			tag = root.addDataTag("float", value cast float)
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			
		elif value typeof int
			tag = root.addDataTag("integer", value cast int)
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			
		elif value typeof bool
			tag = root.addDataTag("boolean", value cast bool)
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			
		elif value typeof Vector
			var Vector vector = value cast Vector
			tag = root.addTag("vector")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("x", vector.getX())
			tag.setAttribute("y", vector.getY())
			tag.setAttribute("z", vector.getZ())
			
		elif value typeof Vector2
			var Vector2 vector = value cast Vector2
			tag = root.addTag("vector2")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("x", vector.getX())
			tag.setAttribute("y", vector.getY())
			
		elif value typeof Point
			var Point point = value cast Point
			tag = root.addTag("point")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("x", point.getX())
			tag.setAttribute("y", point.getY())
			
		elif value typeof Point3
			var Point3 point = value cast Point3
			tag = root.addTag("point3")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("x", point.getX())
			tag.setAttribute("y", point.getY())
			tag.setAttribute("z", point.getZ())
			
		elif value typeof BorderSize
			var BorderSize size = value cast BorderSize
			tag = root.addTag("borderSize")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("left", size.getLeft())
			tag.setAttribute("top", size.getTop())
			tag.setAttribute("right", size.getRight())
			tag.setAttribute("bottom", size.getBottom())
			
		elif value typeof RectArea
			var RectArea area = value cast RectArea
			tag = root.addTag("rectArea")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("x1", area.getX1())
			tag.setAttribute("y1", area.getY1())
			tag.setAttribute("x2", area.getX2())
			tag.setAttribute("y2", area.getY2())
			
		elif value typeof FloatRectArea
			var FloatRectArea area = value cast FloatRectArea
			tag = root.addTag("floatRectArea")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("x1", area.getX1())
			tag.setAttribute("y1", area.getY1())
			tag.setAttribute("x2", area.getX2())
			tag.setAttribute("y2", area.getY2())
			
		elif value typeof Color
			var Color color = value cast Color
			tag = root.addTag("color")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			tag.setAttribute("r", color.getRed())
			tag.setAttribute("g", color.getGreen())
			tag.setAttribute("b", color.getBlue())
			tag.setAttribute("a", color.getAlpha())
			
		elif value == null
			tag = root.addTag("null")
			if path != null
				tag.setAttribute("path", path)
				
			elif key != null
				tag.setAttribute("key", key)
			end
			
		else
			throw EInvalidParam.new("Unsupported object type: " + value.className())
		end
	end
end
