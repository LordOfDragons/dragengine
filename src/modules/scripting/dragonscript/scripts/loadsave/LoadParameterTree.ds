/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.LoadSave

pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.Gui
pin Dragengine.XML



/**
 * \brief Load ParameterTree from XML file.
 * 
 * Example file:
 * 
 * \code{.xml}
 * <?xml version='1.0' encoding='ISO-8859-1'?>
 * <parameterTree>
 *   <string path='ui/mainMenu/bgImage'>/content/mainMenuBG.png</string>
 *   <list path='ui/mainMenu/borderColors'>
 *     <color r='1' g='0' b='0'/>
 *     <color r='0' g='1' b='1'/>
 *   </list>
 * </parameterTree>
 * \endcode
 * 
 * Supported are these tags:
 * - <string></string>: UTF8 encoded string value
 * - <float></float>: Floating point value
 * - <int></int>: Integer value
 * - <bool></bool>: Boolean value ("true", "false")
 * - <point x='' y=''/>: 2-Component point (integer)
 * - <point3 x='' y='' z=''/>: 3-Component point (integer)
 * - <vector2 x='' y=''/>: 2-Component vector (float)
 * - <vector x='' y='' z=''/>: 3-Component vector (float)
 * - <borderSize left='' top='' right='' bottom=''/>: BorderSize (int)
 * - <rectArea x1='' y1='' x2='' y2=''/>: RectArea (int)
 * - <floatRectArea x1='' y1='' x2='' y2=''/>: FloatRectArea (float)
 * - <list></list>: List of values (anything except <list>)
 * - <null/>: null value
 */
class LoadParameterTree extends BaseXmlLoader
	/** \brief Create loader. */
	func new(Console console) this(console, "LoadParameterTree")
	end
	
	/** \brief Create loader. */
	func new(Console console, String name) super(console, name)
	end
	
	
	
	/**
	 * \brief Load element class from file.
	 * 
	 * Loaded content is added to \em parameterTree. It is not cleared first.
	 */
	func void loadFromFile(String filename, ParameterTree parameterTree)
		if parameterTree == null
			throw ENullPointer.new()
		end
		
		loadAndParseRootTag(filename, "parameterTree", block EasyXMLElement	root
			readParameterTree(root, parameterTree)
		end)
	end
	
	/**
	 * \brief Load element class from file into a new ParameterTree.
	 * \version 1.23
	 */
	func ParameterTree loadFromFile(String filename)
		var ParameterTree parameterTree = ParameterTree.new()
		loadFromFile(filename, parameterTree)
		return parameterTree
	end
	
	/** \brief Save parameter tree to file. */
	func void saveToFile(String filename, ParameterTree parameterTree)
		if parameterTree == null
			throw ENullPointer.new()
		end
		
		logSavingFromFile(filename)
		
		var EasyXML document = EasyXML.new()
		var EasyXMLElement root = document.getRootElement()
		
		root.setTagName("parameterTree")
		writeParameterTree(root, parameterTree)
		
		document.writeToFile(FileWriter.new(filename), false)
	end
	
	
	
	/** \brief Parse parameter tree tag. */
	protected func void readParameterTree(EasyXMLElement root, ParameterTree tree)
		var Object value
		var String path
		
		root.forEachTag(block EasyXMLElement element, String tagName
			path = element.getAttributeNamed("path")
			
			if tagName.equals("string")
				value = element.getFirstCData()
				
			elif tagName.equals("float")
				value = element.getFirstCDataFloat()
				
			elif tagName.equals("integer")
				value = element.getFirstCDataInt()
				
			elif tagName.equals("boolean")
				value = element.getFirstCDataBool()
				
			elif tagName.equals("vector")
				value = readVector(element)
				
			elif tagName.equals("vector2")
				value = readVector2(element)
				
			elif tagName.equals("point")
				value = readPoint(element)
				
			elif tagName.equals("point3")
				value = readPoint3(element)
				
			elif tagName.equals("borderSize")
				value = readBorderSize(element)
				
			elif tagName.equals("rectArea")
				value = readRectArea(element)
				
			elif tagName.equals("floatRectArea")
				value = readFloatRectArea(element)
				
			elif tagName.equals("color")
				value = readColor(element)
				
			elif tagName.equals("null")
				value = null
				
			elif tagName.equals("list")
				value = readList(element)
				
			else
				logUnknownTag(element)
				throw EInvalidParam.new()
			end
			
			tree.setAt(path, value)
		end)
	end
	
	/** \brief Parse list tag. */
	protected func Array readList(EasyXMLElement root)
		var Array list = Array.new()
		
		root.forEachTag(block EasyXMLElement element, String tagName
			if tagName.equals("string")
				list.add(element.getFirstCData())
				
			elif tagName.equals("float")
				list.add(element.getFirstCDataFloat())
				
			elif tagName.equals("integer")
				list.add(element.getFirstCDataInt())
				
			elif tagName.equals("boolean")
				list.add(element.getFirstCDataBool())
				
			elif tagName.equals("vector")
				list.add(readVector(element))
				
			elif tagName.equals("vector2")
				list.add(readVector2(element))
				
			elif tagName.equals("point")
				list.add(readPoint(element))
				
			elif tagName.equals("point3")
				list.add(readPoint3(element))
				
			elif tagName.equals("borderSize")
				list.add(readBorderSize(element))
				
			elif tagName.equals("rectArea")
				list.add(readRectArea(element))
				
			elif tagName.equals("floatRectArea")
				list.add(readFloatRectArea(element))
				
			elif tagName.equals("color")
				list.add(readColor(element))
				
			elif tagName.equals("null")
				list.add(null)
				
			else
				logUnknownTag(element)
			end
		end)
		
		return list
	end
	
	
	
	/** \brief Write parameter tree tag. */
	protected func void writeParameterTree(EasyXMLElement root, ParameterTree tree)
		tree.forEach(block String path, Object value
			writeParameter(root, path, value)
		end)
	end
	
	/** \brief Write parameter tag. */
	protected func void writeParameter(EasyXMLElement root, String path, Object value)
		var EasyXMLElement tag
		
		if value typeof Array
			tag = root.addTag("list")
			tag.setAttribute("path", path)
			(value cast Array).forEach(block Object each
				writeParameter(tag, null, each)
			end)
			
		elif value typeof String
			tag = root.addDataTag("string", value cast String)
			if path != null
				tag.setAttribute("path", path)
			end
			
		elif value typeof float
			tag = root.addDataTag("float", value cast float)
			if path != null
				tag.setAttribute("path", path)
			end
			
		elif value typeof int
			tag = root.addDataTag("integer", value cast int)
			if path != null
				tag.setAttribute("path", path)
			end
			
		elif value typeof bool
			tag = root.addDataTag("boolean", value cast bool)
			if path != null
				tag.setAttribute("path", path)
			end
			
		elif value typeof Vector
			var Vector vector = value cast Vector
			tag = root.addTag("vector")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("x", vector.getX())
			tag.setAttribute("y", vector.getY())
			tag.setAttribute("z", vector.getZ())
			
		elif value typeof Vector2
			var Vector2 vector = value cast Vector2
			tag = root.addTag("vector2")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("x", vector.getX())
			tag.setAttribute("y", vector.getY())
			
		elif value typeof Point
			var Point point = value cast Point
			tag = root.addTag("point")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("x", point.getX())
			tag.setAttribute("y", point.getY())
			
		elif value typeof Point3
			var Point3 point = value cast Point3
			tag = root.addTag("point3")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("x", point.getX())
			tag.setAttribute("y", point.getY())
			tag.setAttribute("z", point.getZ())
			
		elif value typeof BorderSize
			var BorderSize size = value cast BorderSize
			tag = root.addTag("borderSize")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("left", size.getLeft())
			tag.setAttribute("top", size.getTop())
			tag.setAttribute("right", size.getRight())
			tag.setAttribute("bottom", size.getBottom())
			
		elif value typeof RectArea
			var RectArea area = value cast RectArea
			tag = root.addTag("rectArea")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("x1", area.getX1())
			tag.setAttribute("y1", area.getY1())
			tag.setAttribute("x2", area.getX2())
			tag.setAttribute("y2", area.getY2())
			
		elif value typeof FloatRectArea
			var FloatRectArea area = value cast FloatRectArea
			tag = root.addTag("floatRectArea")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("x1", area.getX1())
			tag.setAttribute("y1", area.getY1())
			tag.setAttribute("x2", area.getX2())
			tag.setAttribute("y2", area.getY2())
			
		elif value typeof Color
			var Color color = value cast Color
			tag = root.addTag("color")
			if path != null
				tag.setAttribute("path", path)
			end
			tag.setAttribute("r", color.getRed())
			tag.setAttribute("g", color.getGreen())
			tag.setAttribute("b", color.getBlue())
			tag.setAttribute("a", color.getAlpha())
			
		elif value == null
			tag = root.addTag("null")
			if path != null
				tag.setAttribute("path", path)
			end
			
		else
			throw EInvalidParam.new()
		end
	end
end
