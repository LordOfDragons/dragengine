/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.StateMachineSystem

pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Action to run in a state machine state.
 * 
 * SMAction subclasses are created by the game developer and added to the state machine.
 * If state action is run the action is looked up and run.
 */
class SMStateAction
	var String pActionID, pStateID
	var Dictionary pParameters
	var Array pConditions
	var SMTransition.ConditionMode pConditionMode
	
	
	/** \brief Create state machine action. */
	func new(String actionID)
		if actionID == null
			throw ENullPointer.new("actionID")
		end
		
		pActionID = actionID
		pParameters = Dictionary.new()
		pConditions = Array.new()
		pConditionMode = SMTransition.ConditionMode.allTrue
	end
	
	/**
	 * \brief Create copy of state machine action.
	 * \version 1.26
	 */
	func new(SMStateAction action)
		pActionID = action.pActionID
		pParameters = Dictionary.new(action.pParameters)
		pConditions = Array.new(action.pConditions)
		pConditionMode = action.pConditionMode
		pStateID = action.pStateID
	end
	
	
	/** \brief Action identifier. */
	func String getActionID()
		return pActionID
	end
	
	/**
	 * \brief Parameters as Dictionary of type String key and String value.
	 * \warning Do not change the content of the dictionary as it is shared.
	 */
	func Dictionary getParameters()
		return pParameters
	end
	
	/**
	 * \brief State to transition to after action finished or \em null.
	 * \version 1.26
	 */
	func String getStateID()
		return pStateID
	end
	
	/**
	 * \brief Set state to transition to after action finished or \em null.
	 * \version 1.26
	 */
	func void setStateID(String id)
		pStateID = id
	end
	
	
	/**
	 * \brief Count of conditions.
	 * \version 1.26
	 */
	func int getConditionCount()
		return pConditions.getCount()
	end
	
	/**
	 * \brief Get condition at index.
	 * \version 1.26
	 */
	func String getConditionAt(int index)
		return pConditions.getAt(index) cast String
	end
	
	/**
	 * \brief Add condition.
	 * \version 1.26
	 */
	func void addCondition(String condition)
		if condition == null
			throw ENullPointer.new("condition")
		end
		
		pConditions.add(condition)
	end
	
	/**
	 * \brief Visit conditions with block with parameter String.
	 * \version 1.26
	 */
	func void forEachCondition(Block ablock)
		pConditions.forEach(ablock)
	end
	
	/**
	 * \brief Find condition with block with parameter String.
	 * \version 1.26
	 */
	func String findCondition(Block ablock)
		return pConditions.find(ablock) cast String
	end
	
	/**
	 * \brief Condition mode from eConditionModes.
	 * \version 1.26
	 */
	func SMTransition.ConditionMode getConditionMode()
		return pConditionMode
	end
	
	/**
	 * \brief Set condition mode from eConditionModes.
	 * \version 1.26
	 */
	func void setConditionMode(SMTransition.ConditionMode mode)
		if mode == null
			throw ENullPointer.new("mode")
		end
		
		pConditionMode = mode
	end
	
	/**
	 * \brief Evaluate all conditions.
	 * \version 1.25
	 * 
	 * If no condition is present \em true is returned.
	 * 
	 * \param parameters Dictionary with parameters of type String key and String value.
	 *                   Do not change the content of the dictionary as it is shared.
	 */
	func bool evaluateConditions(SMContext context)
		if pConditions.getCount() == 0
			return true
		end
		
		var SMCondition condition
		
		select pConditionMode
		case SMTransition.ConditionMode.allTrue
			return pConditions.find(block String each
				condition = context.getCondition(each)
				return condition == null or not condition.evaluateCondition(context, pParameters)
			end) == null
			
		case SMTransition.ConditionMode.anyTrue
			return pConditions.find(block String each
				condition = context.getCondition(each)
				return condition != null and condition.evaluateCondition(context, pParameters)
			end) != null
			
		case SMTransition.ConditionMode.anyFalse
			return pConditions.find(block String each
				condition = context.getCondition(each)
				return condition == null or not condition.evaluateCondition(context, pParameters)
			end) != null
			
		case SMTransition.ConditionMode.allFalse
			return pConditions.find(block String each
				condition = context.getCondition(each)
				return condition != null and condition.evaluateCondition(context, pParameters)
			end) == null
			
		else
			return false
		end
	end
	
	
	/**
	 * \brief Run action.
	 * \returns true if action ran or false if conditions are not fulfilled.
	 */
	func bool run(SMContext context)
		if not evaluateConditions(context)
			if context.getLogActions() and context.getConsole() != null
				context.getConsole().addMessage("{}: Action '{}' skipped".format(\
					Array.newWith(context.getLoggingName(), pActionID)))
			end
			return false
		end
		
		if context.getLogActions() and context.getConsole() != null
			context.getConsole().addMessage("{}: Action '{}' run".format(\
				Array.newWith(context.getLoggingName(), pActionID)))
		end
		
		var SMAction action = context.getAction(pActionID)
		if action != null
			action.runAction(context, pParameters)
		end
		return true
	end
end
