/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Gui

pin Dragengine.ConversationSystem
pin Dragengine.Gui.Borders
pin Dragengine.Gui.Events
pin Dragengine.Gui.Layouts
pin Dragengine.Utils


/**
 * \brief Displays list of sub titles.
 * 
 * By default texts are stacked from newest to oldest. The text vanish after their hide timer
 * elapses. Optionally the SubTitleBox can be set to remove all texts before adding a new one.
 * This turns the SubTitleBox into an RPG like text box where each new text replaces existing
 * one if present.
 * 
 * The default layout is CentrizedBoxLayout using Y_AXIS_NEG. This displays each block of text
 * with as much width as required but at most the entire width in which case text blocks
 * wrap around using multiple lines. To get all blocks spanning the entire width use FlowLayout.
 */
class SubTitleBox extends Panel implements ConversationPlaybackSubTitle
	private var bool pRemoveAllBeforeAdd
	
	
	
	/** \brief Create sub title box. */
	public func new()
		pRemoveAllBeforeAdd = false
		setLayout(CentrizedBoxLayout.new(LayoutAxis.inverseY))
		setDesignerSelector("SubTitleBox")
	end
	
	
	
	/** \brief Remove all text before adding a new one. */
	public func bool getRemoveAllBeforeAdd()
		return pRemoveAllBeforeAdd
	end
	
	/** \brief Set to remove all text before adding a new one. */
	public func void setRemoveAllBeforeAdd(bool removeAllBeforeAdd)
		pRemoveAllBeforeAdd = removeAllBeforeAdd
	end
	
	
	
	// this is an own class so later on gradual resizing can be implemented to avoid text blocks
	// jumping around on the screen too much. for the time being though this is a simple panel
	// with an y-axis flow layout.
	
	/**
	 * \brief Add sub title text widget.
	 * 
	 * Text is added at the bottom since the layout fills up from the botton starting with
	 * the first text. Adding it at the top would make the newest text to appear at the top
	 * instead of the bottom.
	 * 
	 * This could be also solved by using multiple panels but this is simpler.
	 */
	public func void addSubTitleText(Widget text)
		if pRemoveAllBeforeAdd
			removeAllSubTitleText()
			addWidget(text)
			
		else
			addWidget(text)
			moveWidget(text, 0)
		end
	end
	
	/** \brief Remove all text from the box. */
	public func void removeAllSubTitleText()
		removeAllWidgets()
	end
end
