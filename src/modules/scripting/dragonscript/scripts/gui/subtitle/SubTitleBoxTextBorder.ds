/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland PlÃ¼ss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Gui

pin Dragengine.Gui.Borders
pin Dragengine.Gui.Events
pin Dragengine.Gui.Layouts
pin Dragengine.Utils.StyledText.Definition
pin Dragengine.LoadSave


/**
 * \brief Sub title box text using border layout for name, text and portrait.
 * 
 * Displays a styled text and optionally name and portrait image or video. The box can be
 * designed with selector "SubTitleBoxText".
 * 
 * The name label can be designed with selector "Label.SubTitleBoxText.Name".
 * The text part can be designed with selector "Label.SubTitleBoxText.Text".
 * If StyledTextDefinition is used the label designers are ignored.
 * 
 * The portrait image can be designed with selector "Image.SubTitleBoxText.Portrait".
 * The portrait video can be designed with selector "Video.SubTitleBoxText.Portrait".
 * 
 * The most simple use of SubTitleBoxText is by using the designers mentioned above.
 * This uses the same design for all text.
 * 
 * For situation specific design setDesignerSelector can be used to assign a different
 * selector for different text.
 * 
 * For the most customization support use StyledTextDefinition which give full control over
 * the text combined with setDesignerSelector for desigining the portrait and panel itself.
 * 
 * The placement of the name text can be left or above the text as well as above or below
 * the portrait if present.
 */
class SubTitleBoxTextBorder extends Panel implements Persistable, SubTitleBoxText
	/** \brief Factory for loading actor actions. */
	public class Factory implements PersistencyFactory
		public static fixed var String NAME = "SubTitleBoxTextBorder"
		
		public func new()
		end
		
		public func Persistable readObjectFromFile( PersistencyEnvironment env, FileReader reader )
			return SubTitleBoxTextBorder.readFromFile( env, reader )
		end
		
		public static func void registerFactory( Persistency persistency )
			persistency.addFactory( NAME, Factory.new() )
		end
	end
	
	/** \brief Hide timer. */
	protected class HideTimer extends Timer
		private var SubTitleBoxTextBorder pText
		
		public func new( SubTitleBoxTextBorder text, float timeout ) super( timeout, false )
			this.pText = text
		end
		
		public func void fire()
			this.pText.hideSubtitleNow()
		end
	end
	
	/** \brief Placement. */
	public enum Placement
		/** \brief Place name left of text above portrait. */
		leftAbove
		
		/** \brief Place name left of text below portrait. */
		leftBelow
		
		/** \brief Place name above text. */
		above
	end
	
	
	
	private var Label pLabelName
	private var Label pLabelText
	private var Placement pPlacement
	private var DisplayStyledText pStyledName
	private var DisplayStyledText pStyledText
	private var DisplayImage pImagePortrait
	private var DisplayVideo pVideoPortrait
	private var HideTimer pHideTimer
	
	
	
	/**
	 * \brief Create sub title box text.
	 * 
	 * Text is mandatory. Name and portrait are optional and can be set to \em null. If portrait
	 * is used either \em portraitImage or \em portraitVideo has to be set. The placement of
	 * the name is set using \em placement. Use designer selectors to style the elements.
	 * If \em styledName aor \em styledText is used designer selectors have no effect on \
	 * name or text.
	 * 
	 * \param text Simple text. Ignored if styledText is used.
	 * \param name Name or \em null to not display a name.
	 * \param styledText Styled text or \em null to use simple text.
	 * \param styledName Styled name or \em null to use simple name.
	 * \param portraitImage Portrait image or \em null if not used.
	 * \param portraitVideo Portrait video or \em null if not used.
	 * \param placement Name placement from NAME_*.
	 */
	public func new( String text, String name, StyledTextDefinition styledText, \
	StyledTextDefinition styledName, Image portraitImage, Video portraitVideo, \
	Placement placement )
		if text == null and styledText == null
			throw EInvalidParam.new()
		end
		
		this.pPlacement = placement
		
		// create the individual element. they will be added to the correct place below
		var Widget widgetName, widgetText, widgetPortrait
		
		if styledName != null
			this.pStyledName = DisplayStyledText.new()
			this.pStyledName.setPagedMode( false )
			this.pStyledName.setStyledTextDefinition( styledName )
			widgetName = this.pStyledName
			
		elif name != null
			this.pLabelName = Label.new( name )
			widgetName = this.pLabelName
		end
		
		if styledText != null
			this.pStyledText = DisplayStyledText.new()
			this.pStyledText.setPagedMode( false )
			this.pStyledText.setStyledTextDefinition( styledText )
			widgetText = this.pStyledText
			
		else
			this.pLabelText = Label.new( text )
			this.pLabelText.setWordWrap( true )
			widgetText = this.pLabelText
		end
		
		if portraitVideo != null
			this.pVideoPortrait = DisplayVideo.new( portraitVideo )
			this.pVideoPortrait.play()
			widgetPortrait = this.pVideoPortrait
			
		elif portraitImage != null
			this.pImagePortrait = DisplayImage.new( portraitImage )
			widgetPortrait = this.pImagePortrait
		end
		
		// build layout adding the elements in the right place
		this.runWhileBlockingLayout( block
			// why no FlowLayout.new( LayoutAxis.x, padding, FlowLayout.Stretch.last ) used here?
			// FlowLayout with X_AXIS uses getMinimumSizeForHeight on children. Both Label and
			// DisplayStyledText return in this case the size for a single line even if they
			// would wrap around. we need thus a layout here which uses getMinimumSizeForWidth
			// on all children to work properly. BorderLayout is a candidate for this. Another
			// solution would be FormLayout but sub titles have at most 3 elements arranged
			// in a border shape. BorderLayout here is the best solution
			this.setLayout( BorderLayout.new() )
			
			var Panel sidePanel = Panel.new()
			sidePanel.setLayout( CentrizedBoxLayout.new( LayoutAxis.y ) )
			this.addWidget( sidePanel, BorderLayout.Area.left )
			
			select placement
			case Placement.leftBelow
				if widgetPortrait != null
					sidePanel.addWidget( widgetPortrait )
				end
				if widgetName != null
					sidePanel.addWidget( widgetName )
				end
				
			case Placement.leftAbove
				if widgetName != null
					sidePanel.addWidget( widgetName )
				end
				if widgetPortrait != null
					sidePanel.addWidget( widgetPortrait )
				end
				
			case Placement.above
				if widgetPortrait != null
					sidePanel.addWidget( widgetPortrait )
				end
				if widgetName != null
					this.addWidget( widgetName, BorderLayout.Area.top )
				end
			end
			
			this.addWidget( widgetText, BorderLayout.Area.content )
			
			this.setStateChangeNotifyChildren( true )
			this.setDesignerSelector( "SubTitleBoxText" )
		end )
	end
	
	/** \brief Read action from file. */
	public static func SubTitleBoxTextBorder readFromFile( \
	PersistencyEnvironment env, FileReader reader )
		select reader.readByte() // version
		case 0
			// read content to know what kind of SubTitleBoxTextBorder to create
			var String labelName = env.readString8OrNullIfEmpty( reader )
			var String labelText = env.readString16OrNullIfEmpty( reader )
			var String pathImagePortrait = env.readString16OrNullIfEmpty( reader )
			var String pathVideoPortrait = env.readString16OrNullIfEmpty( reader )
			var SubTitleBoxTextBorder.Placement placement = \
				SubTitleBoxTextBorder.Placement.withOrder( reader.readByte() )
			
			// TODO how to read styled text?
			var StyledTextDefinition styledText
			var StyledTextDefinition styledName
			
			var String designerSelector = reader.readString8()
			
			var float triggerTime = reader.readFloat()
			var float elapsedTime = reader.readFloat()
			
			// create SubTitleBoxTextBorder the right way
			var Image imagePortrait
			if pathImagePortrait != null
				imagePortrait = Image.new( pathImagePortrait )
			end
			
			var Video videoPortrait
			if pathVideoPortrait != null
				videoPortrait = Video.new( pathVideoPortrait )
			end
			
			var SubTitleBoxTextBorder stbt = SubTitleBoxTextBorder.new( labelText, labelName, \
				styledText, styledName, imagePortrait, videoPortrait, placement )
			stbt.setDesignerSelector( designerSelector )
			
			if triggerTime > 0.0
				stbt.pHideTimer = HideTimer.new( stbt, triggerTime )
				stbt.pHideTimer.setElapsedTime( elapsedTime )
			end
			
			return stbt
			
		else
			throw EInvalidParam.new( "unsupported version" )
		end
	end
	
	/** \brief Dispose of widget. */
	public func void dispose()
		if this.pHideTimer != null
			this.pHideTimer.giveUp()
			this.pHideTimer = null
		end
		
		this.pStyledText = null
		this.pStyledName = null
		this.pLabelText = null
		this.pLabelName = null
		this.pImagePortrait = null
		this.pVideoPortrait = null
		
		super.dispose()
	end
	
	
	
	/** \brief Name label or \em null. */
	public func Label getLabelName()
		return this.pLabelName
	end
	
	/** \brief Text label or \em null. */
	public func Label getLabelText()
		return this.pLabelText
	end
	
	/** \brief Placement. */
	public func Placement getPlacement()
		return this.pPlacement
	end
	
	/** \brief Name styled text or \em null. */
	public func DisplayStyledText getStyledName()
		return this.pStyledName
	end
	
	/** \brief Text styled text or \em null. */
	public func DisplayStyledText getStyledText()
		return this.pStyledText
	end
	
	/** \brief Portait image or \em null. */
	public func DisplayImage getImagePortrait()
		return this.pImagePortrait
	end
	
	/** \brief Video portrait or \em null. */
	public func DisplayVideo getVideoPortrait()
		return this.pVideoPortrait
	end
	
	
	/** \brief Hide after timer elapses. */
	public func void hideSubtitleAfter( float seconds )
		if this.pHideTimer != null
			this.pHideTimer.giveUp()
			this.pHideTimer = null
		end
		
		this.pHideTimer = HideTimer.new( this, seconds )
	end
	
	/** \brief Hide now. */
	public func void hideSubtitleNow()
		var Container parent = this.getParent()
		if parent != null
			parent.removeWidget( this )
		end
		this.dispose()
	end
	
	/** \brief Subtitle widget. */
	public func Widget subtitleWidget()
		return this
	end
	
	
	
	/**
	 * \brief Designer selector changed.
	 * 
	 * If name label is present sets designer selector to "Label.\{designerSelector\}.Name".
	 * If text label is present sets designer selector to "Label.\{designerSelector\}.Text".
	 * 
	 * Both labels can be designed with selector 'Label.{designerSelector}'.
	 */
	public func void onDesignerSelectorChanged()
		var String selector = this.getDesignerSelector()
		
		if this.pLabelName != null
			this.pLabelName.setDesignerSelector( "Label." + selector + ".Name" )
		end
		if this.pStyledName != null
			this.pStyledName.setDesignerSelector( "Styled." + selector + ".Name" )
		end
		if this.pLabelText != null
			this.pLabelText.setDesignerSelector( "Label." + selector + ".Text" )
		end
		if this.pStyledText != null
			this.pStyledText.setDesignerSelector( "Styled." + selector + ".Text" )
		end
		if this.pImagePortrait != null
			this.pImagePortrait.setDesignerSelector( "Image." + selector + ".Portrait" )
		end
		if this.pVideoPortrait != null
			this.pVideoPortrait.setDesignerSelector( "Video." + selector + ".Portrait" )
		end
	end
	
	
	
	
	/**
	 * \brief Name of PersistencyFactory required to load object from file.
	 * 
	 * Name of PersistencyFactory is first written to the file as string with
	 * 8-bit length. Then writeToFile() is called to write the object itself.
	 * Empty name is not allowed as is names longer than 255 characters.
	 * Name has to match a PersistencyFactory present in the Persistency
	 * instance used for loading objects. Object is read using
	 * PersistencyFactory.readObjectFromFile() of the matching PersistencyFactory.
	 */
	public func String persistencyFactoryName()
		return Factory.NAME
	end
	
	/** \brief Write object to file writer. */
	public func void writeObjectToFile( PersistencyEnvironment env, FileWriter writer )
		writer.writeByte( 0 ) // version
		
		if this.pLabelName != null
			writer.writeString8( this.pLabelName.getText() )
			
		else
			writer.writeString8( "" )
		end
		
		if this.pLabelText != null
			writer.writeString16( this.pLabelText.getText() )
			
		else
			writer.writeString16( "" )
		end
		
		if this.pImagePortrait != null and this.pImagePortrait.getImage() != null
			writer.writeString16( this.pImagePortrait.getImage().getFilename() )
			
		else
			writer.writeString16( "" )
		end
		
		if this.pVideoPortrait != null and this.pVideoPortrait.getVideo() != null
			writer.writeString16( this.pVideoPortrait.getVideo().getFilename() )
			
		else
			writer.writeString16( "" )
		end
		
		writer.writeByte( this.pPlacement.order() )
		
		// TODO how to write styled text?
		
		// selector name
		writer.writeString8( this.getDesignerSelector() )
		
		// hide timer
		if this.pHideTimer == null
			writer.writeFloat( 0.0 )
			writer.writeFloat( 0.0 )
			
		else
			writer.writeFloat( this.pHideTimer.getTriggerTime() )
			writer.writeFloat( this.pHideTimer.getElapsedTime() )
		end
	end
end
