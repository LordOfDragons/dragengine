/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland PlÃ¼ss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Gui



/**
 * \brief Default implementation of ListModel.
 * 
 * \par Version 1.5 Changes
 * 
 * Added support to set Auto-Select mode. Default is enabled as before version 1.5 . If enabled
 * adding and removing items ensures an item is selected if the list is not empty. If disabled
 * the user is responsible to set the selection after adding or removing items. If disabled
 * and the selected item is removed the selection will be cleared.
 */
class DefaultListModel implements ListModel
	private var Array pElements
	private var Array pListeners
	private var int pSelected
	private var bool pAutoSelect
	
	
	
	/** \brief Create default list model. */
	public func new()
		pElements = Array.new()
		pListeners = Array.new()
		pSelected = -1
		pAutoSelect = true
	end
	
	/** \brief Create default list model with initial content. */
	public func new(Array content)
		pElements = Array.new(content)
		pListeners = Array.new()
		pSelected = content.getCount() > 0 if 0 else -1
		pAutoSelect = true
	end
	
	
	
	/** \brief Add listener. */
	public func void addListener(ListModelListener listener)
		if listener == null
			throw EInvalidParam.new()
		end
		pListeners.add(listener)
	end
	
	/** \brief Remove listener. */
	public func void removeListener(ListModelListener listener)
		pListeners.remove(listener)
	end
	
	
	
	/** \brief Number of elements. */
	public func int getCount()
		return pElements.getCount()
	end
	
	/**
	 * \brief Element at index.
	 * \throws EOutOfBounds \em index is less than 0 or larger than or equal to getCount().
	 */
	public func Object getAt(int index)
		return pElements.getAt(index)
	end
	
	/**
	 * \brief Set element at index.
	 * 
	 * If element at index changed a content changed event is send to listeners. Changing many
	 * elements at once can cause performance problems due to each change causing widgets to
	 * relayout. If this is a problem use one of the mass editing methods.
	 * 
	 * \throws EOutOfBounds \em index is less than 0 or larger than or equal to getCount().
	 */
	public func void setAt(int index, Object element)
		if element == pElements.getAt(index)
			return
		end
		
		pElements.setAt(index, element)
		notifyContentChanged(index, index)
	end
	
	/**
	 * \brief Replaces elements in range.
	 * 
	 * If range is not empty a content changed event is send to listeners. Use this
	 * method if you need to change multiple elements at once to cause only one change
	 * notification resulting in widgets to relayout once instead of multiple times.
	 * 
	 * \throws EOutOfBounds \em firstIndex is less than 0 or larger than or equal getCount().
	 * \throws EOutOfBounds \em lastIndex is less than 0 or larger than or equal getCount().
	 * \throws EInvalidParam \em lastIndex is less than \em firstIndex.
	 * \throws EInvalidParam Number of elements in \em elements does not equal number of
	 *                       elements in range.
	 */
	public func void setRange(int firstIndex, int lastIndex, Array elements)
		if firstIndex == lastIndex
			return
		end
		
		if lastIndex < firstIndex
			throw EInvalidParam.new()
		end
		if lastIndex >= pElements.getCount()
			throw EOutOfBounds.new()
		end
		
		var int count = lastIndex - firstIndex + 1
		if elements.getCount() != count
			throw EInvalidParam.new()
		end
		
		var int i
		for i = 0 to count
			pElements.setAt(firstIndex + i, elements.getAt(i))
		end
		notifyContentChanged(firstIndex, lastIndex)
	end
	
	/**
	 * \brief Replaces all elements.
	 * 
	 * If range is not empty a content changed event is send to listeners. Use this
	 * method if you need to change all elements at once to cause only one change
	 * notification resulting in widgets to relayout once instead of multiple times.
	 * 
	 * \throws EInvalidParam Number of elements in \em elements does not equal number
	 *                       of elements in the model.
	 */
	public func void setAll(Array elements)
		var int count = pElements.getCount()
		if elements.getCount() != count
			throw EInvalidParam.new()
		end
		
		if count == 0
			return
		end
		
		var Object selectedOld, selectedNew
		if pSelected != -1
			selectedOld = pElements.getAt(pSelected)
			selectedNew = elements.getAt(pSelected)
		end
		
		pElements.removeAll()
		pElements.addAll(elements)
		
		notifyContentChanged(0, count - 1)
		
		if not (selectedOld != null and selectedOld.equals(selectedNew))
			if pAutoSelect and count > 0
				pSelected = 0
				
			else
				pSelected = -1
			end
			
			pListeners.forEach(block ListModelListener each
				each.selectionChanged(this)
			end)
		end
	end
	
	/** \brief Index of element or -1 if absent. */
	public func int indexOf(Object element)
		return pElements.indexOf(element)
	end
	
	/** \brief Element is present. */
	public func bool has(Object element)
		return pElements.has(element)
	end
	
	/**
	 * \brief Add element.
	 * 
	 * After adding a content changed event is send to listeners. If you have to set
	 * many elements at once performance problems can arise due to each change causing
	 * widgets to relayout. If this is the case use one of the mass editing methods.
	 */
	public func void add(Object element)
		pElements.add(element)
		
		var int index = pElements.getCount() - 1
		notifyElementsAdded(index, index)
	end
	
	/**
	 * \brief Add elements from array.
	 * 
	 * After adding a content changed event is send to listeners. If you have
	 * to add many elements addAll() instead of add() for performance reasons.
	 */
	public func void addAll(Array content)
		var int count = content.getCount()
		if count == 0
			return
		end
		
		var int indexFrom = pElements.getCount()
		
		pElements.addAll(content)
		
		notifyElementsAdded(indexFrom, indexFrom + count - 1)
	end
	
	/**
	 * \brief Insert element at index.
	 * 
	 * After inserting a content changed event is send to listeners. If you have to insert
	 * many elements at once performance problems can arise due to each change causing
	 * widgets to relayout. If this is the case use one of the mass editing methods.
	 * 
	 * \throws EOutOfBounds \em index is less than 0 or larger than getCount().
	 */
	public func void insert(int index, Object element)
		var int selected = pSelected
		if selected >= index
			selected++
		end
		
		pElements.insert(index, element)
		notifyElementsAdded(index, index)
		setSelected(selected)
	end
	
	/**
	 * \brief Insert elements from array at index.
	 * 
	 * After inserting a content changed event is send to listeners. If you have to
	 * insert many elements insertAll() instead of insert() for performance reasons.
	 * 
	 * \throws EOutOfBounds \em index is less than 0 or larger than getCount().
	 */
	public func void insertAll(int index, Array content)
		var int count = content.getCount()
		if count == 0
			return
		end
		
		var int selected = pSelected
		if selected >= index
			selected += count
		end
		
		pElements.insertAll(index, content)
		notifyElementsAdded(index, index + count - 1)
		setSelected(selected)
	end
	
	/**
	 * \brief Move element to new location.
	 * \version 1.11
	 * 
	 * Same as calling remove(Object) and insert(int,Object) but sending only one change
	 * notification for the entire range of content between the two positions.
	 */
	public func void move(Object object, int index)
		moveIndex(pElements.indexOf(object), index)
	end
	
	/**
	 * \brief Move element to new location.
	 * \version 1.11
	 * 
	 * Same as calling remove(Object) and insert(int,Object) but sending only one change
	 * notification for the entire range of content between the two positions.
	 */
	public func void moveIndex(int indexFrom, int indexTo)
		if indexFrom < 0
			throw EInvalidParam.new("indexFrom < 0")
		end
		if indexFrom >= pElements.getCount()
			throw EInvalidParam.new("indexFrom >= count")
		end
		if indexTo < 0
			throw EInvalidParam.new("indexTo < 0")
		end
		if indexTo > pElements.getCount()
			throw EInvalidParam.new("indexTo > count")
		end
		
		if indexFrom == indexTo
			return
		end
		
		pElements.move(indexFrom, indexTo)
		
		if indexTo > indexFrom
			notifyContentChanged(indexFrom, indexTo)
			
			if pSelected != -1
				if pSelected == indexFrom
					setSelected(indexTo)
					
				elif pSelected > indexFrom and pSelected <= indexTo
					setSelected(pSelected - 1)
				end
			end
			
		else
			notifyContentChanged(indexTo, indexFrom)
			
			if pSelected != -1
				if pSelected == indexFrom
					setSelected(indexTo)
					
				elif pSelected >= indexTo and pSelected < indexFrom
					setSelected(pSelected + 1)
				end
			end
		end
	end
	
	/**
	 * \brief Remove element.
	 * 
	 * After removing a content changed event is send to listeners. If you have to remove
	 * many elements at once performance problems can arise due to each change causing
	 * widgets to relayout. If this is the case use one of the mass editing methods.
	 * 
	 * \throws EOutOfBounds Element is absent.
	 */
	public func void remove(Object element)
		removeFrom(pElements.indexOf(element))
	end
	
	/**
	 * \brief Remove element.
	 * 
	 * After removing a content changed event is send to listeners. If you have to remove
	 * many elements at once performance problems can arise due to each change causing
	 * widgets to relayout. If this is the case use one of the mass editing methods.
	 * 
	 * \throws EOutOfBounds \em index is less than 0 or larger than or equal getCount().
	 */
	public func void removeFrom(int index)
		pElements.removeFrom(index)
		notifyElementsRemoved(index, index)
	end
	
	/**
	 * \brief Remove elements from range.
	 * 
	 * After removing a content changed event is send to listeners.
	 * 
	 * \throws EOutOfBounds \em firstIndex is less than 0 or larger than or equal getCount().
	 * \throws EOutOfBounds \em lastIndex is less than 0 or larger than or equal getCount().
	 * \throws EInvalidParam \em lastIndex is less than \em firstIndex.
	 */
	public func void removeRange(int firstIndex, int lastIndex)
		if firstIndex == lastIndex
			return
		end
		
		if lastIndex < firstIndex
			throw EInvalidParam.new()
		end
		if lastIndex >= pElements.getCount()
			throw EOutOfBounds.new()
		end
		
		var int i
		for i = firstIndex to lastIndex + 1
			pElements.removeFrom(firstIndex)
		end
		
		notifyElementsRemoved(firstIndex, lastIndex)
	end
	
	/**
	 * \brief Remove elements from array.
	 * 
	 * After removing a content changed event is send to listeners.
	 * 
	 * \throws EOutOfBounds Element is absent.
	 */
	public func void removeArray(Array content)
		var int count = content.getCount()
		if count == 0
			return
		end
		
		var int firstIndex = pElements.indexOf(content.getAt(0))
		var int lastIndex = firstIndex
		var int index
		var int i
		
		for i = 1 to count
			index = pElements.indexOf(content.getAt(i))
			if index == -1
				throw EOutOfBounds.new()
			end
			
			firstIndex = DEMath.min(firstIndex, index)
			lastIndex = DEMath.max(lastIndex, index)
		end
		
		content.forEach(block Object each
			pElements.remove(each)
		end)
		
		notifyElementsRemoved(firstIndex, lastIndex)
	end
	
	/**
	 * \brief Remove all elements.
	 * 
	 * If elements are present a content changed event is send to listeners after
	 * removing all elements.
	 */
	public func void removeAll()
		var int count = pElements.getCount()
		if count == 0
			return
		end
		
		pElements.removeAll()
		notifyElementsRemoved(0, count - 1)
	end
	
	/** \brief Get content as array. */
	public func Array getContent()
		return Array.new(pElements)
	end
	
	/**
	 * \brief Set content from array.
	 * 
	 * If count of elements in \em content is equal to getCount() operates like setAll()
	 * sending a single content changed notification unless both counts are 0. If the
	 * counts are different replaces the content as if removeAll() and addAll() is called.
	 * If elements are present sends first an elements removed notification. Then if
	 * \em content contains any elements an elements added notification is send.
	 * 
	 * If you just want to replace all elements without changing the number of elements
	 * use setAll().
	 */
	public func void setContent(Array content)
		var int count = pElements.getCount()
		var int contentCount = content.getCount()
		
		if count == contentCount
			setAll(content)
			return
		end
		
		if count > 0
			pElements.removeAll()
			notifyElementsRemoved(0, count - 1)
		end
		
		pElements.addAll(content)
		
		if contentCount > 0
			notifyElementsAdded(0, contentCount - 1)
		end
	end
	
	/** \brief Sort elements using element compare() method. */
	public func void sort()
		if pElements.getCount() == 0
			return
		end
		
		var Object selected
		if pSelected != -1
			selected = pElements.getAt(pSelected)
		end
		
		pElements.sort()
		
		var bool notifySelectedChanged
		if pSelected != -1
			var int oldSelection = pSelected
			pSelected = indexOf(selected)
			notifySelectedChanged = pSelected != oldSelection
		end
		
		notifyContentChanged(0, pElements.getCount() - 1)
		
		if notifySelectedChanged
			pListeners.forEach(block ListModelListener each
				each.selectionChanged(this)
			end)
		end
	end
	
	/**
	 * \brief Sort elements using block.
	 * 
	 * Block receives elements to compare as argument and returns compare result the same
	 * way as Object.compare() does.
	 */
	public func void sort(Block ablock)
		if pElements.getCount() == 0
			return
		end
		
		var Object selected
		if pSelected != -1
			selected = pElements.getAt(pSelected)
		end
		
		pElements.sort(ablock)
		
		var bool notifySelectedChanged
		if pSelected != -1
			var int oldSelection = pSelected
			pSelected = indexOf(selected)
			notifySelectedChanged = pSelected != oldSelection
		end
		
		notifyContentChanged(0, pElements.getCount() - 1)
		
		if notifySelectedChanged
			pListeners.forEach(block ListModelListener each
				each.selectionChanged(this)
			end)
		end
	end
	
	
	
	/**
	 * \brief Visit content with block.
	 * 
	 * Block is called with argument Object.
	 */
	public func void forEach(Block ablock)
		pElements.forEach(ablock)
	end
	
	/**
	 * \brief Find first object for which block returns true.
	 * 
	 * Block is called with argument Object and has to return bool.
	 */
	public func Object find(Block ablock)
		return pElements.find(ablock)
	end
	
	/**
	 * \brief Return array with content for which block returns true.
	 * \version 1.11
	 * 
	 * Block is called with argument Object and has to return bool.
	 * Returns Array of Object.
	 */
	public func Array collect(Block ablock)
		return pElements.collect(ablock)
	end
	
	
	
	/**
	 * \brief Index of the selected element or -1 if there is no selected element.
	 */
	public func int getSelected()
		return pSelected
	end
	
	/**
	 * \brief Set index of selected element or -1 if none is no selected element.
	 */
	public func void setSelected(int index)
		if index < -1 or index >= pElements.getCount()
			throw EInvalidParam.new()
		end
		
		if index == pSelected
			return
		end
		
		pSelected = index
		
		pListeners.forEach(block ListModelListener each
			each.selectionChanged(this)
		end)
	end
	
	/**
	 * \brief Get selected element or null.
	 * 
	 * If getSelected() is -1 returns null. If getSelected() is not -1 this method
	 * behaves the same as calling getAt(getSelected()).
	 */
	public func Object getSelectedElement()
		if pSelected != -1
			return pElements.getAt(pSelected)
		end
		return null
	end
	
	/**
	 * \brief Set selected element.
	 * \version 1.9
	 * 
	 * Behaves the same as calling setSelected(indexOf(element)).
	 */
	public func void setSelectedElement(Object element)
		setSelected(pElements.indexOf(element))
	end
	
	/**
	 * \brief Automatically select items.
	 * \version 1.5
	 * 
	 * If no item is selected and an item is added it is selected. If selected item is
	 * removed selects the next suitible item if the list is not empty.
	 */
	public func bool getAutoSelect()
		return pAutoSelect
	end
	
	/**
	 * \brief Set to automatically select items.
	 * \version 1.5
	 * 
	 * If no item is selected and an item is added it is selected. If selected item is
	 * removed selects the next suitible item if the list is not empty.
	 * 
	 * If auto select is set to true while no item is selected the first item in the
	 * list is selected if the list is not empty.
	 */
	public func void setAutoSelect(bool autoSelect)
		if autoSelect == pAutoSelect
			return
		end
		
		pAutoSelect = autoSelect
		
		if autoSelect and pSelected == -1 and pElements.getCount() > 0
			setSelected(0)
		end
	end
	
	
	
	/**
	 * \brief Notify all that the content of the model in the given range changed.
	 * \param indexFrom Index of first element that changed
	 * \param indexTo Index of last element that changed
	 */
	public func void notifyContentChanged(int indexFrom, int indexTo)
		pListeners.forEach(block ListModelListener each
			each.contentChanged(this, indexFrom, indexTo)
		end)
	end
	
	/**
	 * \brief Notify all elements have been added.
	 * \param indexFrom Index of first added element.
	 * \param indexTo Index of last added element.
	 */
	public func void notifyElementsAdded(int indexFrom, int indexTo)
		pListeners.forEach(block ListModelListener each
			each.elementsAdded(this, indexFrom, indexTo)
		end)
		
		if pSelected == -1 and pAutoSelect
			setSelected(indexFrom)
		end
	end
	
	/**
	 * \brief Notify all elements have been removed.
	 * \param indexFrom Index of first removed element.
	 * \param indexTo Index of last removed element.
	 */
	public func void notifyElementsRemoved(int indexFrom, int indexTo)
		var bool notifySelectedChanged
		
		if pSelected > indexTo
			pSelected -= indexTo - indexFrom + 1
			//notifySelectedChanged = true // same selection but different index
			
		elif pSelected >= indexFrom
			if pAutoSelect
				pSelected = DEMath.min(indexFrom, pElements.getCount() - 1)
				
			else
				pSelected = -1
			end
			notifySelectedChanged = true
		end
		
		pListeners.forEach(block ListModelListener each
			each.elementsRemoved(this, indexFrom, indexTo)
		end)
		
		if notifySelectedChanged
			pListeners.forEach(block ListModelListener each
				each.selectionChanged(this)
			end)
		end
	end
	
	/**
	 * \brief Notify all content of element in model changed.
	 * \version 1.9
	 * 
	 * Behaves the same as calling notifyContentChanged(index, index).
	 * 
	 * \param index Index of element that changed.
	 */
	public func void notifyElementChanged(int index)
		pListeners.forEach(block ListModelListener each
			each.contentChanged(this, index, index)
		end)
	end
end
