/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Gui

pin Dragengine.Gui.Events



/**
 * \brief Default implementation of ComboBoxEditor using a TextField.
 * 
 * Uses Object.toString() of the value object as text to edit. Returns
 * A String object if asked for the value.
 * 
 * Supports designing using designer selector "TextField.ComboBoxEditor".
 */
class DefaultComboBoxEditor extends TextField implements ComboBoxEditor
	private var Array pEditorActionListeners
	
	
	
	/** \brief Create default combo box editor. */
	public func new() this(DefaultTextModel.new())
	end
	
	/** \brief Create default combo box editor. */
	public func new(TextModel model) super(model)
		pEditorActionListeners = Array.new()
		setDesignerSelector(getDesignerSelector() + ".ComboBoxEditor")
	end
	
	/** \brief Create default combo box editor. */
	public func new(TextModel model, int displayCharCount) this(model)
		setDisplayCharCount(displayCharCount)
	end
	
	/** \brief Dispose of widget. */
	public func void dispose()
		// it is possible the dispose is triggered due to an action listener call.
		// if Array.removeAll() is called in this case an exception is thrown due to
		// modifying the array while iterating over it. dropping the array is enough
		pEditorActionListeners = null
		super.dispose()
	end
	
	
	
	/** \brief Get widget to use for editing. */
	public func Widget getWidget()
		return this
	end
	
	/**
	 * \brief Get editor value.
	 * \details The returned object can be the one previously set by
	 *          setEditorValue() if unchanged or a new object if changed.
	 */
	public func Object getEditorValue()
		return getModel().getText()
	end
	
	/**
	 * \brief Set editor value to object or \em null to clear.
	 * \details \em object is garanteed to not be modified by the editor.
	 */
	public func void setEditorValue(Object object)
		if object == null
			getModel().setText("")
			
		else
			getModel().setText(object.toString())
		end
		
		fireAction(0)
	end
	
	/**
	 * \brief Add action listener.
	 * \details Action listener is called whenever the edited object changes.
	 * \note This shadows TextWidget.addActionListener(). To use it use addTextActionListener().
	 */
	public func void addActionListener(ActionListener listener)
		pEditorActionListeners.add(listener)
	end
	
	/**
	 * \brief Remove action listener.
	 * \note This shadows TextWidget.removeActionListener(). To use it use removeTextActionListener().
	 */
	public func void removeActionListener(ActionListener listener)
		pEditorActionListeners.remove(listener)
	end
	
	/**
	 * \brief Add text widget action listener.
	 * \version 1.8
	 * \note Forwards to shadowed TextWidget.addActionListener().
	 */
	public func void addTextActionListener(ActionListener listener)
		super.addActionListener(listener)
	end
	
	/**
	 * \brief Remove text widget action listener.
	 * \version 1.8
	 * \note Forwards to shadowed TextWidget.removeActionListener().
	 */
	public func void removeTextActionListener(ActionListener listener)
		super.removeActionListener(listener)
	end
	
	/**
	 * \brief Start editing.
	 * \details Typically this selects the entire text or otherwise prepare
	 *          editor for directly manipulating the set value. There is no
	 *          stop editing call since the widget is hidden in this case.
	 */
	public func void startEditing()
		setCursorPosition(getModel().getText().getLength())
		setSelectionAll()
		grabFocus()
	end
	
	/**
	 * \brief Get parent ComboBox.
	 * \version 1.17
	 */
	public func ComboBox getComboBox()
		var Container container = getParent()
		while container != null
			if container castable ComboBox
				return container cast ComboBox
			end
			container = container.getParent()
		end
		return null
	end
	
	
	
	/** \brief Text string has changed. */
	public func void textChanged(TextModel textModel)
		super.textChanged(textModel)
		
		var ActionEvent event = ActionEvent.new(this, ActionEvent.idAction, 0)
		
		pEditorActionListeners.forEach(block ActionListener each
			each.onAction(event)
		end)
	end
end
