/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Gui.MousePointers

pin Dragengine.Gui.CanvasCreators
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * \brief Mouse pointer using video.
 */
class VideoMousePointer extends DefaultMousePointer
	/** \brief Playback timer. */
	class PlaybackTimer extends Timer
		/** \brief Video player to update. */
		var VideoPlayer pVideoPlayer
		
		/** \brief Create playback timer. */
		func new(VideoPlayer videoPlayer) super(0.0, true)
			pVideoPlayer = videoPlayer
		end
		
		/** \brief Timer elapsed. */
		func void fire()
			pVideoPlayer.update(Engine.getElapsedTime())
		end
	end
	
	/** \brief Playback timer. */
	var PlaybackTimer pTimer
	
	
	
	/** \brief Video player. */
	var VideoPlayer pVideoPlayer
	
	
	
	/** \brief Create mouse pointer. */
	func new()
		pVideoPlayer = VideoPlayer.new()
		pVideoPlayer.setLooping(true)
	end
	
	/** \brief Destructor. */
	func destructor()
		if pTimer != null
			pTimer.giveUp()
		end
	end
	
	
	
	/** \brief Video player or \em null if not set. */
	func VideoPlayer getVideoPlayer()
		return pVideoPlayer
	end
	
	/** \brief Video or \em null if not set. */
	func Video getVideo()
		return pVideoPlayer.getVideo()
	end
	
	/** \brief Set video. */
	func void setVideo(Video video)
		if video == pVideoPlayer.getVideo()
			return
		end
		
		if pTimer != null
			pTimer.giveUp()
			pTimer = null
		end
		pVideoPlayer.stop()
		
		pVideoPlayer.setVideo(video)
		
		if video != null
			setSize(video.getSize())
			pVideoPlayer.play()
			pTimer = PlaybackTimer.new(pVideoPlayer)
			
		else
			setSize(Point.new())
		end
		
		updateCanvas()
	end
	
	
	
	/** \brief Update canvas. */
	func void updateCanvas()
		var CanvasView canvasView = getCanvasView()
		
		canvasView.removeAllCanvas()
		
		if pVideoPlayer.getVideo() == null
			return
		end
		
		var CanvasVideoPlayer canvasVideo = CanvasVideoPlayer.new()
		canvasVideo.setVideoPlayer(pVideoPlayer)
		canvasVideo.setSize(pVideoPlayer.getVideo().getSize())
		canvasView.addCanvas(canvasVideo)
	end
end
