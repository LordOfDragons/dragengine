/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Gui

pin Dragengine.Gui.Events


/**
 * \brief Send mouse wheel events to parent of source widget.
 * 
 * Adding mouse listeners to widgets prevents events being send to the parent. If you just
 * want to react to a mouse click this also prevents mouse wheel events from being send
 * to the parent. Add also this listener to rectify 
 */
class ParentWheelActionListener extends DefaultMouseListener
	private var Container pParent
	
	
	
	/**
	 * \brief Create mouse listener.
	 * \deprecated Use new(Container) otherwise event loops can happen if event source
	 *             is a child widget of the widget this listener is added to.
	 */
	public func new()
	end
	
	/**
	 * \brief Create mouse listener sending wheel event to container
	 * \version 1.9
	 * \throws ENullPointer parent is null.
	 */
	public func new(Container parent)
		setParent(parent)
	end
	
	
	
	/**
	 * \brief Parent widget to send events to.
	 * \version 1.9
	 */
	public func Container getParent()
		return pParent
	end
	
	/**
	 * \brief Set parent widget to send events to.
	 * \version 1.9
	 * \throws ENullPointer parent is null.
	 */
	public func void setParent(Container parent)
		if parent == null
			throw ENullPointer.new("parent")
		end
		pParent = parent
	end
	 
	
	
	/** \brief Mouse wheel has moved. */
	public func void onMouseWheel(MouseEvent event)
		if pParent != null
			pParent.sendEvent(event)
			
		else
			deprecatedOnMouseWheel(event)
		end
	end
	
	/** Deprecated behavior: Version < 1.9 . */
	private func void deprecatedOnMouseWheel(MouseEvent event)
		var Object source = event.getSource()
		if source castable Widget
			var Container parent = (source cast Widget).getParent()
			if parent != null
				parent.sendEvent(event)
			end
		end
	end
end
