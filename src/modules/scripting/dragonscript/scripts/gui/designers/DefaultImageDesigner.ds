/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Gui.Designers

pin Dragengine.Gui.CanvasCreators
pin Dragengine.Gui.Borders.Factories
pin Dragengine.Scenery


/**
 * \brief Default implementation of WidgetDesigner for DisplayImage.
 */
class DefaultImageDesigner extends DefaultWidgetDesigner
	/** \brief Design for a particular widget state. */
	class ImageStateDesign extends StateDesign
		var ColorMatrix pImageColorTransform
		var Image pImage
		
		
		
		/** \brief Create state design. */
		func new()
		end
		
		/** \brief Create copy of state design. */
		func new(ImageStateDesign stateDesign) super(stateDesign)
			pImageColorTransform = stateDesign.pImageColorTransform
			pImage = stateDesign.pImage
		end
		
		
		
		/** \brief Image color transform or null. */
		func ColorMatrix getImageColorTransform()
			return pImageColorTransform
		end
		
		/** \brief Set image color transform or null. */
		func void setImageColorTransform(ColorMatrix colorMatrix)
			pImageColorTransform = colorMatrix
		end
		
		/**
		 * \brief Image.
		 * \version 1.23
		 */
		func Image getImage()
			return pImage
		end
		
		/**
		 * \brief Set image.
		 * \version 1.23
		 */
		func void setImage(Image image)
			pImage = image
		end
		
		
		
		/**
		 * \brief Set parameter.
		 * 
		 * Supported parameters:
		 * | name | value | Setter |
		 * | --- | --- | --- |
		 * | imageColorTransform | ColorMatrix | setImageColorTransform |
		 * | image | Image | setImage |
		 */
		func void setParameter(String name, Object value)
			if name.equals("imageColorTransform")
				setImageColorTransform(value cast ColorMatrix)
				
			elif name.equals("image")
				setImage(value cast Image)
				
			else
				super.setParameter(name, value)
			end
		end
		
		
		
		/** \brief Create copy of state. */
		func DefaultWidgetDesigner.StateDesign copyDesign()
			return ImageStateDesign.new(this)
		end
		
		/**
		 * \brief Merge states.
		 * 
		 * Updates state with parameters of this state if defined. Used to find the first
		 * defined parameter for a series of states acting as fallback for each other.
		 */
		func void merge(DefaultWidgetDesigner.StateDesign state)
			super.merge(state)
			
			var ImageStateDesign idesign = state cast ImageStateDesign
			
			if pImageColorTransform != null
				idesign.pImageColorTransform = pImageColorTransform
			end
			if pImage != null
				idesign.pImage = pImage
			end
		end
		
		/**
		 * \brief Apply state design to widget.
		 * 
		 * This is typically called on a merged design to find the first defined parameters.
		 */
		func void apply(Widget widget)
			super.apply(widget)
			
			var DisplayImage displayImage = widget cast DisplayImage
			if not displayImage.getProtectImageColorTransform()
				if pImageColorTransform != null
					displayImage.setImageColorTransform(pImageColorTransform)
					
				else
					displayImage.setImageColorTransform(ColorMatrix.new())
				end
			end
			
			if pImage != null
				displayImage.setImage(pImage)
			end
		end
	end
	
	
	
	/** \brief Create new image designer. */
	func new()
		setDesignNormal(ImageStateDesign.new())
		setDesignDisabled(ImageStateDesign.new())
		setDesignFocused(ImageStateDesign.new())
		setDesignHover(ImageStateDesign.new())
		setDesignFocusedHover(ImageStateDesign.new())
	end
	
	/** \brief Create copy of widget designer designer. */
	func new(DefaultImageDesigner designer) super(designer)
	end
	
	
	
	/** \brief Design for normal state. */
	func ImageStateDesign getImageDesignNormal()
		return getDesignNormal() cast ImageStateDesign
	end
	
	/** \brief Design for disabled state. */
	func ImageStateDesign getImageDesignDisabled()
		return getDesignDisabled() cast ImageStateDesign
	end
	
	/** \brief Design for focused state. */
	func ImageStateDesign getImageDesignFocused()
		return getDesignFocused() cast ImageStateDesign
	end
	
	/** \brief Design for hover state. */
	func ImageStateDesign getImageDesignHover()
		return getDesignHover() cast ImageStateDesign
	end
	
	/** \brief Design for focused hover state. */
	func ImageStateDesign getImageDesignFocusedHover()
		return getDesignFocusedHover() cast ImageStateDesign
	end
	
	
	
	/**
	 * \brief Set parameter.
	 * 
	 * See DefaultWidgetDesigner.setParameter for supported parameters.
	 * 
	 * Additional supported parameters:
	 * | name | value | Setter |
	 * | --- | --- | --- |
	 * | {state}.imageColorTransform | ColorMatrix | getDesign{state}.setImageColorTransform |
	 * | {state}.image | Image | getDesign{state}.setImage |
	 */
	func void setParameter(String name, Object value)
		super.setParameter(name, value)
	end
	
	/** \brief Create copy of designer. */
	func WidgetDesigner copyDesigner()
		return DefaultImageDesigner.new(this)
	end
end
