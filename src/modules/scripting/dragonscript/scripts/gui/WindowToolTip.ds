/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2021, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Gui

pin Dragengine.Gui.Events
pin Dragengine.Gui.Layouts


/**
 * \brief Window for showing tool tip.
 * \version 1.7
 * 
 * Similar to WindowPopup but does not capture the mouse nor does it allow hovering
 * 
 * Supports designing using designer selector "WindowToolTip".
 */
class WindowToolTip extends Window
	/** \brief Create window. */
	public func new()
		runWhileBlockingLayout(block
			setVisible(false)
			setEnabled(true)
			setCanFocus(false)
			setCanReorder(false)
			setCanActivate(false)
			setCanMove(false)
			setCanResize(false)
			setCanClose(false)
			setCanMaximize(false)
			setCanMinimize(false)
			setLayout(FlowLayout.new(LayoutAxis.y))
			setDesignerSelector("WindowToolTip")
		end)
	end
	
	
	
	/**
	 * \brief Pop up window.
	 * 
	 * Shows the window at position.
	 */
	public func void popup(Point position)
		popupInside(position, RectArea.new(Point.new(), getDesktop().getContainerSize() - Point.new(1, 1)))
	end
	
	/**
	 * \brief Pop up window at desktop coordinates.
	 * 
	 * Shows the window at position.
	 */
	public func void popup(int x, int y)
		popup(Point.new(x, y))
	end
	
	/**
	 * \brief Pop up window not clipping outside area.
	 * 
	 * Shows the window at position.
	 */
	public func void popupInside(Point position, RectArea boundary)
		setPosition(boundary.grow(-getSize()).clamp(position))
		setVisible(true)
		moveToTop()
	end
	
	/**
	 * \brief Pop down window.
	 * 
	 * Hides window.
	 */
	public func void popdown()
		setVisible(false)
	end
	
	
	
	/**
	 * \brief Grab focus if possible.
	 * 
	 * Implementation for popup windows is slightly different than for regular
	 * windows. Regular windows focus the first focusable widget if they are not
	 * focusable itself. Popup windows can not grab the focus nor do they focus
	 * the first focusable widget. This avoids popup windows stealing focus from
	 * the underlaying window.
	 */
	public func void grabFocus()
	end
	
	
	
	/**
	 * \brief Widget at coordinates or \em null if not found.
	 * 
	 * Overwritten to always return \em null. Prevents this widget from hovering. Keep this
	 * in mind if you try to call this method. It might not work. If you need original
	 * getWidgetAt behavior from super class use realGetWidgetAt().
	 */
	public func Widget getWidgetAt(Point position)
		return null
	end
	
	/** \brief Super class getWidgetAt without overwritten behavior. */
	public func Widget realGetWidgetAt(Point position)
		return super.getWidgetAt(position)
	end
end
