/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
 
 
namespace Dragengine.Gui.Borders

pin Dragengine.Gui.CanvasCreators
pin Dragengine.Scenery


/**
 * Border using a canvas creator.
 */
class CanvasCreatorBorder extends DefaultBorder
	/** \brief Canvas creator. */
	private var CanvasCreator pCanvasCreator
	
	/** \brief Border size. */
	private var BorderSize pBorderSize
	
	
	
	/** \brief Create border. */
	public func new() this( null, BorderSize.new() )
	end
	
	/** Creates a new line border. */
	public func new( CanvasCreator canvasCreator, BorderSize borderSize )
		if borderSize == null
			throw EInvalidParam.new()
		end
		
		this.pCanvasCreator = canvasCreator
		this.pBorderSize = borderSize
	end
	
	
	
	/** \brief Canvas creator or \em null if not set. */
	public func CanvasCreator getCanvasCreator()
		return this.pCanvasCreator
	end
	
	/** \brief Set canvas creator. */
	public func void setCanvasCreator( CanvasCreator canvasCreator )
		if canvasCreator == this.pCanvasCreator
			return
		end
		
		this.pCanvasCreator = canvasCreator
		this.updateCanvas()
	end
	
	/** \brief Border size. */
	public func BorderSize getBorderSize()
		return this.pBorderSize
	end
	
	/** \brief Set border size. */
	public func void setBorderSize( BorderSize borderSize )
		if borderSize.equals( this.pBorderSize )
			return
		end
		
		this.pBorderSize = borderSize
		this.layoutContainer()
		this.updateCanvas()
	end
	
	
	
	/** Update canvas. */
	public func void updateCanvas()
		var CanvasView borderCanvas = this.getCanvasView()
		
		borderCanvas.removeAllCanvas()
		
		if this.pCanvasCreator != null
			var Canvas canvas = this.pCanvasCreator.createCanvas( borderCanvas.getSize() )
			if canvas != null
				borderCanvas.addCanvas( canvas )
			end
		end
	end
end
