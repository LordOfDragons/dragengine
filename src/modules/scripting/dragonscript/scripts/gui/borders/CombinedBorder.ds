/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
 
 
namespace Dragengine.Gui.Borders

pin Dragengine.Gui
pin Dragengine.Scenery


/**
 * \brief Combine two borders into one.
 * 
 * Borders are called "inner" and "outer" border with outer border layered ontop of inner border.
 */
class CombinedBorder extends DefaultBorder
	/** \brief Inner border or \em null. */
	private var Border pInnerBorder
	
	/** \brief Outer border or \em null. */
	private var Border pOuterBorder
	
	
	
	/** \brief Create border. */
	public func new()
	end
	
	/** \brief Create border. */
	public func new( Border innerBorder, Border outerBorder )
		this.pInnerBorder = innerBorder
		this.pOuterBorder = outerBorder
	end
	
	
	
	/** \brief Inner border or \em null. */
	public func Border getInnerBorder()
		return this.pInnerBorder
	end
	
	/** \brief Set inner border or \em null. */
	public func void setInnerBorder( Border border )
		if border == this.pInnerBorder
			return
		end
		this.pInnerBorder = border
		this.onWidgetChanged()
	end
	
	/** \brief Outer border or \em null. */
	public func Border getOuterBorder()
		return this.pOuterBorder
	end
	
	/** \brief Set outer border or \em null. */
	public func void setOuterBorder( Border border )
		if border == this.pOuterBorder
			return
		end
		this.pOuterBorder = border
		this.onWidgetChanged()
	end
	
	
	
	/** \brief Set widget the border is assigned to or \em null. */
	public func void setWidget( Widget widget )
		if this.pInnerBorder != null
			this.pInnerBorder.setWidget( widget )
		end
		if this.pOuterBorder != null
			this.pOuterBorder.setWidget( widget )
		end
		super.setWidget( widget )
	end
	
	/** \brief Border size. */
	public func BorderSize getBorderSize()
		var BorderSize borderSize = BorderSize.new()
		if this.pInnerBorder != null
			borderSize = this.pInnerBorder.getBorderSize()
		end
		if this.pOuterBorder != null
			borderSize = borderSize.enlarge( this.pOuterBorder.getBorderSize() )
		end
		return borderSize
	end
	
	/** \brief Resize canvas to fit widget if widget is set. */
	public func void resizeCanvas( Point widgetSize )
		super.resizeCanvas( widgetSize )
		
		var Point innerPosition = Point.new()
		var Point outerSize = widgetSize
		
		if this.pInnerBorder != null
			outerSize = widgetSize + this.pInnerBorder.getBorderSize().getSize()
		end
		if this.pOuterBorder != null
			var BorderSize borderSize = this.pOuterBorder.getBorderSize()
			innerPosition = Point.new( borderSize.getLeft(), borderSize.getTop() )
		end
		
		if this.pInnerBorder != null
			this.pInnerBorder.resizeCanvas( widgetSize )
			this.pInnerBorder.getCanvas().setPosition( innerPosition )
		end
		if this.pOuterBorder != null
			this.pOuterBorder.resizeCanvas( outerSize )
		end
	end
	
	/** \brief Update canvas. */
	public func void updateCanvas()
		if this.getWidget() == null
			return
		end
		
		var CanvasView view = this.getCanvasView()
		var Canvas canvas
		
		view.removeAllCanvas()
		
		if this.pOuterBorder != null
			canvas = this.pOuterBorder.getCanvas()
			canvas.setOrder( 1.0 )
			view.addCanvas( canvas )
		end
		
		if this.pInnerBorder != null
			canvas = this.pInnerBorder.getCanvas()
			canvas.setOrder( 0.0 )
			view.addCanvas( canvas )
		end
	end
	
	
	
	/** \brief Gui theme of widget changed. */
	public func void onGuiThemeChanged()
		if this.pInnerBorder != null
			this.pInnerBorder.onGuiThemeChanged()
		end
		if this.pOuterBorder != null
			this.pOuterBorder.onGuiThemeChanged()
		end
		super.onGuiThemeChanged()
	end
	
	/** \brief Widget parameters changed. */
	public func void onWidgetParametersChanged()
		if this.pInnerBorder != null
			this.pInnerBorder.onWidgetParametersChanged()
		end
		if this.pOuterBorder != null
			this.pOuterBorder.onWidgetParametersChanged()
		end
		super.onWidgetParametersChanged()
	end
end
