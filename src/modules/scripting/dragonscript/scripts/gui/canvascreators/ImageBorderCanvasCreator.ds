/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Gui.CanvasCreators

pin Dragengine.Gui
pin Dragengine.Scenery


/**
 * \brief Canvas creator creating a single image bordered by 8 border images canvas.
 * 
 * Creates an image canvas with an image stretched to fit the entire widget with
 * 4 corner and 4 side canvas image located around it. Optionally up to 4 attach
 * images can be used to add non-stretching middle sections to sides. Think of
 * of a cartoon speech bubble. The section pointing down at the speaking actor
 * would be an attach image.
 * 
 * Stores up to 13 images:
 * - corner top left
 * - corner top right
 * - corner bottom left
 * - corner bottom right
 * - side left
 * - side top
 * - side right
 * - side bottom
 * - center
 * - attach left
 * - attach top
 * - attach right
 * - attach bottom
 * 
 * create a canvas view with size and up to 13 image canvas added to it in the appropriate
 * location. corners and attaches are all fixed size. sides are stretched to touch the
 * respective corners. center is stretched to touch all sides. if any image is null it is
 * not used and assumed to have 0 size.
 */
class ImageBorderCanvasCreator extends DefaultCanvasCreator
	private var Image pImageCenter
	private var Image pImageSideLeft
	private var Image pImageSideTop
	private var Image pImageSideRight
	private var Image pImageSideBottom
	private var Image pImageCornerTopLeft
	private var Image pImageCornerTopRight
	private var Image pImageCornerBottomLeft
	private var Image pImageCornerBottomRight
	private var Image pImageAttachLeft
	private var Image pImageAttachTop
	private var Image pImageAttachRight
	private var Image pImageAttachBottom
	private var ColorMatrix pColorMatrix
	
	
	
	/** \brief Create canvas creator. */
	public func new()
	end
	
	/** \brief Create copy of canvas creator. */
	public func new( ImageBorderCanvasCreator creator ) super( creator )
		this.pImageCenter = creator.pImageCenter
		this.pImageSideLeft = creator.pImageSideLeft
		this.pImageSideTop = creator.pImageSideTop
		this.pImageSideRight = creator.pImageSideRight
		this.pImageSideBottom = creator.pImageSideBottom
		this.pImageCornerTopLeft = creator.pImageCornerTopLeft
		this.pImageCornerTopRight = creator.pImageCornerTopRight
		this.pImageCornerBottomLeft = creator.pImageCornerBottomLeft
		this.pImageCornerBottomRight = creator.pImageCornerBottomRight
		this.pImageAttachLeft = creator.pImageAttachLeft
		this.pImageAttachTop = creator.pImageAttachTop
		this.pImageAttachRight = creator.pImageAttachRight
		this.pImageAttachBottom = creator.pImageAttachBottom
		this.pColorMatrix = creator.pColorMatrix
	end
	
	
	
	/** \brief Center image. */
	public func Image getImageCenter()
		return this.pImageCenter
	end
	
	/** \brief Set center image. */
	public func void setImageCenter( Image image )
		this.pImageCenter = image
	end
	
	
	
	/** \brief Left side image. */
	public func Image getImageSideLeft()
		return this.pImageSideLeft
	end
	
	/** \brief Set left side image. */
	public func void setImageSideLeft( Image image )
		this.pImageSideLeft = image
	end
	
	/** \brief Top side image. */
	public func Image getImageSideTop()
		return this.pImageSideTop
	end
	
	/** \brief Set top side image. */
	public func void setImageSideTop( Image image )
		this.pImageSideTop = image
	end
	
	/** \brief Right side image. */
	public func Image getImageSideRight()
		return this.pImageSideRight
	end
	
	/** \brief Set right side image. */
	public func void setImageSideRight( Image image )
		this.pImageSideRight = image
	end
	
	/** \brief Bottom side image. */
	public func Image getImageSideBottom()
		return this.pImageSideBottom
	end
	
	/** \brief Set bottom side image. */
	public func void setImageSideBottom( Image image )
		this.pImageSideBottom = image
	end
	
	
	
	/** \brief Top left corner image. */
	public func Image getImageCornerTopLeft()
		return this.pImageCornerTopLeft
	end
	
	/** \brief Set top left corner image. */
	public func void setImageCornerTopLeft( Image image )
		this.pImageCornerTopLeft = image
	end
	
	/** \brief Top right corner image. */
	public func Image getImageCornerTopRight()
		return this.pImageCornerTopRight
	end
	
	/** \brief Set top right corner image. */
	public func void setImageCornerTopRight( Image image )
		this.pImageCornerTopRight = image
	end
	
	/** \brief Bottom left corner image. */
	public func Image getImageCornerBottomLeft()
		return this.pImageCornerBottomLeft
	end
	
	/** \brief Set bottom left corner image. */
	public func void setImageCornerBottomLeft( Image image )
		this.pImageCornerBottomLeft = image
	end
	
	/** \brief Bottom right corner image. */
	public func Image getImageCornerBottomRight()
		return this.pImageCornerBottomRight
	end
	
	/** \brief Set bottom right corner image. */
	public func void setImageCornerBottomRight( Image image )
		this.pImageCornerBottomRight = image
	end
	
	
	
	/** \brief Left attach image. */
	public func Image getImageAttachLeft()
		return this.pImageAttachLeft
	end
	
	/** \brief Set left attach image. */
	public func void setImageAttachLeft( Image image )
		this.pImageAttachLeft = image
	end
	
	/** \brief Top attach image. */
	public func Image getImageAttachTop()
		return this.pImageAttachTop
	end
	
	/** \brief Set top attach image. */
	public func void setImageAttachTop( Image image )
		this.pImageAttachTop = image
	end
	
	/** \brief Right attach image. */
	public func Image getImageAttachRight()
		return this.pImageAttachRight
	end
	
	/** \brief Set right attach image. */
	public func void setImageAttachRight( Image image )
		this.pImageAttachRight = image
	end
	
	/** \brief Bottom attach image. */
	public func Image getImageAttachBottom()
		return this.pImageAttachBottom
	end
	
	/** \brief Set bottom attach image. */
	public func void setImageAttachBottom( Image image )
		this.pImageAttachBottom = image
	end
	
	
	
	/** \brief Color matrix to use for created canvas or \em null to use none. */
	public func ColorMatrix getColorMatrix()
		return this.pColorMatrix
	end
	
	/** \brief Set color matrix to use for created canvas or \em null to use none. */
	public func void setColorMatrix( ColorMatrix colorMatrix )
		this.pColorMatrix = colorMatrix
	end
	
	
	
	/**
	 * \brief Set parameter.
	 * 
	 * Supported parameters:
	 * | name | value | Setter |
	 * | center | Image | setImageCenter |
	 * | sideLeft | Image | setImageSideLeft |
	 * | sideTop | Image | setImageSideTop |
	 * | sideBottom | Image | setImageSideBottom |
	 * | sideRight | Image | setImageSideRight |
	 * | cornerTopLeft | Image | setImageCornerTopLeft |
	 * | cornerTopRight | Image | setImageCornerTopRight |
	 * | cornerBottomLeft | Image | setImageCornerBottomLeft |
	 * | cornerBottomRight | Image | setImageCornerBottomRight |
	 * | attachLeft | Image | setImageAttachLeft |
	 * | attachTop | Image | setImageAttachTop |
	 * | attachBottom | Image | setImageAttachBottom |
	 * | attachRight | Image | setImageAttachRight |
	 * | colorMatrix | ColorMatrix | setColorMatrix |
	 */
	public func void setParameter( String name, Object value )
		if name.equals( "center" )
			this.setImageCenter( value cast Image )
			
		elif name.equals( "sideLeft" )
			this.setImageSideLeft( value cast Image )
			
		elif name.equals( "sideTop" )
			this.setImageSideTop( value cast Image )
			
		elif name.equals( "sideRight" )
			this.setImageSideRight( value cast Image )
			
		elif name.equals( "sideBottom" )
			this.setImageSideBottom( value cast Image )
			
		elif name.equals( "cornerTopLeft" )
			this.setImageCornerTopLeft( value cast Image )
			
		elif name.equals( "cornerTopRight" )
			this.setImageCornerTopRight( value cast Image )
			
		elif name.equals( "cornerBottomLeft" )
			this.setImageCornerBottomLeft( value cast Image )
			
		elif name.equals( "cornerBottomRight" )
			this.setImageCornerBottomRight( value cast Image )
			
		elif name.equals( "attachLeft" )
			this.setImageAttachLeft( value cast Image )
			
		elif name.equals( "attachTop" )
			this.setImageAttachTop( value cast Image )
			
		elif name.equals( "attachRight" )
			this.setImageAttachRight( value cast Image )
			
		elif name.equals( "attachBottom" )
			this.setImageAttachBottom( value cast Image )
			
		elif name.equals( "colorMatrix" )
			this.setColorMatrix( value cast ColorMatrix )
			
		else
			throw EInvalidParam.new()
		end
	end
	
	/** \brief Create copy of canvas creator. */
	public func CanvasCreator copyCanvasCreator()
		return ImageBorderCanvasCreator.new( this )
	end
	
	
	
	/** \brief Create canvas for a specific widget size. */
	public func Canvas createCanvas( Point size )
		var int width = size.getX()
		var int height = size.getY()
		
		var CanvasView content = CanvasView.new()
		content.setSize( size )
		
		if width < 1 or height < 1
			return content
		end
		
		if this.pColorMatrix != null
			content.setColorTransform( this.pColorMatrix )
		end
		
		var BorderSize bsize = this.borderSize()
		var int left = bsize.getLeft()
		var int top = bsize.getTop()
		var int right = bsize.getRight()
		var int bottom = bsize.getBottom()
		var int innerWidth = width - left - right
		var int innerHeight = height - top - bottom
		var int innerRight = width - right
		var int innerBottom = height - bottom
		
		if innerWidth < 0
			var float factor = ( width cast float ) / ( left + right )
			left = ( factor * left ) cast int
			right = ( factor * right ) cast int
			innerWidth = 0
			innerRight = width - right - 1
		end
		
		if innerHeight < 0
			var float factor = ( height cast float ) / ( top + bottom )
			top = ( factor * top ) cast int
			bottom = ( factor * bottom ) cast int
			innerHeight = 0
			innerBottom = height - bottom - 1
		end
		
		// center area
		if this.pImageCenter != null
			this.addImageCanvas( content, this.pImageCenter, left, top, innerWidth, innerHeight )
		end
		
		// left side
		if this.pImageAttachLeft != null
			var int attachHeight = DEMath.min( this.pImageAttachLeft.getHeight(), innerHeight )
			var int attachTop = top + ( innerHeight - attachHeight ) / 2
			var int attachBottom = attachTop + attachHeight
			
			if this.pImageSideLeft != null
				this.addImageCanvas( content, this.pImageSideLeft, 0, top, left, attachTop - top )
				this.addImageCanvas( content, this.pImageAttachLeft, 0, attachTop, left, attachHeight )
				this.addImageCanvas( content, this.pImageSideLeft, 0, attachBottom, left, innerBottom - attachBottom )
				
			else
				this.addImageCanvas( content, this.pImageAttachLeft, 0, attachTop, left, attachHeight )
			end
			
		else
			if this.pImageSideLeft != null
				this.addImageCanvas( content, this.pImageSideLeft, 0, top, left, innerHeight )
			end
		end
		
		// top side
		if this.pImageAttachTop != null
			var int attachWidth = DEMath.min( this.pImageAttachTop.getWidth(), innerWidth )
			var int attachLeft = left + ( innerWidth - attachWidth ) / 2
			var int attachRight = attachLeft + attachWidth
			
			if this.pImageSideTop != null
				this.addImageCanvas( content, this.pImageSideTop, left, 0, attachLeft - left, top )
				this.addImageCanvas( content, this.pImageAttachTop, attachLeft, 0, attachWidth, top )
				this.addImageCanvas( content, this.pImageSideTop, attachRight, 0, innerRight - attachRight, top )
				
			else
				this.addImageCanvas( content, this.pImageAttachTop, attachLeft, 0, attachWidth, top )
			end
			
		else
			if this.pImageSideTop != null
				this.addImageCanvas( content, this.pImageSideTop, left, 0, innerWidth, top )
			end
		end
		
		// right side
		if this.pImageAttachRight != null
			var int attachHeight = DEMath.min( this.pImageAttachRight.getHeight(), innerHeight )
			var int attachTop = top + ( innerHeight - attachHeight ) / 2
			var int attachBottom = attachTop + attachHeight
			
			if this.pImageSideRight != null
				this.addImageCanvas( content, this.pImageSideRight, innerRight, top, right, attachTop - top )
				this.addImageCanvas( content, this.pImageAttachRight, innerRight, attachTop, right, attachHeight )
				this.addImageCanvas( content, this.pImageSideRight, innerRight, attachBottom, right, innerBottom - attachBottom )
				
			else
				this.addImageCanvas( content, this.pImageAttachRight, innerRight, attachTop, right, attachHeight )
			end
			
		else
			if this.pImageSideRight != null
				this.addImageCanvas( content, this.pImageSideRight, innerRight, top, right, innerHeight )
			end
		end
		
		// bottom side
		if this.pImageAttachBottom != null
			var int attachWidth = DEMath.min( this.pImageAttachBottom.getWidth(), innerWidth )
			var int attachLeft = left + ( innerWidth - attachWidth ) / 2
			var int attachRight = attachLeft + attachWidth
			
			if this.pImageSideBottom != null
				this.addImageCanvas( content, this.pImageSideBottom, left, innerBottom, attachLeft - left, bottom )
				this.addImageCanvas( content, this.pImageAttachBottom, attachLeft, innerBottom, attachWidth, bottom )
				this.addImageCanvas( content, this.pImageSideBottom, attachRight, innerBottom, innerRight - attachRight, bottom )
				
			else
				this.addImageCanvas( content, this.pImageAttachBottom, attachLeft, innerBottom, attachWidth, bottom )
			end
			
		else
			if this.pImageSideBottom != null
				this.addImageCanvas( content, this.pImageSideBottom, left, innerBottom, innerWidth, bottom )
			end
		end
		
		// corners
		if this.pImageCornerTopLeft != null
			this.addImageCanvas( content, this.pImageCornerTopLeft, 0, 0, left, top )
		end
		
		if this.pImageCornerTopRight != null
			this.addImageCanvas( content, this.pImageCornerTopRight, innerRight, 0, right, top )
		end
		
		if this.pImageCornerBottomLeft != null
			this.addImageCanvas( content, this.pImageCornerBottomLeft, 0, innerBottom, left, bottom )
		end
		
		if this.pImageCornerBottomRight != null
			this.addImageCanvas( content, this.pImageCornerBottomRight, innerRight, innerBottom, right, bottom )
		end
		
		return content
	end
	
	/**
	 * \brief Convenience method to create and add image canvas.
	 */
	protected func void addImageCanvas( CanvasView content, Image image, int x, int y, int width, int height )
		var CanvasImage canvas = CanvasImage.new()
		canvas.setImage( image )
		canvas.setPosition( Point.new( x, y ) )
		canvas.setSize( Point.new( width, height ) )
		canvas.setOrder( content.getCanvasCount() )
		content.addCanvas( canvas )
	end
	
	/**
	 * \brief Calculate size of border elements.
	 * \details Returns the minimum and maximum width and height of side and corner
	 *          images if existing for the appropriate border side.
	 */
	public func BorderSize borderSize()
		var int left
		var int top
		var int right
		var int bottom
		
		if this.pImageSideLeft != null
			left = DEMath.max( left, this.pImageSideLeft.getWidth() )
		end
		if this.pImageSideTop != null
			top = DEMath.max( top, this.pImageSideTop.getHeight() )
		end
		if this.pImageSideRight != null
			right = DEMath.max( right, this.pImageSideRight.getWidth() )
		end
		if this.pImageSideBottom != null
			bottom = DEMath.max( bottom, this.pImageSideBottom.getHeight() )
		end
		
		if this.pImageAttachLeft != null
			left = DEMath.max( left, this.pImageAttachLeft.getWidth() )
		end
		if this.pImageAttachTop != null
			top = DEMath.max( top, this.pImageAttachTop.getHeight() )
		end
		if this.pImageAttachRight != null
			right = DEMath.max( right, this.pImageAttachRight.getWidth() )
		end
		if this.pImageAttachBottom != null
			bottom = DEMath.max( bottom, this.pImageAttachBottom.getHeight() )
		end
		
		if this.pImageCornerTopLeft != null
			left = DEMath.max( left, this.pImageCornerTopLeft.getWidth() )
			top = DEMath.max( top, this.pImageCornerTopLeft.getHeight() )
		end
		if this.pImageCornerTopRight != null
			right = DEMath.max( right, this.pImageCornerTopRight.getWidth() )
			top = DEMath.max( top, this.pImageCornerTopRight.getHeight() )
		end
		if this.pImageCornerBottomLeft != null
			left = DEMath.max( left, this.pImageCornerBottomLeft.getWidth() )
			bottom = DEMath.max( bottom, this.pImageCornerBottomLeft.getHeight() )
		end
		if this.pImageCornerBottomRight != null
			right = DEMath.max( right, this.pImageCornerBottomRight.getWidth() )
			bottom = DEMath.max( bottom, this.pImageCornerBottomRight.getHeight() )
		end
		
		return BorderSize.new( left, top, right, bottom )
	end
end
