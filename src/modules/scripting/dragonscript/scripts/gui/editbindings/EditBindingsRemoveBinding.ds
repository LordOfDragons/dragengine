/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Gui

pin Dragengine.Commands
pin Dragengine.Gui.Borders
pin Dragengine.Gui.Events
pin Dragengine.Gui.Layouts
pin Dragengine.Gui.Listeners
pin Dragengine.Scenery
pin Dragengine.Utils


/**
 * \brief Action removing binding from entry.
 */
public class EditBindingsRemoveBinding extends DefaultActionListener \
implements WindowDialog.ResultListener
	/** \brief Model. */
	private var EditBindingsListModel pModel
	
	/** \brief Entry to remove binding from. */
	private var EditBindingsListModelEntry pEntry
	
	/** \brief Binding to remove. */
	private var Binding pBinding
	
	
	
	/** \brief Create action. */
	public func new( EditBindingsListModel model, \
	EditBindingsListModelEntry entry, Binding binding )
		if model == null or entry == null or binding == null
			throw EInvalidParam.new()
		end
		
		this.pModel = model
		this.pEntry = entry
		this.pBinding = binding
	end
	
	
	
	/** \brief Model. */
	public func EditBindingsListModel getModel()
		return this.pModel
	end
	
	/** \brief Entry to remove binding from. */
	public func EditBindingsListModelEntry getEntry()
		return this.pEntry
	end
	
	/** \brief Binding to remove. */
	public func Binding getBinding()
		return this.pBinding
	end
	
	
	
	/** \brief Perform action. */
	public func void onAction( ActionEvent event )
		var Window window = ( event.getSource() cast Widget ).getWindow()
		
		WindowDialog.question( window, "Remove Binding", \
			"Do you really want to remove binding '" + this.getBindingText() + "'?", \
			null, Array.newWith( \
				WindowDialog.ButtonConfiguration.new( "Yes", true ), \
				WindowDialog.ButtonConfiguration.new( "No", false) ), \
			this )
	end
	
	/** \brief Ask user dialog finished. */
	public func void onResult( Object result )
		if result cast bool
			this.removeEntry()
		end
	end
	
	
	
	/** \brief Remove entry from model. */
	public func void removeEntry()
		select this.pEntry.getInputSource()
		case EditBindingsListModelEntry.InputSource.axis
			this.pModel.getBindingManager().removeAxisBinding( this.pBinding )
			
		case EditBindingsListModelEntry.InputSource.button
			this.pModel.getBindingManager().removeButtonBinding( this.pBinding )
		end
		
		var int index = this.pModel.indexOf( this.pEntry )
		this.pModel.notifyContentChanged( index, index )
	end
	
	
	
	/** \brief Binding text for binding. */
	public func String getBindingText()
		var String deviceID = "??"
		var String sourceID = "??"
		
		if this.pBinding.getDeviceIndex() != -1
			var InputDevice device = this.pModel.getBindingManager() \
				.getDeviceAt( this.pBinding.getDeviceIndex() )
			deviceID = device.getName()
			
			if this.pBinding.getSourceIndex() != -1
				select this.pEntry.getInputSource()
				case EditBindingsListModelEntry.InputSource.axis
					sourceID = device.getAxisAt( this.pBinding.getSourceIndex() ).getName()
					
				case EditBindingsListModelEntry.InputSource.button
					sourceID = device.getButtonAt( this.pBinding.getSourceIndex() ).getName()
				end
			end
		end
		
		return deviceID + ": " + sourceID
	end
end
