/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.ConversationSystem


/**
 * \brief SimpleConversation element class property for use with XML loading.
 * \version 1.25
 * 
 * SimpleConversation is set from XML using a path. Loads the SimpleConversation
 * during preloading. The value of the property is then the loaded resource.
 * If loading fails null SimpleConversation is stored.
 */
class ECPSimpleConversation extends ElementClassProperty
	var LoadSimpleConversation pLoader
	var String pPath
	var SimpleConversation pSimpleConversation
	var bool pHasPreloaded
	var Exception pException
	
	
	/** \brief Create property. */
	func new(String name, LoadSimpleConversation loader) this(name, loader, null)
	end
	
	/** \brief Create property with initial path. */
	func new(String name, LoadSimpleConversation loader, String path) super(name)
		pLoader = loader
		pPath = path
	end
	
	
	/** \brief SimpleConversation loader. */
	func LoadSimpleConversation getLoader()
		return pLoader
	end
	
	/** \brief Path to SimpleConversation. */
	func String getPath()
		return pPath
	end
	
	/** \brief Set path to SimpleConversation. */
	func void setPath(String path)
		pPath = path
	end
	
	/** \brief SimpleConversation or null if not present. */
	func SimpleConversation getSimpleConversation()
		return pSimpleConversation
	end
	
	/** \brief Set SimpleConversation or null if not present. */
	func void setSimpleConversation(SimpleConversation SimpleConversation)
		pSimpleConversation = SimpleConversation
	end
	
	/** \brief Property has been preloaded. */
	func bool getHasPreloaded()
		return pHasPreloaded
	end
	
	
	/** \brief Set value of property from XML decoded value. */
	func void setValue(Object value)
		pPath = value cast String
	end
	
	/** \brief Copy value from another element class property of the same type. */
	func void copyValue(ElementClass elementClass, ElementClassProperty property)
		var String path = (property cast ECPSimpleConversation).pPath
		if path != null and not path.empty()
			pPath = File.absolutePath(path, elementClass.getBasePath()).getPath()
		end
	end
	
	
	/** \brief Preload resources if supported. */
	func void preloadResources(ElementClass elementClass, PreloadCounter counter)
		if pHasPreloaded
			return
		end
		
		pHasPreloaded = true
		
		if pPath == null or pPath.empty()
			return
		end
		
		var String path = File.absolutePath(pPath, elementClass.getBasePath()).getPath()
		
		try
			pSimpleConversation = pLoader.loadFromFile(path)
			
		catch Exception e
			pException = e
		end
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	func bool logPreloadProblems(ElementClass elementClass, Console console)
		if pPath != null and not pPath.empty() and pSimpleConversation == null
			console.addError("Property {}.{}: Failed loading SimpleConversation {}".format(\
				Array.newWith(elementClass.getClassname(), getName(),\
					File.absolutePath(pPath, elementClass.getBasePath()).getPath())), pException)
			return true
		end
		return false
	end
	
	
	/**
	 * \brief SimpleConversation modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored SimpleConversation is
	 * returned. If the named stub property is present it is loaded as SimpleConversation resource.
	 */
	func SimpleConversation getSimpleConversation(StubWithProperties stub, ElementClass eclass)
		var String value = stub.getPropertyValueFor(getName(), "")
		if value.empty()
			return pSimpleConversation
		end
		
		try
			return pLoader.loadFromFile(value)
			
		catch Exception e
			throw ELoadResourceFailed.new(value, "{}: {}".format(\
				Array.newWith(eclass.getClassname(), e.getReason())))
		end
	end
end
