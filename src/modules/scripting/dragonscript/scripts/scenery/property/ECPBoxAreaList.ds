/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Utils



/**
 * \brief Box area list element class property for use with XML loading.
 * 
 * Box area list is stored as BoxAreaList instance. From XML the box area list is read
 * using a list type property string. Each list entry is a map defining one box area.
 * The following example defines a box area list with one box area. The box has a
 * position of (2,1,0), size of (3.6,2.4,0.8) and a rotation of (0,20,0) degrees.
 * 
 * \code{.xml}
 * <list name='propertyName'>
 *    <map>
 *       <vector key='position' x='2' y='1' z='0'/>
 *       <vector key='extends' x='1.8' y='1.2' z='0.4'/>
 *       <vector key='rotation' x='0' y='20' z='0'/>
 *    </map>
 * </list>
 * \endcode
 */
class ECPBoxAreaList extends ElementClassProperty
	/** \brief BoxAreaList. */
	private var BoxAreaList pBoxAreaList
	
	
	
	/** \brief Create property. */
	public func new(String name) super(name)
		pBoxAreaList = BoxAreaList.new()
	end
	
	/** \brief Create property with initial value. */
	public func new(String name, BoxAreaList boxAreaList) super(name)
		pBoxAreaList = BoxAreaList.new(boxAreaList)
	end
	
	
	
	/** \brief Box area list. */
	public func BoxAreaList getList()
		return pBoxAreaList
	end
	
	/** \brief Set box area list. */
	public func void setList(BoxAreaList boxAreaList)
		pBoxAreaList = BoxAreaList.new(boxAreaList)
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		pBoxAreaList = BoxAreaList.new((property cast ECPBoxAreaList).pBoxAreaList)
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pBoxAreaList.removeAll()
		
		(value cast Array).forEach(block Dictionary each
			pBoxAreaList.add(BoxArea.new(\
				DVector.new(each.getAt("position", Vector.new()) cast Vector), \
				each.getAt("extends", Vector.new()) cast Vector, \
				Quaternion.newFromEuler(each.getAt("rotation", Vector.new()) cast Vector)))
		end)
	end
end
