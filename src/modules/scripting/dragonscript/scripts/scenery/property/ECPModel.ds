/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * \brief Model resource element class property for use with XML loading.
 * 
 * Model resource is set from XML using a path. Loads the resource during preloading.
 * The value of the property is then the loaded resource. If loading fails null
 * resource is stored.
 */
class ECPModel extends ElementClassProperty implements ResourceListener
	private var String pPath
	private var Model pModel
	private var PreloadCounter pCounter
	private var bool pHasPreloaded
	
	
	
	/** \brief Create property. */
	public func new( String name ) super( name )
	end
	
	/** \brief Create property with initial path. */
	public func new( String name, String path ) super( name )
		this.pPath = path
	end
	
	
	
	/** \brief Path to model resource. */
	public func String getPath()
		return this.pPath
	end
	
	/** \brief Set path to model resource. */
	public func void setPath( String path )
		this.pPath = path
	end
	
	/** \brief Model resource or \em null if not present. */
	public func Model getModel()
		return this.pModel
	end
	
	/** \brief Set model resource or \em null if not present. */
	public func void setModel( Model model )
		this.pModel = model
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return this.pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue( Object value )
		this.pPath = value cast String
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources( ElementClass elementClass, PreloadCounter counter )
		if this.pHasPreloaded
			return
		end
		
		this.pHasPreloaded = true
		
		if this.pPath == null or this.pPath.empty()
			return
		end
		
		Model.loadAsynchron( File.absolutePath( this.pPath, elementClass.getBasePath() ).getPath(), this )
		this.pCounter = counter
		counter.increase()
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems( ElementClass elementClass, Console console )
		if this.pPath != null and not this.pPath.empty() and this.pModel == null
			console.addMessage( "Property " + elementClass.getClassname() + "." \
				+ this.getName() + ": Failed loading model " \
				+ File.absolutePath( this.pPath, elementClass.getBasePath() ).getPath() )
			return true
		end
		return false
	end
	
	
	
	/** \brief Resource has finished loading. */
	public func void finishedLoading( String path, ResourceLoaderType resourceType, Object resource )
		this.pModel = resource cast Model
		this.pCounter.decrease()
	end
	
	/** \brief Resource failed loading. */
	public func void failedLoading( String path, ResourceLoaderType resourceType )
		this.pCounter.decrease()
	end
	
	
	
	/**
	 * \brief Model modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored model is returned.
	 * If the named stub property is present it is loaded as model resource.
	 */
	public func Model getModel( StubWithProperties stub, ElementClass eclass )
		var String value = stub.getPropertyValueFor( this.getName(), "" )
		if value.empty()
			return this.pModel
		end
		
		try
			return Model.new( value )
			
		catch Exception e
			throw ELoadResourceFailed.new( value, eclass.getClassname() )
		end
	end
end
