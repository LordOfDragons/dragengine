/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Utils



/**
 * \brief List of float rect area values class property for use with XML loading.
 */
class ECPFloatRectAreaList extends ElementClassProperty
	private var Array pFloatRectAreas
	
	
	
	/** \brief Create property. */
	public func new(String name) super(name)
		pFloatRectAreas = Array.new()
	end
	
	
	
	/** \brief Number of float rect areas. */
	public func int getCount()
		return pFloatRectAreas.getCount()
	end
	
	/** \brief FloatRectArea at index. */
	public func FloatRectArea getAt(int index)
		return pFloatRectAreas.getAt(index) cast FloatRectArea
	end
	
	/** \brief Get random float react area or \em null if no float react areas are present. */
	public func FloatRectArea getRandom()
		if pFloatRectAreas.getCount() == 0
			return null
			
		else
			return pFloatRectAreas.random() cast FloatRectArea
		end
	end
	
	/** \brief For each float react area with block with FloatRectArea as parameter. */
	public func void forEach(Block ablock)
		pFloatRectAreas.forEach(ablock)
	end
	
	/** \brief Remove all float react areas. */
	public func void removeAll()
		pFloatRectAreas.removeAll()
	end
	
	/** \brief Add float react area. */
	public func void add(FloatRectArea area)
		pFloatRectAreas.add(area)
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pFloatRectAreas.removeAll()
		(value cast Array).forEach(block FloatRectArea each
			pFloatRectAreas.add(each)
		end)
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		pFloatRectAreas = (property cast ECPFloatRectAreaList).pFloatRectAreas.map(block FloatRectArea each
			return FloatRectArea.new(each)
		end)
	end
end
