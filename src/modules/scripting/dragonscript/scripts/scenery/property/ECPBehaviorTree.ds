/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.BehaviorTreeSystem
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * \brief BehaviorTree element class property for use with XML loading.
 * 
 * BehaviorTree is set from XML using a path. Loads the behavior tree during preloading.
 * The value of the property is then the loaded resource. If loading fails null
 * behavior tree is stored.
 */
class ECPBehaviorTree extends ElementClassProperty
	private var LoadBehaviorTree pLoader
	private var String pPath
	private var BehaviorTree pBehaviorTree
	private var bool pHasPreloaded
	
	
	
	/** \brief Create property. */
	public func new(String name, LoadBehaviorTree loader) this(name, loader, null)
	end
	
	/** \brief Create property with initial path. */
	public func new(String name, LoadBehaviorTree loader, String path) super(name)
		pLoader = loader
		pPath = path
	end
	
	
	
	/** \brief BehaviorTree loader. */
	public func LoadBehaviorTree getLoader()
		return pLoader
	end
	
	/** \brief Path to behavior tree. */
	public func String getPath()
		return pPath
	end
	
	/** \brief Set path to behavior tree. */
	public func void setPath(String path)
		pPath = path
	end
	
	/** \brief BehaviorTree or \em null if not present. */
	public func BehaviorTree getBehaviorTree()
		return pBehaviorTree
	end
	
	/** \brief Set behavior tree or \em null if not present. */
	public func void setBehaviorTree(BehaviorTree behaviorTree)
		pBehaviorTree = behaviorTree
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pPath = value cast String
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		var String path = (property cast ECPBehaviorTree).pPath
		if path != null and not path.empty()
			pPath = File.absolutePath(path, elementClass.getBasePath()).getPath()
		end
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources(ElementClass elementClass, PreloadCounter counter)
		if pHasPreloaded
			return
		end
		
		pHasPreloaded = true
		
		if pPath == null or pPath.empty()
			return
		end
		
		var String path = File.absolutePath(pPath, elementClass.getBasePath()).getPath()
		
		try
			pBehaviorTree = pLoader.loadFromFile(path)
			
		catch Exception e
		end
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems(ElementClass elementClass, Console console)
		if pPath != null and not pPath.empty() and pBehaviorTree == null
			console.addMessage("Property " + elementClass.getClassname() + "." \
				+ getName() + ": Failed loading behavior tree " \
				+ File.absolutePath(pPath, elementClass.getBasePath()).getPath())
			return true
		end
		return false
	end
	
	
	
	/**
	 * \brief BehaviorTree modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored behavior tree is
	 * returned. If the named stub property is present it is loaded as behavior tree resource.
	 */
	public func BehaviorTree getBehaviorTree(StubWithProperties stub, ElementClass eclass)
		var String value = stub.getPropertyValueFor(getName(), "")
		if value.empty()
			return pBehaviorTree
		end
		
		try
			return pLoader.loadFromFile(value)
			
		catch Exception e
			throw ELoadResourceFailed.new(value, eclass.getClassname())
		end
	end
end
