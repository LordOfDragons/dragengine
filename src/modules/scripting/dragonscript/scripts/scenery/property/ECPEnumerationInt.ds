/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Utils
pin Dragengine.Preloading



/**
 * \brief Integer enumeration value element class property for use with XML loading.
 * 
 * Value is an integer value using a dictionary mapping string values to enumeration values.
 */
class ECPEnumerationInt extends ElementClassProperty
	private var int pValue
	private var Dictionary pMapping
	
	
	
	/** \brief Create property. */
	public func new(String name, int value) super(name)
		pValue = value
		pMapping = Dictionary.new()
	end
	
	
	
	/** \brief Value. */
	public func int getValue()
		return pValue
	end
	
	/** \brief Set value. */
	public func void setValue(int value)
		pValue = value
	end
	
	/** \brief Mapping between strings and enumerations. */
	public func Dictionary getMapping()
		return pMapping
	end
	
	/** \brief Set mapping between strings and enumerations. */
	public func void setMapping(Dictionary mapping)
		pMapping = mapping
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pValue = pMapping.getAt(value cast String) cast int
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		pValue = (property cast ECPEnumerationInt).pValue
	end
	
	
	
	/**
	 * \brief Enumeration value modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored enumeration value
	 * is returned. If the named stub property is present it is mapped against the mapping
	 * and the matching enumeration value returned. If no match is found an exception is thrown.
	 */
	public func int getValue(StubWithProperties stub)
		var String value = stub.getPropertyValueFor(getName(), "")
		if value.empty()
			return pValue
			
		else
			return pMapping.getAt(value) cast int
		end
	end
end
