/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.Preloading



/**
 * \brief Element class element class property for use with XML loading.
 * 
 * Element class is set from XML using a string. Looks up the matching element
 * class during preloading. The value of the property is then the real element
 * class or \em null if not found.
 */
class ECPElementClass extends ElementClassProperty
	private var ElementClassList pElementClassList
	private var String pElementClassName
	private var ElementClass pElementClass
	private var bool pHasPreloaded
	
	
	
	/** \brief Create property. */
	public func new(String name, ElementClassList elementClassList) super(name)
		if elementClassList == null
			throw EInvalidParam.new()
		end
		pElementClassList = elementClassList
	end
	
	/** \brief Create property with initial element class name. */
	public func new(String name, ElementClassList elementClassList, String elementClassName) super(name)
		if elementClassList == null
			throw EInvalidParam.new()
		end
		pElementClassList = elementClassList
		pElementClassName = elementClassName
	end
	
	/** \brief Dispose of element class property to break reference loops. */
	public func void dispose()
		pElementClassList = null
		pElementClass = null
	end
	
	
	
	/** \brief Element class manager to look up element class in. */
	public func ElementClassList getElementClassList()
		return pElementClassList
	end
	
	/** \brief Name of element class. */
	public func String getElementClassName()
		return pElementClassName
	end
	
	/** \brief Set name of element class. */
	public func void setElementClassName(String elementClassName)
		pElementClassName = elementClassName
	end
	
	/** \brief Element class or \em null if not found. */
	public func ElementClass getElementClass()
		return pElementClass
	end
	
	/** \brief Set element class or \em null if not found. */
	public func void setElementClass(ElementClass elementClass)
		pElementClass = elementClass
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pElementClassName = value cast String
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClassProperty property)
		pElementClassName = (property cast ECPElementClass).pElementClassName
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources(ElementClass elementClass, PreloadCounter counter)
		if pHasPreloaded
			return
		end
		
		pHasPreloaded = true
		
		if pElementClassName == null or pElementClassName.empty()
			return
		end
		
		pElementClass = pElementClassList.getNamed(pElementClassName)
		
		if pElementClass != null
			pElementClass.preloadResources(counter)
		end
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems(ElementClass elementClass, Console console)
		if pElementClassName != null and not pElementClassName.empty() and pElementClass == null
			console.addMessage("Property " + elementClass.getClassname() + "." \
				+ getName() + ": Unknown element class " + pElementClassName)
			return true
		end
		return false
	end
	
	
	
	/**
	 * \brief Element class modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored element class is returned.
	 * If the named stub property is present it is matched against the list of element classes.
	 */
	public func ElementClass getElementClass(StubWithProperties stub)
		var String value = stub.getPropertyValueFor(getName(), "")
		if value.empty()
			return pElementClass
		end
		
		var ElementClass eclass = pElementClassList.getNamed(value)
		if eclass == null
			throw EInvalidParam.new()
		end
		return eclass
	end
end
