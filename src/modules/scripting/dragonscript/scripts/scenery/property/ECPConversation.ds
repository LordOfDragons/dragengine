/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.ConversationSystem
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * \brief Conversation element class property for use with XML loading.
 * 
 * Conversation is set from XML using a path. Loads the conversation during preloading.
 * The value of the property is then the loaded resource. If loading fails null
 * conversation is stored.
 */
class ECPConversation extends ElementClassProperty
	private var LoadConversation pLoader
	private var String pPath
	private var Conversation pConversation
	private var bool pHasPreloaded
	private var Exception pException
	
	
	
	/** \brief Create property. */
	public func new( String name, LoadConversation loader ) this( name, loader, null )
	end
	
	/** \brief Create property with initial path. */
	public func new( String name, LoadConversation loader, String path ) super( name )
		this.pLoader = loader
		this.pPath = path
	end
	
	
	
	/** \brief Conversation loader. */
	public func LoadConversation getLoader()
		return this.pLoader
	end
	
	/** \brief Path to conversation. */
	public func String getPath()
		return this.pPath
	end
	
	/** \brief Set path to conversation. */
	public func void setPath( String path )
		this.pPath = path
	end
	
	/** \brief Conversation or \em null if not present. */
	public func Conversation getConversation()
		return this.pConversation
	end
	
	/** \brief Set conversation or \em null if not present. */
	public func void setConversation( Conversation conversation )
		this.pConversation = conversation
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return this.pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue( Object value )
		this.pPath = value cast String
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources( ElementClass elementClass, PreloadCounter counter )
		if this.pHasPreloaded
			return
		end
		
		this.pHasPreloaded = true
		
		if this.pPath == null or this.pPath.empty()
			return
		end
		
		var String path = File.absolutePath( this.pPath, elementClass.getBasePath() ).getPath()
		
		try
			this.pConversation = this.pLoader.loadFromFile( path )
			
		catch Exception e
			this.pException = e
		end
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems( ElementClass elementClass, Console console )
		if this.pPath != null and not this.pPath.empty() and this.pConversation == null
			console.addError( "Property " + elementClass.getClassname() + "." \
				+ this.getName() + ": Failed loading conversation " \
				+ File.absolutePath( this.pPath, elementClass.getBasePath() ).getPath(), this.pException )
			return true
		end
		return false
	end
	
	
	
	/**
	 * \brief Conversation modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored conversation is
	 * returned. If the named stub property is present it is loaded as conversation resource.
	 */
	public func Conversation getConversation( StubWithProperties stub, ElementClass eclass )
		var String value = stub.getPropertyValueFor( this.getName(), "" )
		if value.empty()
			return this.pConversation
		end
		
		try
			return this.pLoader.loadFromFile( value )
			
		catch Exception e
			throw ELoadResourceFailed.new( value, eclass.getClassname() + ": " + e.getReason() )
		end
	end
end
