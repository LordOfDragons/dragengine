/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Utils
pin Dragengine.Preloading



/**
 * \brief Color list element class property for use with XML loading.
 * \version 1.5
 * 
 * Color list is stored as Array of Color. From XML the color list is read using an encoded
 * string decoded using CodecPropertyString. The following example defines a list with
 * three colors:
 * 
 * \code{.xml}
 * <list name='propertyName'>
 *   <color r='1' g='0' b='0'/>
 *   <color r='0' g='1' b='0.5'/>
 *   <color r='0.25' g='0.5' b='1'/>
 * </list>
 * \endcode
 * 
 * Alternatively a string can be used with the same content as the stub property value:
 * 
 * \code{.xml}
 * <string name='propertyName'>"1 0 0" "0 1 0.5" "0.25 0.5 1"</string>
 * \endcode
 * 
 * In stub properties the coding is of the form "color1 color2 ..." where color is encoded as
 * "r g b". Since both uses space to separate tokens colors have to be wrapped in double quotes.
 * The following example defines the same three colors as above in sub property string form:
 * 
 * \code
 * "1 0 0" "0 1 0.5" "0.25 0.5 1"
 * \endcode
 * 
 * Optionally newline can be used instead of whitespace. This can be easier to write with
 * long list of colors.
 */
class ECPColorList extends ElementClassProperty
	var CodecPropertyString pCodec
	var Array pColorList
	
	
	
	/** \brief Create property. */
	func new(String name, CodecPropertyString codec) super(name)
		pCodec = codec
		pColorList = Array.new()
	end
	
	/** \brief Create property with initial path. */
	func new(String name, CodecPropertyString codec, Array colors) super(name)
		pCodec = codec
		pColorList = colors
	end
	
	
	
	/** \brief Coded used to decode color string. */
	func CodecPropertyString getCodec()
		return pCodec
	end
	
	/** \brief Colors as Array of Color. */
	func Array getColors()
		return pColorList
	end
	
	/** \brief Set colors as Array of Color. */
	func void setColors(Array colors)
		pColorList = colors
	end
	
	/** \brief Count of colors. */
	func int getColorCount()
		return pColorList.getCount()
	end
	
	/** \brief Color at index. */
	func Color getColorAt(int index)
		return pColorList.getAt(index) cast Color
	end
	
	/** \brief Random color or null if list is empty. */
	func Color randomColor()
		if pColorList.getCount() > 0
			return pColorList.random() cast Color
			
		else
			return null
		end
	end
	
	/** \brief Add color. */
	func void addColor(Color color)
		if color == null
			throw ENullPointer.new("color")
		end
		pColorList.add(color)
	end
	
	/** \brief Color list string. */
	func String getColorsString()
		return pCodec.encodeStringList(pColorList.map(block Color each
			return pCodec.encodeColor3(each)
		end))
	end
	
	/** \brief Set color list from color string. */
	func void setColorsFromString(String string)
		pColorList = pCodec.decodeStringList(string).map(block String each
			return pCodec.decodeColor3(each)
		end)
	end
	
	/**
	 * \brief Set color list from \ref Array of \ref Color.
	 * \version 1.25
	 */
	func void setColorsFromList(Array list)
		pColorList = list.map(block Color each // simple way to ensure type
			return each
		end)
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	func void setValue(Object value)
		if value castable Array
			setColorsFromList(value cast Array)
			
		else
			setColorsFromString(value cast String)
		end
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	func void copyValue(ElementClass elementClass, ElementClassProperty property)
		pColorList = Array.new((property cast ECPColorList).pColorList)
	end
	
	
	
	/**
	 * \brief Color list modified by stub property if present.
	 * 
	 * If named stub property is not present the stored color list is returned. If the named
	 * stub property is present it is decoded using the stored decoder and the result returned.
	 * 
	 * Uses the stored property name as stub property name.
	 */
	func Array getColors(StubWithProperties stub)
		return getColors(stub, getName())
	end
	
	/**
	 * \brief Color list modified by stub property if present.
	 * 
	 * If named stub property is not present the stored color list is returned. If the named
	 * stub property is present it is decoded using the stored decoder and the result returned.
	 */
	func Array getColors(StubWithProperties stub, String name)
		var String value = stub.getPropertyValueFor(name, null)
		if value != null
			return pCodec.decodeStringList(value).map(block String each
				return pCodec.decodeColor3(each)
			end)
			
		else
			return pColorList
		end
	end
end
