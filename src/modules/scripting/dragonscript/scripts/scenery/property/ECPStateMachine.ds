/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.StateMachineSystem
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * \brief StateMachine element class property for use with XML loading.
 * 
 * StateMachine is set from XML using a path. Loads the state machine during preloading.
 * The value of the property is then the loaded resource. If loading fails null
 * state machine is stored.
 */
class ECPStateMachine extends ElementClassProperty
	private var LoadStateMachine pLoader
	private var String pPath
	private var StateMachine pStateMachine
	private var bool pHasPreloaded
	
	
	
	/** \brief Create property. */
	public func new( String name, LoadStateMachine loader ) this( name, loader, null )
	end
	
	/** \brief Create property with initial path. */
	public func new( String name, LoadStateMachine loader, String path ) super( name )
		this.pLoader = loader
		this.pPath = path
	end
	
	
	
	/** \brief StateMachine loader. */
	public func LoadStateMachine getLoader()
		return this.pLoader
	end
	
	/** \brief Path to state machine. */
	public func String getPath()
		return this.pPath
	end
	
	/** \brief Set path to state machine. */
	public func void setPath( String path )
		this.pPath = path
	end
	
	/** \brief StateMachine or \em null if not present. */
	public func StateMachine getStateMachine()
		return this.pStateMachine
	end
	
	/** \brief Set state machine or \em null if not present. */
	public func void setStateMachine( StateMachine behaviorTree )
		this.pStateMachine = behaviorTree
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return this.pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue( Object value )
		this.pPath = value cast String
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources( ElementClass elementClass, PreloadCounter counter )
		if this.pHasPreloaded
			return
		end
		
		this.pHasPreloaded = true
		
		if this.pPath == null or this.pPath.empty()
			return
		end
		
		var String path = File.absolutePath( this.pPath, elementClass.getBasePath() ).getPath()
		
		try
			this.pStateMachine = this.pLoader.loadFromFile( path )
			
		catch Exception e
		end
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems( ElementClass elementClass, Console console )
		if this.pPath != null and not this.pPath.empty() and this.pStateMachine == null
			console.addMessage( "Property " + elementClass.getClassname() + "." \
				+ this.getName() + ": Failed loading state machine " \
				+ File.absolutePath( this.pPath, elementClass.getBasePath() ).getPath() )
			return true
		end
		return false
	end
	
	
	
	/**
	 * \brief StateMachine modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored state machine is
	 * returned. If the named stub property is present it is loaded as state machine resource.
	 */
	public func StateMachine getStateMachine( StubWithProperties stub, ElementClass eclass )
		var String value = stub.getPropertyValueFor( this.getName(), "" )
		if value.empty()
			return this.pStateMachine
		end
		
		try
			return this.pLoader.loadFromFile( value )
			
		catch Exception e
			throw ELoadResourceFailed.new( value, eclass.getClassname() )
		end
	end
end
