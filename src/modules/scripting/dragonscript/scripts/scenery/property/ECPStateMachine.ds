/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.StateMachineSystem
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * \brief StateMachine element class property for use with XML loading.
 * 
 * StateMachine is set from XML using a path. Loads the state machine during preloading.
 * The value of the property is then the loaded resource. If loading fails null
 * state machine is stored.
 */
class ECPStateMachine extends ElementClassProperty
	private var LoadStateMachine pLoader
	private var String pPath
	private var StateMachine pStateMachine
	private var bool pHasPreloaded
	
	
	
	/** \brief Create property. */
	public func new(String name, LoadStateMachine loader) this(name, loader, null)
	end
	
	/** \brief Create property with initial path. */
	public func new(String name, LoadStateMachine loader, String path) super(name)
		pLoader = loader
		pPath = path
	end
	
	
	
	/** \brief StateMachine loader. */
	public func LoadStateMachine getLoader()
		return pLoader
	end
	
	/** \brief Path to state machine. */
	public func String getPath()
		return pPath
	end
	
	/** \brief Set path to state machine. */
	public func void setPath(String path)
		pPath = path
	end
	
	/** \brief StateMachine or \em null if not present. */
	public func StateMachine getStateMachine()
		return pStateMachine
	end
	
	/** \brief Set state machine or \em null if not present. */
	public func void setStateMachine(StateMachine behaviorTree)
		pStateMachine = behaviorTree
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pPath = value cast String
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		var String path = (property cast ECPStateMachine).pPath
		if path != null and not path.empty()
			pPath = File.absolutePath(path, elementClass.getBasePath()).getPath()
		end
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources(ElementClass elementClass, PreloadCounter counter)
		if pHasPreloaded
			return
		end
		
		pHasPreloaded = true
		
		if pPath == null or pPath.empty()
			return
		end
		
		var String path = File.absolutePath(pPath, elementClass.getBasePath()).getPath()
		
		try
			pStateMachine = pLoader.loadFromFile(path)
			
		catch Exception e
		end
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems(ElementClass elementClass, Console console)
		if pPath != null and not pPath.empty() and pStateMachine == null
			console.addMessage("Property " + elementClass.getClassname() + "." \
				+ getName() + ": Failed loading state machine " \
				+ File.absolutePath(pPath, elementClass.getBasePath()).getPath())
			return true
		end
		return false
	end
	
	
	
	/**
	 * \brief StateMachine modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored state machine is
	 * returned. If the named stub property is present it is loaded as state machine resource.
	 */
	public func StateMachine getStateMachine(StubWithProperties stub, ElementClass eclass)
		var String value = stub.getPropertyValueFor(getName(), "")
		if value.empty()
			return pStateMachine
		end
		
		try
			return pLoader.loadFromFile(value)
			
		catch Exception e
			throw ELoadResourceFailed.new(value, eclass.getClassname())
		end
	end
end
