/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Utils
pin Dragengine.Preloading



/**
 * \brief String value element class property for use with XML loading.
 */
class ECPString extends ElementClassProperty
	private var String pValue
	
	
	
	/** \brief Create property. */
	public func new(String name, String value) super(name)
		pValue = value
	end
	
	
	
	/** \brief Value. */
	public func String getValue()
		return pValue
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pValue = value cast String
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		pValue = (property cast ECPString).pValue
	end
	
	
	
	/**
	 * \brief String value value modified by stub property if present.
	 * 
	 * If named stub property is not present stored value is returned.
	 * If the named stub property is present it is returned.
	 */
	public func String getValue(StubWithProperties stub)
		return stub.getPropertyValueFor(getName(), pValue)
	end
	
	/**
	 * \brief String value value modified by stub property if present.
	 * \version 1.13
	 * 
	 * If named stub property is not present stored value is returned.
	 * If the named stub property is present it is returned. If the value
	 * to be returned is empty string null is returned.
	 */
	public func String getValueOrNull(StubWithProperties stub)
		var String value = getValue(stub)
		if value != null and value.empty()
			value = null
		end
		return value
	end
	
	/**
	 * \brief String value value modified by stub property if present.
	 * \version 1.13
	 * 
	 * If named stub property is not present stored value is returned.
	 * If the named stub property is present it is returned. If the value
	 * to be returned is null empty string is returned.
	 */
	public func String getValueOrEmpty(StubWithProperties stub)
		var String value = getValue(stub)
		if value == null
			value = ""
		end
		return value
	end
end
