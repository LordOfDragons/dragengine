/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * \brief OcclusionMesh resource element class property for use with XML loading.
 * 
 * OcclusionMesh resource is set from XML using a path. Loads the resource during preloading.
 * The value of the property is then the loaded resource. If loading fails null
 * resource is stored.
 */
class ECPOcclusionMesh extends ElementClassProperty implements ResourceListener
	private var String pPath
	private var OcclusionMesh pOcclusionMesh
	private var PreloadCounter pCounter
	private var bool pHasPreloaded
	
	
	
	/** \brief Create property. */
	public func new(String name) super(name)
	end
	
	/** \brief Create property with initial path. */
	public func new(String name, String path) super(name)
		pPath = path
	end
	
	
	
	/** \brief Path to occlusion mesh resource. */
	public func String getPath()
		return pPath
	end
	
	/** \brief Set path to occlusion mesh resource. */
	public func void setPath(String path)
		pPath = path
	end
	
	/** \brief Occlusion mesh resource or \em null if not present. */
	public func OcclusionMesh getOcclusionMesh()
		return pOcclusionMesh
	end
	
	/** \brief Set occlusion mesh resource or \em null if not present. */
	public func void setOcclusionMesh(OcclusionMesh occlusionMesh)
		pOcclusionMesh = occlusionMesh
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pPath = value cast String
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		var String path = (property cast ECPOcclusionMesh).pPath
		if path != null and not path.empty()
			pPath = File.absolutePath(path, elementClass.getBasePath()).getPath()
		end
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources(ElementClass elementClass, PreloadCounter counter)
		if pHasPreloaded
			return
		end
		
		pHasPreloaded = true
		
		if pPath == null or pPath.empty()
			return
		end
		
		OcclusionMesh.loadAsynchron(File.absolutePath(pPath, elementClass.getBasePath()).getPath(), this)
		pCounter = counter
		counter.increase()
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems(ElementClass elementClass, Console console)
		if pPath != null and not pPath.empty() and pOcclusionMesh == null
			console.addMessage("Property " + elementClass.getClassname() + "." \
				+ getName() + ": Failed loading occlusion mesh " \
				+ File.absolutePath(pPath, elementClass.getBasePath()).getPath())
			return true
		end
		return false
	end
	
	
	
	/** \brief Resource has finished loading. */
	public func void finishedLoading(String path, ResourceLoaderType resourceType, Object resource)
		pOcclusionMesh = resource cast OcclusionMesh
		pCounter.decrease()
	end
	
	/** \brief Resource failed loading. */
	public func void failedLoading(String filename, ResourceLoaderType resourceType)
		pCounter.decrease()
	end
	
	
	
	/**
	 * \brief OcclusionMesh modified by stub property if present.
	 * 
	 * If named stub property is not present or empty string the stored occlusionMesh is returned.
	 * If the named stub property is present it is loaded as occlusionMesh resource.
	 */
	public func OcclusionMesh getOcclusionMesh(StubWithProperties stub, ElementClass eclass)
		var String value = stub.getPropertyValueFor(getName(), "")
		if value.empty()
			return pOcclusionMesh
			
		else
			try
				return OcclusionMesh.new(value)
				
			catch Exception e
				throw ELoadResourceFailed.new(value, eclass.getClassname())
			end
		end
	end
end
