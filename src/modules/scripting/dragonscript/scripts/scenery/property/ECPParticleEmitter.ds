/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils



/**
 * \brief Particle emitter element class property for use with XML loading.
 * 
 * Particle emitter is set from XML using a path. Loads the particle emitter during
 * preloading. The value of the property is then the loaded resource. If loading
 * fails null particle emitter is stored.
 */
class ECPParticleEmitter extends ElementClassProperty
	private var LoadParticleEmitter pLoader
	private var String pPath
	private var ParticleEmitter pParticleEmitter
	private var bool pHasPreloaded
	
	
	
	/** \brief Create property. */
	public func new(String name, LoadParticleEmitter loader) super(name)
		pLoader = loader
	end
	
	/** \brief Create property with initial path. */
	public func new(String name, LoadParticleEmitter loader, String path) super(name)
		pLoader = loader
		pPath = path
	end
	
	
	
	/** \brief Particle emitter loader. */
	public func LoadParticleEmitter getLoader()
		return pLoader
	end
	
	/** \brief Path to particle emitter. */
	public func String getPath()
		return pPath
	end
	
	/** \brief Set path to particle emitter. */
	public func void setPath(String path)
		pPath = path
	end
	
	/** \brief ParticleEmitter or \em null if not present. */
	public func ParticleEmitter getParticleEmitter()
		return pParticleEmitter
	end
	
	/** \brief Set particle emitter or \em null if not present. */
	public func void setParticleEmitter(ParticleEmitter particleEmitter)
		pParticleEmitter = particleEmitter
	end
	
	/** \brief Property has been preloaded. */
	public func bool getHasPreloaded()
		return pHasPreloaded
	end
	
	
	
	/** \brief Set value of property from XML decoded value. */
	public func void setValue(Object value)
		pPath = value cast String
	end
	
	/**
	 * \brief Copy value from another element class property of the same type.
	 * \version 1.17
	 */
	public func void copyValue(ElementClass elementClass, ElementClassProperty property)
		var String path = (property cast ECPParticleEmitter).pPath
		if path != null and not path.empty()
			pPath = File.absolutePath(path, elementClass.getBasePath()).getPath()
		end
	end
	
	
	
	/** \brief Preload resources if supported. */
	public func void preloadResources(ElementClass elementClass, PreloadCounter counter)
		if pHasPreloaded
			return
		end
		
		pHasPreloaded = true
		
		if pPath == null or pPath.empty()
			return
		end
		
		try
			pParticleEmitter = pLoader.loadFromFile(\
				File.absolutePath(pPath, elementClass.getBasePath()).getPath())
			
		catch Exception e
		end
	end
	
	/**
	 * \brief Log preloading problems.
	 * \returns True if problems are present.
	 */
	public func bool logPreloadProblems(ElementClass elementClass, Console console)
		if pPath != null and not pPath.empty() and pParticleEmitter == null
			console.addMessage("Property " + elementClass.getClassname() + "." \
				+ getName() + ": Failed loading particle emitter " \
				+ File.absolutePath(pPath, elementClass.getBasePath()).getPath())
			return true
		end
		return false
	end
end
