/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.BehaviorTreeSystem
pin Dragengine.LoadSave


/**
 * \brief Actor AI using BehaviorTree.
 * 
 * Uses BehaviorTree using BTContext to do AI. For each call to think() the behavior tree
 * is stepped. In case of an exception in the behavior tree the exception is logged and the
 * running of the behavior tree cancelled.
 * 
 * To use this AI set the BTContext subclass set up with the required actions and state
 * initialized to run the behavior tree. Do this after the AI instance is created which
 * can be also due to Persistable interface loading from file reader.
 * 
 * To support behaviors receiving notifications about the state of the context set also
 * the ECBehaviorBehaviorTree used to initialize the context.
 */
class BAAIBehaviorTree extends BaseActorAI
	/**
	 * \brief Factory for loading actor ai.
	 */
	class Factory implements PersistencyFactory, ECBehaviorActorAIAction.AIFactory
		public static fixed var String NAME = "BAAIBehaviorTree"
		
		func new()
		end
		
		func Persistable readObjectFromFile(PersistencyEnvironment env, FileReader reader)
			return BAAIBehaviorTree.new(env, reader)
		end
		
		static func void registerFactory(Persistency persistency)
			persistency.addFactory(NAME, Factory.new())
		end
		
		func BaseActorAI createAI(ECBehaviorActorAIAction.Instance instance)
			return BAAIBehaviorTree.new()
		end
	end
	
	/**
	 * \brief Behavior tree context with link to BAAIBehaviorTree.
	 */
	class BTContextAI extends BTContext
		/** \brief AI. */
		var BAAIBehaviorTree pAI
		
		/** \brief Create context. */
		func new(BAAIBehaviorTree ai, BehaviorTree behaviorTree) super(behaviorTree)
			pAI = ai
		end
		/** \brief Create copy of context. */
		func new(BTContextAI context) super(context)
			pAI = context.pAI
		end
		
		/** \brief Create copy of context. */
		func new(BAAIBehaviorTree ai, BTContext context) super(context)
			pAI = ai
		end
		
		/** \brief Create copy of context using different behavior tree. */
		func new(BAAIBehaviorTree ai, BTContext context, BehaviorTree behaviorTree) super(context, behaviorTree)
			pAI = ai
		end
		
		/** \brief AI. */
		func BAAIBehaviorTree getAI()
			return pAI
		end
		
		/** \brief Actor. */
		func BehaviorElement getActor()
			return pAI.getActor()
		end
	end
	
	/**
	 * \brief Behavior tree action with link to BAAIBehaviorTree.
	 */
	abstract class BTActionAI implements BTAction
		/** \brief AI. */
		var BAAIBehaviorTree pAI
		
		/** \brief Create action. */
		func new(BAAIBehaviorTree ai)
			pAI = ai
		end
		
		/** \brief AI. */
		func BAAIBehaviorTree getAI()
			return pAI
		end
		
		/** \brief Actor. */
		func BehaviorElement getActor()
			return pAI.getActor()
		end
	end
	
	
	
	var BTContextAI pContext
	var bool pRunning
	var bool pPauseInConversation
	var ECBehaviorBehaviorTree.Instance pBehaviorBehaviorTree
	protected var ECBehaviorConversationActor.Instance conversationActor
	
	
	
	/** \brief Create actor ai. */
	func new()
	end
	
	/** \brief Create actor ai restoring from file reader. */
	func new(PersistencyEnvironment env, FileReader reader)
		select reader.readByte() // version
		case 0
			var PersistencyFlags flags = PersistencyFlags.new(3, reader)
			var bool hasContext = flags.getAt(0)
			pRunning = flags.getAt(1)
			pPauseInConversation = flags.getAt(2)
			
			if hasContext and pContext != null
				pContext.readFromFile(env, reader)
			end
			
		else
			throw EInvalidParam.new("unsupported version")
		end
	end
	
	/** \brief Dispose of actor ai. */
	func void dispose()
		pBehaviorBehaviorTree = null
		conversationActor = null
		pContext = null
		super.dispose()
	end
	
	
	
	/** \brief Behavior tree context or \em null if not set. */
	func BTContextAI getContext()
		return pContext
	end
	
	/**
	 * \brief Set behavior tree context or \em null if not set.
	 * 
	 * If context is not null resetContext() is also called.
	 */
	func void setContext(BTContextAI context)
		pContext = context
		resetContext()
	end
	
	/** \brief Behavior tree is running. */
	func bool getRunning()
		return pRunning
	end
	
	/** \brief Set if behavior tree is running. */
	func void setRunning(bool running)
		pRunning = running
	end
	
	/**
	 * \brief Pause behavior tree while actor is in a conversation.
	 * \version 1.19
	 */
	func bool getPauseInConversation()
		return pPauseInConversation
	end
	
	/**
	 * \brief Set to pause behavior tree while actor is in a conversation.
	 * \version 1.19
	 */
	func void setPauseInConversation(bool pauseInConversation)
		pPauseInConversation = pauseInConversation
	end
	
	/**
	 * \brief Reset context to run it again.
	 * \version 1.19
	 */
	func void resetContext()
		if pContext == null
			return
		end
		
		pContext.reset()
		
		if pBehaviorBehaviorTree != null
			pBehaviorBehaviorTree.notifyRestarted(pContext)
		end
	end
	
	/**
	 * \brief Behavior behavior tree to send notifications to.
	 * \version 1.19
	 */
	func ECBehaviorBehaviorTree.Instance getBehaviorBehaviorTree()
		return pBehaviorBehaviorTree
	end
	
	/**
	 * \brief Set behavior behavior tree to send notifications to.
	 * \version 1.19
	 */
	func void setBehaviorBehaviorTree(ECBehaviorBehaviorTree.Instance behavior)
		pBehaviorBehaviorTree = behavior
	end
	
	
	
	/**
	 * \brief Init behaviors.
	 * 
	 * Retrieve and store required and optional behavior instances from the actor element.
	 */
	protected func void initBehaviors()
		super.initBehaviors()
		
		if actor castable BaseActor
			var BaseActor baseActor = actor cast BaseActor
			conversationActor = baseActor.getConversationActor()
			
		else
			conversationActor = ECBehaviorConversationActor.getInstanceIn(actor)
		end
	end
	 
	/**
	 * \brief Thinking.
	 * 
	 * Behavior tree is process if:
	 * - Context is not null
	 * - Running is true
	 * - Actor is not controlled by player
	 * 
	 * If running the behavior tree throws an exception or returns \em false running is set to
	 * false and onBehaviorTreeFailed() is called. To run the context again call resetContext().
	 */
	func void think(float elapsed)
		if pContext == null or isControlledByPlayer()
			return
		end
		
		if pBehaviorBehaviorTree != null
			pBehaviorBehaviorTree.notifyUpdate(pContext, elapsed)
		end
		
		if not pRunning
			return
		end
		if pPauseInConversation and conversationActor != null and conversationActor.isInConversation()
			return
		end
		
		try
			if not pContext.stepBehaviorTree()
				setRunning(false)
				if pBehaviorBehaviorTree != null
					pBehaviorBehaviorTree.notifyFailed(pContext)
				end
				onBehaviorTreeFailed(null)
			end
			
		catch Exception e
			setRunning(false)
			if pBehaviorBehaviorTree != null
				pBehaviorBehaviorTree.notifyFailed(pContext)
			end
			onBehaviorTreeFailed(e)
		end
	end
	
	/**
	 * \brief Running the behavior tree failed.
	 * 
	 * Default implementation logs exception using log(Exception).
	 * 
	 * \param exception Exception if thrown or \em null otherwise.
	 */
	func void onBehaviorTreeFailed(Exception e)
		log(e)
	end
	
	
	
	/**
	 * \brief Name of PersistencyFactory required to load object from file.
	 * 
	 * Name of PersistencyFactory is first written to the file as string with
	 * 8-bit length. Then writeToFile() is called to write the object itself.
	 * Empty name is not allowed as is names longer than 255 characters.
	 * Name has to match a PersistencyFactory present in the Persistency
	 * instance used for loading objects. Object is read using
	 * PersistencyFactory.readObjectFromFile() of the matching PersistencyFactory.
	 */
	func String persistencyFactoryName()
		return Factory.NAME
	end
	
	/** \brief Write object to file writer. */
	func void writeObjectToFile(PersistencyEnvironment env, FileWriter writer)
		super.writeObjectToFile(env, writer)
		
		writer.writeByte(0) // version
		
		var PersistencyFlags flags = PersistencyFlags.new(3)
		flags.setAt(0, pContext != null)
		flags.setAt(1, pRunning)
		flags.setAt(2, pPauseInConversation)
		
		if pContext != null
			pContext.writeToFile(env, writer)
		end
	end
end
