/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.TriggerSystem
pin Dragengine.LoadSave


/**
 * \brief Trigger starting conversation element class.
 * 
 * Convenience version of ECBehaviorConversation as directly usable trigger.
 */
class TriggerConversationClass extends BehaviorElementClass
	private var ECBehaviorConversationActor pConversationActor
	private var ECBehaviorConversation pConversation
	private var ECBCTRRBindings pCTRRBindings
	
	
	
	/** \brief Create element class. */
	public func new( TSTriggerTable triggerTable ) this( "TriggerConversation", triggerTable, \
	sharedFactoryHelper.defaultLoaders(), sharedFactoryHelper.defaultPlayerControlledActor())
	end
	
	public func new( TSTriggerTable triggerTable, Loaders loaders, \
	ECBehaviorPlayerControllable.Tracker playerControlledActor ) \
	this( "TriggerConversation", triggerTable, loaders, playerControlledActor )
	end
	
	public func new( String name, TSTriggerTable triggerTable ) this( name, triggerTable, \
	sharedFactoryHelper.defaultLoaders(), sharedFactoryHelper.defaultPlayerControlledActor())
	end
	
	public func new( String name, TSTriggerTable triggerTable, Loaders loaders, \
	ECBehaviorPlayerControllable.Tracker playerControlledActor ) super( name )
		this.pConversationActor = ECBehaviorConversationActor.new( this )
		
		this.pConversation = ECBehaviorConversation.new( this, loaders, triggerTable, playerControlledActor )
		this.pConversation.setConversationActor( this.pConversationActor )
		
		this.pCTRRBindings = ECBCTRRBindings.new( this, this.pConversationActor )
	end
	
	
	
	/** \brief Conversation actor behavior. */
	public func ECBehaviorConversationActor getConversationActor()
		return this.pConversationActor
	end
	
	/** \brief Conversation behavior. */
	public func ECBehaviorConversation getConversation()
		return this.pConversation
	end
	
	/** \brief Binding tag resolver for conversation actor behavior. */
	public func ECBCTRRBindings getCTRRBindings()
		return this.pCTRRBindings
	end
	
	
	
	/** \brief Create copy of element class. */
	public func ElementClass duplicateNamed( String classname )
		return TriggerConversationClass.new( classname, this.pConversation.getTriggered().getTriggerTable() )
	end
	
	
	
	/**
	 * \brief Create element instance.
	 * 
	 * If this element class does not support creating elements, for example because
	 * the result is not an element, \em null has to be returned. The default
	 * implementation returns \em null. Subclasses not intending to create elements
	 * or requiring special creation handling should subclass createAndAddElement()
	 * and loadAndAddElement() bypassing this method.
	 */
	protected func Element createElement()
		return TriggerConversation.new( this )
	end
end
