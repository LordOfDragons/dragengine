/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui.Events
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils


/**
 * \brief Behavior element behavior adding support to control desktop by player.
 * \version 1.7
 * 
 * The behavior allows player (or other actors) to move the mouse pointer on a
 * ECBehaviorRenderableDesktop by looking or pointing at the desired position. Also the
 * player can simulate a left or right mouse click by using an input binding defined
 * by the actor himself. This kind of input is typically used for VR games but can be
 * also used for non VR games like first person shooters.
 * 
 * The behavior defines three corner points of a virtual rectangular plane. The player
 * looking or pointing direction is intersected with this virtual plane. The relative
 * position of this hit point is mapped to the Desktop widget. The three corners of the
 * virtual plane are the lower left, lower right and upper left corner.
 * 
 * Optionally a bone name can be specified. If the bone name is not empty the virtual
 * plane corners are relative to the bone. This allows to create desktop input zones
 * which can be displaced.
 * 
 * The actor is responsible to move the mouse pointer. This behavior provides convenience
 * methods to do the calculation and updating of the mouse pointer.
 * 
 * A typical usage of this behavior looks like this:
 * 
 * \code{.ds}
 * class MyPlayerAction extends BAAFirstPersonVR
 *    func new()
 *    end
 *    
 *    func void playerThink(float elapsed)
 *       super.think(elapsed)
 *       
 *       // Check if right hand controller is pointing at an element
 *       if vrRightHandPointAt.hasVRHandPointAtElement()
 *          
 *          // Store the hit element and hit point in world space. Typically you want
 *          // to test them against mulitple supported interactions.
 *          var BehaviorElement element = vrRightHandPointAt.getPointAtBehaviorElement()
 *          var DVector hitPoint = vrRightHandPointAt.getPointAtHitPoint()
 *          
 *          // Move mouse on the desktop controller if present in the pointed at element
 *          var ECBehaviorControlDesktop.Instance controlDesktop = ECBehaviorControlDesktop.getInstanceIn(element)
 *          if controlDesktop != null
 *             // The call moveMouse() converts the hit point in world space to desktop
 *             // coordinates. If the world position is outside the desktop area nothing
 *             // is done and false is returned. Otherwise the mouse position is updated
 *             // and true is returned. In this example we stop here if we updated the
 *             // desktop mouse position in case more interactions are check below
 *             if controlDesktop.moveMouse(hitPoint)
 *                return
 *             end
 *          end
 *          
 *          // Here you could check for other supported interactions
 *          
 *       end
 *       
 *       // The same could be done for vrLeftHandPointAt too
 *    end
 * end
 * \endcode
 * 
 * The same example can be done using ECBehaviorLookAt instead of ECBehaviorVRHandPointAt.
 * 
 * This behavior can be used multiple times for example to control different renderable
 * desktops. Element class properties have the prefix "controlDesktop{(id)}.".
 * 
 * This behavior requires ECBehaviorRenderableDesktop to be present.
 * 
 * This behavior require the element class to be persistable (setPersistable).
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_controldesktop
 */
class ECBehaviorControlDesktop extends DefaultECBehavior
	/**
	 * \brief Behavior factory.
	 * \version 1.25
	 * 
	 * Automatically assigns these behaviors if present before adding:
	 * - Required: \ref ECBehaviorRenderableDesktop
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorControlDesktop'>
	 *   <!-- optional: use renderable desktop with id instead of empty string -->
	 *   <string name='renderableDesktop'>second</string>
	 * </behavior>
	 * 
	 * <!-- for adding multiple behaviors use unique identifiers -->
	 * <behavior type='ECBehaviorControlDesktop' id='second'>
	 *   ...
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for example ".propertyName").
	 */
	class Factory implements BehaviorFactory
		func new()
		end
		
		func String factoryId()
			return "ECBehaviorControlDesktop"
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			var ECBehaviorControlDesktop behavior = ECBehaviorControlDesktop.new(eclass,\
				eclass.sharedFactoryHelper.defaultRenderableDesktop(eclass, parameters), id)
			eclass.sharedFactoryHelper.setPropertiesFromParameters(\
				eclass, parameters, "controlDesktop", id)
			return behavior
		end
	end
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		var ECBehaviorControlDesktop pECBehavior
		var ECBehaviorRenderableDesktop.Instance pRenderableDesktop
		var Vector pLowerLeft
		var Vector pLowerRight
		var Vector pUpperLeft
		var String pBone
		
		
		
		/** \brief Create element. */
		func new(ECBehaviorControlDesktop ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			
			pRenderableDesktop = ecbehavior.getRenderableDesktop().instance(element)
		end
		
		/** \brief Dispose of behavior instance. */
		func void dispose()
			pRenderableDesktop = null
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		func void init(StubElement stub)
			var CodecPropertyString codec = getElement().getClass().getCodecPropertyString()
			pLowerLeft = pECBehavior.getLowerLeft().getVector(stub, codec)
			pLowerRight = pECBehavior.getLowerRight().getVector(stub, codec)
			pUpperLeft = pECBehavior.getUpperLeft().getVector(stub, codec)
			pBone = pECBehavior.getBone().getValueOrEmpty(stub)
		end
		
		
		
		/** \brief Behavior. */
		func ECBehaviorControlDesktop getECBehavior()
			return pECBehavior
		end
		
		/** \brief Renderable desktop behavior instance. */
		func ECBehaviorRenderableDesktop.Instance getRenderableDesktop()
			return pRenderableDesktop
		end
		
		/** \brief Virtual plane lower left position. */
		func Vector getLowerLeft()
			return pLowerLeft
		end
		
		/**
		 * \brief Set virtual plane lower left position.
		 * \version 1.26
		 */
		func void setLowerLeft(Vector position)
			if position == null
				throw ENullPointer.new()
			end
			pLowerLeft = position
		end
		
		/** \brief Virtual plane lower right position. */
		func Vector getLowerRight()
			return pLowerRight
		end
		
		/**
		 * \brief Set virtual plane lower right position.
		 * \version 1.26
		 */
		func void setLowerRight(Vector position)
			if position == null
				throw ENullPointer.new()
			end
			pLowerRight = position
		end
		
		/** \brief Virtual plane upper left position. */
		func Vector getUpperLeft()
			return pUpperLeft
		end
		
		/**
		 * \brief Set virtual plane upper left position.
		 * \version 1.26
		 */
		func void setUpperLeft(Vector position)
			if position == null
				throw ENullPointer.new()
			end
			pUpperLeft = position
		end
		
		/** \brief Bone name or empty string if not set. */
		func String getBone()
			return pBone
		end
		
		/**
		 * \brief Set bone name or empty string if not set.
		 * \version 1.26
		 */
		func void setBone(String bone)
			if bone == null
				throw ENullPointer.new()
			end
			pBone = bone
		end
		
		/** \brief Virtual plane matrix in world space. */
		func DMatrix getWorldMatrix()
			var DMatrix matrix = getElement().getElementMatrix()
			if not pBone.empty()
				matrix = pRenderableDesktop.getDynamicSkin().getComponent() \
					.getComponent().getBoneMatrix(pBone).normalize().toDMatrix() * matrix
			end
			return matrix
		end
		
		/** \brief Virtual plane lower left position in world space. */
		func DVector getWorldLowerLeft()
			return getWorldMatrix() * DVector.new(pLowerLeft)
		end
		
		/** \brief Virtual plane lower right position in world space. */
		func DVector getWorldLowerRight()
			return getWorldMatrix() * DVector.new(pLowerRight)
		end
		
		/** \brief Virtual plane upper left position in world space. */
		func DVector getWorldUpperLeft()
			return getWorldMatrix() * DVector.new(pUpperLeft)
		end
		
		
		
		/**
		 * \brief Calculate virtual plane coordinates for world position or null if outside.
		 * 
		 * If the world position is not on the virtual plane the position is projected down
		 * to the virtual plane along the virtual plane normal.
		 * 
		 * The virtual plane coordinates map the lower left corner to (0,0), the lower right
		 * corner to (1,0) and the upper left corner to (0,1).
		 */
		func Vector2 planePosition(DVector worldPosition)
			var Vector2 position = planePositionEverywhere(worldPosition)
			
			/*
			// this code would reject points behind the screen
			var DVector axisZ = (axisY.normalize() % axisX.normalize()).normalize()
			if localPosition * axisZ < -0.001
				return null
			end
			*/
			
			if isInsidePlane(position)
				return position
			end
			return null
		end
		
		/**
		 * \brief Determines if virtual plane coordinates are inside virtual plane.
		 * 
		 * Typically used with position returned by planePositionEverywhere(). The position
		 * returned by planePosition() is guaranteed to be inside the virtual plane.
		 * 
		 * Returns true if position is inside the inclusive range from (0,0) to (1,1).
		 */
		func bool isInsidePlane(Vector2 position)
			return position >= Vector2.new() and position <= Vector2.new(1, 1)
		end
		
		/**
		 * \brief Calculate virtual plane coordinates for world position.
		 * 
		 * Same as planePosition() but always returns also plane positions outside the virtual
		 * plane. This is useful if the player simulated a mouse click event but not yet a
		 * mouse released event. Tracking the virtual plane position outside the virtual plane
		 * allows then to provide a reasonable mouse position until the mouse release event
		 * is simulated
		 */
		func Vector2 planePositionEverywhere(DVector worldPosition)
			var DMatrix matrix = getWorldMatrix()
			var DVector lowerLeft = matrix * DVector.new(pLowerLeft)
			var DVector lowerRight = matrix * DVector.new(pLowerRight)
			var DVector upperLeft = matrix * DVector.new(pUpperLeft)
			var DVector localPosition = worldPosition - lowerLeft
			
			var DVector axisX = lowerRight - lowerLeft
			var DVector axisY = upperLeft - lowerLeft
			return Vector2.new(\
				localPosition * (axisX / axisX.getLengthSquared()), \
				localPosition * (axisY / axisY.getLengthSquared()))
		end
		
		/**
		 * \brief Calculate virtual plane coordinates for world ray intersection with plan.
		 * \version 1.9
		 * 
		 * Calculates the intersection point of the ray with the plane. Returns position
		 * similar to planePositionEverywhere without clamping.
		 */
		func Vector2 planePositionRay(DVector origin, DVector direction)
			var DMatrix matrix = getWorldMatrix()
			
			var DVector lowerLeft = matrix * DVector.new(pLowerLeft)
			var DVector lowerRight = matrix * DVector.new(pLowerRight)
			var DVector upperLeft = matrix * DVector.new(pUpperLeft)
			
			var DVector axisX = lowerRight - lowerLeft
			var DVector axisY = upperLeft - lowerLeft
			
			var DVector axisZ = (axisY.normalize() % axisX.normalize()).normalize()
			var float denom = axisZ * direction
			if DEMath.fabs(denom) < 0.0001
				return planePositionEverywhere(origin)
			end
			
			var float numer = axisZ * (origin - lowerLeft)
			var float distance = -(numer / denom)
			
			var DVector localPosition = origin + direction * distance - lowerLeft
			
			return Vector2.new(\
				localPosition * (axisX / axisX.getLengthSquared()), \
				localPosition * (axisY / axisY.getLengthSquared()))
		end
		
		/**
		 * \brief Calculate desktop position from virtual plane coordinates.
		 * 
		 * Virtual plane Y axis runs from bottom (0) to top(1). Desktop Y axis runs from
		 * top (0) to bottom (height-1).
		 * 
		 * Returned position is clamped to desktop size.
		 */
		func Point planeToDesktop(Vector2 position)
			var Point size = pRenderableDesktop.getDesktop().getSize()
			return Vector2.new(position.getX() * size.getX(), (1.0 - position.getY()) * size.getY()) \
				.round().smallest(size - Point.new(1, 1)).largest(Point.new())
		end
		
		/**
		 * \brief Calculate desktop position for world position or null if outside.
		 * 
		 * Convenience method for calling planePosition() then planeToDesktop() if
		 * virtual plane position is not null.
		 */
		func Point desktopPoint(DVector worldPosition)
			var Vector2 position = planePosition(worldPosition)
			if position == null
				return null
			end
			return planeToDesktop(position)
		end
		
		/**
		 * \brief Calculate desktop position for world position.
		 * 
		 * Same as desktopPoint() but returns desktop position also if the world position
		 * is outside the virtual plane. The returned position is clamped to the desktop size.
		 */
		func Point desktopPointEverywhere(DVector worldPosition)
			return planeToDesktop(planePositionEverywhere(worldPosition))
		end
		
		/**
		 * \brief Calculate desktop position for world ray intersection.
		 * \version 1.9
		 * 
		 * Same as desktopPoint() but returns desktop position also if the world position
		 * is outside the virtual plane. The returned position is clamped to the desktop size.
		 */
		func Point desktopPointRay(DVector origin, DVector direction)
			return planeToDesktop(planePositionRay(origin, direction))
		end
		
		/** \brief Move desktop mouse to new position. */
		func void moveMouse(Point position)
			pRenderableDesktop.getDesktop().warpMousePointer(position)
		end
		
		/**
		 * \brief Move mouse position if world position is inside virtual plane.
		 * 
		 * Convenience method for calling desktopPoint() then moveMouse() if desktop
		 * point is not null.
		 * 
		 * Returns true if the world position is inside the virtual plane.
		 */
		func bool moveMouse(DVector worldPosition)
			var Point point = desktopPoint(worldPosition)
			if point == null
				return false
			end
			moveMouse(point)
			return true
		end
		
		/**
		 * \brief Move mouse position.
		 * 
		 * Same as moveMouse(DVector) but applies also if world position is outside virtual plane.
		 */
		func void moveMouseEverywhere(DVector worldPosition)
			moveMouse(desktopPointEverywhere(worldPosition))
		end
		
		/**
		 * \brief Move mouse position.
		 * \version 1.9
		 * 
		 * Same as moveMouse(DVector) but applies also if world position is outside virtual plane.
		 */
		func void moveMouseRay(DVector origin, DVector direction)
			moveMouse(desktopPointRay(origin, direction))
		end
		
		/**
		 * \brief Simulate mouse button click on desktop at current desktop mouse location.
		 * 
		 * Sends mouse pressed event immediately followed by a mouse release event.
		 * 
		 * \param button Button to click. Value from InputEventMouseButton.
		 */
		func void mouseClick(int button)
			var Desktop desktop = pRenderableDesktop.getDesktop()
			
			desktop.processEvent(MouseEvent.new(desktop, MouseEvent.idMousePressed, \
				button, InputEventModifier.none, -1))
			
			desktop.processEvent(MouseEvent.new(desktop, MouseEvent.idMouseReleased, \
				button, InputEventModifier.none, -1))
		end
		
		/**
		 * \brief Simulate left mouse button click on desktop at current desktop mouse location.
		 * 
		 * Sends mouse pressed event immediately followed by a mouse release event.
		 */
		func void leftMouseClick()
			mouseClick(InputEventMouseButton.left)
		end
		
		/**
		 * \brief Simulate mouse button press on desktop at current desktop mouse location.
		 * \param button Button to press. Value from InputEventMouseButton.
		 */
		func void mousePress(int button)
			var Desktop desktop = pRenderableDesktop.getDesktop()
			
			desktop.processEvent(MouseEvent.new(desktop, MouseEvent.idMousePressed, \
				button, InputEventModifier.none, -1))
		end
		
		/**
		 * \brief Simulate left mouse button press on desktop at current desktop mouse location.
		 */
		func void leftMousePress()
			mousePress(InputEventMouseButton.left)
		end
		
		/**
		 * \brief Simulate mouse button release on desktop at current desktop mouse location.
		 * \param button Button to release. Value from InputEventMouseButton.
		 */
		func void mouseRelease(int button)
			var Desktop desktop = pRenderableDesktop.getDesktop()
			
			desktop.processEvent(MouseEvent.new(desktop, MouseEvent.idMouseReleased, \
				button, InputEventModifier.none, -1))
		end
		
		/**
		 * \brief Simulate left mouse button release on desktop at current desktop mouse location.
		 */
		func void leftMouseRelease()
			mouseRelease(InputEventMouseButton.left)
		end
	end
	
	
	
	var ECBehaviorRenderableDesktop pRenderableDesktop
	var ECPVector pLowerLeft
	var ECPVector pLowerRight
	var ECPVector pUpperLeft
	var ECPString pBone
	
	
	
	/**
	 * \brief Create behavior element class.
	 */
	func new(BehaviorElementClass eclass, ECBehaviorRenderableDesktop renderableDesktop) \
	this(eclass, renderableDesktop, "")
	end
	
	func new(BehaviorElementClass eclass, ECBehaviorRenderableDesktop renderableDesktop, String id) \
	this(eclass, renderableDesktop, id, id.empty() if "controlDesktop." else "controlDesktop(" + id + ").")
	end
	
	func new(BehaviorElementClass eclass, ECBehaviorRenderableDesktop renderableDesktop, \
	String id, String prefix) super(eclass, id)
		if renderableDesktop == null
			throw ENullPointer.new("renderableDesktop")
		end
		
		pRenderableDesktop = renderableDesktop
		
		pLowerLeft = ECPVector.new(prefix + "lowerLeft", Vector.new(-0.5, -0.5, 0))
		eclass.addProperty(pLowerLeft)
		
		pLowerRight = ECPVector.new(prefix + "lowerRight", Vector.new(0.5, -0.5, 0))
		eclass.addProperty(pLowerRight)
		
		pUpperLeft = ECPVector.new(prefix + "upperLeft", Vector.new(-0.5, 0.5, 0))
		eclass.addProperty(pUpperLeft)
		
		pBone = ECPString.new(prefix + "bone", "")
		eclass.addProperty(pBone)
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	func void dispose()
		pRenderableDesktop = null
		super.dispose()
	end
	
	
	
	/** \brief Renderable desktop behavior. */
	func ECBehaviorRenderableDesktop getRenderableDesktop()
		return pRenderableDesktop
	end
	
	/** \brief Virtual plane lower left position. */
	func ECPVector getLowerLeft()
		return pLowerLeft
	end
	
	/** \brief Virtual plane lower right position. */
	func ECPVector getLowerRight()
		return pLowerRight
	end
	
	/** \brief Virtual plane upper left position. */
	func ECPVector getUpperLeft()
		return pUpperLeft
	end
	
	/** \brief Bone name or empty string to not use. */
	func ECPString getBone()
		return pBone
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorComponent.
	 */
	static func ECBehaviorControlDesktop getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorControlDesktop each
			return true
		end) cast ECBehaviorControlDesktop
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorControlDesktop with specific identifier.
	 */
	static func ECBehaviorControlDesktop getBehaviorIn(BehaviorElementClass eclass, String id)
		return eclass.findBehavior(block ECBehaviorControlDesktop each
			return each.getID().equals(id)
		end) cast ECBehaviorControlDesktop
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorControlDesktop behaviors in the order they have
	 * been created in the element.
	 */
	static func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBehaviorControlDesktop each
			return true
		end)
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorControlDesktop.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorControlDesktop.Instance with specific identifier.
	 */
	static func Instance getInstanceIn(BehaviorElement element, String id)
		return element.findInstance(block Instance each
			return each.getECBehavior().getID().equals(id)
		end) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorControlDesktop.Instance instances in the order they have
	 * been created in the element.
	 */
	static func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** \brief Create Behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
