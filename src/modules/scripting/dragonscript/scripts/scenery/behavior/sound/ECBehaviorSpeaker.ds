/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.TriggerSystem
pin Dragengine.BehaviorTreeSystem


/**
 * \brief Behavior element behavior adding speaker support.
 * 
 * Behavior adds a Speaker resource to the the behavior element. This behavior can be added
 * multiple times to an element. Each instance creates one speaker attached to the element
 * collider which can be individually modified. To distinguish the speakers each instance
 * has an identifier which can be used to retrieve a specific instance. Hence to use more
 * than one speaker use code like this in your subclass constructor:
 * 
 * \code{.ds}
 * class MyElement extends BehaviorElementClass
 *    func new()
 *       ECBehaviorSpeaker.new(this)
 *       ECBehaviorSpeaker.new(this, "subSpeaker")
 *    end
 * end
 * \endcode
 * 
 * You can now define the parameters to use for both speakers using for example the properties
 * "speaker.volume" for the first speaker and "speaker(subSpeaker).volume" for the second speaker.
 * 
 * It is recommended to always specify an identifier even if only one speaker is used.
 * This avoids potentially name conflicts especially if other behaviors are added attaching
 * other resources too.
 * 
 * If the \em ECBehaviorCollider behavior is present in the behavior element before this
 * behavior is added the speaker is attached. The speaker is attached to the named bone if
 * defined otherwise it is attached statically.
 * 
 * Element class properties have the prefix "speaker{(id)}.".
 * 
 * This behavior does require the element class to be persistable (setPersistable).
 * 
 * 
 * \par Synthesizer Handling
 * 
 * Speakers can play either a static Sound resource or use a Synthesizer. By using a synthesizer
 * dynamic sound can be played. To use a synthesizer use the "speaker{(id)}.synthesizer" property
 * instead of the "speaker{(id)}.sound" property. If both properties are present the synthesizer
 * one is used if valid otherwise sound.
 * 
 * If synthesizer is valid a SynthesizerInstance will be created. Subclasses of ECBehaviorSpeaker
 * or other behaviors can then obtain SynthesizerController from the SynthesizerInstance to
 * modify the played sound. Since controllers use curves behaviors have to set the value ahead
 * of time before playing back the speaker. It is possible to change the controller curves while
 * sound is playing. In this case though care has to be taken to be set the curve early enough
 * since audio modules typically render sound into buffered blocks. If the curve is changed too
 * late the changes can be missed or result in strange sound jumping.
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_speaker
 */
class ECBehaviorSpeaker extends DefaultECBehavior
	/** \brief Listener for behavior instance events. */
	interface Listener
		/** \brief Start playing back. */
		func void startPlaying(Instance instance)
		
		/** \brief Stop playing. */
		func void stopPlaying(Instance instance)
		
		/** \brief Muted. */
		func void muted(Instance instance)
		
		/** \brief Unmuted. */
		func void unmuted(Instance instance)
	end
	
	/** \brief Default implementation of behavior instance listener. */
	class DefaultListener implements Listener
		/** \brief Create default listener. */
		func new()
		end
		
		/** \brief Start playing back. */
		func void startPlaying(Instance instance)
		end
		
		/** \brief Stop playing. */
		func void stopPlaying(Instance instance)
		end
		
		/** \brief Muted. */
		func void muted(Instance instance)
		end
		
		/** \brief Unmuted. */
		func void unmuted(Instance instance)
		end
	end
	
	/** \brief Factory creating listeners. */
	interface ListenerFactory
		/** \brief Create listener. */
		func Listener createListener(Instance instance)
	end
	
	/** \brief Factory creating listeners using block. */
	class BlockListenerFactory implements ListenerFactory
		var Block pBlock
		
		/** \brief Create factory using block receiving Instance as parameter returning Listener. */
		func new(Block blockFactory)
			if blockFactory == null
				throw ENullPointer.new()
			end
			pBlock = blockFactory
		end
		
		/** \brief Create listener. */
		func Listener createListener(Instance instance)
			return pBlock.run(instance) cast Listener
		end
	end
	
	/**
	 * \brief Behavior factory.
	 * \version 1.25
	 * 
	 * Automatically assigns these behaviors if present before adding:
	 * - Optional: \ref ECBehaviorCollider
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorSpeaker'>
	 *   <!-- optional: set layer mask (default '2' meaning BaseGameApp.WorldLayerBit.audio).
	 *                  list of bits to set. -->
	 *   <string name='layerMask'>0 1 4</string>
	 * 
	 *   <!-- optional: use BaseGameApp trigger table. game can add more supported values -->
	 *   <string name='triggerTable'>default</string>
	 *   
	 *   <!-- optional: identifier of ECBehaviorTriggered to synchronize with or empty
	 *                  string to not synchronize. default is empty string. -->
	 *   <string name='trigger.synchronize'>other</string>
	 *   
	 *   <!-- optional: identifier of ECBehaviorTriggered to synchronize with or empty
	 *                  string to not synchronize. default is empty string. -->
	 *   <string name='triggerMuted.synchronize'>other</string>
	 *   
	 *   <!-- optional: use behavior tree with id instead of empty string -->
	 *   <string name='behaviorTree'>second</string>
	 * </behavior>
	 * 
	 * <!-- for adding multiple behaviors use unique identifiers -->
	 * <behavior type='ECBehaviorSpeaker' id='second'>
	 *   ...
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for example ".propertyName").
	 */
	class Factory implements BehaviorFactory
		func new()
		end
		
		func String factoryId()
			return "ECBehaviorSpeaker"
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			var ECBehaviorSpeaker behavior = ECBehaviorSpeaker.new(eclass,\
				ECBehaviorCollider.getBehaviorIn(eclass), BaseGameApp.getApp().getLoaders(),\
				getTriggerTableFor(parameters.getAt("triggerTable", "default") cast String), id)
			
			if parameters.has("layerMask")
				behavior.getSpeaker().setLayerMask(ElementClass.getSharedCodecPropertyString().\
					decodeLayerMask(parameters.getAt("layerMask") cast String))
			end
			
			behavior.getTrigger().setSynchronize(eclass,\
				parameters.getAt("trigger.synchronize", null) cast String)
			
			behavior.getTriggerMuted().setSynchronize(eclass,\
				parameters.getAt("triggerMuted.synchronize", null) cast String)
			
			behavior.setBehaviorTree(eclass.sharedFactoryHelper.optionalBehaviorTree(eclass, parameters))
			
			eclass.sharedFactoryHelper.setPropertiesFromParameters(eclass, parameters, "speaker", id)
			return behavior
		end
		
		/**
		 * \brief Trigger table matching value.
		 * 
		 * Default implementation returns \ref BaseGameApp#getTriggerTable() for value 'default'
		 * and null for all other values. Overwrite to accept other values.
		 */
		func TSTriggerTable getTriggerTableFor(String value)
			return BehaviorElementClass.sharedFactoryHelper.triggerTable(value)
		end
	end
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/**
		 * \brief Behavior tree action "speaker{(id)}.set".
		 * \version 1.26
		 */
		class BTASet implements BTAction
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func BTResult runAction(BTContext context, Dictionary parameters)
				var Object value
				
				value = parameters.getAt("playing", null)
				if value != null
					pInstance.setPlaying(value.equals("true"))
				end
				
				value = parameters.getAt("muted", null)
				if value != null
					pInstance.setMuted(value.equals("true"))
				end
				
				return BTResult.success
			end
		end
		
		/**
		 * \brief Behavior tree action/condition "speaker{(id)}.check".
		 * \version 1.26
		 */
		class BTACheck implements BTAction, BTCondition
			var Instance pInstance
			var String pPrefix
			
			func new(Instance instance, String prefix)
				pInstance = instance
				pPrefix = prefix
			end
			
			func BTResult runAction(BTContext context, Dictionary parameters)
				return check("", parameters) if BTResult.success else BTResult.failure
			end
			
			func bool evaluateCondition(BTContext context, Dictionary parameters)
				return check(pPrefix, parameters)
			end
			
			func bool check(String prefix, Dictionary parameters)
				var bool result = true
				var Object value
				
				value = parameters.getAt(prefix + "playing", null)
				if value != null
					result &= pInstance.getPlaying() == value.equals("true")
				end
				
				value = parameters.getAt(prefix + "muted", null)
				if value != null
					result &= pInstance.getMuted() == value.equals("true")
				end
				
				return result
			end
		end
		
		/** \brief Trigger listener to change playing state of speaker. */
		public class PlayingListener extends ECBehaviorTriggered.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void triggerExpressionTrue(ECBehaviorTriggered.Instance instance)
				pInstance.setPlaying(true)
			end
			
			func void triggerExpressionFalse(ECBehaviorTriggered.Instance instance)
				pInstance.setPlaying(false)
			end
		end
		
		/** \brief Trigger listener to change muted state of speaker. */
		public class MuteListener extends ECBehaviorTriggered.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void triggerExpressionTrue(ECBehaviorTriggered.Instance instance)
				pInstance.setMuted(true)
			end
			
			func void triggerExpressionFalse(ECBehaviorTriggered.Instance instance)
				pInstance.setMuted(false)
			end
		end
		
		var ECBehaviorSpeaker pECBehavior
		var ECBehaviorCollider.Instance pCollider
		var bool pPlaying
		var bool pMuted
		var Speaker pSpeaker
		var ECBehaviorTriggered.Instance pTrigger
		var ECBehaviorTriggered.Instance pTriggerMuted
		var Array pListeners
		
		
		
		/** \brief Create element. */
		func new(ECBehaviorSpeaker ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pListeners = Array.new()
			pPlaying = false
			pMuted = false
		end
		
		/** \brief Dispose of behavior instance. */
		func void dispose()
			if pSpeaker != null
				pSpeaker.setOwner(null)
				pSpeaker = null
			end
			pTrigger = null
			pTriggerMuted = null
			pCollider = null
			pListeners = null
			
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		func void init(StubElement stub)
			var BehaviorElement element = getElement()
			
			if pECBehavior.getCollider() != null
				pCollider = pECBehavior.getCollider().instance(element)
			end
			
			pSpeaker = createSpeaker(stub)
			if pSpeaker == null
				return
			end
			
			pECBehavior.createListeners(this)
			
			pSpeaker.setOwner(element)
			
			if pECBehavior.getTriggerTable() != null
				pTrigger = getTriggerBehavior()
				pTrigger.addListener(PlayingListener.new(this))
				
				pTriggerMuted = getTriggerMutedBehavior()
				pTriggerMuted.addListener(MuteListener.new(this))
			end
			
			if pTriggerMuted == null or pTriggerMuted.getTriggerExpression() == null
				setMuted(pSpeaker.getMuted())
			end
			
			if pTrigger != null and pTrigger.getTriggerExpression() != null
				pSpeaker.stop() // required because of createSpeaker() starting playing
				pPlaying = false
				
			else
				setPlaying(pSpeaker.getPlaying())
			end
			
			if pCollider != null and pCollider.getCollider() != null
				attachSpeaker(stub, pCollider)
			end
			
			if pECBehavior.getBehaviorTree() != null
				var String pnp = pECBehavior.getPrefix()
				var BTContext c = pECBehavior.getBehaviorTree().instance(element).getContext()
				c.setAction(pnp + "set", BTASet.new(this))
				
				var String name = pnp + "check"
				var BTACheck btacheck = BTACheck.new(this, pnp)
				c.setAction(name, btacheck)
				c.setCondition(name, btacheck)
			end
		end
		
		/**
		 * \brief Create speaker.
		 * 
		 * Default implementation calls ECComposeSpeaker.createSpeaker().
		 */
		protected func Speaker createSpeaker(StubElement stub)
			return pECBehavior.getSpeaker().createSpeaker(getElement(), stub)
		end
		
		/**
		 * \brief Get playing trigger behavior.
		 * 
		 * Default implementation uses owner class trigger behavior. If getSyncTrigger()
		 * of parent ECBehaviorSpeaker is not null uses referenced behavior trigger behavior.
		 */
		protected func ECBehaviorTriggered.Instance getTriggerBehavior()
			var ECBehaviorSpeaker behavior = pECBehavior.getSyncTrigger()
			var BehaviorElement element = getElement()
			
			if behavior != null
				return behavior.instance(element).getTrigger()
				
			else
				return pECBehavior.getTrigger().synchronizedOrSelf().instance(element)
			end
		end
		
		/**
		 * \brief Get muted trigger behavior.
		 * 
		 * Default implementation uses owner class trigger behavior. If getSyncTrigger()
		 * of parent ECBehaviorSpeaker is not null uses referenced behavior trigger behavior.
		 */
		protected func ECBehaviorTriggered.Instance getTriggerMutedBehavior()
			var ECBehaviorSpeaker behavior = pECBehavior.getSyncTrigger()
			var BehaviorElement element = getElement()
			
			if behavior != null
				return behavior.instance(element).getTriggerMuted()
				
			else
				return pECBehavior.getTriggerMuted().synchronizedOrSelf().instance(element)
			end
		end
		
		/**
		 * \brief Attach speaker to collider.
		 * 
		 * Default implementation calls ECComposeAttachResource.attach().
		 */
		protected func void attachSpeaker(StubElement stub, ECBehaviorCollider.Instance collider)
			pECBehavior.getAttach().attach(collider.getCollider(), pSpeaker, stub)
		end
		
		/**
		 * \brief Create speaker layer mask.
		 * 
		 * Default implementation calls ECComposeSpeaker.createLayerMask.
		 */
		/*protected func LayerMask createLayerMask(StubElement stub)
			return pECBehavior.getSpeaker().createLayerMask(stub)
		end*/
		
		
		
		/** \brief Behavior. */
		func ECBehaviorSpeaker getECBehavior()
			return pECBehavior
		end
		
		/** \brief Speaker. */
		func Speaker getSpeaker()
			return pSpeaker
		end
		
		/** \brief Synthesizer instance or \em null if not used. */
		func SynthesizerInstance getSynthesizerInstance()
			return pSpeaker.getSynthesizer()
		end
		
		/** \brief Snthesizer controller or \em null if not used. */
		func SynthesizerController getSynthesizerControllerAt(int index)
			var SynthesizerInstance synthInst = pSpeaker.getSynthesizer()
			if synthInst != null
				return synthInst.getControllerAt(index)
			end
			return null
		end
		
		/** \brief Named snthesizer controller or \em null if not used. */
		func SynthesizerController getSynthesizerControllerNamed(String name)
			var SynthesizerInstance synthInst = pSpeaker.getSynthesizer()
			if synthInst != null
				return synthInst.getControllerNamed(name)
			end
			return null
		end
		
		
		
		/** \brief Speaker is playing. */
		func bool getPlaying()
			return pPlaying
		end
		
		/** \brief Set if speaker is playing. */
		func void setPlaying(bool playing)
			if playing == pPlaying
				return
			end
			
			pPlaying = playing
			
			if playing
				pSpeaker.play()
				pListeners.forEach(block Listener each
					each.startPlaying(this)
				end)
				
			elif pSpeaker.getLooping()
				pSpeaker.stop()
				pListeners.forEach(block Listener each
					each.stopPlaying(this)
				end)
			end
		end
		
		/** \brief Start playing. */
		func void play()
			setPlaying(true)
		end
		
		/** \brief Stop playing. */
		func void stop()
			setPlaying(false)
		end
		
		/** \brief Toggle speaker playing state. */
		func void toggle()
			setPlaying(not pPlaying)
		end
		
		
		
		/** \brief Speaker is muted. */
		func bool getMuted()
			return pMuted
		end
		
		/** \brief Set if speaker is muted. */
		func void setMuted(bool muted)
			if muted == pMuted
				return
			end
			
			pMuted = muted
			
			pSpeaker.setMuted(muted and getElement().getVisible())
			
			if muted
				pListeners.forEach(block Listener each
					each.muted(this)
				end)
				
			else
				pListeners.forEach(block Listener each
					each.unmuted(this)
				end)
			end
		end
		
		/** \brief Toggle speaker muted state. */
		func void toggleMuted()
			setMuted(not pMuted)
		end
		
		
		
		/** \brief Playing speaker trigger expression or \em null if not used. */
		func ECBehaviorTriggered.Instance getTrigger()
			return pTrigger
		end
		
		/** \brief Muted speaker trigger expression or \em null if not used. */
		func ECBehaviorTriggered.Instance getTriggerMuted()
			return pTriggerMuted
		end
		
		
		
		/** \brief Add listener. */
		func void addListener(Listener listener)
			if listener == null
				throw EInvalidParam.new()
			end
			pListeners.add(listener)
		end
		
		/** \brief Remove listener. */
		func void removeListener(Listener listener)
			pListeners.remove(listener)
		end
		
		
		
		/** \brief State changed (visble, touchable). */
		func void stateChanged()
			if pSpeaker != null
				pSpeaker.setMuted(pMuted and getElement().getVisible())
			end
		end
		
		/** \brief Add element to game world. */
		func void addToGameWorld()
			getElement().getGameWorld().getWorld().addSpeaker(pSpeaker)
		end
		
		/** \brief Remove element from game world. */
		func void removeFromGameWorld()
			getElement().getGameWorld().getWorld().removeSpeaker(pSpeaker)
		end
		
		
		
		/** \brief Read element from file. */
		func void readFromFile(PersistencyEnvironment env, FileReader reader)
			select reader.readByte() // version
			case 0
				var PersistencyFlags flags = PersistencyFlags.new(2, reader)
				var bool playing = flags.getAt(0)
				var bool muted = flags.getAt(1)
				
				setMuted(muted)
				setPlaying(playing)
				
			else
				throw EInvalidParam.new("unsupported version")
			end
		end
		
		/** Writes the element to a file. */
		func void writeToFile(PersistencyEnvironment env, FileWriter writer)
			writer.writeByte(0) // version
			
			var PersistencyFlags flags = PersistencyFlags.new(2)
			flags.setAt(0, pPlaying)
			flags.setAt(1, pMuted)
			flags.writeToFile(writer)
		end
	end
	
	
	
	var ECBehaviorCollider pCollider
	var ECComposeSpeaker pSpeaker
	var ECComposeAttachResource pAttach
	var ECBehaviorTriggered pTrigger
	var ECBehaviorTriggered pTriggerMuted
	var ECBehaviorSpeaker pSyncTrigger
	var TSTriggerTable pTriggerTable
	var Array pListenerFactories
	var ECBehaviorBehaviorTree pBehaviorTree
	var String pPrefix
	
	
	
	/**
	 * \brief Create behavior element class.
	 * 
	 * These value are optional and can be null: \em collider.
	 */
	func new(BehaviorElementClass eclass, ECBehaviorCollider collider) \
	this(eclass, collider, BaseGameApp.getApp().getLoaders(), null)
	end
	
	/** \brief Create behavior element class. */
	func new(BehaviorElementClass eclass, ECBehaviorCollider collider, String id) \
	this(eclass, collider, BaseGameApp.getApp().getLoaders(), null, id)
	end
	
	/** \brief Create behavior element class. */
	func new(BehaviorElementClass eclass, ECBehaviorCollider collider, Loaders loaders, \
	TSTriggerTable triggerTable) this(eclass, collider, loaders, triggerTable, "")
	end
	
	/** \brief Create behavior element class. */
	func new(BehaviorElementClass eclass, ECBehaviorCollider collider, Loaders loaders, \
	TSTriggerTable triggerTable, String id) this(eclass, collider, loaders, triggerTable, \
	id, id.empty() if "speaker" else "speaker(" + id + ")")
	end
	
	func new(BehaviorElementClass eclass, ECBehaviorCollider collider, \
	Loaders loaders, TSTriggerTable triggerTable, String id, String subID) \
	this(eclass, collider, loaders, triggerTable, id, subID, subID + ".")
	end
	
	func new(BehaviorElementClass eclass, ECBehaviorCollider collider, Loaders loaders, \
	TSTriggerTable triggerTable, String id, String subID, String prefix) super(eclass, id)
		pPrefix = prefix
		pCollider = collider
		pTriggerTable = triggerTable
		pListenerFactories = Array.new()
		
		pSpeaker = ECComposeSpeaker.new(eclass, loaders, prefix)
		pAttach = ECComposeAttachResource.new(eclass, prefix)
		
		pTrigger = ECBehaviorTriggered.new(eclass, triggerTable, subID, prefix + "trigger")
		
		pTriggerMuted = ECBehaviorTriggered.new(eclass, triggerTable, \
			prefix + "muted", prefix + "trigger(muted)")
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	func void dispose()
		pCollider = null
		pSyncTrigger = null
		pListenerFactories = null
		pBehaviorTree = null
		super.dispose()
	end
	
	
	
	/**
	 * \brief Prefix.
	 * \version 1.26
	 */
	func String getPrefix()
		return pPrefix
	end
	
	/** \brief Collider behavior or null to not attach. */
	func ECBehaviorCollider getCollider()
		return pCollider
	end
	
	/** \brief Composeable speaker for element class. */
	func ECComposeSpeaker getSpeaker()
		return pSpeaker
	end
	
	/** \brief Attach resource. */
	func ECComposeAttachResource getAttach()
		return pAttach
	end
	
	/** \brief Trigger playing. */
	func ECBehaviorTriggered getTrigger()
		return pTrigger
	end
	
	/** \brief Trigger muted. */
	func ECBehaviorTriggered getTriggerMuted()
		return pTriggerMuted
	end
	
	/** \brief ECBehaviorSpeaker to synchronize triggers with or \em null to disable. */
	func ECBehaviorSpeaker getSyncTrigger()
		return pSyncTrigger
	end
	
	/** \brief Set ECBehaviorSpeaker to synchronize triggers with or \em null to disable. */
	func void setSyncTrigger(ECBehaviorSpeaker behavior)
		pSyncTrigger = behavior
	end
	
	/** \brief Trigger table or \em null if not used. */
	func TSTriggerTable getTriggerTable()
		return pTriggerTable
	end
	
	/** \brief Set trigger table or \em null if not used. */
	protected func void setTriggerTable(Loaders loaders, TSTriggerTable triggerTable)
		pTriggerTable = triggerTable
	end
	
	/**
	 * \brief Behavior tree or null.
	 * \version 1.26
	 */
	func ECBehaviorBehaviorTree getBehaviorTree()
		return pBehaviorTree
	end
	
	/**
	 * \brief Set behavior tree or null.
	 * \version 1.26
	 */
	func void setBehaviorTree(ECBehaviorBehaviorTree behaviorTree)
		pBehaviorTree = behaviorTree
	end
	
	/** \brief Add listener factory. */
	func void addListenerFactory(ListenerFactory factory)
		if factory == null
			throw ENullPointer.new()
		end
		pListenerFactories.add(factory)
	end
	
	/**
	 * \brief Add listener factory using block.
	 * 
	 * Block receives as parameter Instance and returns Listener.
	 */
	func void addListenerFactory(Block blockFactory)
		pListenerFactories.add(BlockListenerFactory.new(blockFactory))
	end
	
	/** \brief Visit listener factories with block with argument ListenerFactory. */
	func void forEachListenerFactory(Block ablock)
		pListenerFactories.forEach(ablock)
	end
	
	/** \brief Create listeners from factories adding them to behavior instance. */
	func void createListeners(Instance instance)
		pListenerFactories.forEach(block ListenerFactory each
			instance.addListener(each.createListener(instance))
		end)
	end
	
	
	
	/**
	 * \brief Set to point speaker with parameters.
	 * 
	 * Helper method to quickly set up playing sound.
	 */
	func void set(String sound, float range, float volume)
		set(sound, range, volume, Vector.new(), Vector.new(), "")
	end
	
	func void set(String sound, float range, float volume, Vector position)
		set(sound, range, volume, position, Vector.new(), "")
	end
	
	func void set(String sound, float range, float volume, Vector position, \
	Vector orientation)
		set(sound, range, volume, position, orientation, "")
	end
	
	func void set(String sound, float range, float volume, Vector position, \
	Vector orientation, String bone)
		pSpeaker.getSpeakerType().setValue(SpeakerType.point)
		pSpeaker.getSound().setPath(sound)
		pSpeaker.getRange().setValue(range)
		pSpeaker.getVolume().setValue(volume)
		pSpeaker.getRollOff().setValue(1)
		pSpeaker.getDistanceOffset().setValue(0)
		pSpeaker.getPlaySpeed().setValue(1)
		pAttach.getPosition().setVector(position)
		pAttach.getOrientation().setVector(orientation)
		pAttach.getBone().setValue(bone)
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorCollider.
	 */
	static func ECBehaviorSpeaker getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorSpeaker each
			return true
		end) cast ECBehaviorSpeaker
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorSpeaker with specific identifier.
	 */
	static func ECBehaviorSpeaker getBehaviorIn(BehaviorElementClass eclass, String id)
		return eclass.findBehavior(block ECBehaviorSpeaker each
			return each.getID().equals(id)
		end) cast ECBehaviorSpeaker
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorSpeaker behaviors in the order they have
	 * been created in the element.
	 */
	static func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBehaviorSpeaker each
			return true
		end)
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorSpeaker.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorSpeaker.Instance with specific identifier.
	 */
	static func Instance getInstanceIn(BehaviorElement element, String id)
		return element.findInstance(block Instance each
			return each.getECBehavior().getID().equals(id)
		end) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorSpeaker.Instance instances in the order they have
	 * been created in the element.
	 */
	static func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** \brief Create Behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
