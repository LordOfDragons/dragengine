/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.BehaviorTreeSystem


/**
 * \brief Behavior element behavior adding outline support to components.
 * 
 * Renders outline around a component.
 * 
 * This is done by creating a new component resource. The model and rig of the existing
 * component is assigned to the new component. The new component resource is attached to
 * an existing collider using rig attachment mode. This makes the new component deform
 * the same way as the existing component. The new component is thus a kind of "copy"
 * of the existing component.
 * 
 * Then an outline skin is assigned to the new component and all textures it contains.
 * This replaces every texture in the new component with the outline skin.
 * 
 * The outline skin is set by the user. To work properly it has to use the "outline.*"
 * skin texture properties to render the outline and "solidity" with value "0" to hide
 * everything but the outline. A typical outline skin looks like this:
 * 
 * \code{.xml}
 * <?xml version='1.0' encoding='UTF-8'?>
 * <skin>
 *   <texture name='material'>
 *     <value property='solidity'>0</value>
 *     <color property='outline.color' r='0' g='0' b='0' a='1' renderable='color'/>
 *     <value property='outline.thickness' renderable='thickness'>0.005</value>
 *     <value property='outline.thickness.screen'>1</value>
 *     <color property='outline.emissivity' r='0' g='0' b='0' a='1' renderable='color'/>
 *     <value property='outline.emissivity.intensity' renderable='intensity'>4</value>
 *   </texture>
 * </skin>
 * \endcode
 * 
 * By default the skin "/shareddata/materials/outlined.deskin" is used which is provided
 * by the shared DragonScript Module data and provides tintable and emissive outlines.
 * 
 * The emissivity is optional and allows to make the outline glow in the dark. Using
 * renderable "color" is also optional but recommended. This allows this behavior to
 * change the outline color using a dynamic skin.
 * 
 * This set up allows to add an outline skin to any ECBehaviorComponent without the need
 * to create skins with built-in support for outline rendering. If you want to use outline
 * for all objects by default better build it into their skins which is faster.
 * 
 * This behavior can be also used to add outer skins in general to an ECBehaviorComponent.
 * You have to adjust the outline skin to achieve the desired result.
 * 
 * This is an effect type behavior and starts out invisible. Use setVisible() to switch
 * the outline on and off. Typically this is done in response to game events or by other
 * behaviors managing outline parameters.
 * 
 * Multiple instances of ECBehaviorOutline can be used for example to create different
 * outlines to switch on and off or to add multiple outline skins to a single
 * ECBehaviorComponent. Keep in mind though that each instance of ECBehaviorOutline creates
 * a new component and dynamic skin resource which can impact performance if the used model
 * has high polygon count. To use multiple instances use code like this in your subclass
 * constructor:
 * 
 * \code{.ds}
 * class MyElement extends BehaviorElementClass
 *    public var ECBehaviorComponent component
 *    public var ECBehaviorCollider collider
 *    
 *    func new()
 *       component = ECBehaviorComponent.new(this)
 *       collider = ECBehaviorCollider.new(this, component)
 *       ECBehaviorOutline.new(this, component, collider)
 *       ECBehaviorOutline.new(this, component, collider, "secondOutline")
 *    end
 * end
 * \endcode
 * 
 * Element class properties have the prefix "outline{(id)}.".
 * 
 * This behavior does support element class to be persistable (setPersistable). Saves color,
 * intensity and visiblity state.
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_outline
 */
class ECBehaviorOutline extends DefaultECBehavior
	/**
	 * \brief Behavior factory.
	 * \version 1.25
	 * 
	 * Automatically assigns these behaviors if present before adding:
	 * - Required: \ref ECBehaviorComponent
	 * - Required: \ref ECBehaviorCollider
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorOutline'>
	 *   <!-- optional: use component with id instead of empty string -->
	 *   <string name='component'>second</string>
	 *   
	 *   <!-- optional: use behavior tree with id instead of empty string -->
	 *   <string name='behaviorTree'>second</string>
	 * </behavior>
	 * 
	 * <!-- for adding multiple behaviors use unique identifiers -->
	 * <behavior type='ECBehaviorOutline' id='second'>
	 *   ...
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for example ".propertyName").
	 */
	class Factory implements BehaviorFactory
		func new()
		end
		
		func String factoryId()
			return "ECBehaviorOutline"
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			var ECBehaviorOutline behavior = ECBehaviorOutline.new(eclass,\
				eclass.sharedFactoryHelper.defaultComponent(eclass, parameters),\
				ECBehaviorCollider.getBehaviorIn(eclass), id)
			
			behavior.setBehaviorTree(eclass.sharedFactoryHelper.optionalBehaviorTree(eclass, parameters))
			
			BehaviorElementClass.sharedFactoryHelper.setPropertiesFromParameters(\
				eclass, parameters, "outline", id)
			return behavior
		end
	end
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/**
		 * \brief Behavior tree action "outline{(id)}.set".
		 * \version 1.26
		 */
		class BTASet implements BTAction
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func BTResult runAction(BTContext context, Dictionary parameters)
				var Object value
				
				value = parameters.getAt("visible", null)
				if value != null
					pInstance.setVisible(value.equals("true"))
				end
				
				value = parameters.getAt("color", null)
				if value != null
					pInstance.setColor(pInstance.getElement().getClass().\
						getCodecPropertyString().decodeColor3(value cast String))
				end
				
				value = parameters.getAt("intensity", null)
				if value != null
					pInstance.setIntensity((value cast String).toFloat())
				end
				
				value = parameters.getAt("thickness", null)
				if value != null
					pInstance.setThickness((value cast String).toFloat())
				end
				
				return BTResult.success
			end
		end
		
		/**
		 * \brief Behavior tree action/condition "outline{(id)}.check".
		 * \version 1.26
		 */
		class BTACheck implements BTAction, BTCondition
			var Instance pInstance
			var String pPrefix
			
			func new(Instance instance, String prefix)
				pInstance = instance
				pPrefix = prefix
			end
			
			func BTResult runAction(BTContext context, Dictionary parameters)
				return check("", parameters) if BTResult.success else BTResult.failure
			end
			
			func bool evaluateCondition(BTContext context, Dictionary parameters)
				return check(pPrefix, parameters)
			end
			
			func bool check(String prefix, Dictionary parameters)
				var Object value
				
				value = parameters.getAt(prefix + "visible", null)
				if value != null and pInstance.getVisible() != value.equals("true")
					return false
				end
				
				return true
			end
		end
		
		
		var ECBehaviorOutline pECBehavior
		var Component pComponent
		var DynamicSkin pDynamicSkin
		var Skin pSkin
		var Color pColor
		var float pIntensity
		var float pThickness
		var bool pVisible
		
		
		
		/** \brief Create element. */
		func new(ECBehaviorOutline ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
		end
		
		
		
		/** \brief Initialize element from stub. */
		func void init(StubElement stub)
			var BehaviorElement element = getElement()
			var ElementClass eclass = element.getClass()
			var CodecPropertyString codec = eclass.getSharedCodecPropertyString()
			
			var Component component = pECBehavior.getComponent().instance(element).getComponent()
			pSkin = pECBehavior.getSkin().getSkin(stub, eclass)
			pColor = pECBehavior.getColor().getColor(stub, codec)
			pIntensity = pECBehavior.getIntensity().getValue(stub)
			pThickness = pECBehavior.getThickness().getValue(stub)
			
			var Model model = pECBehavior.getModel().getModel(stub, eclass)
			if model == null
				model = component.getModel()
			end
			
			var Rig rig = pECBehavior.getRig().getRig(stub, eclass)
			if rig == null
				rig = component.getRig()
			end
			
			pComponent = Component.new(model, pSkin)
			pComponent.setRig(rig)
			pComponent.setLayerMask(component.getLayerMask())
			pComponent.setVisible(pVisible)
			pComponent.setPosition(component.getPosition())
			pComponent.setOrientation(component.getOrientation())
			pComponent.setScaling(component.getScaling())
			
			pDynamicSkin = DynamicSkin.new()
			pDynamicSkin.addRenderable("color", DynamicSkinRenderableType.color)
			pDynamicSkin.setColorAt(0, pColor)
			pDynamicSkin.addRenderable("intensity", DynamicSkinRenderableType.value)
			pDynamicSkin.setValueAt(1, pIntensity)
			pDynamicSkin.addRenderable("thickness", DynamicSkinRenderableType.value)
			pDynamicSkin.setValueAt(2, pThickness)
			
			pComponent.setDynamicSkin(pDynamicSkin)
			
			var int i, textureCount = pComponent.getTextureCount()
			for i = 0 to textureCount
				pComponent.setTextureSkinAt(i, pSkin, 0)
			end
			
			if pECBehavior.getCollider() != null
				var Collider collider = pECBehavior.getCollider().instance(element).getCollider()
				if collider != null
					if collider castable ColliderRig
						(collider cast ColliderRig).attachRig(pComponent)
						
					else
						collider.attachStatic(pComponent, Vector.new(), Quaternion.new())
					end
				end
			end
			
			if pECBehavior.getBehaviorTree() != null
				var String pnp = pECBehavior.getPropertyNamePrefix()
				var BTContext c = pECBehavior.getBehaviorTree().instance(element).getContext()
				c.setAction(pnp + "set", BTASet.new(this))
				
				var String name = pnp + "check"
				var BTACheck btacheck = BTACheck.new(this, pnp)
				c.setAction(name, btacheck)
				c.setCondition(name, btacheck)
			end
		end
		
		
		
		/** \brief Behavior. */
		func ECBehaviorOutline getECBehavior()
			return pECBehavior
		end
		
		/** \brief Outline component. */
		func Component getComponent()
			return pComponent
		end
		
		/** \brief Dynamic skin. */
		func DynamicSkin getDynamicSkin()
			return pDynamicSkin
		end
		
		/** \brief Outline skin. */
		func Skin getSkin()
			return pSkin
		end
		
		/** \brief Set outline skin. */
		func void setSkin(Skin skin)
			pComponent.setSkin(skin)
			
			var int i, textureCount = pComponent.getTextureCount()
			for i = 0 to textureCount
				pComponent.setTextureSkinAt(i, skin, 0)
			end
		end
		
		/** \brief Outline color. */
		func Color getColor()
			return pColor
		end
		
		/** \brief Set outline color. */
		func void setColor(Color color)
			if color == null
				throw ENullPointer.new()
			end
			
			pColor = color
			pDynamicSkin.setColorAt(0, color)
		end
		
		/** \brief Outline emissivity intensity. */
		func float getIntensity()
			return pIntensity
		end
		
		/** \brief Set outline emissivity intensity. */
		func void setIntensity(float intensity)
			pIntensity = DEMath.max(intensity, 0.0)
			pDynamicSkin.setValueAt(1, pIntensity)
		end
		
		/** \brief Outline emissivity thickness. */
		func float getThickness()
			return pThickness
		end
		
		/** \brief Set outline emissivity thickness. */
		func void setThickness(float thickness)
			pThickness = DEMath.max(thickness, 0.0)
			pDynamicSkin.setValueAt(2, pThickness)
		end
		
		/** \brief Outline is visible. */
		func bool getVisible()
			return pVisible
		end
		
		/** \brief Set if outline is visible. */
		func void setVisible(bool visible)
			pVisible = visible
			pComponent.setVisible(visible and getElement().getVisible())
		end
		
		
		
		/** \brief State changed (visble, touchable). */
		func void stateChanged()
			if pComponent != null
				pComponent.setVisible(pVisible and getElement().getVisible())
			end
		end
		
		/** \brief Add element to game world. */
		func void addToGameWorld()
			if pComponent != null
				getElement().getGameWorld().getWorld().addComponent(pComponent)
			end
		end
		
		/** \brief Remove element from game world. */
		func void removeFromGameWorld()
			if pComponent != null
				getElement().getGameWorld().getWorld().removeComponent(pComponent)
			end
		end
		
		
		
		/** \brief Read behavior from file. */
		func void readFromFile(PersistencyEnvironment env, FileReader reader)
			select reader.readByte() // version
			case 0
				var PersistencyFlags flags = PersistencyFlags.new(1, reader)
				setColor(Color.readFromFile(reader))
				setIntensity(reader.readFloat())
				setThickness(reader.readFloat())
				setVisible(flags.getAt(0))
				
			else
				throw EInvalidParam.new("unsupported version")
			end
		end
		
		/** \brief Write behavior to file. */
		func void writeToFile(PersistencyEnvironment env, FileWriter writer)
			writer.writeByte(0) // version
			
			var PersistencyFlags flags = PersistencyFlags.new(1)
			flags.setAt(0, pVisible)
			flags.writeToFile(writer)
			
			pColor.writeToFile(writer)
			writer.writeFloat(pIntensity)
			writer.writeFloat(pThickness)
		end
	end
	
	
	
	var ECBehaviorComponent pComponent
	var ECBehaviorCollider pCollider
	var ECPSkin pSkin
	var ECPModel pModel
	var ECPRig pRig
	var ECPColor pColor
	var ECPFloat pIntensity
	var ECPFloat pThickness
	var ECBehaviorBehaviorTree pBehaviorTree
	var String pPropertyNamePrefix
	
	
	
	/**
	 * \brief Create behavior element class.
	 */
	func new(BehaviorElementClass eclass, ECBehaviorComponent component, \
	ECBehaviorCollider collider) this(eclass, component, collider, "")
	end
	
	func new(BehaviorElementClass eclass, ECBehaviorComponent component, \
	ECBehaviorCollider collider, String id) super(eclass, id)
		if component == null
			throw ENullPointer.new("compoent")
		end
		if collider == null
			throw ENullPointer.new("collider")
		end
		
		var String prefix = id.empty() if "outline." else "outline(" + id + ")."
		
		pPropertyNamePrefix = prefix
		pComponent = component
		pCollider = collider
		
		eclass.addProperty(pSkin = ECPSkin.new(prefix + "skin", "/shareddata/materials/outlined.deskin"))
		eclass.addProperty(pModel = ECPModel.new(prefix + "model"))
		eclass.addProperty(pRig = ECPRig.new(prefix + "rig"))
		eclass.addProperty(pColor = ECPColor.new(prefix + "color", Color.black))
		eclass.addProperty(pIntensity = ECPFloat.new(prefix + "intensity", 0, 0))
		eclass.addProperty(pThickness = ECPFloat.new(prefix + "thickness", 0.005, 0))
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	func void dispose()
		pCollider = null
		pComponent = null
		pBehaviorTree = null
		super.dispose()
	end
	
	
	
	/**
	 * \brief Property name prefix.
	 * \version 1.26
	 */
	func String getPropertyNamePrefix()
		return pPropertyNamePrefix
	end
	
	/** \brief Component behavior. */
	func ECBehaviorComponent getComponent()
		return pComponent
	end
	
	/** \brief Collider behavior. */
	func ECBehaviorCollider getCollider()
		return pCollider
	end
	
	/** \brief Skin property. */
	func ECPSkin getSkin()
		return pSkin
	end
	
	/** \brief Model property. */
	func ECPModel getModel()
		return pModel
	end
	
	/** \brief Rig property. */
	func ECPRig getRig()
		return pRig
	end
	
	/** \brief Color property. */
	func ECPColor getColor()
		return pColor
	end
	
	/** \brief Intensity property. */
	func ECPFloat getIntensity()
		return pIntensity
	end
	
	/** \brief THickness property. */
	func ECPFloat getThickness()
		return pThickness
	end
	
	/**
	 * \brief Behavior tree or null.
	 * \version 1.26
	 */
	func ECBehaviorBehaviorTree getBehaviorTree()
		return pBehaviorTree
	end
	
	/**
	 * \brief Set behavior tree or null.
	 * \version 1.26
	 */
	func void setBehaviorTree(ECBehaviorBehaviorTree behaviorTree)
		pBehaviorTree = behaviorTree
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorCollider.
	 */
	static func ECBehaviorOutline getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorOutline each
			return true
		end) cast ECBehaviorOutline
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorOutline with specific identifier.
	 */
	static func ECBehaviorOutline getBehaviorIn(BehaviorElementClass eclass, String id)
		return eclass.findBehavior(block ECBehaviorOutline each
			return each.getID().equals(id)
		end) cast ECBehaviorOutline
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorOutline behaviors in the order they have
	 * been created in the element.
	 */
	static func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBehaviorOutline each
			return true
		end)
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorOutline.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorOutline.Instance with specific identifier.
	 */
	static func Instance getInstanceIn(BehaviorElement element, String id)
		return element.findInstance(block Instance each
			return each.getECBehavior().getID().equals(id)
		end) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorOutline.Instance instances in the order they have
	 * been created in the element.
	 */
	static func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** \brief Create Behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
