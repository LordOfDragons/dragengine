/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.ConversationSystem
pin Dragengine.Preloading
pin Dragengine.Utils


/**
 * \brief Behavior element behavior adding conversation coordinate support.
 * 
 * Instance adds a ElementConvoCoordSystem to the game world the element is added to. These can
 * be added to conversation playbacks as targets for actions. Multiple ECBehaviorConvoCoordSystem
 * can be added creating individual ElementConvoCoordSystem instances. The ElementConvoCoordSystem
 * returns the position relative to the element if queried.
 * 
 * The default implementation registers the ElementConvoCoordSystem in the conversation
 * coordinate system list of the GameWorld the element is added to. If name is empty string
 * no convo coordinate system is created.
 * 
 * This behavior does require the element class to be persistable (setPersistable).
 */
class ECBehaviorConvoCoordSystem extends DefaultECBehavior
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		private var ECBehaviorConvoCoordSystem pECBehavior
		private var ElementConvoCoordSystem pConvoCoordSystem
		
		
		
		/** \brief Create element. */
		public func new( ECBehaviorConvoCoordSystem ecbehavior, BehaviorElement element ) super( element )
			this.pECBehavior = ecbehavior
		end
		
		/** \brief Dispose of composeable instance. */
		public func void dispose()
			this.pConvoCoordSystem = null
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		public func void init( StubElement stub )
			var StringID name = this.pECBehavior.getName().getValue( stub )
			if name == null
				return
			end
			
			var CodecPropertyString codec = this.getElement().getClass().getCodecPropertyString()
			var Vector position = this.pECBehavior.getPosition().getVector( stub, codec )
			var Vector orientation = this.pECBehavior.getOrientation().getVector( stub, codec )
			
			this.pConvoCoordSystem = this.createCoordSystem( stub, name, position, orientation )
		end
		
		/**
		 * \brief Create conversation coordinate system.
		 * 
		 * Default implementation creates instance of ElementConvoCoordSystem.
		 */
		protected func ElementConvoCoordSystem createCoordSystem( StubElement stub, \
		StringID name, Vector position, Vector orientation )
			return ElementConvoCoordSystem.new( name, \
				DMatrix.newRT( DVector.new( orientation ), DVector.new( position ) ), \
				this.getElement() )
		end
		
		
		
		/** \brief Behavior. */
		public func ECBehaviorConvoCoordSystem getECBehavior()
			return this.pECBehavior
		end
		
		/** \brief Conversation coordinate system or \em null. */
		public func ElementConvoCoordSystem getConvoCoordSystem()
			return this.pConvoCoordSystem
		end
		
		
		
		/** \brief Add element to game world. */
		public func void addToGameWorld()
			if this.pConvoCoordSystem != null
				this.getElement().getGameWorld().getConvoCoordSystemList().add( this.pConvoCoordSystem )
			end
		end
		
		/** \brief Remove element from game world. */
		public func void removeFromGameWorld()
			if this.pConvoCoordSystem != null
				this.getElement().getGameWorld().getConvoCoordSystemList().remove( this.pConvoCoordSystem.getID() )
			end
		end
	end
	
	
	
	private var ECComposeAnimator pAnimator
	private var ECPStringID pName
	private var ECPVector pPosition
	private var ECPVector pOrientation
	
	
	
	/** \brief Create behavior element class. */
	public func new( BehaviorElementClass eclass ) this( eclass, "" )
	end
	
	/** \brief Create behavior element class. */
	public func new( BehaviorElementClass eclass, String id ) \
	this( eclass, id, id.empty() if "convoCoordSystem." else "convoCoordSystem(" + id + ")." )
	end
	
	public func new( BehaviorElementClass eclass, String id, String prefix ) super( eclass, id )
		this.pName = ECPStringID.new( prefix + "name" )
		eclass.addProperty( this.pName )
		
		this.pPosition = ECPVector.new( prefix + "position" )
		eclass.addProperty( this.pPosition )
		
		this.pOrientation = ECPVector.new( prefix + "orientation" )
		eclass.addProperty( this.pOrientation )
		
		eclass.addBehavior( this )
	end
	
	
	
	/** \brief Name of conversation coordinate system. */
	public func ECPStringID getName()
		return this.pName
	end
	
	/** \brief Position relative to behavior element. */
	public func ECPVector getPosition()
		return this.pPosition
	end
	
	/** \brief Orientation relative to behavior element. */
	public func ECPVector getOrientation()
		return this.pOrientation
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	public func Instance instance( BehaviorElement element )
		return element.getInstanceAt( this.getInstanceIndex() ) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorComponent.
	 */
	static public func ECBehaviorConvoCoordSystem getBehaviorIn( BehaviorElementClass eclass )
		return eclass.findBehavior( block ECBehavior each
			return each castable ECBehaviorConvoCoordSystem
		end ) cast ECBehaviorConvoCoordSystem
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorConvoCoordSystem with specific identifier.
	 */
	static public func ECBehaviorConvoCoordSystem getBehaviorIn( BehaviorElementClass eclass, String id )
		return eclass.findBehavior( block ECBehaviorConvoCoordSystem each
			return each castable ECBehaviorConvoCoordSystem and \
				( each cast ECBehaviorConvoCoordSystem ).getID().equals( id )
		end ) cast ECBehaviorConvoCoordSystem
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorConvoCoordSystem behaviors in the order they have
	 * been created in the element.
	 */
	static public func Array getAllBehaviorsIn( BehaviorElementClass eclass )
		var Array list = Array.new()
		eclass.forEachBehavior( block ECBehavior each
			if each castable ECBehaviorConvoCoordSystem
				list.add( each )
			end
		end )
		return list
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorConvoCoordSystem.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static public func Instance getInstanceIn( BehaviorElement element )
		return element.findInstance( block ECBehaviorInstance each
			return each castable Instance
		end ) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorConvoCoordSystem.Instance with specific identifier.
	 */
	static public func Instance getInstanceIn( BehaviorElement element, String id )
		return element.findInstance( block ECBehaviorInstance each
			return each castable Instance and \
				( each cast Instance ).getECBehavior().getID().equals( id )
		end ) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorConvoCoordSystem.Instance instances in the order they have
	 * been created in the element.
	 */
	static public func Array getAllInstancesIn( BehaviorElement element )
		var Array list = Array.new()
		element.forEachInstance( block ECBehaviorInstance each
			if each castable Instance
				list.add( each )
			end
		end )
		return list
	end
	
	
	
	/** \brief Create Behavior instance. */
	public func ECBehaviorInstance createInstance( BehaviorElement element )
		return Instance.new( this, element )
	end
end
