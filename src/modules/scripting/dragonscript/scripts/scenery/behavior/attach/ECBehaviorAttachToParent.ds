/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.Scenery


/**
 * \brief Behavior element behavior adding attach to parent support.
 * 
 * Objects can be attached to parent objects in the IGDE World Editor. During enterFrame()
 * BehaviorElement sets the target object as the parent object. The actual attaching needs
 * knowledge about the target object. This is delegated to behavior instances knowing how
 * to do the attaching.
 * 
 * This behavior knows how to attach to BehaviorElement instances. By default the element to
 * attach to is expected to a BehaviorElement. The element will be casted to BehaviorElement
 * and attachToBehaviorElement() called. Optionally an ElementVisitor can be set which
 * disables the default behavior and visits the element instead. This is useful for complex
 * situations where even non-BehaviorElement elements can be used.
 * 
 * If the stub property "attachToParent.attachSlot" is used the element is attached to the
 * named target ECBehaviorAttachSlot. This attach method can only be used if these
 * requirements are fulfilled:
 * <ul>
 * <li>The stub property name "attachToParent.attachSlot" is present</li>
 * <li>This element has ECBehaviorAttachable.</li>
 * <li>The target element has ECBehaviorAttachSlot with matching name.</li>
 * </ul>
 * If any of these requirements are not fulfilled an exception will be thrown.
 * 
 * If target element contains ECBehaviorCollider regular attaching is used.
 * 
 * An optional "bone" property is supported to allow attaching the element to a specific
 * bone in the target object ECBehaviorCollider given the collider is a ColliderRig or
 * ColliderComponent instance.
 * 
 * If no "bone" property is used and both colliders are ColliderRig or ColliderComponent
 * and both used rigs have bones then rig attachment is used. This is typically used to
 * attach cloth to actors copying the actor bone states to the cloth bone states.
 * 
 * In all other cases static attaching is used.
 * 
 * Element class properties have the prefix "attachToParent.".
 * 
 * This behavior requires ECBehaviorCollider to be present.
 * 
 * This element behavior can be present only once in a BehaviorElement. Trying to add more
 * than one instance results in an exception thrown.
 * 
 * This behavior does not require the element class to be persistable (setPersistable).
 */
class ECBehaviorAttachToParent extends DefaultECBehavior
	/**
	 * \brief Behavior factory.
	 * \ref 1.25
	 * 
	 * Automatically assigns these behaviors if present before adding:
	 * - Required: \ref ECBehaviorCollider
	 * - Optional: \ref ECBehaviorAttachable
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorCamera'>
	 *   <!-- optional: set layer mask (default empty). list of bits to set. -->
	 *   <string name='layerMask'>0 1 4</string>
	 * </behavior>
	 * \endcode
	 */
	class Factory implements BehaviorFactory
		func new()
		end
		
		func String factoryId()
			return "ECBehaviorAttachToParent"
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			if not id.empty()
				throw EInvalidParam.new("id is not empty string")
			end
			return ECBehaviorAttachToParent.new(eclass, ECBehaviorCollider.getBehaviorIn(eclass),\
				ECBehaviorAttachable.getBehaviorIn(eclass))
		end
	end
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/** \brief Example attach element visitor. */
		public class DefaultAttachElement extends ElementVisitor
			private var Instance pInstance
			
			public func new(Instance instance)
				pInstance = instance
			end
			
			public func void visitBehaviorElement(BehaviorElement element)
				pInstance.attachToBehaviorElement(element)
			end
		end
		
		private var ECBehaviorAttachToParent pECBehavior
		private var ECBehaviorCollider.Instance pCollider
		private var ECBehaviorAttachable.Instance pAttachable
		private var ElementVisitor pAttachVisitor
		private var String pBone
		private var String pAttachSlot
		private var bool pRelative
		private var bool pRigMode
		private var Vector pPosition
		private var Quaternion pOrientation
		
		
		
		/** \brief Create element. */
		public func new(ECBehaviorAttachToParent ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			
			pCollider = ecbehavior.getCollider().instance(element)
		end
		
		/** \brief Dispose of behavior instance. */
		public func void dispose()
			pAttachVisitor = null
			pCollider = null
			pAttachable = null
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		public func void init(StubElement stub)
			if pECBehavior.getAttachable() != null
				pAttachable = pECBehavior.getAttachable().instance(getElement())
			end
			
			var CodecPropertyString codec = getElement().getClass().getCodecPropertyString()
			
			pBone = pECBehavior.getBone().getValue(stub)
			pAttachSlot = pECBehavior.getAttachSlot().getValue(stub)
			pRelative = pECBehavior.getRelative().getValue(stub)
			pRigMode = pECBehavior.getRigMode().getValue(stub)
			pPosition = pECBehavior.getPosition().getVector(stub, codec)
			pOrientation = Quaternion.newFromEuler(\
				pECBehavior.getOrientation().getVector(stub, codec))
		end
		
		
		
		/** \brief Behavior. */
		public func ECBehaviorAttachToParent getECBehavior()
			return pECBehavior
		end
		
		/** \brief Collider behavior. */
		public func ECBehaviorCollider.Instance getCollider()
			return pCollider
		end
		
		/** \brief Attachable behavior. */
		public func ECBehaviorAttachable.Instance getAttachable()
			return pAttachable
		end
		
		/** \brief Name of bone to attach to or empty string to attach statically. */
		public func String getBone()
			return pBone
		end
		
		/** \brief Attach slot to attach to. */
		public func String getAttachSlot()
			return pAttachSlot
		end
		
		/** \brief Attach relative to world position or using explicit position. */
		public func bool getRelative()
			return pRelative
		end
		
		/** \brief Attach using rig mode if supported by colliders. */
		public func bool getRigMode()
			return pRigMode
		end
		
		/** \brief Explicit attach position. */
		public func Vector getPosition()
			return pPosition
		end
		
		/** \brief Explicit attach rotation. */
		public func Quaternion getOrientation()
			return pOrientation
		end
		
		/** \brief Element visitor used to attach elements or \em null to use default behavior. */
		protected func ElementVisitor getAttachVisitor()
			return pAttachVisitor
		end
		
		/** \brief Set element visitor used to attach elements or \em null to use default behavior. */
		protected func void setAttachVisitor(ElementVisitor visitor)
			pAttachVisitor = visitor
		end
		
		
		
		/**
		 * \brief Attach element to object set by the mapper in the IGDE World Editor.
		 * 
		 * Behavior instance is responsible to call BehaviorElement.setParent() if the
		 * attaching succeedes. If multiple behaviors support attaching they have to negotiate
		 * with each other who does the attaching for example by using BehaviorElement.getParent().
		 * 
		 * Implementation either visits the element using getAttachVisitor() or by calling
		 * attachToBehaviorElement() if the element is castable to BehaviorElement.
		 * 
		 * Furthermore If the parent has ECBehaviorAttachments behavior add element as attachment.
		 * This is not done if special attaching is used like ECBehaviorAttachSlot.
		 */
		public func void attachToParent(Element parent)
			if pCollider.getCollider() == null
				return
			end
			
			if pAttachVisitor != null
				parent.visit(pAttachVisitor)
				
			elif parent castable BehaviorElement
				attachToBehaviorElement(parent cast BehaviorElement)
			end
		end
		
		/**
		 * \brief Attach to behavior element.
		 * 
		 * Calls attachToCollider() if behavior element has ECBehaviorCollider. Overwrite in
		 * subclass if you use only behavior elements and you do not want to use custom visitor.
		 */
		public func void attachToBehaviorElement(BehaviorElement parent)
			if pAttachSlot != null
				attachToAttachSlot(parent)
				return
			end
			
			if pCollider.getCollider() == null
				return
			end
			
			var ECBehaviorCollider.Instance parentCollider = ECBehaviorCollider.getInstanceIn(parent)
			if parentCollider == null or parentCollider.getCollider() == null
				return
			end
			
			attachToCollider(parentCollider.getCollider())
			getElement().setParent(parent)
			addToAttachments(parent)
		end
		
		/**
		 * \brief Attach to collider.
		 * 
		 * If collider is castable to ColliderRig and bone is not null the element is attach
		 * to the collider bone. Otherwise the element is attached statically.
		 */
		public func void attachToCollider(Collider collider)
			var Collider thisCollider = pCollider.getCollider()
			
			if collider castable ColliderRig
				var ColliderRig colliderRig = collider cast ColliderRig
				
				if pBone.empty()
					if pRigMode and colliderRig.getRig() != null \
					and colliderRig.getRig().getBoneCount() > 0 and thisCollider castable ColliderRig
						var ColliderRig thisColliderRig = thisCollider cast ColliderRig
						if thisColliderRig.getRig() != null and thisColliderRig.getRig().getBoneCount() > 0
							colliderRig.attachRig(thisColliderRig)
							return
						end
					end
					
				else
					if pRelative
						colliderRig.attachBone(thisCollider, pBone)
						
					else
						colliderRig.attachBone(thisCollider, pBone, pPosition, pOrientation)
					end
					return
				end
			end
			
			if pRelative
				collider.attachStatic(thisCollider)
				
			else
				collider.attachStatic(thisCollider, pPosition, pOrientation)
			end
		end
		
		/**
		 * \brief Attach to attach slot in world coordinates.
		 * 
		 * Element has to be in the correct position before attaching.
		 */
		public func void attachToAttachSlot(BehaviorElement parent)
			// ECBehaviorAttachSlot calls BehaviorElement.setParent() if successfull
			if pRelative
				ECBehaviorAttachSlot.getInstanceIn(parent, pAttachSlot).attachWorld(pAttachable)
				
			else
				ECBehaviorAttachSlot.getInstanceIn(parent, pAttachSlot) \
					.attach(pAttachable, pPosition, pOrientation)
			end
		end
		
		/**
		 * \brief Add as attachment to ECBehaviorAttachments if parent has this behavior.
		 */
		public func void addToAttachments(BehaviorElement parent)
			var ECBehaviorAttachments.Instance behavior = ECBehaviorAttachments.getInstanceIn(parent)
			if behavior != null
				behavior.addAttachment(getElement())
			end
		end
	end
	
	
	
	private var ECBehaviorCollider pCollider
	private var ECBehaviorAttachable pAttachable
	private var ECPString pBone
	private var ECPString pAttachSlot
	private var ECPBoolean pRelative
	private var ECPBoolean pRigMode
	private var ECPVector pPosition
	private var ECPVector pOrientation
	
	
	
	/**
	 * \brief Create behavior element class.
	 * 
	 * These values are optional and can be null: \em attachable.
	 */
	public func new(BehaviorElementClass eclass, ECBehaviorCollider collider, \
	ECBehaviorAttachable attachable) this(eclass, collider, attachable, "attachToParent.")
	end
	
	public func new(BehaviorElementClass eclass, ECBehaviorCollider collider, \
	ECBehaviorAttachable attachable, String prefix) super(eclass)
		if collider == null
			throw ENullPointer.new("collider")
		end
		if getBehaviorIn(eclass) != null
			throw BEMultipleInstanceException.new(eclass, this)
		end
		
		pCollider = collider
		pAttachable = attachable
		
		pBone = ECPString.new(prefix + "bone", "")
		eclass.addProperty(pBone)
		
		pAttachSlot = ECPString.new(prefix + "attachSlot", null)
		eclass.addProperty(pAttachSlot)
		
		pRelative = ECPBoolean.new(prefix + "relative", true)
		eclass.addProperty(pRelative)
		
		pRigMode = ECPBoolean.new(prefix + "rigMode", true)
		eclass.addProperty(pRigMode)
		
		pPosition = ECPVector.new(prefix + "position", Vector.new())
		eclass.addProperty(pPosition)
		
		pOrientation = ECPVector.new(prefix + "orientation", Vector.new())
		eclass.addProperty(pOrientation)
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	public func void dispose()
		pCollider = null
		pAttachable = null
		super.dispose()
	end
	
	
	
	/** \brief Collider behavior. */
	public func ECBehaviorCollider getCollider()
		return pCollider
	end
	
	/** \brief Attachable behavior or null if not used. */
	public func ECBehaviorAttachable getAttachable()
		return pAttachable
	end
	
	/** \brief Bone to attach to. */
	public func ECPString getBone()
		return pBone
	end
	
	/** \brief Attach slot to attach to. */
	public func ECPString getAttachSlot()
		return pAttachSlot
	end
	
	/** \brief Attach relative to world position or using explicit position. */
	public func ECPBoolean getRelative()
		return pRelative
	end
	
	/** \brief Attach using rig mode if supported by colliders. */
	public func ECPBoolean getRigMode()
		return pRigMode
	end
	
	/** \brief Explicit attach position. */
	public func ECPVector getPosition()
		return pPosition
	end
	
	/** \brief Explicit attach rotation. */
	public func ECPVector getOrientation()
		return pOrientation
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	public func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorCollider.
	 */
	static public func ECBehaviorAttachToParent getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorAttachToParent each
			return true
		end) cast ECBehaviorAttachToParent
	end
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorAttachToParent.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static public func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	
	
	/** \brief Create Behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
