/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.TriggerSystem


/**
 * \brief Behavior element behavior matching trigger target to touching sensor.
 * 
 * Fires and resets a trigger target depending if elements are in touch sensor.
 * Optionally pulses trigger target instead of just firing it.
 * 
 * Element class properties have the prefix "touching{(id)}.".
 * 
 * This behavior does not require the element class to be persistable (setPersistable).
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_touching
 */
class ECBehaviorTouching extends DefaultECBehavior
	/** \brief Listener for behavior instance events. */
	interface Listener
		/** \brief First element entered touch sensor. */
		func void startTouching( Instance instance )
		
		/** \brief Last element left touch sensor. */
		func void stopTouching( Instance instance )
	end
	
	/** \brief Default implementation of behavior instance listener. */
	class DefaultListener implements Listener
		/** \brief Create default listener. */
		public func new()
		end
		
		/** \brief First element entered touch sensor. */
		public func void startTouching( Instance instance )
		end
		
		/** \brief Last element left touch sensor. */
		public func void stopTouching( Instance instance )
		end
	end
	
	/** \brief Implementation of behavior instance listener using BooleanBehaviorListener. */
	class BooleanListener extends DefaultListener
		private var BooleanBehaviorListener pListener
		
		public func new( BooleanBehaviorListener listener )
			this.pListener = listener
		end
		
		public func void startTouching( Instance instance )
			this.pListener.enabled()
		end
		
		public func void stopTouching( Instance instance )
			this.pListener.disabled()
		end
	end
	
	/** \brief Factory creating listeners. */
	interface ListenerFactory
		/** \brief Create listener. */
		func Listener createListener( Instance instance )
	end
	
	/** \brief Factory creating listeners using block. */
	class BlockListenerFactory implements ListenerFactory
		private var Block pBlock
		
		/** \brief Create factory using block receiving Instance as parameter returning Listener. */
		public func new( Block blockFactory )
			if blockFactory == null
				throw ENullPointer.new()
			end
			this.pBlock = blockFactory
		end
		
		/** \brief Create listener. */
		public func Listener createListener( Instance instance )
			return this.pBlock.run( instance ) cast Listener
		end
	end
	
	/**
	 * \brief Behavior factory.
	 * \version 1.25
	 * 
	 * Automatically assigns these behaviors if present before adding:
	 * - Optional: \ref ECBehaviorCollider
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorTouching'>
	 *   <!-- optional: use BaseGameApp trigger table. game can add more supported values.
	 *                  default is 'default' -->
	 *   <string name='triggerTable'>default</string>
	 * </behavior>
	 * 
	 * <!-- for adding multiple behaviors use unique identifiers -->
	 * <behavior type='ECBehaviorTouching' id='second'>
	 *   ...
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for example ".propertyName").
	 */
	class Factory implements BehaviorFactory
		func new()
		end
		
		func String factoryId()
			return "ECBehaviorTouching"
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			var ECBehaviorTouching behavior = ECBehaviorTouching.new(eclass,\
				ECBehaviorCollider.getBehaviorIn(eclass),\
				getTriggerTableFor(parameters.getAt("triggerTable", "default") cast String), id)
			BehaviorElementClass.sharedFactoryHelper.setPropertiesFromParameters(\
				eclass, parameters, "touching", id)
			return behavior
		end
		
		/**
		 * \brief Trigger table matching value.
		 * 
		 * Default implementation returns \ref BaseGameApp#getTriggerTable() for value 'default'
		 * and null for all other values. Overwrite to accept other values.
		 */
		func TSTriggerTable getTriggerTableFor(String value)
			return BehaviorElementClass.sharedFactoryHelper.triggerTable(value)
		end
	end
	
	
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/** \brief Trigger listener. */
		class TouchListener extends ECBehaviorTouchSensor.DefaultListener
			private var Instance pInstance 
			
			public func new( Instance instance )
				this.pInstance = instance
			end
			
			public func void elementEntered( ECBehaviorTouchSensor.Instance instance, Element element )
				if instance.getTouchingElementCount() == 1
					this.pInstance.startTouching()
				end
			end
			
			public func void elementLeft( ECBehaviorTouchSensor.Instance instance, Element element )
				if instance.getTouchingElementCount() == 0
					this.pInstance.stopTouching()
				end
			end
			
			public func bool elementCanTouch( ECBehaviorTouchSensor.Instance instance, Element element )
				return true
			end
		end
		
		
		
		private var ECBehaviorTouching pECBehavior
		private var ECBehaviorTouchSensor.Instance pTouchSensor
		private var TSNamedTriggerTarget pTarget
		private var bool pPulse
		private var Array pListeners
		
		
		
		/** \brief Create element. */
		public func new( ECBehaviorTouching ecbehavior, BehaviorElement element ) super( element )
			this.pECBehavior = ecbehavior
			this.pPulse = false
			this.pListeners = Array.new()
		end
		
		/** \brief Dispose of composeable instance. */
		public func void dispose()
			this.pTarget = null
			this.pTouchSensor = null
			this.pListeners = null
			
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		public func void init( StubElement stub )
			if this.pECBehavior.getTriggerTable() == null
				return
			end
			
			this.pTouchSensor = this.pECBehavior.getTouchSensor().instance( this.getElement() )
			this.pTouchSensor.addListener( TouchListener.new( this ) )
			
			this.pTarget = TSNamedTriggerTarget.new( this.pECBehavior.getTarget().getValue( stub ) )
			this.pPulse = this.pECBehavior.getPulse().getValue( stub )
			
			this.pECBehavior.createListeners( this )
		end
		
		
		
		/** \brief Behavior. */
		public func ECBehaviorTouching getECBehavior()
			return this.pECBehavior
		end
		
		/** \brief Touch sensor or \em null. */
		public func ECBehaviorTouchSensor.Instance getTouchSensor()
			return this.pTouchSensor
		end
		
		/** \brief Trigger target or \em null. */
		public func TSNamedTriggerTarget getTarget()
			return this.pTarget
		end
		
		/** \brief Pulse trigger target instead of fire. */
		public func bool getPulse()
			return this.pPulse
		end
		
		
		
		/** \brief First element entered touch sensor. */
		public func void startTouching()
			this.fireTarget()
			
			this.pListeners.forEach( block Listener each
				each.startTouching( this )
			end )
		end
		
		/** \brief Last element left touch sensor. */
		public func void stopTouching()
			this.resetTarget()
			
			this.pListeners.forEach( block Listener each
				each.stopTouching( this )
			end )
		end
		
		
		
		/** \brief Add listener. */
		public func void addListener( Listener listener )
			if listener == null
				throw EInvalidParam.new()
			end
			this.pListeners.add( listener )
		end
		
		/** \brief Remove listener. */
		public func void removeListener( Listener listener )
			this.pListeners.remove( listener )
		end
		
		
		
		/** \brief Add element to game world. */
		public func void addToGameWorld()
			if this.pTarget == null
				return
			end
			
			this.pTarget.acquireTarget( this.pECBehavior.getTriggerTable() )
			
			// ECBehaviorTouchSensor.addToGameWorld() is called before this method is called.
			// if elements are touching right now startTouching() has been called firing pTarget.
			// at that time though pTarget has not acquired yet the target. ensure target is
			// properly fired if this is the case
			if this.pTouchSensor.getTouchingElementCount() > 0
				this.fireTarget()
			end
		end
		
		/** \brief Remove element from game world. */
		public func void removeFromGameWorld()
			if this.pTarget != null
				this.pTarget.dropTarget()
			end
		end
		
		
		
		/**
		 * \brief Fire target if not null.
		 * \version 1.6
		 */
		protected func void fireTarget()
			if this.pTarget != null
				if this.pPulse
					this.pTarget.pulse()
					
				else
					this.pTarget.fire()
				end
			end
		end
		
		/**
		 * \brief Reset target if not null.
		 * \version 1.6
		 */
		protected func void resetTarget()
			if this.pTarget != null
				this.pTarget.reset()
			end
		end
	end
	
	
	
	private var ECBehaviorTouchSensor pTouchSensor
	private var ECPString pTarget
	private var ECPBoolean pPulse
	private var TSTriggerTable pTouchSensorTable
	private var Array pListenerFactories
	
	
	
	/**
	 * \brief Create behavior element class.
	 * 
	 * These values are optional and can be null: \em null.
	 */
	public func new( BehaviorElementClass eclass, ECBehaviorCollider collider ) \
	this( eclass, collider, BaseGameApp.getApp().getTriggerTable(), "" )
	end
	
	public func new( BehaviorElementClass eclass, ECBehaviorCollider collider, String id ) \
	this( eclass, collider, BaseGameApp.getApp().getTriggerTable(), id )
	end
	
	public func new( BehaviorElementClass eclass, ECBehaviorCollider collider, \
	TSTriggerTable triggerTable ) this( eclass, collider, triggerTable, "" )
	end
	
	public func new( BehaviorElementClass eclass, ECBehaviorCollider collider, \
	TSTriggerTable triggerTable, String id ) this( eclass, collider, triggerTable, \
	id, id.empty() if "touching" else "touching(" + id + ")" )
	end
	
	public func new( BehaviorElementClass eclass, ECBehaviorCollider collider, \
	TSTriggerTable triggerTable, String id, String subID ) \
	this( eclass, collider, triggerTable, id, subID, subID + "." )
	end
	
	public func new( BehaviorElementClass eclass, ECBehaviorCollider collider, \
	TSTriggerTable triggerTable, String id, String subID, String prefix ) super( eclass, id )
		this.pTouchSensorTable = triggerTable
		this.pListenerFactories = Array.new()
		
		this.pTouchSensor = ECBehaviorTouchSensor.new( eclass, collider, subID, prefix + "touchSensor." )
		
		this.pTarget = ECPString.new( prefix + "target", null )
		eclass.addProperty( this.pTarget )
		
		this.pPulse = ECPBoolean.new( prefix + "pulse", false )
		eclass.addProperty( this.pPulse )
		
		eclass.addBehavior( this )
	end
	
	/** \brief Dispose of behavior. */
	public func void dispose()
		this.pListenerFactories = null
		super.dispose()
	end
	
	
	
	/** \brief Touch sensor. */
	public func ECBehaviorTouchSensor getTouchSensor()
		return this.pTouchSensor
	end
	
	/** \brief Trigger target to operate. */
	public func ECPString getTarget()
		return this.pTarget
	end
	
	/** \brief Pulse trigger target instead of fire it. */
	public func ECPBoolean getPulse()
		return this.pPulse
	end
	
	/** \brief Trigger table or \em null if not used. */
	public func TSTriggerTable getTriggerTable()
		return this.pTouchSensorTable
	end
	
	/** \brief Set trigger table or \em null if not used. */
	protected func void setTriggerTable( TSTriggerTable triggerTable )
		this.pTouchSensorTable = triggerTable
	end
	
	/** \brief Add listener factory. */
	public func void addListenerFactory( ListenerFactory factory )
		if factory == null
			throw ENullPointer.new()
		end
		this.pListenerFactories.add( factory )
	end
	
	/**
	 * \brief Add listener factory using block.
	 * 
	 * Block receives as parameter Instance and returns Listener.
	 */
	public func void addListenerFactory( Block blockFactory )
		this.pListenerFactories.add( BlockListenerFactory.new( blockFactory ) )
	end
	
	/** \brief Visit listener factories with block with argument ListenerFactory. */
	public func void forEachListenerFactory( Block ablock )
		this.pListenerFactories.forEach( ablock )
	end
	
	/** \brief Create listeners from factories adding them to behavior instance. */
	public func void createListeners( Instance instance )
		this.pListenerFactories.forEach( block ListenerFactory each
			instance.addListener( each.createListener( instance ) )
		end )
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	public func Instance instance( BehaviorElement element )
		return element.getInstanceAt( this.getInstanceIndex() ) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorComponent.
	 */
	static public func ECBehaviorTouching getBehaviorIn( BehaviorElementClass eclass )
		return eclass.findBehavior( block ECBehaviorTouching each
			return true
		end ) cast ECBehaviorTouching
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorTouching with specific identifier.
	 */
	static public func ECBehaviorTouching getBehaviorIn( BehaviorElementClass eclass, String id )
		return eclass.findBehavior( block ECBehaviorTouching each
			return each.getID().equals( id )
		end ) cast ECBehaviorTouching
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorTouching behaviors in the order they have
	 * been created in the element.
	 */
	static public func Array getAllBehaviorsIn( BehaviorElementClass eclass )
		return eclass.collectBehaviors( block ECBehaviorTouching each
			return true
		end )
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorTouching.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static public func Instance getInstanceIn( BehaviorElement element )
		return element.findInstance( block Instance each
			return true
		end ) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorTouching.Instance with specific identifier.
	 */
	static public func Instance getInstanceIn( BehaviorElement element, String id )
		return element.findInstance( block Instance each
			return each.getECBehavior().getID().equals( id )
		end ) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorTouching.Instance instances in the order they have
	 * been created in the element.
	 */
	static public func Array getAllInstancesIn( BehaviorElement element )
		return element.collectInstances( block Instance each
			return true
		end )
	end
	
	
	
	/** \brief Create Behavior instance. */
	public func ECBehaviorInstance createInstance( BehaviorElement element )
		return Instance.new( this, element )
	end
end
