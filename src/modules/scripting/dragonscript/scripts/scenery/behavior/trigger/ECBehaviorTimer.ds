/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.TriggerSystem
pin Dragengine.BehaviorTreeSystem
pin Dragengine.StateMachineSystem


/**
 * \brief Behavior element behavior timer trigger target.
 * 
 * Fires or resets trigger target after timeout after evaluation state of trigger expression
 * changes to true.
 * 
 * Element class properties have the prefix "timer{(id)}.".
 * 
 * This behavior does require the element class to be persistable (setPersistable).
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_timer
 */
class ECBehaviorTimer extends DefaultECBehavior
	/** \brief Listener for behavior instance events. */
	interface Listener
		/** \brief Timer started. */
		func void timerStarted(Instance instance)
		
		/** \brief Timer elapsed. */
		func void timerElapsed(Instance instance)
		
		/** \brief Timer cancelled. */
		func void timerCancelled(Instance instance)
	end
	
	/** \brief Default implementation of behavior instance listener. */
	class DefaultListener implements Listener
		/** \brief Create default listener. */
		func new()
		end
		
		/** \brief Timer started. */
		func void timerStarted(Instance instance)
		end
		
		/** \brief Timer elapsed. */
		func void timerElapsed(Instance instance)
		end
		
		/** \brief Timer cancelled. */
		func void timerCancelled(Instance instance)
		end
	end
	
	/** \brief Factory creating listeners. */
	interface ListenerFactory
		/** \brief Create listener. */
		func Listener createListener(Instance instance)
	end
	
	/** \brief Factory creating listeners using block. */
	class BlockListenerFactory implements ListenerFactory
		var Block pBlock
		
		/** \brief Create factory using block receiving Instance as parameter returning Listener. */
		func new(Block blockFactory)
			if blockFactory == null
				throw ENullPointer.new()
			end
			pBlock = blockFactory
		end
		
		/** \brief Create listener. */
		func Listener createListener(Instance instance)
			return pBlock.run(instance) cast Listener
		end
	end
	
	/**
	 * \brief Behavior factory.
	 * \version 1.25
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorTimer'>
	 *   <!-- optional: use BaseGameApp trigger table. game can add more supported values.
	 *                  default is 'default' -->
	 *   <string name='triggerTable'>default</string>
	 *   
	 *   <!-- optional: identifier of ECBehaviorTriggered to synchronize with or empty
	 *                  string to not synchronize. default is empty string. -->
	 *   <string name='trigger.synchronize'>other</string>
	 *   
	 *   <!-- optional: add behavior trees. default adds all behavior trees. -->
	 *   <list name='behaviorTrees'>
	 *     <string/> <!-- add behavior with empty identifier -->
	 *     <string>default</string> <!-- add behavior with 'default' identifier -->
	 *   </list>
	 * 
	 *   <!-- optional: add state machines. default adds all state machines. -->
	 *   <list name='stateMachines'>
	 *     <string/> <!-- add behavior with empty identifier -->
	 *     <string>default</string> <!-- add behavior with 'default' identifier -->
	 *   </list>
	 * </behavior>
	 * 
	 * <!-- for adding multiple behaviors use unique identifiers -->
	 * <behavior type='ECBehaviorTimer' id='second'>
	 *   ...
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for example ".propertyName").
	 */
	class Factory implements BehaviorFactory
		func new()
		end
		
		func String factoryId()
			return "ECBehaviorTimer"
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			var ECBehaviorTimer behavior = ECBehaviorTimer.new(eclass,\
				getTriggerTableFor(parameters.getAt("triggerTable", "default") cast String), id)
			
			behavior.getTriggered().setSynchronize(eclass,\
				parameters.getAt("trigger.synchronize", null) cast String)
			
			eclass.sharedFactoryHelper.allBTSM(behavior.getBTSMs(), eclass, parameters)
			
			eclass.sharedFactoryHelper.setPropertiesFromParameters(eclass, parameters, "timer", id)
			return behavior
		end
		
		/**
		 * \brief Trigger table matching value.
		 * 
		 * Default implementation returns \ref BaseGameApp#getTriggerTable() for value 'default'
		 * and null for all other values. Overwrite to accept other values.
		 */
		func TSTriggerTable getTriggerTableFor(String value)
			return BehaviorElementClass.sharedFactoryHelper.triggerTable(value)
		end
	end
	
	
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/**
		 * \brief Behavior tree action/condition "timer{(id)}.set".
		 * \version 1.26
		 */
		private class BTASet extends BTSMActionCondition
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func BTResult runActionShared(Object context, Dictionary parameters)
				var Object value
				
				value = parameters.getAt("target", null)
				if value != null
					if value.equals("fire")
						pInstance.getTarget().fire()
						
					elif value.equals("reset")
						pInstance.getTarget().reset()
						
					elif value.equals("fullReset")
						pInstance.getTarget().reset()
						
					else
						return BTResult.failure
					end
				end
				
				value = parameters.getAt("timer", null)
				if value != null
					if value.equals("arm")
						pInstance.armTimer()
						
					elif value.equals("cancel")
						pInstance.cancelTimer()
						
					elif value.equals("rearm")
						pInstance.cancelTimer()
						pInstance.armTimer()
						
					elif value.equals("timeout")
						pInstance.cancelTimer()
						pInstance.onTimeout()
						
					else
						return BTResult.failure
					end
				end
				
				return BTResult.success
			end
		end
		
		/**
		 * \brief Behavior tree action/condition "timer{(id)}.check".
		 * \version 1.26
		 */
		private class BTACheck extends BTSMActionCondition
			var Instance pInstance
			var String pPrefix
			
			func new(Instance instance, String prefix)
				pInstance = instance
				pPrefix = prefix
			end
			
			func BTResult runActionShared(Object context, Dictionary parameters)
				return check("", parameters) if BTResult.success\
					else (parameters.has("wait") if BTResult.running else BTResult.failure)
			end
			
			func bool evaluateConditionShared(Object context, Dictionary parameters)
				return check(pPrefix, parameters)
			end
			
			func bool check(String prefix, Dictionary parameters)
				var bool result = true
				var Object value
				
				value = parameters.getAt(prefix + "trigger", null)
				if value != null
					result &= pInstance.getTrigger().getState() == value.equals("true")
				end
				
				value = parameters.getAt(prefix + "target.fired", null)
				if value != null
					result &= pInstance.getTarget().getFired() == value.equals("true")
				end
				
				value = parameters.getAt(prefix + "target.everFired", null)
				if value != null
					result &= pInstance.getTarget().getHasFired() == value.equals("true")
				end
				
				value = parameters.getAt(prefix + "running", null)
				if value != null
					result &= pInstance.getTimerRunning() == value.equals("true")
				end
				
				value = parameters.getAt(prefix + "remaining.less", null)
				if value != null
					result &= pInstance.getRemainingTime() < (value cast String).toFloat()
				end
				
				value = parameters.getAt(prefix + "remaining.greater", null)
				if value != null
					result &= pInstance.getRemainingTime() > (value cast String).toFloat()
				end
				
				return result
			end
		end
		
		/** \brief Trigger listener. */
		private class TimerListener extends ECBehaviorTriggered.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void triggerExpressionTrue(ECBehaviorTriggered.Instance instance)
				pInstance.armTimer()
			end
			
			func void triggerExpressionFalse(ECBehaviorTriggered.Instance instance)
				if pInstance.getCancelOnReset()
					pInstance.cancelTimer()
				end
			end
		end
		
		
		
		var ECBehaviorTimer pECBehavior
		var ECBehaviorTriggered.Instance pTrigger
		var TSNamedTriggerTarget pTarget
		var float pTimeout, pTimeoutSpread
		var bool pTimeoutRearm, pFireOnTimeout, pStartActivated, pFullReset, pPulse, pCancelOnReset
		var float pRemainingTime
		var bool pTimerRunning, pApplyStartActivated
		var Array pListeners
		var ECComposeBTSM.Instance pBTSMs
		
		
		
		/** \brief Create element. */
		func new(ECBehaviorTimer ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pTimeout = 1
			pFireOnTimeout = true
			pApplyStartActivated = true
			pListeners = Array.new()
		end
		
		/** \brief Dispose of composeable instance. */
		func void dispose()
			pTarget = null
			pTrigger = null
			pListeners = null
			pBTSMs = null
			
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		func void init(StubElement stub)
			if pECBehavior.getTriggerTable() == null
				return
			end
			
			var BehaviorElement element = getElement()
			
			pTrigger = pECBehavior.getTriggered().synchronizedOrSelf().instance(element)
			pTrigger.addListener(TimerListener.new(this))
			
			pTimeout = pECBehavior.getTimeout().getValue(stub)
			pTimeoutSpread = pECBehavior.getTimeoutSpread().getValue(stub)
			pTimeoutRearm = pECBehavior.getTimeoutRearm().getValue(stub)
			pFireOnTimeout = pECBehavior.getFireOnTimeout().getValue(stub)
			pFullReset = pECBehavior.getFullReset().getValue(stub)
			pPulse = pECBehavior.getPulse().getValue(stub)
			pStartActivated = pECBehavior.getStartActivated().getValue(stub)
			pCancelOnReset = pECBehavior.getCancelOnReset().getValue(stub)
			
			pTarget = TSNamedTriggerTarget.new(pECBehavior.getTarget().getValue(stub))
			
			pECBehavior.createListeners(this)
			
			if pECBehavior.getBTSMs().hasAny()
				var String prefix = pECBehavior.getPrefix()
				pBTSMs = ECComposeBTSM.Instance.new(element, pECBehavior.getBTSMs())
				pBTSMs.addAllAction(prefix + "set", BTASet.new(this))
				pBTSMs.addAllActionCondition(prefix + "check", BTACheck.new(this, prefix))
				pBTSMs.dropBTContexts()
			end
		end
		
		
		
		/** \brief Behavior. */
		func ECBehaviorTimer getECBehavior()
			return pECBehavior
		end
		
		/** \brief Trigger or \em null. */
		func ECBehaviorTriggered.Instance getTrigger()
			return pTrigger
		end
		
		/** \brief Trigger target or \em null. */
		func TSNamedTriggerTarget getTarget()
			return pTarget
		end
		
		/** \brief Time in seconds until target is fired/reset after expression becomes true. */
		func float getTimeout()
			return pTimeout
		end
		
		/**
		 * \brief Random timeout spread in seconds.
		 * \version 1.12
		 * 
		 * Timeout spread is added to regular timeout. Timeout is chosen randomly between -spread
		 * to spread seconds. Hence random timeout spreads the chosen time above and below the
		 * regular timeout up to "spread" seconds.
		 */
		func float getTimeoutSpread()
			return pTimeoutSpread
		end
		
		/**
		 * \brief Rearm trigger on timeout if still enabled.
		 * \version 1.12
		 */
		func bool getTimeoutRearm()
			return pTimeoutRearm
		end
		
		/** \brief Target is fired (true) or reset (false) after timeout elapses. */
		func bool getFireOnTimeout()
			return pFireOnTimeout
		end
		
		/** \brief Full reset target instead of simple reset. */
		func bool getFullReset()
			return pFullReset
		end
		
		/**
		 * \brief Pulse target if timeout elapses.
		 * 
		 * If FireOnTimeout is true invokes fire() then reset()/fullReset() on the target.
		 * If FireOnTimeout is false invokes reset()/fullReset() then fire() on the target.
		 * Using pulse avoids the need to use two TriggerTimer to trigger a target multiple
		 * times in a row.
		 */
		func bool getPulse()
			return pPulse
		end
		
		/** \brief Trigger starts out activated. */
		func bool getStartActivated()
			return pStartActivated
		end
		
		/** \brief Trigger timer is cancelled if trigger expression evaluates to false. */
		func bool getCancelOnReset()
			return pCancelOnReset
		end
		
		/** \brief Remaining time until timeout. */
		func float getRemainingTime()
			return pRemainingTime
		end
		
		/** \brief Timer is running. */
		func bool getTimerRunning()
			return pTimerRunning
		end
		
		
		
		/** \brief Arm timer if timeout is larger than 0 otherwise operate target immediately. */
		func void armTimer()
			if pTimerRunning and pRemainingTime > 0
				return
			end
			
			pListeners.forEach(block Listener each
				each.timerStarted(this)
			end)
			if pBTSMs != null
				pBTSMs.runEvent(pECBehavior.getPrefix() + "started")
			end
			
			var float timeout = pTimeout + DEMath.random(-pTimeoutSpread, pTimeoutSpread)
			if timeout > 0.001
				pRemainingTime = timeout
				pTimerRunning = true
				setRequiresThink(true)
				
			else
				onTimeout()
			end
		end
		
		/** \brief Cancel timer if running. */
		func void cancelTimer()
			if not pTimerRunning
				return
			end
			
			pRemainingTime = 0
			pTimerRunning = false
			setRequiresThink(false)
			
			pListeners.forEach(block Listener each
				each.timerCancelled(this)
			end)
			if pBTSMs != null
				pBTSMs.runEvent(pECBehavior.getPrefix() + "cancelled")
			end
		end
		
		/** \brief Operate target after timer elapsed. */
		func void onTimeout()
			pRemainingTime = 0
			pTimerRunning = false
			setRequiresThink(false)
			
			if pTarget != null
				if pFireOnTimeout
					pTarget.fire()
					
					if pPulse
						if pFullReset
							pTarget.fullReset()
							
						else
							pTarget.reset()
						end
					end
					
				else
					if pFullReset
						pTarget.fullReset()
						
					else
						pTarget.reset()
					end
					
					if pPulse
						pTarget.fire()
					end
				end
			end
			
			pListeners.forEach(block Listener each
				each.timerElapsed(this)
			end)
			if pBTSMs != null
				pBTSMs.runEvent(pECBehavior.getPrefix() + "elapsed")
			end
			
			if pTimeoutRearm
				armTimer()
			end
		end
		
		
		
		/** \brief Add listener. */
		func void addListener(Listener listener)
			if listener == null
				throw EInvalidParam.new()
			end
			pListeners.add(listener)
		end
		
		/** \brief Remove listener. */
		func void removeListener(Listener listener)
			pListeners.remove(listener)
		end
		
		
		
		/** \brief Think about what to do for the next frame update. */
		func void think(float elapsed)
			if not pTimerRunning
				return
			end
			
			pRemainingTime -= elapsed
			if pRemainingTime <= 0
				onTimeout()
			end
		end
		
		
		
		/** \brief Add element to game world. */
		func void addToGameWorld()
			if pTarget == null
				return
			end
			
			pTarget.acquireTarget(pECBehavior.getTriggerTable())
			
			if pApplyStartActivated
				pApplyStartActivated = false
				
				if pStartActivated
					armTimer()
				end
			end
		end
		
		/** \brief Remove element from game world. */
		func void removeFromGameWorld()
			if pTarget != null
				pTarget.dropTarget()
			end
		end
		
		
		
		/** \brief Read behavior instance from file. */
		func void readFromFile(PersistencyEnvironment env, FileReader reader)
			select reader.readByte() // version
			case 0
				pApplyStartActivated = false
				
				pRemainingTime = reader.readFloat()
				pTimerRunning = env.readBool(reader)
				
			else
				throw EInvalidParam.new("unsupported version")
			end
		end
		
		/** \brief Write behavior instance to file. */
		func void writeToFile(PersistencyEnvironment env, FileWriter writer)
			writer.writeByte(0) // version
			
			writer.writeFloat(pRemainingTime)
			env.writeBool(writer, pTimerRunning)
		end
	end
	
	
	
	var ECBehaviorTriggered pTriggered
	var ECPString pTarget
	var ECPFloat pTimeout, pTimeoutSpread
	var ECPBoolean pTimeoutRearm, pFireOnTimeout, pStartActivated, pFullReset, pPulse, pCancelOnReset
	var TSTriggerTable pTriggerTable
	var Array pListenerFactories
	var ECComposeBTSM pBTSMs
	var String pPrefix
	
	
	
	/** \brief Create behavior element class. */
	func new(BehaviorElementClass eclass) \
	this(eclass, BaseGameApp.getApp().getTriggerTable(), "")
	end
	
	func new(BehaviorElementClass eclass, String id) \
	this(eclass, BaseGameApp.getApp().getTriggerTable(), id)
	end
	
	func new(BehaviorElementClass eclass, TSTriggerTable triggerTable) \
	this(eclass, triggerTable, "")
	end
	
	func new(BehaviorElementClass eclass, TSTriggerTable triggerTable, String id) \
	this(eclass, triggerTable, id, id.empty() if "timer" else "timer(" + id + ")")
	end
	
	func new(BehaviorElementClass eclass, TSTriggerTable triggerTable, String id, String subID) \
	this(eclass, triggerTable, id, subID, subID + ".")
	end
	
	func new(BehaviorElementClass eclass, TSTriggerTable triggerTable, \
	String id, String subID, String prefix) super(eclass, id)
		pPrefix = prefix
		pTriggerTable = triggerTable
		pListenerFactories = Array.new()
		pBTSMs = ECComposeBTSM.new()
		
		pTriggered = ECBehaviorTriggered.new(eclass, triggerTable, subID, prefix + "trigger")
		
		pTarget = ECPString.new(prefix + "target", null)
		eclass.addProperty(pTarget)
		
		pTimeout = ECPFloat.new(prefix + "timeout", 0, 0)
		eclass.addProperty(pTimeout)
		
		pTimeoutSpread = ECPFloat.new(prefix + "timeoutSpread", 0, 0)
		eclass.addProperty(pTimeoutSpread)
		
		pTimeoutRearm = ECPBoolean.new(prefix + "timeoutRearm", false)
		eclass.addProperty(pTimeoutRearm)
		
		pFireOnTimeout = ECPBoolean.new(prefix + "fireOnTimeout", true)
		eclass.addProperty(pFireOnTimeout)
		
		pStartActivated = ECPBoolean.new(prefix + "startActivated", false)
		eclass.addProperty(pStartActivated)
		
		pFullReset = ECPBoolean.new(prefix + "fullReset", false)
		eclass.addProperty(pFullReset)
		
		pPulse = ECPBoolean.new(prefix + "pulse", false)
		eclass.addProperty(pPulse)
		
		pCancelOnReset = ECPBoolean.new(prefix + "cancelOnReset", false)
		eclass.addProperty(pCancelOnReset)
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	func void dispose()
		pListenerFactories = null
		pBTSMs = null
		super.dispose()
	end
	
	
	
	/**
	 * \brief Prefix.
	 * \version 1.26
	 */
	func String getPrefix()
		return pPrefix
	end
	
	/** \brief Trigger to start timer. */
	func ECBehaviorTriggered getTriggered()
		return pTriggered
	end
	
	/** \brief Trigger target to operate. */
	func ECPString getTarget()
		return pTarget
	end
	
	/** \brief Time in seconds until target is fired/reset after expression becomes true. */
	func ECPFloat getTimeout()
		return pTimeout
	end
	
	/**
	 * \brief Random timeout spread in seconds.
	 * \version 1.12
	 * 
	 * Timeout spread is added to regular timeout. Timeout is chosen randomly between -spread
	 * to spread seconds. Hence random timeout spreads the chosen time above and below the
	 * regular timeout up to "spread" seconds.
	 */
	func ECPFloat getTimeoutSpread()
		return pTimeoutSpread
	end
	
	/**
	 * \brief Rearm trigger on timeout if still enabled.
	 * \version 1.12
	 */
	func ECPBoolean getTimeoutRearm()
		return pTimeoutRearm
	end
	
	/** \brief Target is fired (true) or reset (false) after timeout elapses. */
	func ECPBoolean getFireOnTimeout()
		return pFireOnTimeout
	end
	
	/** \brief Full reset target instead of simple reset. */
	func ECPBoolean getFullReset()
		return pFullReset
	end
	
	/**
	 * \brief Pulse target if timeout elapses.
	 * 
	 * If FireOnTimeout is true invokes fire() then reset()/fullReset() on the target.
	 * If FireOnTimeout is false invokes reset()/fullReset() then fire() on the target.
	 * Using pulse avoids the need to use two TriggerTimer to trigger a target multiple
	 * times in a row.
	 */
	func ECPBoolean getPulse()
		return pPulse
	end
	
	/** \brief Trigger starts out activated. */
	func ECPBoolean getStartActivated()
		return pStartActivated
	end
	
	/** \brief Trigger timer is cancelled if trigger expression evaluates to false. */
	func ECPBoolean getCancelOnReset()
		return pCancelOnReset
	end
	
	/** \brief Trigger table or \em null if not used. */
	func TSTriggerTable getTriggerTable()
		return pTriggerTable
	end
	
	/** \brief Set trigger table or \em null if not used. */
	protected func void setTriggerTable(TSTriggerTable triggerTable)
		pTriggerTable = triggerTable
	end
	
	/**
	 * \brief Compose behavior trees and state machines.
	 * \version 1.26
	 */
	func ECComposeBTSM getBTSMs()
		return pBTSMs
	end
	
	/** \brief Add listener factory. */
	func void addListenerFactory(ListenerFactory factory)
		if factory == null
			throw ENullPointer.new()
		end
		pListenerFactories.add(factory)
	end
	
	/**
	 * \brief Add listener factory using block.
	 * 
	 * Block receives as parameter Instance and returns Listener.
	 */
	func void addListenerFactory(Block blockFactory)
		pListenerFactories.add(BlockListenerFactory.new(blockFactory))
	end
	
	/** \brief Visit listener factories with block with argument ListenerFactory. */
	func void forEachListenerFactory(Block ablock)
		pListenerFactories.forEach(ablock)
	end
	
	/** \brief Create listeners from factories adding them to behavior instance. */
	func void createListeners(Instance instance)
		pListenerFactories.forEach(block ListenerFactory each
			instance.addListener(each.createListener(instance))
		end)
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorComponent.
	 */
	static func ECBehaviorTimer getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorTimer each
			return true
		end) cast ECBehaviorTimer
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorTimer with specific identifier.
	 */
	static func ECBehaviorTimer getBehaviorIn(BehaviorElementClass eclass, String id)
		return eclass.findBehavior(block ECBehaviorTimer each
			return each.getID().equals(id)
		end) cast ECBehaviorTimer
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorTimer behaviors in the order they have
	 * been created in the element.
	 */
	static func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBehaviorTimer each
			return true
		end)
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorTimer.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorTimer.Instance with specific identifier.
	 */
	static func Instance getInstanceIn(BehaviorElement element, String id)
		return element.findInstance(block Instance each
			return each.getECBehavior().getID().equals(id)
		end) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorTimer.Instance instances in the order they have
	 * been created in the element.
	 */
	static func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** \brief Create Behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
