/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.TriggerSystem


/**
 * \brief Behavior element behavior timer trigger target.
 * 
 * Fires or resets trigger target after timeout after evaluation state of trigger expression
 * changes to true.
 * 
 * Element class properties have the prefix "timer{(id)}.".
 * 
 * This behavior does require the element class to be persistable (setPersistable).
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_timer
 */
class ECBehaviorTimer extends DefaultECBehavior
	/** \brief Listener for behavior instance events. */
	interface Listener
		/** \brief Timer started. */
		func void timerStarted(Instance instance)
		
		/** \brief Timer elapsed. */
		func void timerElapsed(Instance instance)
		
		/** \brief Timer cancelled. */
		func void timerCancelled(Instance instance)
	end
	
	/** \brief Default implementation of behavior instance listener. */
	class DefaultListener implements Listener
		/** \brief Create default listener. */
		public func new()
		end
		
		/** \brief Timer started. */
		public func void timerStarted(Instance instance)
		end
		
		/** \brief Timer elapsed. */
		public func void timerElapsed(Instance instance)
		end
		
		/** \brief Timer cancelled. */
		public func void timerCancelled(Instance instance)
		end
	end
	
	/** \brief Factory creating listeners. */
	interface ListenerFactory
		/** \brief Create listener. */
		func Listener createListener(Instance instance)
	end
	
	/** \brief Factory creating listeners using block. */
	class BlockListenerFactory implements ListenerFactory
		private var Block pBlock
		
		/** \brief Create factory using block receiving Instance as parameter returning Listener. */
		public func new(Block blockFactory)
			if blockFactory == null
				throw ENullPointer.new()
			end
			pBlock = blockFactory
		end
		
		/** \brief Create listener. */
		public func Listener createListener(Instance instance)
			return pBlock.run(instance) cast Listener
		end
	end
	
	/**
	 * \brief Behavior factory.
	 * \ref 1.25
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorTimer'>
	 *   <!-- optional: use BaseGameApp trigger table. game can add more supported values.
	 *                  default is 'default' -->
	 *   <string name='triggerTable'>default</string>
	 *   
	 *   <!-- optional: identifier of ECBehaviorTriggered to synchronize with or empty
	 *                  string to not synchronize. default is empty string. -->
	 *   <string name='trigger.synchronize'>other</string>
	 * </behavior>
	 * 
	 * <!-- for adding multiple behaviors use unique identifiers -->
	 * <behavior type='ECBehaviorTimer' id='second'>
	 *   ...
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for exampler ".propertyName").
	 */
	class Factory implements BehaviorFactory
		func new()
		end
		
		func String factoryId()
			return "ECBehaviorTimer"
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			var ECBehaviorTimer behavior = ECBehaviorTimer.new(eclass,\
				getTriggerTableFor(parameters.getAt("triggerTable", "default") cast String), id)
			
			behavior.getTriggered().setSynchronize(eclass,\
				parameters.getAt("trigger.synchronize", null) cast String)
			
			BehaviorElementClass.sharedFactoryHelper.setPropertiesFromParameters(\
				eclass, parameters, "timer", id)
			return behavior
		end
		
		/**
		 * \brief Trigger table matching value.
		 * 
		 * Default implementation returns \ref BaseGameApp#getTriggerTable() for value 'default'
		 * and null for all other values. Overwrite to accept other values.
		 */
		func TSTriggerTable getTriggerTableFor(String value)
			return BehaviorElementClass.sharedFactoryHelper.triggerTable(value)
		end
	end
	
	
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/** \brief Trigger listener. */
		public class TimerListener extends ECBehaviorTriggered.DefaultListener
			private var Instance pInstance
			
			public func new(Instance instance)
				pInstance = instance
			end
			
			public func void triggerExpressionTrue(ECBehaviorTriggered.Instance instance)
				pInstance.armTimer()
			end
			
			public func void triggerExpressionFalse(ECBehaviorTriggered.Instance instance)
				if pInstance.getCancelOnReset()
					pInstance.cancelTimer()
				end
			end
		end
		
		
		
		private var ECBehaviorTimer pECBehavior
		private var ECBehaviorTriggered.Instance pTrigger
		private var TSNamedTriggerTarget pTarget
		private var float pTimeout
		private var float pTimeoutSpread
		private var bool pTimeoutRearm
		private var bool pFireOnTimeout
		private var bool pStartActivated
		private var bool pFullReset
		private var bool pPulse
		private var bool pCancelOnReset
		private var float pRemainingTime
		private var bool pTimerRunning
		private var bool pApplyStartActivated
		private var Array pListeners
		
		
		
		/** \brief Create element. */
		public func new(ECBehaviorTimer ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pTimeout = 1
			pTimeoutSpread = 0
			pTimeoutRearm = false
			pFireOnTimeout = true
			pFullReset = false
			pPulse = false
			pStartActivated = false
			pCancelOnReset = false
			pRemainingTime = 0.0
			pTimerRunning = false
			pApplyStartActivated = true
			pListeners = Array.new()
		end
		
		/** \brief Dispose of composeable instance. */
		public func void dispose()
			pTarget = null
			pTrigger = null
			pListeners = null
			
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		public func void init(StubElement stub)
			if pECBehavior.getTriggerTable() == null
				return
			end
			
			pTrigger = pECBehavior.getTriggered().synchronizedOrSelf().instance(getElement())
			pTrigger.addListener(TimerListener.new(this))
			
			pTimeout = pECBehavior.getTimeout().getValue(stub)
			pTimeoutSpread = pECBehavior.getTimeoutSpread().getValue(stub)
			pTimeoutRearm = pECBehavior.getTimeoutRearm().getValue(stub)
			pFireOnTimeout = pECBehavior.getFireOnTimeout().getValue(stub)
			pFullReset = pECBehavior.getFullReset().getValue(stub)
			pPulse = pECBehavior.getPulse().getValue(stub)
			pStartActivated = pECBehavior.getStartActivated().getValue(stub)
			pCancelOnReset = pECBehavior.getCancelOnReset().getValue(stub)
			
			pTarget = TSNamedTriggerTarget.new(pECBehavior.getTarget().getValue(stub))
			
			pECBehavior.createListeners(this)
		end
		
		
		
		/** \brief Behavior. */
		public func ECBehaviorTimer getECBehavior()
			return pECBehavior
		end
		
		/** \brief Trigger or \em null. */
		public func ECBehaviorTriggered.Instance getTrigger()
			return pTrigger
		end
		
		/** \brief Trigger target or \em null. */
		public func TSNamedTriggerTarget getTarget()
			return pTarget
		end
		
		/** \brief Time in seconds until target is fired/reset after expression becomes true. */
		public func float getTimeout()
			return pTimeout
		end
		
		/**
		 * \brief Random timeout spread in seconds.
		 * \version 1.12
		 * 
		 * Timeout spread is added to regular timeout. Timeout is chosen randomly between -spread
		 * to spread seconds. Hence random timeout spreads the chosen time above and below the
		 * regular timeout up to "spread" seconds.
		 */
		public func float getTimeoutSpread()
			return pTimeoutSpread
		end
		
		/**
		 * \brief Rearm trigger on timeout if still enabled.
		 * \version 1.12
		 */
		public func bool getTimeoutRearm()
			return pTimeoutRearm
		end
		
		/** \brief Target is fired (true) or reset (false) after timeout elapses. */
		public func bool getFireOnTimeout()
			return pFireOnTimeout
		end
		
		/** \brief Full reset target instead of simple reset. */
		public func bool getFullReset()
			return pFullReset
		end
		
		/**
		 * \brief Pulse target if timeout elapses.
		 * 
		 * If FireOnTimeout is true invokes fire() then reset()/fullReset() on the target.
		 * If FireOnTimeout is false invokes reset()/fullReset() then fire() on the target.
		 * Using pulse avoids the need to use two TriggerTimer to trigger a target multiple
		 * times in a row.
		 */
		public func bool getPulse()
			return pPulse
		end
		
		/** \brief Trigger starts out activated. */
		public func bool getStartActivated()
			return pStartActivated
		end
		
		/** \brief Trigger timer is cancelled if trigger expression evaluates to false. */
		public func bool getCancelOnReset()
			return pCancelOnReset
		end
		
		/** \brief Remaining time until timeout. */
		public func float getRemainingTime()
			return pRemainingTime
		end
		
		/** \brief Timer is running. */
		public func bool getTimerRunning()
			return pTimerRunning
		end
		
		
		
		/** \brief Arm timer if timeout is larger than 0 otherwise operate target immediately. */
		public func void armTimer()
			if pTimerRunning and pRemainingTime > 0
				return
			end
			
			pListeners.forEach(block Listener each
				each.timerStarted(this)
			end)
			
			var float timeout = pTimeout + DEMath.random(-pTimeoutSpread, pTimeoutSpread)
			if timeout > 0.001
				pRemainingTime = timeout
				pTimerRunning = true
				setRequiresThink(true)
				
			else
				onTimeout()
			end
		end
		
		/** \brief Cancel timer if running. */
		public func void cancelTimer()
			if not pTimerRunning
				return
			end
			
			pRemainingTime = 0
			pTimerRunning = false
			setRequiresThink(false)
			
			pListeners.forEach(block Listener each
				each.timerCancelled(this)
			end)
		end
		
		/** \brief Operate target after timer elapsed. */
		public func void onTimeout()
			pRemainingTime = 0
			pTimerRunning = false
			setRequiresThink(false)
			
			if pTarget != null
				if pFireOnTimeout
					pTarget.fire()
					
					if pPulse
						if pFullReset
							pTarget.fullReset()
							
						else
							pTarget.reset()
						end
					end
					
				else
					if pFullReset
						pTarget.fullReset()
						
					else
						pTarget.reset()
					end
					
					if pPulse
						pTarget.fire()
					end
				end
			end
			
			pListeners.forEach(block Listener each
				each.timerElapsed(this)
			end)
			
			if pTimeoutRearm
				armTimer()
			end
		end
		
		
		
		/** \brief Add listener. */
		public func void addListener(Listener listener)
			if listener == null
				throw EInvalidParam.new()
			end
			pListeners.add(listener)
		end
		
		/** \brief Remove listener. */
		public func void removeListener(Listener listener)
			pListeners.remove(listener)
		end
		
		
		
		/** \brief Think about what to do for the next frame update. */
		public func void think(float elapsed)
			if not pTimerRunning
				return
			end
			
			pRemainingTime -= elapsed
			if pRemainingTime <= 0
				onTimeout()
			end
		end
		
		
		
		/** \brief Add element to game world. */
		public func void addToGameWorld()
			if pTarget == null
				return
			end
			
			pTarget.acquireTarget(pECBehavior.getTriggerTable())
			
			if pApplyStartActivated
				pApplyStartActivated = false
				
				if pStartActivated
					armTimer()
				end
			end
		end
		
		/** \brief Remove element from game world. */
		public func void removeFromGameWorld()
			if pTarget != null
				pTarget.dropTarget()
			end
		end
		
		
		
		/** \brief Read behavior instance from file. */
		public func void readFromFile(PersistencyEnvironment env, FileReader reader)
			select reader.readByte() // version
			case 0
				pApplyStartActivated = false
				
				pRemainingTime = reader.readFloat()
				pTimerRunning = env.readBool(reader)
				
			else
				throw EInvalidParam.new("unsupported version")
			end
		end
		
		/** \brief Write behavior instance to file. */
		public func void writeToFile(PersistencyEnvironment env, FileWriter writer)
			writer.writeByte(0) // version
			
			writer.writeFloat(pRemainingTime)
			env.writeBool(writer, pTimerRunning)
		end
	end
	
	
	
	private var ECBehaviorTriggered pTriggered
	private var ECPString pTarget
	private var ECPFloat pTimeout
	private var ECPFloat pTimeoutSpread
	private var ECPBoolean pTimeoutRearm
	private var ECPBoolean pFireOnTimeout
	private var ECPBoolean pStartActivated
	private var ECPBoolean pFullReset
	private var ECPBoolean pPulse
	private var ECPBoolean pCancelOnReset
	private var TSTriggerTable pTriggerTable
	private var Array pListenerFactories
	
	
	
	/** \brief Create behavior element class. */
	public func new(BehaviorElementClass eclass) \
	this(eclass, BaseGameApp.getApp().getTriggerTable(), "")
	end
	
	public func new(BehaviorElementClass eclass, String id) \
	this(eclass, BaseGameApp.getApp().getTriggerTable(), id)
	end
	
	public func new(BehaviorElementClass eclass, TSTriggerTable triggerTable) \
	this(eclass, triggerTable, "")
	end
	
	public func new(BehaviorElementClass eclass, TSTriggerTable triggerTable, String id) \
	this(eclass, triggerTable, id, id.empty() if "timer" else "timer(" + id + ")")
	end
	
	public func new(BehaviorElementClass eclass, TSTriggerTable triggerTable, String id, String subID) \
	this(eclass, triggerTable, id, subID, subID + ".")
	end
	
	public func new(BehaviorElementClass eclass, TSTriggerTable triggerTable, \
	String id, String subID, String prefix) super(eclass, id)
		pTriggerTable = triggerTable
		pListenerFactories = Array.new()
		
		pTriggered = ECBehaviorTriggered.new(eclass, triggerTable, subID, prefix + "trigger")
		
		pTarget = ECPString.new(prefix + "target", null)
		eclass.addProperty(pTarget)
		
		pTimeout = ECPFloat.new(prefix + "timeout", 0, 0)
		eclass.addProperty(pTimeout)
		
		pTimeoutSpread = ECPFloat.new(prefix + "timeoutSpread", 0, 0)
		eclass.addProperty(pTimeoutSpread)
		
		pTimeoutRearm = ECPBoolean.new(prefix + "timeoutRearm", false)
		eclass.addProperty(pTimeoutRearm)
		
		pFireOnTimeout = ECPBoolean.new(prefix + "fireOnTimeout", true)
		eclass.addProperty(pFireOnTimeout)
		
		pStartActivated = ECPBoolean.new(prefix + "startActivated", false)
		eclass.addProperty(pStartActivated)
		
		pFullReset = ECPBoolean.new(prefix + "fullReset", false)
		eclass.addProperty(pFullReset)
		
		pPulse = ECPBoolean.new(prefix + "pulse", false)
		eclass.addProperty(pPulse)
		
		pCancelOnReset = ECPBoolean.new(prefix + "cancelOnReset", false)
		eclass.addProperty(pCancelOnReset)
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	public func void dispose()
		pListenerFactories = null
		super.dispose()
	end
	
	
	
	/** \brief Trigger to start timer. */
	public func ECBehaviorTriggered getTriggered()
		return pTriggered
	end
	
	/** \brief Trigger target to operate. */
	public func ECPString getTarget()
		return pTarget
	end
	
	/** \brief Time in seconds until target is fired/reset after expression becomes true. */
	public func ECPFloat getTimeout()
		return pTimeout
	end
	
	/**
	 * \brief Random timeout spread in seconds.
	 * \version 1.12
	 * 
	 * Timeout spread is added to regular timeout. Timeout is chosen randomly between -spread
	 * to spread seconds. Hence random timeout spreads the chosen time above and below the
	 * regular timeout up to "spread" seconds.
	 */
	public func ECPFloat getTimeoutSpread()
		return pTimeoutSpread
	end
	
	/**
	 * \brief Rearm trigger on timeout if still enabled.
	 * \version 1.12
	 */
	public func ECPBoolean getTimeoutRearm()
		return pTimeoutRearm
	end
	
	/** \brief Target is fired (true) or reset (false) after timeout elapses. */
	public func ECPBoolean getFireOnTimeout()
		return pFireOnTimeout
	end
	
	/** \brief Full reset target instead of simple reset. */
	public func ECPBoolean getFullReset()
		return pFullReset
	end
	
	/**
	 * \brief Pulse target if timeout elapses.
	 * 
	 * If FireOnTimeout is true invokes fire() then reset()/fullReset() on the target.
	 * If FireOnTimeout is false invokes reset()/fullReset() then fire() on the target.
	 * Using pulse avoids the need to use two TriggerTimer to trigger a target multiple
	 * times in a row.
	 */
	public func ECPBoolean getPulse()
		return pPulse
	end
	
	/** \brief Trigger starts out activated. */
	public func ECPBoolean getStartActivated()
		return pStartActivated
	end
	
	/** \brief Trigger timer is cancelled if trigger expression evaluates to false. */
	public func ECPBoolean getCancelOnReset()
		return pCancelOnReset
	end
	
	/** \brief Trigger table or \em null if not used. */
	public func TSTriggerTable getTriggerTable()
		return pTriggerTable
	end
	
	/** \brief Set trigger table or \em null if not used. */
	protected func void setTriggerTable(TSTriggerTable triggerTable)
		pTriggerTable = triggerTable
	end
	
	/** \brief Add listener factory. */
	public func void addListenerFactory(ListenerFactory factory)
		if factory == null
			throw ENullPointer.new()
		end
		pListenerFactories.add(factory)
	end
	
	/**
	 * \brief Add listener factory using block.
	 * 
	 * Block receives as parameter Instance and returns Listener.
	 */
	public func void addListenerFactory(Block blockFactory)
		pListenerFactories.add(BlockListenerFactory.new(blockFactory))
	end
	
	/** \brief Visit listener factories with block with argument ListenerFactory. */
	public func void forEachListenerFactory(Block ablock)
		pListenerFactories.forEach(ablock)
	end
	
	/** \brief Create listeners from factories adding them to behavior instance. */
	public func void createListeners(Instance instance)
		pListenerFactories.forEach(block ListenerFactory each
			instance.addListener(each.createListener(instance))
		end)
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	public func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorComponent.
	 */
	static public func ECBehaviorTimer getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorTimer each
			return true
		end) cast ECBehaviorTimer
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorTimer with specific identifier.
	 */
	static public func ECBehaviorTimer getBehaviorIn(BehaviorElementClass eclass, String id)
		return eclass.findBehavior(block ECBehaviorTimer each
			return each.getID().equals(id)
		end) cast ECBehaviorTimer
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorTimer behaviors in the order they have
	 * been created in the element.
	 */
	static public func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBehaviorTimer each
			return true
		end)
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorTimer.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static public func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorTimer.Instance with specific identifier.
	 */
	static public func Instance getInstanceIn(BehaviorElement element, String id)
		return element.findInstance(block Instance each
			return each.getECBehavior().getID().equals(id)
		end) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorTimer.Instance instances in the order they have
	 * been created in the element.
	 */
	static public func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** \brief Create Behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
