/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.Gui.Layouts
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils


/**
 * \brief Behavior element behavior adding a renderable Desktop widget.
 * \version 1.7
 * 
 * This behavior is useful for models displaying interactive UI where the content of the
 * texture is provided by a Desktop widget. Multiple other behaviors can add their content
 * Window to the Desktop separating logic for reuse. Using ECBehaviorRenderableDesktop
 * allows to use the full power of the GUI ToolKit including using GuiThemes to create
 * complex content easily.
 * 
 * The default gui theme is "/shareddata/guithemes/modern/modern.degt". The default
 * Desktop designer selector is "RenderableDesktop".
 * 
 * To use this behavior add an ECBehaviorDynamicSkin to the element class before adding this
 * behavior. Create ECBehaviorRenderableDesktop behavior and set the dynamic skin and designer
 * selector and GuiTheme for the Desktop. Now you can add other behaviors targeting the
 * ECBehaviorRenderableDesktop to add Window resources to.
 * 
 * The Desktop is created with a default size of 1024x1024 . You can change the size using the
 * element property to fit the texture requirements.
 * 
 * Using WidgetInputProcessor the player can control the Desktop like regular UI Desktops.
 * 
 * Using the "renderableSolidity" parameter an additional renderable can be defined.
 * If used this renderable is set up to contain a CanvasCanvasView using the regular
 * renderable CanvasView as input using a ColorMatrix to convert the transparency
 * to a grayscale image. This can then be used to create transparent HUD displays
 * or holographic displays with fully or partial transparent content.
 * 
 * Multiple ECBehaviorRenderableDesktop instances can be added to affect individual
 * dynamic skin textures. The example below uses one component with one dynamic skin to two
 * individual renderables named "content1" and "content2" each allowing to be individiually
 * filled with content by different behaviors.
 * 
 * \code{.ds}
 * class MyElement extends BehaviorElementClass
 *    public func new()
 *       ECBehaviorComponent.new(this)
 *       ECBehaviorDynamicSkin.new(this)
 *       
 *       ECBehaviorCanvas.new(this)
 *       ECBehaviorCanvas.new(this, "canvas2")
 *       
 *       var ECBehaviorRenderableDesktop behavior = ECBehaviorRenderableDesktop.new(this)
 *       behavior.getRenderable().setValue("content1")
 *       behavior.getGuiTheme().setPath("/content/renderable.degt")
 *       behavior.getDesignerSelector().setValue("Desktop.MyElement")
 *       
 *       behavior = ECBehaviorRenderableDesktop.new(this, "renderableDesktop2")
 *       behavior.setBackgroundColor(Color.blue)
 *       behavior.getRenderable().setValue("intensity2")
 *       behavior.getGuiTheme().setPath("/content/renderable.degt")
 *       behavior.getDesignerSelector().setValue("Desktop.MyElement")
 *    end
 * end
 * \endcode
 * 
 * Element class properties have the prefix "renderableDesktop{(id)}.".
 * 
 * This behavior does not require the element class to be persistable (setPersistable).
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_renderabledesktop
 */
class ECBehaviorRenderableDesktop extends DefaultECBehavior
	/**
	 * \brief Behavior factory.
	 * \version 1.25
	 * 
	 * Automatically assigns these behaviors if present before adding:
	 * - Required: \ref ECBehaviorDynamicSkin
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorRenderableDesktop'>
	 *   <!-- optional: use dynamic skin with id instead of empty string -->
	 *   <string name='dynamicSkin'>second</string>
	 * </behavior>
	 * 
	 * <!-- for adding multiple behaviors use unique identifiers -->
	 * <behavior type='ECBehaviorRenderableDesktop' id='second'>
	 *   ...
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for example ".propertyName").
	 */
	class Factory extends DefaultBehaviorFactory
		func new() super("ECBehaviorRenderableDesktop")
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			var ECBehaviorRenderableDesktop behavior = ECBehaviorRenderableDesktop.new(eclass,\
				getHelper().defaultDynamicSkin(eclass, parameters), id)
			getHelper().setPropertiesFromParameters(eclass, parameters, "renderableDesktop", id)
			return behavior
		end
	end
	
	/**
	 * \brief Create renderable desktop content.
	 * \version 1.9
	 */
	interface ContentCreator
		/** \brief Create content. */
		func void createContent(Instance instance)
	end
	
	/**
	 * \brief Factory creating renderable content using block.
	 * \version 1.9
	 */
	class BlockContentCreator implements ContentCreator
		private var Block pBlock
		
		/** \brief Create content using block receiving Instance as parameter. */
		public func new(Block blockFactory)
			if blockFactory == null
				throw ENullPointer.new()
			end
			pBlock = blockFactory
		end
		
		/** \brief Create content. */
		public func void createContent(Instance instance)
			pBlock.run(instance)
		end
	end
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		private var ECBehaviorRenderableDesktop pECBehavior
		private var ECBehaviorDynamicSkin.Instance pDynamicSkin
		private var int pRenderable
		private var Desktop pDesktop
		private var int pRenderableSolidity
		private var CanvasView pCanvasViewSolidity
		
		
		
		/** \brief Create element. */
		public func new(ECBehaviorRenderableDesktop ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pRenderable = -1
			pRenderableSolidity = -1
			
			pDynamicSkin = ecbehavior.getDynamicSkin().instance(element)
		end
		
		/** \brief Dispose of behavior instance. */
		public func void dispose()
			pDynamicSkin = null
			if pDesktop != null
				pDesktop.dispose()
				pDesktop = null
			end
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		public func void init(StubElement stub)
			var String renderableName = pECBehavior.getRenderable().getValue(stub)
			if renderableName.empty()
				return
			end
			
			var ElementClass eclass = getElement().getClass()
			var CodecPropertyString codec = eclass.getCodecPropertyString()
			
			pDesktop = Desktop.new(pECBehavior.getSize().getPoint(stub, codec))
			pDesktop.setGuiTheme(pECBehavior.getGuiTheme().getGuiTheme(stub, eclass))
			pDesktop.setDesignerSelector(pECBehavior.getDesignerSelector().getValue(stub))
			
			var DynamicSkin dynamicSkin = pDynamicSkin.getDynamicSkin()
			pRenderable = dynamicSkin.getRenderableCount()
			dynamicSkin.addRenderable(renderableName, DynamicSkinRenderableType.canvas)
			dynamicSkin.setCanvasAt(pRenderable, pDesktop.getCanvasWidget())
			
			// solidity
			renderableName = pECBehavior.getRenderableSolidity().getValue(stub)
			if renderableName.empty()
				return
			end
			
			pCanvasViewSolidity = CanvasView.new()
			pCanvasViewSolidity.setSize(pDesktop.getCanvasWidget().getSize())
			
			var CanvasCanvasView ccv = CanvasCanvasView.new()
			ccv.setCanvasView(pDesktop.getCanvasWidget())
			ccv.setColorTransform(ColorMatrix.new(\
				Color.new(0, 0, 0, 0), Color.new(0, 0, 0, 0), Color.new(0, 0, 0, 0),\
				Color.new(1, 1, 1, 0), Color.new(0, 0, 0, 1)))
			ccv.setSize(pDesktop.getCanvasWidget().getSize())
			pCanvasViewSolidity.addCanvas(ccv)
			
			dynamicSkin.setCanvasAt(pRenderable, pDesktop.getCanvasWidget(), 4)
			
			pRenderableSolidity = dynamicSkin.getRenderableCount()
			dynamicSkin.addRenderable(renderableName, DynamicSkinRenderableType.canvas)
			dynamicSkin.setCanvasAt(pRenderableSolidity, pCanvasViewSolidity, 1)
			
			// create content using content creators
			pECBehavior.createContent(this)
		end
		
		
		
		/** \brief Behavior. */
		public func ECBehaviorRenderableDesktop getECBehavior()
			return pECBehavior
		end
		
		/** \brief Dynamic skin behavior instance. */
		public func ECBehaviorDynamicSkin.Instance getDynamicSkin()
			return pDynamicSkin
		end
		
		/** \brief Desktop or \em null if no dynamic skin is present. */
		public func Desktop getDesktop()
			return pDesktop
		end
		
		/** \brief Solidity canvas view or \em null if not used. */
		public func CanvasView getCanvasViewSolidity()
			return pCanvasViewSolidity
		end
	end
	
	
	
	private var ECBehaviorDynamicSkin pDynamicSkin
	private var ECPString pRenderable
	private var ECPPoint pSize
	private var ECPGuiTheme pGuiTheme
	private var ECPString pDesignerSelector
	private var ECPString pRenderableSolidity
	private var Array pContentCreators
	
	
	
	/** \brief Create behavior element class. */
	public func new(BehaviorElementClass eclass, ECBehaviorDynamicSkin dynamicSkin) \
	this(eclass, dynamicSkin, eclass.sharedFactoryHelper.defaultLoaders(), "")
	end
	
	public func new(BehaviorElementClass eclass, ECBehaviorDynamicSkin dynamicSkin, String id) \
	this(eclass, dynamicSkin, eclass.sharedFactoryHelper.defaultLoaders(), id)
	end
	
	public func new(BehaviorElementClass eclass, ECBehaviorDynamicSkin dynamicSkin, Loaders loaders) \
	this(eclass, dynamicSkin, loaders, "")
	end
	
	public func new(BehaviorElementClass eclass, ECBehaviorDynamicSkin dynamicSkin, Loaders loaders, String id) \
	this(eclass, dynamicSkin, loaders, id, id.empty() if "renderableDesktop." else "renderableDesktop(" + id + ").")
	end
	
	public func new(BehaviorElementClass eclass, ECBehaviorDynamicSkin dynamicSkin, Loaders loaders, \
	String id, String prefix) super(eclass, id)
		if dynamicSkin == null
			throw ENullPointer.new("dynamicSkin")
		end
		
		pDynamicSkin = dynamicSkin
		pContentCreators = Array.new()
		
		pRenderable = ECPString.new(prefix + "renderable", "")
		eclass.addProperty(pRenderable)
		
		pSize = ECPPoint.new(prefix + "size", Point.new(1024, 1024))
		eclass.addProperty(pSize)
		
		pGuiTheme = ECPGuiTheme.new(prefix + "guiTheme", loaders.getGuiTheme(), \
			"/shareddata/guithemes/modern/modern.degt")
		eclass.addProperty(pGuiTheme)
		
		pDesignerSelector = ECPString.new(prefix + "designerSelector", "RenderableDesktop")
		eclass.addProperty(pDesignerSelector)
		
		pRenderableSolidity = ECPString.new(prefix + "renderableSolidity", "")
		eclass.addProperty(pRenderableSolidity)
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	public func void dispose()
		pContentCreators = null
		pDynamicSkin = null
		super.dispose()
	end
	
	
	
	/** \brief Dynamic skin behavior. */
	public func ECBehaviorDynamicSkin getDynamicSkin()
		return pDynamicSkin
	end
	
	/** \brief Name of renderable to create or empty string if not used. */
	public func ECPString getRenderable()
		return pRenderable
	end
	
	/** \brief Size of canvas. */
	public func ECPPoint getSize()
		return pSize
	end
	
	/** \brief Gui Theme to use for Desktop. */
	public func ECPGuiTheme getGuiTheme()
		return pGuiTheme
	end
	
	/** \brief Designer selector to use for Desktop. */
	public func ECPString getDesignerSelector()
		return pDesignerSelector
	end
	
	/**
	 * \brief Name of solidity renderable to create or empty string if not used.
	 * 
	 * If not empty the solidity renderable is set to contain the transparency of the regular
	 * renderable converted to grayscale image. This can be used to create HUD displays or
	 * holographic displays where content is fully or partially transparent.
	 */
	public func ECPString getRenderableSolidity()
		return pRenderableSolidity
	end
	
	
	
	/**
	 * \brief Add content creator.
	 * \version 1.9
	 */
	public func void addContentCreator(ContentCreator creator)
		if creator == null
			throw ENullPointer.new("creator")
		end
		pContentCreators.add(creator)
	end
	
	/**
	 * \brief Add content creator using block.
	 * \version 1.9
	 * 
	 * Block receives as parameter Instance.
	 */
	public func void addContentCreator(Block blockCreator)
		pContentCreators.add(BlockContentCreator.new(blockCreator))
	end
	
	/**
	 * \brief Visit content creators with block with argument ContentCreator.
	 * \version 1.9
	 */
	public func void forEachContentCreator(Block ablock)
		pContentCreators.forEach(ablock)
	end
	
	/**
	 * \brief Create content using creators.
	 * \version 1.9
	 */
	public func void createContent(Instance instance)
		pContentCreators.forEach(block ContentCreator each
			each.createContent(instance)
		end)
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	public func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorComponent.
	 */
	static public func ECBehaviorRenderableDesktop getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorRenderableDesktop each
			return true
		end) cast ECBehaviorRenderableDesktop
	end
	
	/**
	 * \brief Get behavior with id in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorRenderableDesktop with specific identifier.
	 */
	static public func ECBehaviorRenderableDesktop getBehaviorIn(BehaviorElementClass eclass, String id)
		return eclass.findBehavior(block ECBehaviorRenderableDesktop each
			return each.getID().equals(id)
		end) cast ECBehaviorRenderableDesktop
	end
	
	/**
	 * \brief Get list of all behaviors in element.
	 * 
	 * Returns list contains ECBehaviorRenderableDesktop behaviors in the order they have
	 * been created in the element.
	 */
	static public func Array getAllBehaviorsIn(BehaviorElementClass eclass)
		return eclass.collectBehaviors(block ECBehaviorRenderableDesktop each
			return true
		end)
	end
	
	
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorRenderableDesktop.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static public func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	/**
	 * \brief Get instance with id in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorRenderableDesktop.Instance with specific identifier.
	 */
	static public func Instance getInstanceIn(BehaviorElement element, String id)
		return element.findInstance(block Instance each
			return each.getECBehavior().getID().equals(id)
		end) cast Instance
	end
	
	/**
	 * \brief Get list of all instances in element.
	 * 
	 * Returns list contains ECBehaviorRenderableDesktop.Instance instances in the order they have
	 * been created in the element.
	 */
	static public func Array getAllInstancesIn(BehaviorElement element)
		return element.collectInstances(block Instance each
			return true
		end)
	end
	
	
	
	/** \brief Create Behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
