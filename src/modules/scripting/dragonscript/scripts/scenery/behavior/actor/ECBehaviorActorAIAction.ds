/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Scenery

pin Dragengine.ConversationSystem
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.BehaviorTreeSystem
pin Dragengine.StateMachineSystem


/**
 * \brief Behavior adding AI and Action support to actors.
 * 
 * Allows instance of BaseActorAction and BaseActorAI to be assigned to actor. During thinking
 * first the AI then the Action is called.
 * 
 * Actions allow actors to perform a specific, well defined and enclosed action like interacting
 * with an object. Actions are suitable to be used by player and computer controlled actors
 * alike. This concept allows using the same actions for both player and computer controlled
 * actors reducing coding effort, misbehavior and overall ensures that what a player can do
 * a computer controlled actor can do and vice versa.
 * 
 * AIs allow computer controlled actors to act on their own. With player controlled actors the
 * AI is skipped and the player is the AI. AI provides long running actor actions like guarding
 * a zone with the possiblity to switch between different actions depending on how the actor
 * wants to react to events. This separation makes it simple to design AI in coarse grain
 * placing actual small grained actions into reusable action instances. This separation also
 * allows actors to easily switch between player and computer controlled because both can reuse
 * the same action instances.
 * 
 * Supports these optional behaviors:
 * - ECBehaviorConversationActor: Adds conversation commands to control behavior.
 * 
 * If conversation actor is used see ECBehaviorActorAIAction.ConvoCondition and
 * ECBehaviorActorAIAction.ConvoCommand for supported syntax.
 * 
 * \see https://developer.dragondreams.ch/wiki/doku.php/dragengine:modules:dragonscript:behavior_actoraiaction
 */
class ECBehaviorActorAIAction extends DefaultECBehavior
	/** \brief Factory for creating action. */
	interface ActionFactory
		/** \brief Create action for behavior instance. */
		func BaseActorAction createAction(Instance instance)
	end
	
	/** \brief Factory for creating AI. */
	interface AIFactory
		/** \brief Create AI for behavior instance. */
		func BaseActorAI createAI(Instance instance)
	end
	
	/**
	 * \brief Behavior factory.
	 * \version 1.25
	 * 
	 * Automatically assigns these behaviors if present before adding:
	 * - Optional: \ref ECBehaviorColliderAI
	 * - Optional: \ref ECBehaviorConversationActor
	 * - Optional: \ref ECBehaviorPlayerControllable
	 * 
	 * Example XML element class:
	 * \code{.xml}
	 * <behavior type='ECBehaviorActorAIAction'>
	 *   <!-- optional: add behavior trees. default adds all behavior trees. -->
	 *   <list name='behaviorTrees'>
	 *     <string/> <!-- add behavior with empty identifier -->
	 *     <string>default</string> <!-- add behavior with 'default' identifier -->
	 *   </list>
	 * 
	 *   <!-- optional: add state machines. default adds all state machines. -->
	 *   <list name='stateMachines'>
	 *     <string/> <!-- add behavior with empty identifier -->
	 *     <string>default</string> <!-- add behavior with 'default' identifier -->
	 *   </list>
	 * </behavior>
	 * \endcode
	 * 
	 * Element class properties added by the behavior can also be set directly inside the tag
	 * be removing the property name prefix and starting with a "." (for example ".propertyName").
	 */
	class Factory extends DefaultBehaviorFactory
		func new() super("ECBehaviorActorAIAction")
		end
		
		func ECBehavior addBehavior(BehaviorElementClass eclass, String id, Dictionary parameters)
			if not id.empty()
				throw EInvalidParam.new("id is not empty string")
			end
			var ECBehaviorActorAIAction behavior = ECBehaviorActorAIAction.new(eclass,\
				ECBehaviorColliderAI.getBehaviorIn(eclass),\
				ECBehaviorConversationActor.getBehaviorIn(eclass),\
				ECBehaviorPlayerControllable.getBehaviorIn(eclass))
			
			getHelper().allBTSM(behavior.getBTSMs(), eclass, parameters)
			
			getHelper().setPropertiesFromParameters(eclass, parameters, "aiAction")
			return behavior
		end
	end
	
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		/**
		 * \brief Behavior tree action "aiAction.set".
		 * \version 1.26
		 */
		private class BTASet extends BTSMActionCondition
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func BTResult runActionShared(Object context, Dictionary parameters)
				var Object value
				
				value = parameters.getAt("action.conversationWait", null)
				if value != null
					pInstance.setActionConversationWait(value.equals("true"))
				end
				
				return BTResult.success
			end
			
			func BTResult runAction(BTContext context, Dictionary parameters)
				var Object value
				
				value = parameters.getAt("ai.behaviorTree", null)
				if value != null
					var String basePath = context.getBehaviorTree().getFilename()
					if basePath != null
						value = File.absolutePath(value cast String,\
							File.new(basePath).getParent().getPath()).getPath()
					end
					pInstance.setAI(BAAIBehaviorTree.new(pInstance.getECBehavior().\
						getLoadBehaviorTree().loadFromFile(value cast String)))
				end
				
				value = parameters.getAt("action.stateMachine", null)
				if value != null
					var String basePath = context.getBehaviorTree().getFilename()
					if basePath != null
						value = File.absolutePath(value cast String,\
							File.new(basePath).getParent().getPath()).getPath()
					end
					pInstance.setAction(BAAStateMachine.new(pInstance.getECBehavior().\
						getLoadStateMachine().loadFromFile(value cast String)))
				end
				
				return runActionShared(context, parameters)
			end
			
			func void runAction(SMContext context, Dictionary parameters)
				var Object value
				
				value = parameters.getAt("ai.behaviorTree", null)
				if value != null
					var String basePath = context.getStateMachine().getFilename()
					if basePath != null
						value = File.absolutePath(value cast String,\
							File.new(basePath).getParent().getPath()).getPath()
					end
					pInstance.setAI(BAAIBehaviorTree.new(pInstance.getECBehavior().\
						getLoadBehaviorTree().loadFromFile(value cast String)))
				end
				
				value = parameters.getAt("action.stateMachine", null)
				if value != null
					var String basePath = context.getStateMachine().getFilename()
					if basePath != null
						value = File.absolutePath(value cast String,\
							File.new(basePath).getParent().getPath()).getPath()
					end
					pInstance.setAction(BAAStateMachine.new(pInstance.getECBehavior().\
						getLoadStateMachine().loadFromFile(value cast String)))
				end
				
				runActionShared(context, parameters)
			end
		end
		
		/**
		 * \brief Behavior tree action "aiAction.update".
		 * \version 1.26
		 */
		private class BTAUpdate extends BTSMActionCondition
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func BTResult runActionShared(Object context, Dictionary parameters)
				var Object value
				
				value = parameters.getAt("ai.actionFinished", null)
				if value != null
					if value.equals("success")
						pInstance.aiActionSuccess()
						
					elif value.equals("failure")
						pInstance.aiActionFailure()
						
					else
						return BTResult.failure
					end
				end
				
				return BTResult.success
			end
		end
		
		/**
		 * \brief Behavior tree action/condition "aiAction.check".
		 * \version 1.26
		 */
		private class BTACheck extends BTSMActionCondition
			var Instance pInstance
			var String pPrefix
			
			func new(Instance instance, String prefix)
				pInstance = instance
				pPrefix = prefix
			end
			
			func BTResult runActionShared(Object context, Dictionary parameters)
				return check("", parameters) if BTResult.success\
					else (parameters.has("wait") if BTResult.running else BTResult.failure)
			end
			
			func bool evaluateConditionShared(Object context, Dictionary parameters)
				return check(pPrefix, parameters)
			end
			
			func bool check(String prefix, Dictionary parameters)
				var bool result = true
				var Object value
				
				value = parameters.getAt(prefix + "action.conversationWait", null)
				if value != null
					result &= pInstance.getActionConversationWait() == value.equals("true")
				end
				
				return result
			end
		end
		
		
		/** \brief Collider AI listener forwarding to Action/AI. */
		private class ColliderAIListener extends ECBehaviorColliderAI.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void collisionResponse(ECBehaviorColliderAI.Instance instance, \
			Collider collider, CollisionInfo info)
				if pInstance.getAction() != null
					pInstance.getAction().collisionResponse(collider, info)
				end
				if pInstance.getAI() != null
					pInstance.getAI().collision(collider, info)
				end
			end
			
			func bool canHitCollider(ECBehaviorColliderAI.Instance instance, \
			Collider collider, Collider hitCollider)
				return pInstance.getAction() != null \
					and pInstance.getAction().canHitCollider(collider, hitCollider)
			end
			
			func void colliderChanged(ECBehaviorColliderAI.Instance instance, Collider collider)
				if pInstance.getAction() != null
					pInstance.getAction().colliderChanged(collider)
				end
			end
		end
		
		/** \brief Conversation actor listener forwarding to Action/AI. */
		private class ConvoActorListener extends ECBehaviorConversationActor.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func bool wait(ECBehaviorConversationActor.Instance instance)
				if pInstance.getActionConversationWait()
					return true
					
				elif pInstance.getAction() != null
					return pInstance.getAction().conversationWait()
					
				else
					return false
				end
			end
			
			func void enterConversation(ECBehaviorConversationActor.Instance instance)
				if pInstance.getAI() != null
					pInstance.getAI().enterConversation()
				end
			end
			
			func void leaveConversation(ECBehaviorConversationActor.Instance instance)
				if pInstance.getAI() != null
					pInstance.getAI().leaveConversation()
				end
			end
		end
		
		/** \brief Forward player takes controll to Action/AI. */
		private class PlayerTakeControlListener extends ECBehaviorPlayerControllable.DefaultListener
			var Instance pInstance
			
			func new(Instance instance)
				pInstance = instance
			end
			
			func void playerTakesControl(ECBehaviorPlayerControllable.Instance instance)
				if pInstance.getAction() != null
					pInstance.getAction().playerTakesControl()
				end
				if pInstance.getAI() != null
					pInstance.getAI().playerTakesControl()
				end
			end
			
			func void playerReleasesControl(ECBehaviorPlayerControllable.Instance instance)
				if pInstance.getAction() != null
					pInstance.getAction().playerReleasesControl()
				end
				if pInstance.getAI() != null
					pInstance.getAI().playerReleasesControl()
				end
			end
		end
		
		
		
		var ECBehaviorActorAIAction pECBehavior
		var ECBehaviorColliderAI.Instance pColliderAI
		var ECBehaviorConversationActor.Instance pConversationActor
		var ECBehaviorPlayerControllable.Instance pPlayerControllable
		
		var BaseActorAction pAction
		var BaseActorAI pAI
		var bool pActionConversationWait
		
		
		/** \brief Create behavior instance. */
		func new(ECBehaviorActorAIAction ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			
			setRequiresEnterFrame(true)
			setRequiresThink(true)
		end
		
		/** \brief Dispose of behavior instance. */
		func void dispose()
			setAIAndDispose(null)
			setActionAndDispose(null)
			
			pConversationActor = null
			pPlayerControllable = null
			pColliderAI = null
			super.dispose()
		end
		
		
		
		/** \brief Initialize behavior from stub. */
		func void init(StubElement stub)
			var BehaviorElement element = getElement()
			
			if pECBehavior.getColliderAI() != null
				pColliderAI = pECBehavior.getColliderAI().instance(element)
			end
			if pECBehavior.getConversationActor() != null
				pConversationActor = pECBehavior.getConversationActor().instance(element)
			end
			if pECBehavior.getPlayerControllable() != null
				pPlayerControllable = pECBehavior.getPlayerControllable().instance(element)
			end
			
			if pColliderAI != null
				pColliderAI.addListener(ColliderAIListener.new(this))
			end
			if pConversationActor != null
				pConversationActor.addListener(ConvoActorListener.new(this))
			end
			if pPlayerControllable != null
				pPlayerControllable.addListener(PlayerTakeControlListener.new(this))
			end
			
			if pECBehavior.getBTSMs().hasAny()
				var String prefix = pECBehavior.getPrefix()
				var ECComposeBTSM.Instance btsms = ECComposeBTSM.Instance.new(element, pECBehavior.getBTSMs())
				btsms.addAllAction(prefix, "set", BTASet.new(this))
				btsms.addAllAction(prefix, "update", BTAUpdate.new(this))
				btsms.addAllActionCondition(prefix, "check", BTACheck.new(this, prefix))
			end
			
			var ActionFactory actionFactory = pECBehavior.getActionFactory()
			var String factoryName = pECBehavior.getAction().getValueOrNull(stub)
			if factoryName != null
				actionFactory = pECBehavior.getPersistency().getFactoryNamed(factoryName) cast ActionFactory
			end
			setAction(actionFactory.createAction(this))
			
			var AIFactory aiFactory = pECBehavior.getAIFactory()
			factoryName = pECBehavior.getAI().getValueOrNull(stub)
			if factoryName != null
				aiFactory = pECBehavior.getPersistency().getFactoryNamed(factoryName) cast AIFactory
			end
			setAI(aiFactory.createAI(this))
		end
		
		
		
		/** \brief Behavior. */
		func ECBehaviorActorAIAction getECBehavior()
			return pECBehavior
		end
		
		/** \brief Conversation actor if present. */
		func ECBehaviorConversationActor.Instance getConversationActor()
			return pConversationActor
		end
		
		/** \brief Player controllable actor if present. */
		func ECBehaviorPlayerControllable.Instance getPlayerControllable()
			return pPlayerControllable
		end
		
		
		
		/** \brief Active action or \em null. */
		func BaseActorAction getAction()
			return pAction
		end
		
		/**
		 * \brief Set active action or \em null.
		 * 
		 * Does not call dispose() on old action if present.
		 */
		func void setAction(BaseActorAction action)
			if action == pAction
				return
			end
			
			if pAction != null
				pAction.deactivate()
			end
			
			pAction = action
			pActionConversationWait = false
			
			if action != null
				action.activate(getElement())
			end
		end
		
		/**
		 * \brief Set active action or \em null.
		 * 
		 * Calls dispose() on old action if present.
		 */
		func void setActionAndDispose(BaseActorAction action)
			var BaseActorAction disposeAction = pAction
			
			setAction(action)
			
			if disposeAction != null
				disposeAction.dispose()
			end
		end
		
		/**
		 * \brief Action causes conversation to wait.
		 * \version 1.26
		 */
		public func bool getActionConversationWait()
			return pActionConversationWait
		end
		
		/**
		 * \brief Set if action causes conversation to wait.
		 * \version 1.26
		 * 
		 * Reset to false when action is changed.
		 */
		func void setActionConversationWait(bool wait)
			pActionConversationWait = wait
		end
		
		
		/** \brief Active AI or \em null. */
		func BaseActorAI getAI()
			return pAI
		end
		
		/**
		 * \brief Set active AI or \em null.
		 * 
		 * Does not call dispose() on old AI if present.
		 */
		func void setAI(BaseActorAI ai)
			if ai == pAI
				return
			end
			
			if pAI != null
				pAI.deactivate()
			end
			
			pAI = ai
			
			if ai != null
				ai.activate(getElement())
			end
		end
		
		/**
		 * \brief Set active AI or \em null.
		 * 
		 * Calls dispose() on old AI if present.
		 */
		func void setAIAndDispose(BaseActorAI ai)
			var BaseActorAI disposeAI = pAI
			
			setAI(ai)
			
			if disposeAI != null
				disposeAI.dispose()
			end
		end
		
		
		
		/**
		 * \brief Notify actor AI active action failed.
		 * \version 1.26
		 * 
		 * Calls actionFailed() on actor AI if all of these conditions apply:
		 * - Actor is not player controlled
		 * - Actor AI is not null
		 */
		func void aiActionFailure()
			if pPlayerControllable != null and pPlayerControllable.isPlayerControlled()
				return
			end
			if pAI != null
				pAI.actionFailed()
			end
		end
		
		/**
		 * \brief Notify actor AI this action failed.
		 * 
		 * Calls actionFinished() on actor AI if all of these conditions apply:
		 * - Actor is not player controlled
		 * - Actor AI is not null
		 */
		public func void aiActionSuccess()
			if pPlayerControllable != null and pPlayerControllable.isPlayerControlled()
				return
			end
			if pAI != null
				pAI.actionFinished()
			end
		end
		
		
		
		/** \brief Frame update started. */
		func void enterFrame()
			if pAI != null
				pAI.enterFrame()
			end
			if pAction != null
				pAction.enterFrame()
			end
		end
		
		/**
		 * \brief Think about what to do for the next frame update.
		 * 
		 * If actor is player controlled calls BaseActorAction.playerThink() and
		 * BaseActorAction.think(). If actor is not player controlled calls
		 * BaseActorAI.think() and BaseActorAction.think(). This avoids the need for
		 * AI and Action to check if they are player controlled or not.
		 */
		func void think(float elapsed)
			if pPlayerControllable.isPlayerControlled()
				if pAction != null
					pAction.playerThink(elapsed)
					pAction.think(elapsed)
				end
				
			else
				if pAI != null
					pAI.think(elapsed)
				end
				if pAction != null
					pAction.think(elapsed)
				end
			end
		end
		
		/**
		 * \brief Post thinking after physics processing is finished.
		 * 
		 * If actor is player controlled calls BaseActorAction.playerPostThink(float)
		 * and BaseActorAction.postThink(float). If actor is not player controlled only
		 * BaseActorAction.postThink(float) is called.
		 */
		func void postThink(float elapsed)
			if pAction != null
				if pPlayerControllable.isPlayerControlled()
					pAction.playerPostThink(elapsed)
				end
				pAction.postThink(elapsed)
			end
		end
		
		/** \brief Add element to game world. */
		func void addToGameWorld()
			if pAI != null
				pAI.enteredGameWorld()
			end
			if pAction != null
				pAction.enteredGameWorld()
			end
		end
		
		/** \brief Remove element from game world. */
		func void removeFromGameWorld()
			if pAI != null
				pAI.leftGameWorld()
			end
			if pAction != null
				pAction.leftGameWorld()
			end
		end
		
		
		
		/** \brief Read behavior from file. */
		func void readFromFile(PersistencyEnvironment env, FileReader reader)
			select reader.readByte() // version
			case 0
				readFromFileV0(env, reader)
				
			case 1
				var PersistencyFlags flags = PersistencyFlags.new(1, reader)
				pActionConversationWait = flags.getAt(0)
				
				readFromFileV0(env, reader)
				
			else
				throw EInvalidParam.new("unsupported version")
			end
		end
		
		private func void readFromFileV0(PersistencyEnvironment env, FileReader reader)
			var BehaviorElement element = getElement()
			
			pAI = env.readObjectOrNull(reader) cast BaseActorAI
			if pAI != null
				pAI.activateWhileLoading(element)
			end
			
			pAction = env.readObjectOrNull(reader) cast BaseActorAction
			if pAction != null
				pAction.activateWhileLoading(element)
			end
		end
		
		/** \brief Write behavior to file. */
		func void writeToFile(PersistencyEnvironment env, FileWriter writer)
			writer.writeByte(1)
			
			var PersistencyFlags flags = PersistencyFlags.new(1)
			flags.setAt(0, pActionConversationWait)
			flags.writeToFile(writer)
			
			env.writeObjectOrNull(writer, pAI)
			env.writeObjectOrNull(writer, pAction)
		end
	end
	
	
	
	var ECBehaviorColliderAI pColliderAI
	var ECBehaviorConversationActor pConversationActor
	var ECBehaviorPlayerControllable pPlayerControllable
	var ECComposeBTSM pBTSMs
	var ActionFactory pActionFactory
	var AIFactory pAIFactory
	var ECPString pAction, pAI
	var String pPrefix
	var LoadBehaviorTree pLoadBehaviorTree
	var LoadStateMachine pLoadStateMachine
	var Persistency pPersistency
	
	
	
	/**
	 * \brief Create behavior element class.
	 * 
	 * These values are optional and can be null: \em colliderAI, \em conversationActor,
	 * \em playerControllable.
	 * 
	 * The default Action and AI factory are BAAFirstPerson and BAAIBehaviorTree.
	 */
	func new(BehaviorElementClass eclass, ECBehaviorColliderAI colliderAI, \
	ECBehaviorConversationActor conversationActor, ECBehaviorPlayerControllable playerControllable) \
	this(eclass, colliderAI, conversationActor, playerControllable, "aiAction.")
	end
	
	func new(BehaviorElementClass eclass, ECBehaviorColliderAI colliderAI, \
	ECBehaviorConversationActor conversationActor, ECBehaviorPlayerControllable playerControllable, \
	String prefix) super(eclass)
		pPrefix = prefix
		pColliderAI = colliderAI
		pConversationActor = conversationActor
		pPlayerControllable = playerControllable
		pActionFactory = BAAFirstPerson.Factory.new()
		pAIFactory = BAAIBehaviorTree.Factory.new()
		pBTSMs = ECComposeBTSM.new()
		
		var Loaders loaders = eclass.sharedFactoryHelper.defaultLoaders()
		if loaders != null
			pLoadBehaviorTree = loaders.getBehaviorTree()
			pLoadStateMachine = loaders.getStateMachine()
			pPersistency = loaders.getPersistency()
		end
		
		eclass.addProperty(pAction = ECPString.new(prefix + "action", null))
		eclass.addProperty(pAI = ECPString.new(prefix + "ai", null))
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	func void dispose()
		pColliderAI = null
		pConversationActor = null
		pPlayerControllable = null
		pActionFactory = null
		pAIFactory = null
		pBTSMs = null
		super.dispose()
	end
	
	
	
	/**
	 * \brief Prefix.
	 * \version 1.26
	 */
	func String getPrefix()
		return pPrefix
	end
	
	/** \brief Collider AI behavior or null. */
	func ECBehaviorColliderAI getColliderAI()
		return pColliderAI
	end
	
	/** \brief Set collider AI behavior or null. */
	func void setColliderAI(ECBehaviorColliderAI colliderAI)
		pColliderAI = colliderAI
	end
	
	/** \brief Conversation actor behavior or null. */
	func ECBehaviorConversationActor getConversationActor()
		return pConversationActor
	end
	
	/**
	 * \brief Set conversation actor behavior or null.
	 * 
	 * If set conversation conditions and commands are added.
	 */
	func void setConversationActor(ECBehaviorConversationActor conversationActor)
		pConversationActor = conversationActor
	end
	
	/** \brief Player controllable behavior or null. */
	func ECBehaviorPlayerControllable getPlayerControllable()
		return pPlayerControllable
	end
	
	/** \brief Set player controllable behavior or null. */
	func void setPlayerControllable(ECBehaviorPlayerControllable playerControllable)
		pPlayerControllable = playerControllable
	end
	
	/** \brief Action factory. */
	func ActionFactory getActionFactory()
		return pActionFactory
	end
	
	/** \brief Set action factory. */
	func void setActionFactory(ActionFactory factory)
		if factory == null
			throw ENullPointer.new()
		end
		pActionFactory = factory
	end
	
	/**
	 * \brief Action to use instead of factory action.
	 * \version 1.26
	 */
	func ECPString getAction()
		return pAction
	end
	
	/** \brief AI factory. */
	func AIFactory getAIFactory()
		return pAIFactory
	end
	
	/** \brief Set AI factory. */
	func void setAIFactory(AIFactory factory)
		if factory == null
			throw ENullPointer.new()
		end
		pAIFactory = factory
	end
	
	/**
	 * \brief AI to use instead of factory AI.
	 * \version 1.26
	 */
	func ECPString getAI()
		return pAI
	end
	
	/**
	 * \brief Compose behavior trees and state machines.
	 * \version 1.26
	 */
	func ECComposeBTSM getBTSMs()
		return pBTSMs
	end
	
	/**
	 * \brief Behavior tree loader.
	 * \version 1.26
	 */
	func LoadBehaviorTree getLoadBehaviorTree()
		return pLoadBehaviorTree
	end
	
	/**
	 * \brief Set behavior tree loader.
	 * \version 1.26
	 */
	func void setLoadBehaviorTree(LoadBehaviorTree loader)
		if loader == null
			throw ENullPointer.new("loader")
		end
		pLoadBehaviorTree = loader
	end
	
	/**
	 * \brief State machine loader.
	 * \version 1.26
	 */
	func LoadStateMachine getLoadStateMachine()
		return pLoadStateMachine
	end
	
	/**
	 * \brief Set state machine loader.
	 * \version 1.26
	 */
	func void setLoadStateMachine(LoadStateMachine loader)
		if loader == null
			throw ENullPointer.new("loader")
		end
		pLoadStateMachine = loader
	end
	
	/**
	 * \brief Persistency used to create action.
	 * \version 1.26
	 */
	func Persistency getPersistency()
		return pPersistency
	end
	
	/**
	 * \brief Set persistency used to create action.
	 * \version 1.26
	 */
	func void setPersistency(Persistency persistency)
		if persistency == null
			throw ENullPointer.new()
		end
		pPersistency = persistency
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorActorAIAction.
	 */
	static func ECBehaviorActorAIAction getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block ECBehaviorActorAIAction each
			return true
		end) cast ECBehaviorActorAIAction
	end
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type ECBehaviorActorAIAction.Instance .
	 */
	static func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	
	
	/** \brief Create Behavior instance. */
	func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
