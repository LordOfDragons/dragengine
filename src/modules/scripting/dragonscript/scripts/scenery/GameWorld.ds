/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland PlÃ¼ss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.Scenery

pin Dragengine.ConversationSystem
pin Dragengine.Gui
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils


/**
 * \brief Game world.
 * 
 * A single simulation unit in the game. Simple games require usually only one game world
 * containing all elements of the currently loaded map or level. More complex games can use
 * multiple game worlds for example to simulate development of the game world outside the
 * currently visible section. Every game world wraps an engine world resource. Elements
 * removed from the game world using removeElement are not disposed of. This allows to move
 * elements between game worlds. Elements removed using removeAllElements though get disposed.
 */
class GameWorld
	private var World pWorld
	private var SafeArray pElements
	private var SafeArray pThinkers
	private var Dictionary pElementIDMap
	private var Array pEnterFrameOnce
	private var LayerMask pLayerMaskOneShotSpeaker
	private var Array pOneShotSpeakers
	private var SpawnPointList pSpawnPoints
	private var TemporaryDecalManager pTempDecalMgr
	private var CachedVegetation pCachedVegetation
	private var UniqueID pNextObjectID
	private var UniqueIDRemapper pUniqueIDRemapper
	private var Vector pGravity
	private var RailList pRailList
	private var ConversationActorList pConvoActorList
	private var ConversationCoordSystemList pConvoCoordSystemList
	private var AnchoredElementList pAnchoredElementList
	
	
	
	/** \brief Create game world. */
	public func new()
		this.pWorld = World.new()
		
		this.pElements = SafeArray.new()
		this.pThinkers = SafeArray.new()
		this.pElementIDMap = Dictionary.new()
		this.pEnterFrameOnce = Array.new()
		this.pLayerMaskOneShotSpeaker = LayerMask.newWith( BaseGameApp.WorldLayerBit.audio )
		this.pOneShotSpeakers = Array.new()
		this.pSpawnPoints = SpawnPointList.new()
		this.pTempDecalMgr = TemporaryDecalManager.new()
		this.pUniqueIDRemapper = UniqueIDRemapper.new()
		this.pGravity = Vector.new( 0.0, -9.81, 0.0 )
		this.pRailList = RailList.new()
		this.pConvoActorList = ConversationActorList.new()
		this.pConvoCoordSystemList = ConversationCoordSystemList.new()
		this.pAnchoredElementList = AnchoredElementList.new()
	end
	
	
	
	/** \brief Dispose of game world. */
	public func void dispose()
		/*
		// for debugging purpose this code here can be used
		this.removeAllElements()
		if this.pThinkers != null
			this.pThinkers.forEach( block Element each
				BaseGameApp.getApp().getConsole().addMessage( "GameWorld.dispose: Object of class " + each.className() + " left behind thinker" )
			end )
		end
		*/
		
		this.pElements.removeAll( block Element each
			each.setGameWorld( null )
			each.safeDispose()
		end )
		this.pElementIDMap = null
		
		this.pThinkers = null
		this.pEnterFrameOnce = null
		
		if this.pCachedVegetation != null
			this.pCachedVegetation.setWorld( null )
			this.pCachedVegetation = null
		end
		
		this.pConvoActorList = null
		this.pConvoCoordSystemList = null
		this.pAnchoredElementList = null
		this.pTempDecalMgr = null
		this.pOneShotSpeakers = null
		this.pSpawnPoints = null
		
		if this.pRailList != null
			this.pRailList.dispose()
			this.pRailList = null
		end
		
		this.pWorld = null
	end
	
	
	
	/** \brief Engine world resource. */
	public func World getWorld()
		return this.pWorld
	end
	
	
	
	/** \brief Add Element. */
	public func void addElement( Element element )
		if element.getGameWorld() == this
			return // adding more than once is allowed to make scripts simpler and more robust
		end
		if element.getGameWorld() != null
			throw EInvalidParam.new()
		end
		
		this.pElements.add( element )
		element.setGameWorld( this )
		this.pElementIDMap.setAt( element.getID(), element )
	end
	
// 	public static var RuntimeMeter rtm = null
	/** \brief Remove Element without disposing it. */
	public func void removeElement( Element element )
		/*if rtm != null
			rtm.reset( 0, 1 )
			
			this.pElements.remove( element )
			rtm.counterAdd( 0, 6 )
			
			element.setGameWorld( null )
			rtm.counterAdd( 0, 7 )
			
			if this.pThinkers.has( element )
				BaseGameApp.getApp().getConsole().addMessage( "GameWorld.removeElement: Object of class " + element.className() + " left behind thinker" )
				this.pThinkers.remove( element )
			end
			var int index = this.pEnterFrameOnce.indexOf( element )
			if index != -1
				BaseGameApp.getApp().getConsole().addMessage( "GameWorld.removeElement: Object of class " + element.className() + " left behind enter frame once" )
				this.pEnterFrameOnce.removeFrom( index )
			end
			rtm.counterAdd( 0, 8 )
			return
		end*/
		
		this.pElements.remove( element )
		element.setGameWorld( null )
		this.pElementIDMap.removeIfExisting( element.getID() )
		
		if this.pThinkers.has( element )
			BaseGameApp.getApp().getConsole().addMessage( "GameWorld.removeElement: Object of class " + element.className() + " left behind thinker" )
			this.pThinkers.remove( element )
		end
		
		var int index = this.pEnterFrameOnce.indexOf( element )
		if index != -1
			BaseGameApp.getApp().getConsole().addMessage( "GameWorld.removeElement: Object of class " + element.className() + " left behind enter frame once" )
			this.pEnterFrameOnce.removeFrom( index )
		end
	end
	
	/** \brief Remove all elements calling safeDispose() on them. */
	public func void removeAllElements()
		this.pElements.removeAll( block Element each
			each.setGameWorld( null )
			each.safeDispose()
		end )
		this.pElementIDMap.removeAll()
	end
	
	/** \brief Element is present. */
	public func bool hasElement( Element element )
		return this.pElements.has( element )
	end
	
	/** \brief Element with identifier or \em null if absent. */
	public func Element getElementWithID( UniqueID id )
		return this.pElementIDMap.getAt( id, null ) cast Element
// 		return this.pElements.find( block Element each
// 			return each.getID().equals( id )
// 		end ) cast Element
	end
	
	/** \brief Visit elements. */
	public func void visitElements( ElementVisitor visitor )
		this.pElements.forEach( block Element each
			each.visit( visitor )
		end )
	end
	
	/** \brief Visit elements with block with parameter Element. */
	public func void forEachElement( Block ablock )
		this.pElements.forEach( ablock )
	end
	
	/** \brief Find element with block with parameter Element. */
	public func Element findElement( Block ablock )
		return this.pElements.find( ablock ) cast Element
	end
	
	
	
	/** \brief Add element requiring thinking on frame updates. */
	public func void addThinker( Element element )
		if element == null
			throw EInvalidParam.new()
		end
		this.pThinkers.addIfAbsent( element )
	end
	
	/** \brief Remove element requiring thinking on frame updates. */
	public func void removeThinker( Element element )
		this.pThinkers.removeIfPresent( element )
	end
	
	/** \brief Visit thinkers with block with parameter Element. */
	public func void forEachThinker( Block ablock )
		this.pThinkers.forEach( ablock )
	end
	
	
	
	/** \brief Call Element.enterFrame() on element once at start of next frame update. */
	public func void addEnterFrameOnce( Element element )
		if element == null
			throw EInvalidParam.new()
		end
		if not this.pEnterFrameOnce.has( element )
			this.pEnterFrameOnce.add( element )
		end
	end
	
	/** \brief Stop call Element.enterFrame() on element once at start of next frame update. */
	public func void removeEnterFrameOnce( Element element )
		var int index = this.pEnterFrameOnce.indexOf( element )
		if index != -1
			this.pEnterFrameOnce.removeFrom( index )
		end
	end
	
	
	
	/** \brief Cached vegetation. */
	public func CachedVegetation getCachedVegetation()
		return this.pCachedVegetation
	end
	
	/** \brief Set cached vegetation. */
	public func void setCachedVegetation( CachedVegetation cachedVegetation )
		this.pCachedVegetation = cachedVegetation
		if cachedVegetation != null
			cachedVegetation.setWorld( this.pWorld )
		end
	end
	
	/** \brief Gravity. */
	public func Vector getGravity()
		return this.pGravity
	end
	
	/** \brief Set gravity. */
	public func void setGravity( Vector gravity )
		if gravity == null
			throw EInvalidParam.new()
		end
		this.pGravity = gravity
	end
	
	
	
	/** \brief Layer mask to use for one shot speaker. */
	public func LayerMask getLayerMaskOneShotSpeaker()
		return pLayerMaskOneShotSpeaker
	end
	
	/** \brief Set layer mask to use for one shot speaker. */
	public func void setLayerMaskOneShotSpeaker( LayerMask layerMask )
		if layerMask == null
			throw EInvalidParam.new()
		end
		pLayerMaskOneShotSpeaker = layerMask
	end
	
	/**
	 * \brief Add one shot speaker.
	 * \details Plays a short sound once and will be deleted once finished playing.
	 */
	public func void addOneShotSpeaker( Speaker speaker )
		speaker.setLooping( false ) // just in case somebody forgot it
		speaker.play()
		this.pWorld.addSpeaker( speaker )
		this.pOneShotSpeakers.add( speaker )
	end
	
	/**
	 * \brief Play sound once.
	 * \details Creates a speaker with the sound and parameters playing it once.
	 *          If sound it \em null nothing is done.
	 */
	public func void playSoundOnce( Sound sound, Element element, float volume, float range )
		this.playSoundOnce( sound, element.getPosition(), element.getOrientation(), volume, range )
	end
	
	/**
	 * \brief Play sound once.
	 * \details Creates a speaker with the sound and parameters playing it once.
	 *          If sound it \em null nothing is done.
	 */
	public func void playSoundOnce( Sound sound, DVector position, float volume, float range )
		this.playSoundOnce( sound, position, Quaternion.new(), volume, range )
	end
	
	/**
	 * \brief Play sound once.
	 * \details Creates a speaker with the sound and parameters playing it once.
	 *          If sound it \em null nothing is done.
	 */
	public func void playSoundOnce( Sound sound, DVector position, Quaternion orientation, \
	float volume, float range )
		if sound == null
			return
		end
		
		var Speaker speaker = Speaker.new()
		speaker.setSound( sound )
		speaker.setPosition( position )
		speaker.setOrientation( orientation )
		speaker.setLooping( false )
		speaker.setVolume( volume )
		speaker.setRange( range )
		speaker.setLayerMask( this.pLayerMaskOneShotSpeaker )
		speaker.play()
		
		this.pWorld.addSpeaker( speaker )
		this.pOneShotSpeakers.add( speaker )
	end
	
	/**
	 * \brief Play synthesizer instance once.
	 * \details Creates speaker with synthesizer instance and parameters playing it once.
	 *          If synthesizer is \em null nothing is done.
	 */
	public func void playSynthesizerOnce( SynthesizerInstance synthesizer, Element element, \
	float volume, float range )
		this.playSynthesizerOnce( synthesizer, element.getPosition(), \
			element.getOrientation(), volume, range )
	end
	
	/**
	 * \brief Play synthesizer instance once.
	 * \details Creates speaker with synthesizer instance and parameters playing it once.
	 *          If synthesizer is \em null nothing is done.
	 */
	public func void playSynthesizerOnce( SynthesizerInstance synthesizer, DVector position, \
	float volume, float range )
		this.playSynthesizerOnce( synthesizer, position, Quaternion.new(), volume, range )
	end
	
	/**
	 * \brief Play synthesizer instance once.
	 * \details Creates speaker with synthesizer instance and parameters playing it once.
	 *          If synthesizer is \em null nothing is done.
	 */
	public func void playSynthesizerOnce( SynthesizerInstance synthesizer, DVector position, \
	Quaternion orientation, float volume, float range )
		if synthesizer == null
			return
		end
		
		var Speaker speaker = Speaker.new()
		
		speaker.setSynthesizer( synthesizer )
		speaker.setPosition( position )
		speaker.setOrientation( orientation )
		speaker.setLooping( false )
		speaker.setVolume( volume )
		speaker.setRange( range )
		speaker.setLayerMask( this.pLayerMaskOneShotSpeaker )
		speaker.play()
		
		this.pWorld.addSpeaker( speaker )
		this.pOneShotSpeakers.add( speaker )
	end
	
	/**
	 * \brief Remove one shot speakers that finished playing.
	 */
	public func void removeFinishedOneShotSpeakers()
		this.pOneShotSpeakers.removeIf( block Speaker each
			if each.getStopped()
				this.pWorld.removeSpeaker( each )
				return true
				
			else
				return false
			end
		end )
	end
	
	/**
	 * \brief Stop and remove all one shot speakers.
	 */
	public func void removeAllOneShotSpeakers()
		this.pOneShotSpeakers.forEach( block Speaker each
			each.stop()
			this.pWorld.removeSpeaker( each )
		end )
		this.pOneShotSpeakers.removeAll()
	end
	
	
	
	/** \brief Make elements think. */
	public func void think( float elapsed )
		this.pThinkers.forEach( block Element each
			each.think( elapsed )
		end )
	end
	
	/** \brief Make elements think after physics simulation. */
	public func void postThink( float elapsed )
		this.pThinkers.forEach( block Element each
			each.postThink( elapsed )
		end )
	end
	
	/**
	 * \brief Call Element.enterFrame() on registered elements then unregister all elements.
	 * \warning Do not call addEnterFrameOnce or removeEnterFrameOnce from inside enterFrame().
	 */
	public func void enterFrame()
		if this.pEnterFrameOnce.getCount() == 0
			return
		end
		
		this.pEnterFrameOnce.forEach( block Element each
			each.enterFrame()
		end )
		this.pEnterFrameOnce.removeAll()
	end
	
	/**
	 * \brief Update game world.
	 * \details Call once at beginning of frame update cycle. Afterwards world can
	 *          be rendered multiple times.
	 */
	public func void update( float elapsed )
		this.enterFrame()
		
		this.pWorld.update( elapsed )
		this.pTempDecalMgr.update( elapsed )
		
		this.think( elapsed )
		this.pWorld.detectCollisions( elapsed )
		this.postThink( elapsed )
		
		this.removeFinishedOneShotSpeakers()
	end
	
	/** \brief Update game world with debug timing. */
	public func void updateDebug( float elapsed, \
			Debugging.DebugPerformanceCounter debugUpdate, \
			Debugging.DebugPerformanceCounter debugThink, \
			Debugging.DebugPerformanceCounter debugCollisions, \
			Debugging.DebugPerformanceCounter debugPostThink )
		this.enterFrame()
		
		debugUpdate.start()
		this.pWorld.update( elapsed )
		debugUpdate.stop()
		this.pTempDecalMgr.update( elapsed )
		
		debugThink.start()
		this.think( elapsed )
		debugThink.stop()
		debugCollisions.start()
		this.pWorld.detectCollisions( elapsed )
		debugCollisions.stop()
		debugPostThink.start()
		this.postThink( elapsed )
		debugPostThink.stop()
		
		this.removeFinishedOneShotSpeakers()
	end
	
	
	
	/** \brief Next object ID from world file required for saving and remapping ID ranges. */
	public func UniqueID getNextObjectID()
		return this.pNextObjectID
	end
	
	/** \brief Set next object ID from world file required for saving and remapping ID ranges. */
	public func void setNextObjectID( UniqueID nextObjectID )
		this.pNextObjectID = nextObjectID
	end
	
	/** \brief Unique ID remapper. */
	public func UniqueIDRemapper getUniqueIDRemapper()
		return this.pUniqueIDRemapper
	end
	
	/** \brief Set Unique ID remapper. */
	public func void setUniqueIDRemapper( UniqueIDRemapper remapper )
		if remapper == null
			throw EInvalidParam.new()
		end
		this.pUniqueIDRemapper = remapper
	end
	
	
	
	/** \brief Read from file. */
	public func void readFromFile( PersistencyEnvironment env, FileReader reader )
	end
	
	/** \brief Write to file. */
	public func void writeToFile( PersistencyEnvironment env, FileWriter writer )
	end
	
	/**
	 * \brief Read element stubs from file.
	 * 
	 * Reads first the next object ID and Unique ID remapper. Then remaps the stub elements
	 * in the stub world. Then reads the element stubs.
	 */
	public func void readElementStubsFromFile( PersistencyEnvironment env, FileReader reader, \
	MutableID idGenerator, ElementClassList elementClassList, StubGameWorld stubWorld )
		select reader.readByte() // version
		case 0
			var UniqueID lastNextObjectID = UniqueID.readFromFile( reader )
			this.pUniqueIDRemapper = UniqueIDRemapper.readFromFile( reader )
			
			var UniqueID nextObjectID = stubWorld.getNextObjectID()
			if nextObjectID > lastNextObjectID
				stubWorld.setNewElementsRange( this.pUniqueIDRemapper.add( \
					lastNextObjectID, nextObjectID, idGenerator.toUniqueID() ) )
				idGenerator.incrementBy( nextObjectID - lastNextObjectID )
			end
			
			stubWorld.remapObjectIDs( this.pUniqueIDRemapper )
			stubWorld.readElementStubsFromFile( env, reader, elementClassList )
			
		else
			throw EInvalidParam.new( "unsupported version" )
		end
	end
	
	/**
	 * \brief Write element stubs to file.
	 * 
	 * Writes first the next object ID and Unique ID remapper. While loading the next object
	 * ID and mapper can be used to modify remapping in case the object ID range of the
	 * world changed in the mean time.
	 * 
	 * Ignores elements with element classes marked not persistable. For each element the
	 * element stub is written used to load or create the element.
	 * 
	 * Not persistable elements have to be loaded from a world file even if using a file reader. 
	 * 
	 * The two methods writeElementStubsToFile() and writeDynamicElementDataToFile() are
	 * both required to properly restore elements. They are not merged in one to allow
	 * preloading. For this to work element stubs have to be read first to start preloading.
	 * Once done the elements are created and their dynamic data read. The order of stubs
	 * has to stay the same for them to properly read their dynamic data.
	 */
	public func void writeElementStubsToFile( PersistencyEnvironment env, FileWriter writer )
		writer.writeByte( 0 ) // version
		
		this.pNextObjectID.writeToFile( writer )
		this.pUniqueIDRemapper.writeToFile( writer )
		
		this.pElements.forEach( block Element each
			if each.getClass().getPersistable()
				each.getStub().writeToFile( writer )
			end
		end )
		writer.writeString8( "" ) // empty classname is end of list marker
	end
	
	/**
	 * \brief Write dynamic element data to file.
	 * 
	 * Ignores elements with element classes marked not persistable. For each element the
	 * Element.writeToFile() method is called to save dynamic parameters.
	 * 
	 * The two methods writeElementStubsToFile() and writeDynamicElementDataToFile() are
	 * both required to properly restore elements. They are not merged in one to allow
	 * preloading. For this to work element stubs have to be read first to start preloading.
	 * Once done the elements are created and their dynamic data read. The order of stubs
	 * has to stay the same for them to properly read their dynamic data.
	 */
	public func void writeDynamicElementDataToFile( PersistencyEnvironment env, FileWriter writer )
		if env.getPersistency().getDebugEnableCanaryValue()
			this.pElements.forEach( block Element each
				if each.getClass().getPersistable()
					writer.writeString( Persistency.debugCanaryValue_BEGIN )
					each.writeToFile( env, writer )
					writer.writeString( Persistency.debugCanaryValue_END )
				end
			end )
			
		else
			this.pElements.forEach( block Element each
				if each.getClass().getPersistable()
					each.writeToFile( env, writer )
				end
			end )
		end
	end
	
	
	
	/** \brief Spawn point list. */
	public func SpawnPointList getSpawnPoints()
		return this.pSpawnPoints
	end
	
	/** \brief Rail list. */
	public func RailList getRailList()
		return this.pRailList
	end
	
	/** \brief Conversation actor list. */
	public func ConversationActorList getConvoActorList()
		return this.pConvoActorList
	end
	
	/** \brief Conversation coordinate system list. */
	public func ConversationCoordSystemList getConvoCoordSystemList()
		return this.pConvoCoordSystemList
	end
	
	/** \brief Anchored element list. */
	public func AnchoredElementList getAnchoredElementList()
		return this.pAnchoredElementList
	end
	
	
	
	/** \brief Temporary decal manager. */
	public func TemporaryDecalManager getTemporaryDecalManager()
		return this.pTempDecalMgr
	end
	
	/** \brief Set temporary decal manager. */
	public func void setTemporaryDecalManager( TemporaryDecalManager manager )
		if manager == null
			throw EInvalidParam.new()
		end
		this.pTempDecalMgr = manager
	end
	
	
	
	/**
	 * \brief Collect debug statistics.
	 * 
	 * Returns a dictionary with these values:
	 * - 'elements': Number of elements.
	 * - 'thinkers': Number of thinking elements.
	 * - 'enterFrameOnce': Number of enter frame once elements.
	 * - 'oneShotSpeakers': Number of one shot speakers.
	 * - 'spawnPoints': Number of spawn points.
	 * - 'temporaryDecals': Number of temporary decals.
	 * - 'resources.camera': Number of camera resources.
	 * - 'resources.speaker': Number of speaker resources.
	 * - 'resources.microphone': Number of microphone resources.
	 * - 'resources.networkState': Number of network state resources.
	 * - 'resources.component': Number of component resources.
	 * - 'resources.light': Number of light resources.
	 * - 'resources.collider': Number of collider resources.
	 * - 'resources.lumimeter': Number of lumimeter resources.
	 * - 'resources.touchSensor': Number of touch sensor resources.
	 * - 'resources.propField': Number of prop field resources.
	 * - 'resources.forceField': Number of force field resources.
	 * - 'resources.particleEmitter': Number of particle emitter instance resources.
	 * - 'resources.debugDrawer': Number of debug drawer resources.
	 * - 'resources.navigationSpace': Number of navigation space resources.
	 * - 'resources.navigationBlocker': Number of navigation blocker resources.
	 * - 'resources.navigator': Number of navigator resources.
	 * - 'resources.envMapProbe': Number of environment map probe resources.
	 */
	public func Dictionary debugStatistics()
		var Dictionary stats = Dictionary.new()
		stats.setAt( "elements", this.pElements.getCount() )
		stats.setAt( "thinkers", this.pThinkers.getCount() )
		stats.setAt( "enterFrameOnce", this.pEnterFrameOnce.getCount() )
		stats.setAt( "oneShotSpeakers", this.pOneShotSpeakers.getCount() )
		stats.setAt( "spawnPoints", this.pSpawnPoints.getCount() )
		stats.setAt( "rails", this.pRailList.getCount() )
		stats.setAt( "temporaryDecals", this.pTempDecalMgr.getCount() )
		stats.setAt( "resources.camera", this.pWorld.getCameraCount() )
		stats.setAt( "resources.speaker", this.pWorld.getSpeakerCount() )
		stats.setAt( "resources.microphone", this.pWorld.getMicrophoneCount() )
		stats.setAt( "resources.networkState", this.pWorld.getNetworkStateCount() )
		stats.setAt( "resources.component", this.pWorld.getComponentCount() )
		stats.setAt( "resources.light", this.pWorld.getLightCount() )
		stats.setAt( "resources.collider", this.pWorld.getColliderCount() )
		stats.setAt( "resources.lumimeter", this.pWorld.getLumimeterCount() )
		stats.setAt( "resources.touchSensor", this.pWorld.getTouchSensorCount() )
		stats.setAt( "resources.propField", this.pWorld.getPropFieldCount() )
		stats.setAt( "resources.forceField", this.pWorld.getForceFieldCount() )
		stats.setAt( "resources.particleEmitter", this.pWorld.getParticleEmitterCount() )
		stats.setAt( "resources.debugDrawer", this.pWorld.getDebugDrawerCount() )
		stats.setAt( "resources.navigationSpace", this.pWorld.getNavigationSpaceCount() )
		stats.setAt( "resources.navigationBlocker", this.pWorld.getNavigationBlockerCount() )
		stats.setAt( "resources.navigator", this.pWorld.getNavigatorCount() )
		stats.setAt( "resources.envMapProbe", this.pWorld.getEnvMapProbeCount() )
		return stats
	end
	
	/**
	 * \brief Retrieves some summary statistics.
	 * 
	 * Returns a dictionary with these values.
	 * - 'thinkers': Dictionary containing as keys the element class name of all
	 *               thinkers with value occurance count.
	 * - 'enterFrameOnce': Dictionary containing as keys the element class name of all
	 *                     enter frame once with value occurance count.
	 */
	public func Dictionary debugStatisticsSummary()
		var Dictionary summary, stats = Dictionary.new()
		var String key
		
		summary = Dictionary.new()
		this.pThinkers.forEach( block Element each
			key = each.getClass().getClassname()
			summary.setAt( key, summary.getAt( key, 0 ) cast int + 1 )
		end )
		stats.setAt( "thinkers", summary )
		
		summary = Dictionary.new()
		this.pEnterFrameOnce.forEach( block Element each
			key = each.getClass().getClassname()
			summary.setAt( key, summary.getAt( key, 0 ) cast int + 1 )
		end )
		stats.setAt( "enterFrameOnce", summary )
		
		return stats
	end
	
	/** \brief Print statistics to console. */
	public func void debugPrintStatistics( Console console )
		var Dictionary stats = this.debugStatistics()
		stats.getKeys().sorted().forEach( block String key
			console.addMessage( "- " + key + ": " + stats.getAt( key ) )
		end )
	end
	
	/** \brief Print statistics summary to console. */
	public func void debugPrintStatisticsSummary( Console console )
		var Dictionary summary, stats = this.debugStatisticsSummary()
		stats.getKeys().sorted().forEach( block String key
			console.addMessage( "- " + key + ":" )
			summary = stats.getAt( key ) cast Dictionary
			summary.getKeys().sorted().forEach( block String key2
				console.addMessage( "  - " + key2 + ": " + summary.getAt( key2 ) )
			end )
		end )
	end
end
