/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

namespace Dragengine.Services

pin Dragengine.Services.Stats
pin Dragengine.Services.User
pin Dragengine.Utils
pin Dragengine.Gui


/**
 * \brief Service providing access to EOS SDK functionality if supported.
 * \version 1.23
 * 
 * Provides access to these functionalities:
 * - Stats/Achievements
 * - User
 */
class ServiceEos implements ServiceStats, ServiceUser, ServiceListener
	/**
	 * \brief Service initialization parameters.
	 */
	class InitParameters
		/**
		 * \brief Name of the product using the Epic Online Services SDK.
		 * 
		 * Name is required to be non-empty and at maximum of 64 bytes long.
		 * Name can consist of the following characters:
		 * A-Z, a-z, 0-9, dot, underscore, space, exclamation mark, question mark,
		 * and sign, hyphen, parenthesis, plus, minus, colon.
		 */
		public var String productName
		
		/**
		 * \brief Product version of the running application.
		 * 
		 * Version is required to be non-empty and at maximum of 64 bytes long.
		 * Version can consist of the following characters:
		 * A-Z, a-z, 0-9, dot, underscore, space, exclamation mark, question mark,
		 * and sign, hyphen, parenthesis, plus, minus, colon.
		 */
		public var String productVersion
		
		/**
		 * \brief Product ID as found on the dev portal.
		 * 
		 * Max length is 64.
		 */
		public var String productId
		
		/**
		 * \brief Sandbox ID as found on the dev portal.
		 * 
		 * Max length is 64.
		 */
		public var String sandboxId
		
		/**
		 * \brief Deployment ID as found on the dev portal.
		 * 
		 * Max length is 64.
		 */
		public var String deploymentId
		
		/**
		 * \brief Client ID of service permissions entry.
		 * 
		 * Use null if no service permissions are used. Max length is 64.
		 */
		public var String clientId
		
		/**
		 * \brief Client secret for accessing the set of permissions.
		 * 
		 * Use null if no service permissions are used. Max length is 64.
		 */
		public var String clientSecret
		
		/**
		 * \brief Enable support for friends list.
		 * 
		 * Requires friends-list scope to be enabled in DevPortal before it can be used.
		 */
		public var bool enableFriendsList
		
		/**
		 * \brief Enable support for presence.
		 * 
		 * Requires presence scope to be enabled in DevPortal before it can be used.
		 */
		public var bool enablePresence
		
		/**
		 * \brief Server mode.
		 */
		public var bool isServer
		
		
		
		/**
		 * \brief Create new instance of class InitParameters.
		 */
		func new()
		end
	end
	
	/**
	 * \brief Authentification provider identifier.
	 */
	static public fixed var String authProviderId = "epic"
	
	
	
	var Service pService
	var InitParameters pInitParameters
	var ServiceInitListener pInitListener
	var Boolean pIsInitialized
	var SafeArray pStatsListeners, pUserListeners
	var BaseServiceConversions pConversions
	var Dictionary pOnceListeners
	
	
	/**
	 * \brief Create service ServiceEosSdk.
	 * 
	 * The service is asynchronously initialized. Once initialization finished
	 * the provided listener is called with success or failure outcome. If the
	 * outcome is success the service can be used. If the outcome is failure
	 * the service can not be used and should be disposed.
	 * 
	 * \param parameters Init parameters.
	 * \param listener Listener called if initialization finished. Can be null.
	 * \throws EInvalidParam EOS SDK service module is not present.
	 * \throws EInvalidAction EOS SDK can not be initialized.
	 */
	func new(InitParameters parameters, ServiceInitListener listener)
		pInitParameters = InitParameters.new()
		pInitParameters.productName = parameters.productName
		pInitParameters.productVersion = parameters.productVersion
		pInitParameters.productId = parameters.productId
		pInitParameters.sandboxId = parameters.sandboxId
		pInitParameters.deploymentId = parameters.deploymentId
		pInitParameters.clientId = parameters.clientId
		pInitParameters.clientSecret = parameters.clientSecret
		pInitParameters.enableFriendsList = parameters.enableFriendsList
		pInitParameters.enablePresence = parameters.enablePresence
		pInitParameters.isServer = parameters.isServer
		pInitListener = listener
		
		pStatsListeners = SafeArray.new()
		pUserListeners = SafeArray.new()
		pConversions = BaseServiceConversions.new()
		pOnceListeners = Dictionary.new()
		
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("productName", parameters.productName)
		so.setStringChildAt("productVersion", parameters.productVersion)
		so.setStringChildAt("productId", parameters.productId)
		so.setStringChildAt("sandboxId", parameters.sandboxId)
		so.setStringChildAt("deploymentId", parameters.deploymentId)
		if parameters.clientId != null
			so.setStringChildAt("clientId", parameters.clientId)
		end
		if parameters.clientSecret != null
			so.setStringChildAt("clientSecret", parameters.clientSecret)
		end
		so.setBoolChildAt("isServer", parameters.isServer)
		pService = Service.new("EOSSDK", so)
		pService.setListener(this)
	end
	
	/**
	 * \brief Dispose of EOS SDK service.
	 */
	func void dispose()
		pInitListener = null
		if pStatsListeners != null
			pStatsListeners.removeAll()
		end
		if pUserListeners != null
			pUserListeners.removeAll()
		end
		if pOnceListeners != null
			pOnceListeners.removeAll()
		end
		
		if pService != null
			pService.setListener(null)
		end
		pService = null
	end
	
	
	
	/**
	 * \brief Checks if EOS SDK service is supported.
	 * 
	 * Service is reported supported if the EOS SDK Service Module is present.
	 */
	static func bool isServiceSupported()
		return Engine.getSupportedServices().has("EOSSDK")
	end
	
	
	
	/**
	 * \brief Determines if service is initialized and ready to be used.
	 * 
	 * If null is returned the service is still initializing. If returned object has true
	 * value the service is initialized and ready to be used. If returned object has false
	 * value the service failed to initialized and has to be disposed.
	 */
	func Boolean isInitialized()
		return pIsInitialized
	end
	
	
	
	func void addStatListener(ServiceStatsListener listener)
		if listener == null
			throw ENullPointer.new("listener")
		end
		pStatsListeners.add(listener)
	end
	
	func void removeStatsListener(ServiceStatsListener listener)
		pStatsListeners.remove(listener)
	end
	
	func void getStatsAndAchievements(Set stats, Set achievements, ServiceStatsListener listener)
		if stats == null
			throw ENullPointer.new("stats")
		end
		if achievements == null
			throw ENullPointer.new("achievements")
		end
		
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "getStatsAndAchievements")
		
		var ServiceObject so2 = ServiceObject.newList()
		stats.forEach(block String each
			so2.addStringChild(each)
		end)
		so.setChildAt("stats", so2)
		
		so2 = ServiceObject.newList()
		achievements.forEach(block String each
			so2.addStringChild(each)
		end)
		so.setChildAt("achievements", so2)
		
		runRequest(so, listener)
	end
	
	func void setStatsAndAchievements(Dictionary stats, Dictionary achievements, ServiceStatsListener listener)
		if stats == null
			throw ENullPointer.new("stats")
		end
		if achievements == null
			throw ENullPointer.new("achievements")
		end
		
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "setStatsAndAchievements")
		
		if stats.getCount() > 0
			var ServiceObject so2 = ServiceObject.new()
			stats.forEach(block String key, int value
				so2.setIntChildAt(key, value)
			end)
			so.setChildAt("stats", so2)
		end
		
		if achievements.getCount() > 0
			var ServiceObject so2 = ServiceObject.new()
			achievements.forEach(block String key, bool value
				so2.setBoolChildAt(key, value)
			end)
			so.setChildAt("achievements", so2)
		end
		
		runRequest(so, listener)
	end
	
	func void resetAllStats(bool resetAchievements)
		throw EInvalidAction.new("Not supported")
	end
	
	
	
	func void addUserListener(ServiceUserListener listener)
		if listener == null
			throw ENullPointer.new("listener")
		end
		pUserListeners.add(listener)
	end
	
	func void removeUserListener(ServiceUserListener listener)
		pUserListeners.remove(listener)
	end
	
	func ServiceUserFeatures getUserFeatures()
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "getUserFeatures")
		return pConversions.convertUserFeatures(pService.runAction(so))
	end
	
	func void loginUser(ServiceUserCredentials credentials, ServiceUserListener listener)
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "authLogin")
		
		if credentials != null
			if credentials.exchangeCode != null
				so.setStringChildAt("exchangeCode", credentials.exchangeCode)
			end
			if credentials.username != null
				so.setStringChildAt("userId", credentials.username)
			end
			if credentials.password != null
				so.setStringChildAt("userPassword", credentials.password)
			end
			
		else
			// epic game launcher appends these command line parameters:
			// -AUTH_LOGIN=unused -AUTH_PASSWORD=<password> -AUTH_TYPE=exchangecode
			// -epicapp=<appid> -epicenv=Prod -EpicPortal  -epicusername=<username>
			// -epicuserid=<userid> -epiclocale=en-US -epicsandboxid=<sandboxid>
			// -epicdeploymentid=<deploymentid>
			var BaseGameApp app = BaseGameApp.getApp()
			if app != null
				// find parameters
				var String exchangeCode
				var bool foundAuthType
				var int i, count = app.getArgumentCount()
				var String argument
				
				for i = 0 to count
					argument = app.getArgumentAt(i)
					if argument.equals("-AUTH_TYPE=exchangecode")
						foundAuthType = true
						
					elif argument.startsWith("-AUTH_PASSWORD=")
						exchangeCode = argument.substring(15)
					end
				end
				
				if foundAuthType and exchangeCode != null
					so.setStringChildAt("exchangeCode", exchangeCode)
				end
			end
		end
		
		so.setBoolChildAt("scopeFriends", pInitParameters.enableFriendsList)
		so.setBoolChildAt("scopePresence", pInitParameters.enablePresence)
		
		runRequest(so, listener)
	end
	
	func void logoutUser(ServiceUserListener listener)
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "authLogout")
		
		runRequest(so, listener)
	end
	
	func bool isUserLoggedIn()
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "isUserLoggedIn")
		return pService.runAction(so).getBoolean()
	end
	
	func void getAuthToken(ServiceUserListener listener)
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "copyIdToken")
		so = pService.runAction(so)
		
		var ServiceUserAuthToken token = ServiceUserAuthToken.new()
		token.provider = authProviderId
		token.token = so.getString()
		
		TimerBlock.new(0, false, block
			pUserListeners.forEach(block ServiceUserListener each
				each.onGetAuthToken(this, token, null)
			end)
			if listener != null
				listener.onGetAuthToken(this, token, null)
			end
		end)
		
		pService.nextId()
	end
	
	func void getUserInfo(ServiceUserListener listener)
		var ServiceObject so = ServiceObject.new()
		so.setStringChildAt("function", "queryUserInfo")
		
		runRequest(so, listener)
	end
	
	func void loadUserResource(String url, ServiceUserListener listener)
		throw EInvalidParam.new("not supported")
	end
	
	
	
	/** \brief Conversion helper */
	public func BaseServiceConversions getConversions()
		return pConversions
	end
	
	
	
	func void requestResponse(Service service, UniqueID id, ServiceObject response, bool finished)
		var String function = response.getChildAt("function").getString()
		var Object onceListener = getOnceListener(id)
		
		if function.equals("authLogin")
			pUserListeners.forEach(block ServiceUserListener each
				each.onLoginUser(this, null)
			end)
			if onceListener != null
				(onceListener cast ServiceUserListener).onLoginUser(this, null)
			end
			
		elif function.equals("authLogout")
			pUserListeners.forEach(block ServiceUserListener each
				each.onLogoutUser(this, null)
			end)
			if onceListener != null
				(onceListener cast ServiceUserListener).onLogoutUser(this, null)
			end
			
		elif function.equals("queryUserInfo")
			var ServiceUserInfo info = convertUserInfo(response)
			
			pUserListeners.forEach(block ServiceUserListener each
				each.onGetUserInfo(this, info, null)
			end)
			if onceListener != null
				(onceListener cast ServiceUserListener).onGetUserInfo(this, info, null)
			end
			
		elif function.equals("getStatsAndAchievements")
			var Dictionary stats = Dictionary.new()
			var ServiceObject soChild, so = response.getChildAt("stats")
			so.getChildrenKeys().forEach(block String each
				soChild = so.getChildAt(each)
				if soChild != null
					stats.setAt(each, soChild.getInteger())
					
				else
					stats.setAt(each, null)
				end
			end)
			
			var Dictionary achievements = Dictionary.new()
			so = response.getChildAt("achievements")
			so.getChildrenKeys().forEach(block String each
				achievements.setAt(each, so.getChildAt(each).getBoolean())
			end)
			
			pStatsListeners.forEach(block ServiceStatsListener each
				each.onGetStats(this, stats, achievements, null)
			end)
			if onceListener != null
				(onceListener cast ServiceStatsListener).onGetStats(this, stats, achievements, null)
			end
			
		elif function.equals("setStatsAndAchievements")
			var Dictionary stats = Dictionary.new()
			var ServiceObject so = response.getChildAt("stats")
			so.getChildrenKeys().forEach(block String each
				stats.setAt(each, so.getChildAt(each).getInteger())
			end)
			
			var Dictionary achievements = Dictionary.new()
			so = response.getChildAt("achievements")
			so.getChildrenKeys().forEach(block String each
				achievements.setAt(each, so.getChildAt(each).getBoolean())
			end)
			
			pStatsListeners.forEach(block ServiceStatsListener each
				each.onSetStats(this, stats, achievements, null)
			end)
			if onceListener != null
				(onceListener cast ServiceStatsListener).onSetStats(this, stats, achievements, null)
			end
		end
	end
	
	func void requestFailed(Service service, UniqueID id, ServiceObject error)
		var String function = error.getChildAt("function").getString()
		var ServiceException exception = pConversions.createException(service, error)
		var Object onceListener = getOnceListener(id)
		
		if function.equals("authLogin")
			pUserListeners.forEach(block ServiceUserListener each
				each.onLoginUser(this, exception)
			end)
			if onceListener != null
				(onceListener cast ServiceUserListener).onLoginUser(this, exception)
			end
			
		elif function.equals("authLogout")
			pUserListeners.forEach(block ServiceUserListener each
				each.onLogoutUser(this, exception)
			end)
			if onceListener != null
				(onceListener cast ServiceUserListener).onLogoutUser(this, exception)
			end
			
		elif function.equals("queryUserInfo")
			pUserListeners.forEach(block ServiceUserListener each
				each.onGetUserInfo(this, null, exception)
			end)
			if onceListener != null
				(onceListener cast ServiceUserListener).onGetUserInfo(this, null, exception)
			end
			
		elif function.equals("getStatsAndAchievements")
			pStatsListeners.forEach(block ServiceStatsListener each
				each.onGetStats(this, null, null, exception)
			end)
			if onceListener != null
				(onceListener cast ServiceStatsListener).onGetStats(this, null, null, exception)
			end
			
		elif function.equals("setStatsAndAchievements")
			pStatsListeners.forEach(block ServiceStatsListener each
				each.onSetStats(this, null, null, exception)
			end)
			if onceListener != null
				(onceListener cast ServiceStatsListener).onSetStats(this, null, null, exception)
			end
		end
	end
	
	func void eventReceived(Service service, ServiceObject event)
		var String eventName = event.getChildAt("event").getString()
		var bool success = event.getChildAt("success").getBoolean()
		var ServiceException exception
		
		if not success
			exception = pConversions.createException(service, event)
		end
		
		if eventName.equals("initialized")
			pIsInitialized = Boolean.new(success)
			
			if pInitListener != null
				pInitListener.onServiceInit(this, exception)
			end
			pInitListener = null
		end
	end
	
	
	
	/** \brief Once listener matching id or null. */
	protected func Object getOnceListener(UniqueID id)
		var Object listener = pOnceListeners.getAt(id, null)
		if listener != null
			pOnceListeners.remove(id)
		end
		return listener
	end
	
	/** \brief Run request with optional once listener. */
	protected func UniqueID runRequest(ServiceObject request, Object listener)
		var UniqueID id = pService.nextId()
		pService.startRequest(id, request)
		if listener != null
			pOnceListeners.setAt(id, listener)
		end
		return id
	end
	
	
	
	protected func ServiceUserInfo convertUserInfo(ServiceObject so)
		var ServiceUserInfo info = ServiceUserInfo.new()
		info.id = pConversions.convertString(so, "userId")
		info.displayName = pConversions.convertString(so, "displayName")
		info.nickname = pConversions.convertString(so, "nickname")
		info.country = pConversions.convertString(so, "country")
		info.preferredLanguage = pConversions.convertString(so, "preferredLanguage")
		info.profileUrl = pConversions.convertString(so, "profileUrl")
		return info
	end
end

