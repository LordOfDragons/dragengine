/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.BehaviorTreeSystem

pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Behavior tree condition evaluating a floating point parameter table entry.
 * 
 * Compares the value of a parameter table entry against a static value or another parameter
 * table entry. A threshold is used for the compare operations defaulting to 0.0001.
 */
class BTConditionParameterTableFloat implements BTCondition
	/** \brief Compare operator. */
	public enum Operator
		/** \brief Entry is equal to value inside threshold. */
		equal
		
		/** \brief Entry is not equal to value inside threshold. */
		notEqual
		
		/** \brief Entry is less than value including threshold. */
		lessThan
		
		/** \brief Entry is less than or equal to value including threshold. */
		lessThanOrEqual
		
		/** \brief Entry is greater than value including threshold. */
		greaterThan
		
		/** \brief Entry is greater than or equal to value including threshold. */
		greaterThanOrEqual
	end
	
	
	
	/** \brief Parameter table entry to compare. */
	var ParameterTableEntry pEntry
	
	/** \brief Default value if entry to compare is not null. */
	var float pDefaultValue
	
	/** \brief Compare operator from eOperators. */
	var Operator pOperator
	
	/** \brief Threshold value. */
	var float pThreshold
	
	/** \brief Parameter table entry to compare against. */
	var ParameterTableEntry pEntryCompareValue
	
	/** \brief Default compare against value. */
	var float pCompareValue
	
	/** \brief Use named parameter as source of compare value if not null. */
	var String pParameterNameCompareValue
	
	
	
	/** \brief Create condition. */
	func new(ParameterTableEntry entry, float defaultValue, Operator operator, float compareValue) \
	this(entry, defaultValue, operator, null, compareValue, null)
	end
	
	/** \brief Create condition. */
	func new(ParameterTableEntry entry, float defaultValue, Operator operator, \
	float compareValue, String parameterNameCompareValue) \
	this(entry, defaultValue, operator, null, compareValue, parameterNameCompareValue)
	end
	
	/** \brief Create condition. */
	func new(ParameterTableEntry entry, float defaultValue, Operator operator, \
	ParameterTableEntry entryCompareValue, float compareValue, String parameterNameCompareValue)
		if entry == null
			throw EInvalidParam.new()
		end
		pEntry = entry
		pDefaultValue = defaultValue
		pOperator = operator
		pThreshold = 0.0001
		pEntryCompareValue = entryCompareValue
		pCompareValue = compareValue
		pParameterNameCompareValue = parameterNameCompareValue
	end
	
	
	
	/** \brief Parameter table entry to compare. */
	func ParameterTableEntry getEntry()
		return pEntry
	end
	
	/** \brief Set parameter table entry to compare. */
	func void setEntry(ParameterTableEntry entry)
		if entry == null
			throw EInvalidParam.new()
		end
		pEntry = entry
	end
	
	/** \brief Default value if entry to compare is not null. */
	func float getDefaultValue()
		return pDefaultValue
	end
	
	/** \brief Set default value if entry to compare is not null. */
	func void setDefaultValue(float defaultValue)
		pDefaultValue = defaultValue
	end
	
	/** \brief Compare operator from eOperators. */
	func Operator getOperator()
		return pOperator
	end
	
	/** \brief Set compare operator from eOperators. */
	func void setOperator(Operator operator)
		pOperator = operator
	end
	
	/** \brief Threshold value. */
	func float getThreshold()
		return pThreshold
	end
	
	/** \brief Set threshold value. */
	func void setThreshold(float threshold)
		pThreshold = threshold
	end
	
	/** \brief Parameter table entry to compare against. */
	func ParameterTableEntry getEntryCompareValue()
		return pEntryCompareValue
	end
	
	/** \brief Set parameter table entry to compare against. */
	func void setEntryCompareValue(ParameterTableEntry entry)
		pEntryCompareValue = entry
	end
	
	/** \brief Default compare against value. */
	func float getCompareValue()
		return pCompareValue
	end
	
	/** \brief Set default compare against value. */
	func void setCompareValue(float compareValue)
		pCompareValue = compareValue
	end
	
	/** \brief Use named parameter as source of compare value if not null. */
	func String getParameterNameCompareValue()
		return pParameterNameCompareValue
	end
	
	/** \brief Use named parameter as source of compare value if not null. */
	func void setParameterNameCompareValue(String name)
		pParameterNameCompareValue = name
	end
	
	
	
	/** \brief Create condition testing the exact opposite. */
	func BTConditionParameterTableFloat negated()
		var Operator operator = pOperator
		
		select operator
		case Operator.equal
			operator = Operator.notEqual
			
		case Operator.notEqual
			operator = Operator.equal
			
		case Operator.lessThan
			operator = Operator.greaterThanOrEqual
			
		case Operator.lessThanOrEqual
			operator = Operator.greaterThan
			
		case Operator.greaterThan
			operator = Operator.lessThanOrEqual
			
		case Operator.greaterThanOrEqual
			operator = Operator.lessThan
		end
		
		var BTConditionParameterTableFloat condition = BTConditionParameterTableFloat.new(\
			pEntry, pDefaultValue, operator, pEntryCompareValue, \
			pCompareValue, pParameterNameCompareValue)
		condition.pThreshold = pThreshold
		return condition
	end
	
	
	
	/**
	 * \brief Evaluate condition.
	 * \param parameters Dictionary with parameters of type String key and String value.
	 *                   Do not change the content of the dictionary as it is shared.
	 */
	func bool evaluateCondition(BTContext context, Dictionary parameters)
		var float compareValue = pCompareValue
		if pEntryCompareValue != null
			compareValue = pEntryCompareValue.getValue(compareValue)
		end
		if pParameterNameCompareValue != null
			var String value = parameters.getAt(pParameterNameCompareValue, null) cast String
			if value != null
				compareValue = value.toFloat()
			end
		end
		
		select pOperator
		case Operator.equal
			return DEMath.fabs(pEntry.getValue(pDefaultValue) - compareValue) <= pThreshold
			
		case Operator.notEqual
			return DEMath.fabs(pEntry.getValue(pDefaultValue) - compareValue) > pThreshold
			
		case Operator.lessThan
			return pEntry.getValue(pDefaultValue) < compareValue - pThreshold
			
		case Operator.lessThanOrEqual
			return pEntry.getValue(pDefaultValue) <= compareValue + pThreshold
			
		case Operator.greaterThan
			return pEntry.getValue(pDefaultValue) > compareValue + pThreshold
			
		case Operator.greaterThanOrEqual
			return pEntry.getValue(pDefaultValue) >= compareValue - pThreshold
			
		else
			return false
		end
	end
end
