/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.BehaviorTreeSystem

pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Behavior tree condition evaluating an integer parameter table entry.
 * 
 * Compares the value of a parameter table entry against a static value or another parameter
 * table entry.
 */
class BTConditionParameterTableInt implements BTCondition
	/** \brief Compare operator. */
	public enum Operator
		/** \brief Entry is equal to value. */
		equal
		
		/** \brief Entry is not equal to value. */
		notEqual
		
		/** \brief Entry is less than value. */
		lessThan
		
		/** \brief Entry is less than or equal to value. */
		lessThanOrEqual
		
		/** \brief Entry is greater than value. */
		greaterThan
		
		/** \brief Entry is greater than or equal to value. */
		greaterThanOrEqual
	end
	
	
	
	var ParameterTableEntry pEntry
	var int pDefaultValue
	var Operator pOperator
	var ParameterTableEntry pEntryCompareValue
	var int pCompareValue
	var String pParameterNameCompareValue
	
	
	
	/** \brief Create condition. */
	func new(ParameterTableEntry entry, int defaultValue, Operator operator, int compareValue) \
	this(entry, defaultValue, operator, null, compareValue, null)
	end
	
	/** \brief Create condition. */
	func new(ParameterTableEntry entry, int defaultValue, Operator operator, \
	int compareValue, String parameterNameCompareValue) \
	this(entry, defaultValue, operator, null, compareValue, parameterNameCompareValue)
	end
	
	/** \brief Create condition. */
	func new(ParameterTableEntry entry, int defaultValue, Operator operator, \
	ParameterTableEntry entryCompareValue, int compareValue, String parameterNameCompareValue)
		if entry == null
			throw EInvalidParam.new()
		end
		pEntry = entry
		pDefaultValue = defaultValue
		pOperator = operator
		pEntryCompareValue = entryCompareValue
		pCompareValue = compareValue
		pParameterNameCompareValue = parameterNameCompareValue
	end
	
	
	
	/** \brief Parameter table entry to compare. */
	func ParameterTableEntry getEntry()
		return pEntry
	end
	
	/** \brief Set parameter table entry to compare. */
	func void setEntry(ParameterTableEntry entry)
		if entry == null
			throw EInvalidParam.new()
		end
		pEntry = entry
	end
	
	/** \brief Default value if entry to compare is not null. */
	func int getDefaultValue()
		return pDefaultValue
	end
	
	/** \brief Set default value if entry to compare is not null. */
	func void setDefaultValue(int defaultValue)
		pDefaultValue = defaultValue
	end
	
	/** \brief Compare operator from eOperators. */
	func Operator getOperator()
		return pOperator
	end
	
	/** \brief Set compare operator from eOperators. */
	func void setOperator(Operator operator)
		pOperator = operator
	end
	
	/** \brief Parameter table entry to compare against. */
	func ParameterTableEntry getEntryCompareValue()
		return pEntryCompareValue
	end
	
	/** \brief Set parameter table entry to compare against. */
	func void setEntryCompareValue(ParameterTableEntry entry)
		pEntryCompareValue = entry
	end
	
	/** \brief Default compare against value. */
	func int getCompareValue()
		return pCompareValue
	end
	
	/** \brief Set default compare against value. */
	func void setCompareValue(int compareValue)
		pCompareValue = compareValue
	end
	
	/** \brief Use named parameter as source of compare value if not null. */
	func String getParameterNameCompareValue()
		return pParameterNameCompareValue
	end
	
	/** \brief Use named parameter as source of compare value if not null. */
	func void setParameterNameCompareValue(String name)
		pParameterNameCompareValue = name
	end
	
	
	
	/** \brief Create condition testing the exact opposite. */
	func BTConditionParameterTableInt negated()
		var Operator operator = pOperator
		
		select operator
		case Operator.equal
			operator = Operator.notEqual
			
		case Operator.notEqual
			operator = Operator.equal
			
		case Operator.lessThan
			operator = Operator.greaterThanOrEqual
			
		case Operator.lessThanOrEqual
			operator = Operator.greaterThan
			
		case Operator.greaterThan
			operator = Operator.lessThanOrEqual
			
		case Operator.greaterThanOrEqual
			operator = Operator.lessThan
		end
		
		return BTConditionParameterTableInt.new(pEntry, pDefaultValue, \
			operator, pEntryCompareValue, pCompareValue, \
			pParameterNameCompareValue)
	end
	
	
	
	/**
	 * \brief Evaluate condition.
	 * \param parameters Dictionary with parameters of type String key and String value.
	 *                   Do not change the content of the dictionary as it is shared.
	 */
	func bool evaluateCondition(BTContext context, Dictionary parameters)
		var int compareValue = pCompareValue
		if pEntryCompareValue != null
			compareValue = pEntryCompareValue.getValue(compareValue)
		end
		if pParameterNameCompareValue != null
			var String value = parameters.getAt(pParameterNameCompareValue, null) cast String
			if value != null
				compareValue = value.toInt()
			end
		end
		
		select pOperator
		case Operator.equal
			return pEntry.getValue(pDefaultValue) == compareValue
			
		case Operator.notEqual
			return pEntry.getValue(pDefaultValue) != compareValue
			
		case Operator.lessThan
			return pEntry.getValue(pDefaultValue) < compareValue
			
		case Operator.lessThanOrEqual
			return pEntry.getValue(pDefaultValue) <= compareValue
			
		case Operator.greaterThan
			return pEntry.getValue(pDefaultValue) > compareValue
			
		case Operator.greaterThanOrEqual
			return pEntry.getValue(pDefaultValue) >= compareValue
			
		else
			return false
		end
	end
end
