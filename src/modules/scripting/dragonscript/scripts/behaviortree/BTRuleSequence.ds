/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.BehaviorTreeSystem

pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Behavior tree sequence rule.
 * 
 * Runs all actions in sequence. Returns BTResult.success if all actions returned
 * BTResult.success . If an action return BTResult.failure processing of the rule stops
 * and BTResult.failure is returned. If no actions are present BTResult.failure is returned.
 * 
 * If looping is set to true the sequence restarts at the beginning if all actions returns
 * BTResult.success . This allows to keep looping a sequence until the first time any action
 * returns BTResult.failure .
 */
class BTRuleSequence extends BTRuleList
	var bool pLooping
	var bool pDoNoFail
	
	
	
	/** \brief Create behavior tree rule. */
	func new(String id) super(id)
	end
	
	
	
	/** \brief Looping. */
	func bool getLooping()
		return pLooping
	end
	
	/** \brief Set looping. */
	func void setLooping(bool looping)
		pLooping = looping
	end
	
	/**
	 * \brief Return success if all child actions fail.
	 * \version 1.19
	 */
	func bool getDoNotFail()
		return pDoNoFail
	end
	
	/**
	 * \brief Set to return success if all child actions fail.
	 * \version 1.19
	 */
	func void setDoNotFail(bool doNotFail)
		pDoNoFail = doNotFail
	end
	
	
	
	/**
	 * \brief Run action.
	 * 
	 * \returns Value from BTAction.eReturnValues .
	 */
	func BTResult run(BTContext context)
		var BTRule rule = context.getCurrentRule()
		
		if rule == this
			// first time the rule is run
			if getRuleCount() == 0 or not evaluateConditions(context)
				return pDoNoFail if BTResult.success else BTResult.failure  // context does another step
			end
			
			context.setCurrentRule(getRuleAt(0))
			return BTResult.success  // context does another step
			
		else
			// child rule has run. check last result
			var int index = indexOfRule(rule)
			
			select context.getLastResult()
			case BTResult.running
				return BTResult.running  // context stops stepping
				
			case BTResult.success
				if not evaluateConditions(context)
					// condition failed. pass up to parent
					context.setCurrentRule(this)
					return pDoNoFail if BTResult.success else BTResult.failure
				end
				
				if index < getRuleCount() - 1
					// more actions to run
					context.setCurrentRule(getRuleAt(index + 1))
					
				else
					// finished all actions successfully. pass up to parent unless looping
					if pLooping
						context.setCurrentRule(getRuleAt(0))
						
					else
						context.setCurrentRule(this)
					end
				end
				return BTResult.success  // context does another step
				
			case BTResult.failure
				// sequence failed. pass up to parent
				context.setCurrentRule(this)
				return pDoNoFail if BTResult.success else BTResult.failure  // context does another step
				
			else
				throw EInvalidParam.new()
			end
		end
	end
	
	
	
	/** String representation of object. */
	func String toString()
		return "BTRuleSequence(id='{}')".format(Array.newWith(getID()))
	end
end
