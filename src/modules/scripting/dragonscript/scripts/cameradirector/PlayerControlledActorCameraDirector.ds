/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */



namespace Dragengine.CameraDirectors

pin Dragengine.Scenery



/**
 * \brief Place camera relative player controlled actor with orbit support.
 * 
 * Extends ElementCameraDirector with support to set the tracked element from the player
 * controlled actor set in BaseGameApp at every update call. Using this class the game can
 * switch player control from one actor to another without needing to update the camera
 * directory unless different actors require different parameters.
 * 
 * For convenience the actorChanged() hook method called whenever an actor switched.
 * 
 * The pivot point is set at the center of the actor head. For best results this should
 * be straight above the actor position. The setPivotAboveGround() takes care of this.
 * The actor locomotion is used to obtain the actor looking direction. The camera director
 * azimuth and elevation are set to match the looking direction. The distance is not
 * changed and can be set by users of the class to place the camera behind the actor for
 * example for 3rd person views.
 */
class PlayerControlledActorCameraDirector extends ElementCameraDirector
	/** \brief Tracker listener. */
	class ActorChanged extends ECBehaviorPlayerControllable.Tracker.DefaultListener
		private var WeakReference pDirector
		
		public func new( PlayerControlledActorCameraDirector director )
			this.pDirector = WeakReference.new( director )
		end
		
		public func void activeActorChanged( ECBehaviorPlayerControllable.Tracker tracker )
			var PlayerControlledActorCameraDirector director = \
				this.pDirector.get() cast PlayerControlledActorCameraDirector
			if director != null
				director.setActor( tracker.getActor() )
			end
		end
	end
	
	
	
	private var ECBehaviorPlayerControllable.Tracker pTracker
	private var ActorChanged pActorChanged
	
	/** \brief Collider AI or \em null if absent. */
	protected var ECBehaviorColliderAI.Instance colliderAI
	
	/** \brief Locomotion or \em null if absent. */
	protected var ECBehaviorLocomotion.Instance locomotion
	
	
	
	/**
	 * \brief Create camera director.
	 * 
	 * Pivot point is set to 1.6m above ground.
	 */
	public func new() this( null, null )
	end
	
	/**
	 * \brief Create camera director.
	 * 
	 * Pivot point is set to 1.6m above ground.
	 */
	public func new( Camera camera, Microphone microphone ) \
	this( camera, microphone, BaseGameApp.getApp().getPlayerControlledActor() )
	end
	
	/**
	 * \brief Create camera director.
	 * 
	 * Pivot point is set to 1.6m above ground.
	 */
	public func new( Camera camera, Microphone microphone, ECBehaviorPlayerControllable.Tracker tracker ) \
	super( null, camera, microphone )
		this.pActorChanged = ActorChanged.new( this )
		this.setPivotAboveGround( 1.6 )
		this.setTracker( tracker )
	end
	
	
	
	/** \brief Tracker or null. */
	public func ECBehaviorPlayerControllable.Tracker getTracker()
		return this.pTracker
	end
	
	/** \brief Set tracker or null. */
	public func void setTracker( ECBehaviorPlayerControllable.Tracker tracker )
		if tracker == this.pTracker
			return
		end
		
		if this.pTracker != null
			this.pTracker.removeListener( this.pActorChanged )
		end
		
		this.pTracker = tracker
		
		if tracker != null
			tracker.addListener( this.pActorChanged )
			this.setActor( tracker.getActor() )
			
		else
			this.setActor( null )
		end
	end
	
	/** \brief Actor or \em null. */
	public func ECBehaviorPlayerControllable.Instance getActor()
		if this.pTracker != null
			return this.pTracker.getActor()
			
		else
			return null
		end
	end
	
	/** \brief Set actor or \em null. */
	public func void setActor( ECBehaviorPlayerControllable.Instance actor )
		if actor != null
			this.setElement( actor.getElement() )
			this.initBehaviors( actor.getElement() )
			
		else
			this.setElement( null )
			this.colliderAI = null
			this.locomotion = null
		end
		
		this.actorChanged()
	end
	
	/** \brief Init behaviors. */
	protected func void initBehaviors( BehaviorElement element )
		this.colliderAI = ECBehaviorColliderAI.getInstanceIn( element )
		this.locomotion = ECBehaviorLocomotion.getInstanceIn( element )
	end
	
	/** \brief Collider AI or \em null if absent. */
	public func ECBehaviorColliderAI.Instance getColliderAI()
		return this.colliderAI
	end
	
	/** \brief Locomotion or \em null if absent. */
	public func ECBehaviorLocomotion.Instance getLocomotion()
		return this.locomotion
	end
	
	
	
	/**
	 * \brief Set pivot point at desired distance above ground.
	 * 
	 * Typical value for \em distance is 1.6 for a adult human actor.
	 */
	public func void setPivotAboveGround( float distance )
		this.setPivot( DVector.new( 0, distance, 0 ) )
	end
	
	
	
	/**
	 * \brief Update camera.
	 * 
	 * Set camera director azimuth and elevation to match actor locomotion. Azimuth is set to
	 * locomotion.getOrientation() plus locomotion.getLookHorizontal(). Elevation is set to
	 * locomotion.getLookVertical(). This equals to actor orientation around his up-axis combined
	 * with actor looking around.
	 * 
	 * If no actor is present azimuth and elevation are set to 0.
	 */
	public func void updateCamera( float elapsed )
		if this.locomotion != null
			var Locomotion l = this.locomotion.getLocomotion()
			this.setAzimuth( l.getOrientation() + l.getLookHorizontal() )
			this.setElevation( -l.getLookVertical() )
			
		else
			this.setAzimuth( 0 )
			this.setElevation( 0 )
		end
		
		super.updateCamera( elapsed )
	end
	
	/**
	 * \brief Update microphone.
	 * 
	 * Extends the base functionality setting velocity to match actor velocity.
	 */
	public func void updateMicrophone( float elapsed )
		super.updateMicrophone( elapsed )
		
		if this.colliderAI != null
			this.getMicrophone().setVelocity( this.colliderAI.getCollider().getLinearVelocity() )
			
		else
			this.getMicrophone().setVelocity( Vector.new() )
		end
	end
	
	
	
	/**
	 * \brief Player controlled actor changed.
	 * 
	 * Called after the new actor has been assigned to the camera directory.
	 * Hook method for subclass to apply additional logic for actor switching.
	 */
	protected func void actorChanged()
	end
end

