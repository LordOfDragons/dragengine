/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Gui.MainMenus.Interactive

pin Dragengine.Gui.Events
pin Dragengine.Gui.Layouts
pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.Debugging


/**
 * \brief Load world for nteractive main menu.
 * 
 * Loads a world file using it as the menu the user can interact with. The elements placed
 * in the world are special element classes providing the menu interaction support. This
 * makes the world file fully conclusive not requiring anything else than the world file
 * path to create the menu.
 * 
 * The main camera spot is named "main". The world is required to have one camera spot
 * with this name for the interactive main menu to work correctly.
 */
class IMMLoadWorld
	private var InteractiveMainMenu pMainMenu
	private var GameWorld pWorld
	private var PreloadManager pPreloadManager
	private var StubGameWorld pStubWorld
	
	private var DebugVerifyStubProperties pDebugVerifyStubProperties
	
	
	
	/** \brief Create load world for interactive main menu. */
	public func new(InteractiveMainMenu mainMenu)
		if mainMenu == null
			throw EInvalidParam.new()
		end
		
		pMainMenu = mainMenu
	end
	
	/** \brief Dispose of widget. */
	public func void dispose()
		if pWorld != null
			pWorld.dispose()
			pWorld = null
		end
		pMainMenu = null
		pDebugVerifyStubProperties = null
	end
	
	
	
	/** \brief Interactive main menu. */
	public func InteractiveMainMenu getMainMenu()
		return pMainMenu
	end
	
	/** \brief World or \em null. */
	public func GameWorld getWorld()
		return pWorld
	end
	
	/** \brief Preload manager. */
	public func PreloadManager getPreloadManager()
		return pPreloadManager
	end
	
	/** \brief World stub. */
	public func StubGameWorld getStubWorld()
		return pStubWorld
	end
	
	
	
	/** \brief Debug verify stub properties while loading or null if not used. */
	public func DebugVerifyStubProperties getDebugVerifyStubProperties()
		return pDebugVerifyStubProperties
	end
	
	/** \brief Set debug verify stub properties while loading or null if not used. */
	public func void setDebugVerifyStubProperties(DebugVerifyStubProperties debug)
		pDebugVerifyStubProperties = debug
	end
	
	
	
	/**
	 * \brief Start loading world.
	 * 
	 * \throws EInvalidParam Loading in progress.
	 */
	public func void loadWorld(String path)
		if path == null or pStubWorld != null
			throw EInvalidParam.new()
		end
		
		try
			pPreloadManager = PreloadManager.new()
			
			pWorld = GameWorld.new()
			pStubWorld = StubGameWorld.new(pWorld, pPreloadManager.getCounter())
			pMainMenu.getLoaders().getWorld().loadFromFile(pStubWorld, path, false)
			pStubWorld.getGameWorld().init(pStubWorld)
			
			// one solution is to use preloadAll to preload all element classes including
			// xml ones if present. another solution is to preload only actually used
			// classes. for a gmae it is often better to preload all element classes.
			// for a simple main menu world file it is better to preload only what is used.
			// this reduces loading time especially if multiple world files are present
			// pStubWorld.addElementClassesTo(pPreloadManager)
			// pPreloadManager.addElementClassDependencies()
			// 
			// preloading only used classes can cause problems so load all. to cut down
			// the loading time it is enough to restrict the directories to load xml
			// element classes from.
			
			pMainMenu.getElementClasses().preloadAll(pPreloadManager)
			pPreloadManager.preloadElementClasses()
			pStubWorld.beginPreloading()
			
		catch Exception e
			pPreloadManager = null
			pStubWorld = null
			if pWorld != null
				pWorld.dispose()
				pWorld = null
			end
			throw
		end
	end
	
	/**
	 * \brief Update loading.
	 * 
	 * If loading fails an exception is thrown and the all temporary instances cleaned up.
	 * 
	 * \returns loaded world instance or \em null if still loading.
	 */
	public func GameWorld update(float elapsed)
		var bool preloadProblemsReported = false
		
		try
			if not pStubWorld.updatePreloading()
				return null
			end
			
			preloadProblemsReported = true
			if reportPreloadProblems()
				throw EInvalidParam.new()
			end
			finishLoading()
			
			pStubWorld = null
			pPreloadManager = null
			
			var GameWorld world = pWorld
			pWorld = null
			return world 
			
		catch Exception e
			if not preloadProblemsReported
				reportPreloadProblems()
			end
			pPreloadManager = null
			pStubWorld = null
			if pWorld != null
				pWorld.dispose()
				pWorld = null
			end
			throw
		end
	end
	
	
	
	/** \brief Finish preloading. */
	protected func void finishLoading()
		var Console console = pMainMenu.getConsole()
		var DecalBuilder decalBuilder = DecalBuilder.new()
		
		try
			pStubWorld.verifyPreloading(console)
			if pDebugVerifyStubProperties != null
				pDebugVerifyStubProperties.verifyWorld(pStubWorld)
			end
			pStubWorld.createAndAddElements(console)
			pStubWorld.createWorldDecals(decalBuilder, console)
			
			// TODO add a world property to define the sky to use. right now just use nothing
			var String value = pStubWorld.getPropertyValueFor("sky", null)
			if value != null
				var Sky sky = pMainMenu.getLoaders().getSky().loadFromFile(value)
				
				var SkyInstance skyInstance = SkyInstance.new()
				skyInstance.setSky(sky)
				pWorld.getWorld().addSky(skyInstance)
				
			end
			
			decalBuilder.dispose()
			
		catch Exception e
			reportPreloadProblems()
			decalBuilder.dispose()
			throw
		end
			
		pMainMenu.setMenuFadeInTime(pStubWorld.getPropertyFloatFor(\
			"menuFadeInTime", pMainMenu.getMenuFadeInTime()))
	end
	
	/** \brief Report preload problems. */
	protected func bool reportPreloadProblems()
		return pMainMenu.getElementClasses().logPreloadProblems(pMainMenu.getConsole())
	end
end
