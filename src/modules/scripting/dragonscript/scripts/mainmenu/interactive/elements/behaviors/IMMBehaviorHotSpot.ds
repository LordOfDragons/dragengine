/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.Gui.MainMenus.Interactive

pin Dragengine.LoadSave
pin Dragengine.Preloading
pin Dragengine.Utils
pin Dragengine.Scenery
pin Dragengine.TriggerSystem


/**
 * \brief Behavior element behavior adding interactive main menu hot spot support.
 */
class IMMBehaviorHotSpot extends DefaultECBehavior
	/** \brief Behavior instance. */
	class Instance extends DefaultECBehaviorInstance
		private var IMMBehaviorHotSpot pECBehavior
		private var ECBehaviorTwoStateAnimated.Instance pAnimated
		private var String pAction
		private var String pCameraSpot
		private var float pTransitionTime
		private var String pDisplayText
		private var String pDisplayTextTranslate
		private var float pDelayActivate
		private var float pDelayDeactivate
		private var float pRemainingDelay
		private var bool pHotSpotActive
		private var TSNamedTriggerTarget pTargetHotSpotActive
		private var TSNamedTriggerTarget pTargetHotSpotActivated
		private var TSNamedTriggerTarget pTargetHotSpotDeactivated
		private var TSNamedTriggerTarget pTargetHotSpotHover
		private var TSNamedTriggerTarget pTargetHotSpotEntered
		private var TSNamedTriggerTarget pTargetHotSpotLeft
		
		
		
		/** \brief Create element. */
		public func new(IMMBehaviorHotSpot ecbehavior, BehaviorElement element) super(element)
			pECBehavior = ecbehavior
			pRemainingDelay = 0
			pHotSpotActive = false
			
			pAnimated = ecbehavior.getAnimated().instance(getElement())
		end
		
		/** \brief Dispose of composeable instance. */
		public func void dispose()
			pTargetHotSpotActive = null
			pTargetHotSpotActivated = null
			pTargetHotSpotDeactivated = null
			pTargetHotSpotHover = null
			pTargetHotSpotEntered = null
			pTargetHotSpotLeft = null
			pAnimated = null
			super.dispose()
		end
		
		
		
		/** \brief Initialize element from stub. */
		public func void init(StubElement stub)
			pAction = pECBehavior.getAction().getValue(stub)
			pCameraSpot = pECBehavior.getCameraSpot().getValue(stub)
			pTransitionTime = pECBehavior.getTransitionTime().getValue(stub)
			pDelayActivate = pECBehavior.getDelayActivate().getValue(stub)
			pDelayDeactivate = pECBehavior.getDelayDeactivate().getValue(stub)
			pDisplayText = pECBehavior.getDisplayText().getValue(stub)
			pDisplayTextTranslate = pECBehavior.getDisplayTextTranslate().getValueOrNull(stub)
			pTargetHotSpotActive = TSNamedTriggerTarget.new(\
				pECBehavior.getTargetHotSpotActive().getValue(stub))
			pTargetHotSpotActivated = TSNamedTriggerTarget.new(\
				pECBehavior.getTargetHotSpotActivated().getValue(stub))
			pTargetHotSpotDeactivated = TSNamedTriggerTarget.new(\
				pECBehavior.getTargetHotSpotDeactivated().getValue(stub))
			pTargetHotSpotHover = TSNamedTriggerTarget.new(\
				pECBehavior.getTargetHotSpotHover().getValue(stub))
			pTargetHotSpotEntered = TSNamedTriggerTarget.new(\
				pECBehavior.getTargetHotSpotEntered().getValue(stub))
			pTargetHotSpotLeft = TSNamedTriggerTarget.new(\
				pECBehavior.getTargetHotSpotLeft().getValue(stub))
		end
		
		
		
		/** \brief Element class behavior. */
		public func IMMBehaviorHotSpot getECBehavior()
			return pECBehavior
		end
		
		
		
		/** \brief Action to carry out. */
		public func String getAction()
			return pAction
		end
		
		/** \brief Set action to carry out. */
		protected func void setAction(String action)
			pAction = action
		end
		
		/** \brief Camera spot to activate when hot-spot is activated. */
		public func String getCameraSpot()
			return pCameraSpot
		end
		
		/** \brief Set camera spot to activate when hot-spot is activated. */
		protected func void setCameraSpot(String cameraSpot)
			pCameraSpot = cameraSpot
		end
		
		/** \brief Transition time. */
		public func float getTransitionTime()
			return pTransitionTime
		end
		
		/** \brief Set transition time. */
		protected func void setTransitionTime(float time)
			pTransitionTime = DEMath.max(time, 0.0)
		end
		
		/** \brief Display text. */
		public func String getDisplayText()
			return pDisplayText
		end
		
		/** \brief Set display text. */
		protected func void setDisplayText(String text)
			pDisplayText = text
		end
		
		/**
		 * \brief Translated display text or null.
		 * \version 1.16
		 */
		public func String getDisplayTextTranslate()
			return pDisplayTextTranslate
		end
		
		/**
		 * \brief Set translated display text or null.
		 * \version 1.16
		 */
		protected func void setDisplayTextTranslate(String text)
			pDisplayTextTranslate = text
		end
		
		/** \brief Delay for activating IMMProp after hotspot has been activated. */
		public func float getDelayActivate()
			return pDelayActivate
		end
		
		/** \brief Set delay for activating IMMProp after hotspot has been activated. */
		protected func void setDelayActivate(float delay)
			pDelayActivate = DEMath.max(delay, 0.0)
		end
		
		/** \brief Delay for deactivating IMMProp after hotspot has been deactivated. */
		public func float getDelayDeactivate()
			return pDelayDeactivate
		end
		
		/** \brief Set delay for deactivating IMMProp after hotspot has been deactivated. */
		protected func void setDelayDeactivate(float delay)
			pDelayDeactivate = DEMath.max(delay, 0.0)
		end
		
		/** \brief Remaing prop (de)activate prop delay. */
		public func float getRemainingDelay()
			return pRemainingDelay
		end
		
		/** \brief Set remaing prop (de)activate prop delay. */
		protected func void setRemainingDelay(float time)
			pRemainingDelay = time
		end
		
		/** \brief Hot-spot is active. */
		public func bool getHotSpotActive()
			return pHotSpotActive
		end
		
		
		
		/** \brief Trigger target to reflect activated state. */
		public func TSNamedTriggerTarget getTargetHotSpotActivated()
			return pTargetHotSpotActive
		end
		
		/** \brief Trigger target to notify activating. */
		public func TSNamedTriggerTarget getTargetHotSpotActivating()
			return pTargetHotSpotActivated
		end
		
		/** \brief Trigger target to notify deactivating. */
		public func TSNamedTriggerTarget getTargetHotSpotDeactivating()
			return pTargetHotSpotDeactivated
		end
		
		/** \brief Trigger target to reflect hovering state. */
		public func TSNamedTriggerTarget getTargetHotSpotHover()
			return pTargetHotSpotHover
		end
		
		/** \brief Trigger target to notify entering. */
		public func TSNamedTriggerTarget getTargetHotSpotEntered()
			return pTargetHotSpotEntered
		end
		
		/** \brief Trigger target to notify leaving. */
		public func TSNamedTriggerTarget getTargetHotSpotLeft()
			return pTargetHotSpotLeft
		end
		
		
		
		/**
		 * \brief Hot-spot has been activated.
		 * 
		 * Hot-spot has to start activate transition (possibly delayed).
		 */
		public func void onHotSpotActivated()
			pRemainingDelay = pDelayActivate
			pHotSpotActive = true
			setRequiresThink(true)
			
			pTargetHotSpotActive.fire()
			pTargetHotSpotActivated.pulse()
		end
		
		/**
		 * \brief Hot-spot has been deactivated.
		 * 
		 * Hot-spot has to stop running the action and start deactivate transition (possibly delayed).
		 */
		public func void onHotSpotDeactivated()
			pRemainingDelay = pDelayDeactivate
			pHotSpotActive = false
			setRequiresThink(true)
			
			pTargetHotSpotActive.reset()
			pTargetHotSpotDeactivated.pulse()
		end
		
		
		
		/** \brief Reset initial state immediately. */
		public func void reset()
			pRemainingDelay = 0
			pHotSpotActive = false
			setRequiresThink(false)
			pTargetHotSpotActive.reset()
			pAnimated.setDeactivated()
		end
		
		/** \brief Set activated now. */
		public func void setActivated()
			pRemainingDelay = 0
			pHotSpotActive = true
			setRequiresThink(false)
			pTargetHotSpotActive.fire()
			pAnimated.setActivated()
		end
		
		
		
		/**
		 * \brief Mouse pointer is hovering over Hot-spot.
		 * 
		 * Hot-spot has to start hover actions if present.
		 */
		public func void onHotSpotHoverEnter()
			var IMMTextDisplay textDisplay = pECBehavior.getTextDisplay()
			if textDisplay != null
				var String text = pDisplayText
				if pDisplayTextTranslate != null
					text = "@" + pDisplayTextTranslate
				end
				textDisplay.setText(text)
			end
			
			pTargetHotSpotHover.fire()
			pTargetHotSpotEntered.pulse()
		end
		
		/**
		 * \brief Mouse pointer is no more hovering over Hot-spot.
		 * 
		 * Hot-spot has to stop hover actions if present.
		 */
		public func void onHotSpotHoverExit()
			if pECBehavior.getTextDisplay() != null
				pECBehavior.getTextDisplay().clearText()
			end
			
			pTargetHotSpotHover.reset()
			pTargetHotSpotLeft.pulse()
		end
		
		
		
		/** \brief Add to game world. */
		public func void addToGameWorld()
			var TSTriggerTable triggerTable = pECBehavior.getTriggerTable()
			pTargetHotSpotActive.acquireTarget(triggerTable)
			pTargetHotSpotActivated.acquireTarget(triggerTable)
			pTargetHotSpotDeactivated.acquireTarget(triggerTable)
			pTargetHotSpotHover.acquireTarget(triggerTable)
			pTargetHotSpotEntered.acquireTarget(triggerTable)
			pTargetHotSpotLeft.acquireTarget(triggerTable)
		end
		
		/** \brief Remove from game world. */
		public func void removeFromGameWorld()
			pTargetHotSpotActive.dropTarget()
			pTargetHotSpotActivated.dropTarget()
			pTargetHotSpotDeactivated.dropTarget()
			pTargetHotSpotHover.dropTarget()
			pTargetHotSpotEntered.dropTarget()
			pTargetHotSpotLeft.dropTarget()
		end
		
		
		
		/** \brief Think about what the element is going to do in this frame. */
		public func void think(float elapsed)
			if pHotSpotActive
				if not pAnimated.isDeactivated()
					return
				end
				
				pRemainingDelay -= elapsed
				if pRemainingDelay > 0
					return
				end
				
				setRequiresThink(false)
				pAnimated.activate()
				
			else
				if not pAnimated.isActivated()
					return
				end
				
				pRemainingDelay -= elapsed
				if pRemainingDelay > 0
					return
				end
				
				setRequiresThink(false)
				pAnimated.deactivate()
			end
		end
	end
	
	
	
	private var ECBehaviorTwoStateAnimated pAnimated
	private var ECPString pAction
	private var ECPString pCameraSpot
	private var ECPFloat pTransitionTime
	private var ECPString pDisplayText
	private var ECPString pDisplayTextTranslate
	private var ECPFloat pDelayActivate
	private var ECPFloat pDelayDeactivate
	private var ECPString pTargetHotSpotActive
	private var ECPString pTargetHotSpotActivated
	private var ECPString pTargetHotSpotDeactivated
	private var ECPString pTargetHotSpotHover
	private var ECPString pTargetHotSpotEntered
	private var ECPString pTargetHotSpotLeft
	private var TSTriggerTable pTriggerTable
	private var IMMTextDisplay pTextDisplay
	
	
	
	/** \brief Create behavior element class. */
	public func new(BehaviorElementClass eclass, ECBehaviorTwoStateAnimated animated) \
	this(eclass, animated, eclass.sharedFactoryHelper.defaultLoaders(), null)
	end
	
	/** \brief Create behavior element class. */
	public func new(BehaviorElementClass eclass, ECBehaviorTwoStateAnimated animated, \
	Loaders loaders, TSTriggerTable triggerTable) \
	this(eclass, animated, loaders, triggerTable, "hotspot.")
	end
	
	public func new(BehaviorElementClass eclass, ECBehaviorTwoStateAnimated animated, \
	Loaders loaders, TSTriggerTable triggerTable, String prefix) super(eclass)
		if animated == null
			throw ENullPointer.new("animated")
		end
		
		pAnimated = animated
		pTriggerTable = triggerTable
		
		pAction = ECPString.new(prefix + "action", "")
		eclass.addProperty(pAction)
		
		pCameraSpot = ECPString.new(prefix + "cameraSpot", "")
		eclass.addProperty(pCameraSpot)
		
		pTransitionTime = ECPFloat.new(prefix + "transitionTime", 1.5, 0)
		eclass.addProperty(pTransitionTime)
		
		pDisplayText = ECPString.new(prefix + "displayText", "")
		eclass.addProperty(pDisplayText)
		
		pDisplayTextTranslate = ECPString.new(prefix + "displayTextTranslate", null)
		eclass.addProperty(pDisplayTextTranslate)
		
		pDelayActivate = ECPFloat.new(prefix + "delayActivate", 0, 0)
		eclass.addProperty(pDelayActivate)
		
		pDelayDeactivate = ECPFloat.new(prefix + "delayDeactivate", 0, 0)
		eclass.addProperty(pDelayDeactivate)
		
		pTargetHotSpotActive = ECPString.new(prefix + "targetHotSpotActive", "")
		eclass.addProperty(pTargetHotSpotActive)
		
		pTargetHotSpotActivated = ECPString.new(prefix + "targetHotSpotActivated", "")
		eclass.addProperty(pTargetHotSpotActivated)
		
		pTargetHotSpotDeactivated = ECPString.new(prefix + "targetHotSpotDeactivated", "")
		eclass.addProperty(pTargetHotSpotDeactivated)
		
		pTargetHotSpotHover = ECPString.new(prefix + "targetHotSpotHover", "")
		eclass.addProperty(pTargetHotSpotHover)
		
		pTargetHotSpotEntered = ECPString.new(prefix + "targetHotSpotEntered", "")
		eclass.addProperty(pTargetHotSpotEntered)
		
		pTargetHotSpotLeft = ECPString.new(prefix + "targetHotSpotLeft", "")
		eclass.addProperty(pTargetHotSpotLeft)
		
		eclass.addBehavior(this)
	end
	
	/** \brief Dispose of behavior. */
	public func void dispose()
		pAnimated = null
		super.dispose()
	end
	
	
	
	public func ECBehaviorTwoStateAnimated getAnimated()
		return pAnimated
	end
	
	public func ECPString getAction()
		return pAction
	end
	
	public func ECPString getCameraSpot()
		return pCameraSpot
	end
	
	public func ECPFloat getTransitionTime()
		return pTransitionTime
	end
	
	public func ECPString getDisplayText()
		return pDisplayText
	end
	
	/**
	 * \brief Translated display text.
	 * \version 1.16
	 */
	public func ECPString getDisplayTextTranslate()
		return pDisplayTextTranslate
	end
	
	public func ECPFloat getDelayActivate()
		return pDelayActivate
	end
	
	public func ECPFloat getDelayDeactivate()
		return pDelayDeactivate
	end
	
	public func ECPString getTargetHotSpotActive()
		return pTargetHotSpotActive
	end
	
	public func ECPString getTargetHotSpotActivated()
		return pTargetHotSpotActivated
	end
	
	public func ECPString getTargetHotSpotDeactivated()
		return pTargetHotSpotDeactivated
	end
	
	public func ECPString getTargetHotSpotHover()
		return pTargetHotSpotHover
	end
	
	public func ECPString getTargetHotSpotEntered()
		return pTargetHotSpotEntered
	end
	
	public func ECPString getTargetHotSpotLeft()
		return pTargetHotSpotLeft
	end
	
	/** \brief Trigger table or \em null if not used. */
	public func TSTriggerTable getTriggerTable()
		return pTriggerTable
	end
	
	/** \brief Set trigger table or \em null if not used. */
	public func void setTriggerTable(TSTriggerTable triggerTable)
		pTriggerTable = triggerTable
	end
	
	/** \brief Text display or \em null if not used. */
	public func IMMTextDisplay getTextDisplay()
		return pTextDisplay
	end
	
	/** \brief Set text display or \em null if not used. */
	public func void setTextDisplay(IMMTextDisplay textDisplay)
		pTextDisplay = textDisplay
	end
	
	
	
	/** \brief Get instance in element from owner element class. */
	public func Instance instance(BehaviorElement element)
		return element.getInstanceAt(getInstanceIndex()) cast Instance
	end
	
	
	
	/**
	 * \brief Get behavior in element class or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElementClass contains a behavior
	 * of type ECBehaviorComponent.
	 */
	static public func IMMBehaviorHotSpot getBehaviorIn(BehaviorElementClass eclass)
		return eclass.findBehavior(block IMMBehaviorHotSpot each
			return true
		end) cast IMMBehaviorHotSpot
	end
	
	/**
	 * \brief Get instance in element or \em null if absent.
	 * 
	 * Use this method to check if a particular BehaviorElement contains a behavior instance
	 * of type IMMBehaviorHotSpot.Instance . If more than one instance is present returns
	 * the first instance.
	 */
	static public func Instance getInstanceIn(BehaviorElement element)
		return element.findInstance(block Instance each
			return true
		end) cast Instance
	end
	
	
	
	/** \brief Create element class behavior instance. */
	public func ECBehaviorInstance createInstance(BehaviorElement element)
		return Instance.new(this, element)
	end
end
