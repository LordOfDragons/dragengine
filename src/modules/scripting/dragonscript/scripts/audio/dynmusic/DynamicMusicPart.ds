/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

namespace Dragengine.Audio.DynamicMusic

pin Dragengine.Gui
pin Dragengine.Scenery



/**
 * \brief Dynamic music part.
 *
 * Music to play with transitions to next parts. If no transitions are defined
 * or no transitions match the playback stops after this part. If music is
 * \em null playback stops after this part. Use \em null music only for parts
 * used as init part which are used only to find the first part to play.
 * 
 */
class DynamicMusicPart
	/** \brief Name. */
	private var String pName
	
	/** \brief Music to play. */
	private var Sound pMusic
	
	/** \brief Transitions. */
	private var Array pTransitions
	
	/** \brief Controller value to set if this part is selected. */
	private var float pControllerValue
	
	
	
	/** \brief Create dynamic music part with \em null music for init use. */
	public func new()
		this.pTransitions = Array.new()
		this.pControllerValue = 0.0
	end
	
	/** \brief Create dynamic music part. */
	public func new( String name )
		this.pName = name
		this.pTransitions = Array.new()
		this.pControllerValue = 0.0
	end
	
	
	
	/** \brief Name. */
	public func String getName()
		return this.pName
	end
	
	/** \brief Music to play. */
	public func Sound getMusic()
		return this.pMusic
	end
	
	/** \brief Set music to play. */
	public func void setMusic( Sound music )
		this.pMusic = music
	end
	
	
	
	/** \brief Number of transitions. */
	public func int getTransitionCount()
		return this.pTransitions.getCount()
	end
	
	/** \brief Get transition at index. */
	public func DynamicMusicTransition getTransitionAt( int index )
		return this.pTransitions.getAt( index ) cast DynamicMusicTransition
	end
	
	/** \brief Transition is present. */
	public func bool hasTransition( DynamicMusicTransition transition )
		return this.pTransitions.has( transition )
	end
	
	/** \brief Add transition. */
	public func void addTransition( DynamicMusicTransition transition )
		if transition == null or this.pTransitions.has( transition )
			throw EInvalidParam.new()
		end
		this.pTransitions.add( transition )
	end
	
	/** \brief Remove transition. */
	public func void removeTransition( DynamicMusicTransition transition )
		this.pTransitions.remove( transition )
	end
	
	/** \brief Remove all transitions. */
	public func void removeAllTransitions()
		this.pTransitions.removeAll()
	end
	
	
	
	/** \brief Controller value to set if this part is selected. */
	public func float getControllerValue()
		return this.pControllerValue
	end
	
	/** \brief Set controller value to set if this part is selected. */
	public func void setControllerValue( float value )
		this.pControllerValue = value
	end
	
	
	
	/**
	 * \brief Name of next part to play or \em null to stop.
	 * \details Returns the part of the first valid transition or our name.
	 */
	public func String nextPart()
		var DynamicMusicTransition transition = this.pTransitions.find( block DynamicMusicTransition each
			return each.areConditionsFulfilled()
		end ) cast DynamicMusicTransition
		
		if transition == null
			return null
			
		else
			return transition.getPart()
		end
	end
end
