/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.LoadSave



/**
 * \brief Iterator for conversation actions.
 */
class ConversationActionIterator
	var Array pActions
	var int pPosition
	
	
	
	/** \brief Create conversation action iterator. */
	func new(Array actions, int position)
		if actions == null or position < 0 or position > actions.getCount()
			throw EInvalidParam.new()
		end
		
		pActions = actions
		pPosition = position
	end
	
	
	
	/** \brief Actions. */
	func Array getActions()
		return pActions
	end
	
	/** \brief Current position. */
	func int getPosition()
		return pPosition
	end
	
	
	
	/** \brief Next action is present. */
	func bool hasNext()
		return pPosition < pActions.getCount()
	end
	
	/** \brief Next action and advance position or \em null if absent. */
	func ConversationAction getNext()
		if pPosition < pActions.getCount()
			return pActions.getAt(pPosition++) cast ConversationAction
		end
		return null
	end
	
	/** \brief Set to first action. */
	func void first()
		pPosition = 0
	end
	
	/** \brief Set to last action. */
	func void last()
		if pActions.getCount() > 0
			pPosition = pActions.getCount() - 1
		end
	end
	
	/**
	 * \brief Set to one past last action.
	 * \details hasNext() returns false and getNext() returns null.
	 */
	func void onePastLast()
		pPosition = pActions.getCount()
	end
	
	/**
	 * \brief Return to previous action if not at the start.
	 * \version 1.21
	 */
	func void previous()
		if pPosition > 0
			pPosition--
		end
	end
	
	
	
	/** \brief Read layer from file. */
	static func ConversationActionIterator readFromFile(PersistencyEnvironment env, FileReader reader)
		select reader.readByte() // version
		case 0
			var Array actions = Array.new()
			var int i, count = reader.readUShort()
			for i = 0 to count
				actions.add(env.readObject(reader))
			end
			var int position = reader.readUShort()
			
			return ConversationActionIterator.new(actions, position)
			
		else
			throw EInvalidParam.new("unsupported version")
		end
	end
	
	/** \brief Write layer to file. */
	func void writeToFile(PersistencyEnvironment env, FileWriter writer)
		writer.writeByte(0) // version
		
		writer.writeUShort(pActions.getCount())
		pActions.forEach(block ConversationAction each
			env.writeObject(writer, each)
		end)
		writer.writeUShort(pPosition)
	end
end
