/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland Pl√ºss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.Gui
pin Dragengine.Scenery
pin Dragengine.Utils
pin Dragengine.LoadSave



/**
 * \brief Conversation gesture playback.
 */
class ConversationGesturePlayback
	/** \brief Gesture. */
	class Gesture
		private var StringID pAnimatorID
		private var String pAnimatorPath
		private var Animator pAnimator
		private var bool pHold
		private var ConversationStrip pStrip
		
		/** \brief Create gesture. */
		public func new( StringID animatorID, bool hold, ConversationStrip strip )
			this.pAnimatorID = animatorID
			this.pHold = hold
			this.pStrip = strip
		end
		
		/** \brief Animator ID. */
		public func StringID getAnimatorID()
			return this.pAnimatorID
		end
		
		/** \brief Animator. */
		public func Animator getAnimator()
			return this.pAnimator
		end
		
		/** \brief Animator. */
		public func void setAnimator( Animator animator, String animatorPath )
			this.pAnimator = animator
			this.pAnimatorPath = animatorPath
		end
		
		/** \brief Path to animator. */
		public func String getAnimatorPath()
			return this.pAnimatorPath
		end
		
		/** \brief Hold gesture. */
		public func bool getHold()
			return this.pHold
		end
		
		/** \brief Set if gesture is held. */
		public func void setHold( bool hold )
			this.pHold = hold
		end
		
		/** \brief Strip. */
		public func ConversationStrip getStrip()
			return this.pStrip
		end
		
		/** \brief Read from file. */
		static public func Gesture readFromFile( PersistencyEnvironment env, FileReader reader )
			select reader.readByte() // version
			case 0
				var StringID animatorId = env.readStringIDOrNullIfEmpty( reader )
				var String animatorPath = env.readString16OrNullIfEmpty( reader )
				var bool hold = env.readBool( reader )
				var ConversationStrip strip = ConversationStrip.readFromFile( reader )
				
				var Gesture gesture = Gesture.new( animatorId, hold, strip )
				
				if animatorPath != null
					var LoadAnimator loadAnimator = env.getVariable( PersistencyEnvironment.varLoadAnimator ) cast LoadAnimator
					gesture.setAnimator( loadAnimator.loadFromFile( animatorPath ), animatorPath )
				end
				
				return gesture
				
			else
				throw EInvalidParam.new( "unsupported version" )
			end
		end
		
		/** \brief Write to file. */
		public func void writeToFile( PersistencyEnvironment env, FileWriter writer )
			writer.writeByte( 0 ) // version
			
			env.writeStringIDOrEmptyIfNull( writer, this.pAnimatorID )
			env.writeString16OrEmptyIfNull( writer, this.pAnimatorPath )
			env.writeBool( writer, this.pHold )
			this.pStrip.writeToFile( writer )
		end
	end
	
	
	
	private var AnimatorInstance pAnimatorInstance
	private var Array pGestures
	private var int pPosition
	private var float pElapsed
	private var bool pRunning
	private var bool pInitNextGesture
	private var bool pRequiresApply
	private var String pNameControllerProgress
	private var String pNameControllerPlayback
	private var String pNameControllerVariation
	private var AnimatorController pControllerProgress
	private var AnimatorController pControllerPlayback
	private var AnimatorController pControllerVariation
	
	
	
	/** \brief Create gesture playback. */
	public func new()
		this.pAnimatorInstance = AnimatorInstance.new()
		this.pGestures = Array.new()
		this.pPosition = 0
		this.pElapsed = 0
		this.pRunning = false
		this.pInitNextGesture = false
		this.pRequiresApply = false
		this.pNameControllerProgress = "gesture.progress"
		this.pNameControllerPlayback = "gesture.playback"
		this.pNameControllerVariation = "gesture.variation"
	end
	
	
	
	/** \brief Animator instance. */
	public func AnimatorInstance getAnimatorInstance()
		return this.pAnimatorInstance
	end
	
	/** \brief Elapsed time. */
	public func float getElapsed()
		return this.pElapsed
	end
	
	/** \brief Playback position. */
	public func int getPosition()
		return this.pPosition
	end
	
	/** \brief Playback is running. */
	public func bool getRunning()
		return this.pRunning
	end
	
	
	
	/** \brief Number of gestures. */
	public func int getGestureCount()
		return this.pGestures.getCount()
	end
	
	/** \brief Gesture by position. */
	public func Gesture getGestureAt( int position )
		return this.pGestures.getAt( position ) cast Gesture
	end
	
	/** \brief Remove all gestures. */
	public func void removeAllGestures()
		var bool hasBeenRunning = this.pRunning
		
		this.pGestures.removeAll()
		this.pPosition = 0
		this.pElapsed = 0
		this.pRunning = false
		this.pInitNextGesture = false
		this.pRequiresApply = false
		
		if hasBeenRunning
			this.onFinished()
		end
	end
	
	/** \brief Add gesture. */
	public func void addGesture( StringID animatorID, bool hold, ConversationStrip strip )
		this.pGestures.add( Gesture.new( animatorID, hold, strip ) )
		
		// if the playback running, is located at the last gesture before adding the new one
		// and last gesture is held then we need to check if the gesture elapsed and advance
		// the playback otherwise we get into problems
		if not this.pRunning
			this.pPosition = 0
			this.pElapsed = 0
			this.pRunning = true
			this.pInitNextGesture = true
			return
		end
		
		if this.pPosition < this.pGestures.getCount() - 2
			return
		end
		
		var Gesture pbgesture = this.pGestures.getAt( this.pPosition ) cast Gesture
		if not pbgesture.getHold()
			return
		end
		
		var ConversationStrip pbgstrip = pbgesture.getStrip()
		this.pElapsed = DEMath.min( this.pElapsed, pbgstrip.getPause() + pbgstrip.getDuration() )
	end
	
	/** \brief Add gesture at playback position replacing gestures after pause time. */
	public func void addGestureFromNow( StringID animatorID, bool hold, ConversationStrip strip )
		// if not running this is the same as removing all gestures then add a new one
		var float pause = strip.getPause()
		
		if not this.pRunning //or pause <= 0.001
			this.removeAllGestures()
			this.addGesture( animatorID, hold, strip )
			return
		end
		
		// remove all done gestures
		while this.pPosition > 0
			this.pGestures.removeFrom( 0 )
			this.pPosition--
		end
		
		// find the last gesture fully finished before pause time
		var int count = this.pGestures.getCount()
		var ConversationStrip pbgstrip
		var float length, time = 0
		var int i
		
		for i = 0 to count
			pbgstrip = ( this.pGestures.getAt( i ) cast Gesture ).getStrip()
			length = pbgstrip.getPause() + pbgstrip.getDuration()
			if i == 0
				length = DEMath.max( length - this.pElapsed, 0.0 )
			end
			if time + length > pause
				break
			end
			time += length
		end
		
		if i == 0
			this.removeAllGestures()
			this.addGesture( animatorID, hold, strip )
			return
		end
		
		this.pGestures.resize( i )
		this.addGesture( animatorID, hold, ConversationStrip.new( \
			strip.getID(), pause - time, strip.getDuration() ) )
	end
	
	
	
	/** \brief Progress controller name. */
	public func String getNameControllerProgress()
		return this.pNameControllerProgress
	end
	
	/** \brief Set progress controller name. */
	public func void setNameControllerProgress( String name )
		this.pNameControllerProgress = name
	end
	
	/** \brief Playback controller name. */
	public func String getNameControllerPlayback()
		return this.pNameControllerPlayback
	end
	
	/** \brief Set playback controller name. */
	public func void setNameControllerPlayback( String name )
		this.pNameControllerPlayback = name
	end
	
	/** \brief Variation controller name. */
	public func String getNameControllerVariation()
		return this.pNameControllerVariation
	end
	
	/** \brief Set variation controller name. */
	public func void setNameControllerVariation( String name )
		this.pNameControllerVariation = name
	end
	
	/** \brief Progress animator controllers. */
	public func AnimatorController getControllerProgress()
		return this.pControllerProgress
	end
	
	/** \brief Playback animator controllers. */
	public func AnimatorController getControllerPlayback()
		return this.pControllerPlayback
	end
	
	/** \brief Variation animator controllers. */
	public func AnimatorController getControllerVariation()
		return this.pControllerVariation
	end
	
	
	
	/**
	 * \brief Update playback.
	 * 
	 * This updates the playback and updates animator instances if required. This does not
	 * apply the animation yet to allow proper use of state capturing.
	 */
	public func void update( ConversationActor actor, AnimatorInstance poseAnimatorInstance, float elapsed )
		if not this.pRunning or this.pGestures.getCount() == 0
			return
		end
		
		var Animator animator = this.pAnimatorInstance.getAnimator()
		var int gestureCount = this.pGestures.getCount()
		var bool resetAnimation = false
		var float startTime = 0
		var float endTime = 0
		
		this.pElapsed += elapsed
		
		while this.pPosition < gestureCount
			var Gesture pbgesture = this.pGestures.getAt( this.pPosition ) cast Gesture
			var ConversationStrip strip = pbgesture.getStrip()
			var float duration = strip.getDuration()
			var float pause = strip.getPause()
			var float length = pause + duration
			
			if this.pElapsed < length or ( pbgesture.getHold() and this.pPosition == gestureCount - 1 )
				startTime = pause
				endTime = length
				if this.pElapsed >= startTime
					if this.pInitNextGesture
						this.initNextGesture( actor )
						resetAnimation = true
					end
					animator = pbgesture.getAnimator()
				end
				
				break
				
			else
				this.pElapsed -= length
				this.pPosition++
				this.pInitNextGesture = true
			end
		end
		
		if this.pPosition >= gestureCount
			this.removeAllGestures()
			this.onFinished()
			return
		end
		
		if not Animator.equals( animator, this.pAnimatorInstance.getAnimator() )
			this.pAnimatorInstance.setAnimator( animator )
			resetAnimation = true
			
			this.pControllerProgress = null
			this.pControllerPlayback = null
			this.pControllerVariation = null
			
			if animator != null
				this.pControllerProgress = this.pAnimatorInstance.getControllerNamed( this.pNameControllerProgress )
				this.pControllerPlayback = this.pAnimatorInstance.getControllerNamed( this.pNameControllerPlayback )
				this.pControllerVariation = this.pAnimatorInstance.getControllerNamed( this.pNameControllerVariation )
			end
		end
		
		// update animator if present
		if animator == null
			return
		end
		
		// copy actor controller states to the gesture controllers with matching name if present
		if poseAnimatorInstance != null
			this.pAnimatorInstance.copyNamedControllerStates( poseAnimatorInstance )
		end
		
		// update gesture playback. sets progress, playback and variation controller if present.
		// also captures state with number 0 if used by animator
		if resetAnimation
			if this.pControllerProgress != null
				this.pControllerProgress.setRange( startTime, endTime )
			end
			if this.pControllerVariation != null
				this.pControllerVariation.setValue( DEMath.random() )
			end
			this.pAnimatorInstance.captureState( 0 )
			
			if this.pControllerProgress != null
				this.pControllerProgress.setValue( this.pElapsed )
			end
			if this.pControllerPlayback != null
				this.pControllerPlayback.setValue( this.pElapsed - startTime )
			end
			
		else
			if this.pControllerProgress != null
				this.pControllerProgress.increment( elapsed )
			end
			if this.pControllerPlayback != null
				this.pControllerPlayback.increment( elapsed )
			end
		end
		
		this.pRequiresApply = true
	end
	
	/**
	 * \brief Apply animation.
	 * 
	 * Called after update() to apply animation. Can do nothing if no change has been done.
	 */
	public func void applyAnimation()
		if not this.pRequiresApply
			return
		end
		
		this.pRequiresApply = false
		this.pAnimatorInstance.apply()
	end
	
	/** \brief Init next gesture to play. */
	public func void initNextGesture( ConversationActor actor )
		this.pInitNextGesture = false
		
		if this.pPosition >= this.pGestures.getCount()
			return
		end
		
		var Gesture pbgesture = this.pGestures.getAt( this.pPosition ) cast Gesture
		var StringID animatorID = pbgesture.getAnimatorID()
		if animatorID == null
			return
		end
		
		var ConversationPlaybackGesturePose pose = actor.getGesturePose( animatorID )
		if pose == null
			return
		end
		
		pbgesture.setAnimator( pose.getAnimator(), pose.getPathAnimator() )
	end
	
	
	
	/** Finished playing back the gestures. Hook for subclasses. */
	public func void onFinished()
	end
	
	
	
	/** \brief Read from file. */
	public func void readFromFile( PersistencyEnvironment env, FileReader reader )
		select reader.readByte() // version
		case 0
			this.pRunning = env.readBool( reader )
			
			var int i, count = reader.readShort()
			for i = 0 to count
				this.pGestures.add( Gesture.readFromFile( env, reader ) )
			end
			
			this.pPosition = reader.readShort()
			this.pElapsed = reader.readFloat()
			
		else
			throw EInvalidParam.new( "unsupported version" )
		end
	end
	
	/** \brief Write to file. */
	public func void writeToFile( PersistencyEnvironment env, FileWriter writer )
		writer.writeByte( 0 ) // version
		
		env.writeBool( writer, this.pRunning )
		
		writer.writeUShort( this.pGestures.getCount() )
		this.pGestures.forEach( block Gesture each
			each.writeToFile( env, writer )
		end )
		
		writer.writeUShort( this.pPosition )
		writer.writeFloat( this.pElapsed )
	end
end
