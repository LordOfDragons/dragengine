/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.LoadSave
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Option for a player choice conversation action.
 */
class CActionPlayerChoiceOption
	var UnicodeString pText
	var ConversationCondition pCondition
	var Array pActions
	
	
	
	/** \brief Create conversation action. */
	func new()
		pText = UnicodeString.new()
		pActions = Array.new()
	end
	
	
	
	/** \brief Text. */
	func UnicodeString getText()
		return pText
	end
	
	/** \brief Set text. */
	func void setText(UnicodeString text)
		if text == null
			throw EInvalidParam.new()
		end
		pText = text
	end
	
	/** \brief Condition or \em null if not set. */
	func ConversationCondition getCondition()
		return pCondition
	end
	
	/** \brief Set condition or \em null if not set. */
	func void setCondition(ConversationCondition condition)
		pCondition = condition
	end
	
	
	
	/** \brief Visits all actions. */
	func void forEachAction(Block aBlock)
		pActions.forEach(aBlock)
	end
	
	/** \brief Adds an action. */
	func void addAction(ConversationAction action)
		if action == null
			throw EInvalidParam.new()
		end
		pActions.add(action)
	end
	
	/** \brief Actions. */
	func Array getActions()
		return pActions
	end
	
	/** \brief Iterator for actions. */
	func ConversationActionIterator getIteratorActions()
		return ConversationActionIterator.new(pActions, 0)
	end
	
	
	
	
	/** \brief Read from file. */
	static func CActionPlayerChoiceOption readFromFile(PersistencyEnvironment env, FileReader reader)
		var CActionPlayerChoiceOption option = CActionPlayerChoiceOption.new()
		
		select reader.readByte() // version
		case 0
			option.pText = UnicodeString.newFromUTF8(reader.readString16())
			option.pCondition = env.readObjectOrNull(reader) cast ConversationCondition
			
			var int i, count = reader.readUShort()
			for i = 0 to count
				option.pActions.add(env.readObject(reader))
			end
			
		else
			throw EInvalidParam.new("unsupported version")
		end
		
		return option
	end
	
	/** \brief Write to file writer. */
	func void writeToFile(PersistencyEnvironment env, FileWriter writer)
		writer.writeByte(0) // version
		
		writer.writeString16(pText.toString())
		env.writeObjectOrNull(writer, pCondition)
		
		writer.writeUShort(pActions.getCount())
		pActions.forEach(block ConversationAction each
			env.writeObject(writer, each)
		end)
	end
end
