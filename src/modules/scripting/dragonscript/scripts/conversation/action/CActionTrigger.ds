/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.LoadSave
pin Dragengine.Scenery
pin Dragengine.TriggerSystem
pin Dragengine.Utils



/**
 * \brief Conversation action operating a trigger.
 */
class CActionTrigger extends ConversationAction
	/** \brief Factory for loading actor actions. */
	class Factory implements PersistencyFactory
		public static fixed var String NAME = "CActionTrigger"
		
		func new()
		end
		
		func Persistable readObjectFromFile(PersistencyEnvironment env, FileReader reader)
			return CActionTrigger.new(env, reader)
		end
		
		static func void registerFactory(Persistency persistency)
			persistency.addFactory(NAME, Factory.new())
		end
	end
	
	
	
	/** \brief Actions. */
	public enum Action
		/** \brief Fire the trigger. */
		fire
		
		/** \brief Reset the trigger. */
		reset
		
		/** \brief Fire and immediately reset the trigger. */
		pulse
		
		/** \brief Fully resets the trigger. */
		fullReset
	end
	
	
	
	private var StringID pName
	private var Action pAction
	
	// NOTE we can not store the trigger target here to avoid fetching it all the time.
	//      this is because conversation are shared resources held by the conversation loader.
	//      each conversation playback can have different trigger table. by storing the target
	//      we can reference a wrong trigger target
// 	private var TSTriggerTarget pTriggerTarget
	
	
	
	/** \brief Create conversation action. */
	func new()
		pAction = Action.fire
	end
	
	/** \brief Read from file. */
	func new(PersistencyEnvironment env, FileReader reader) super(env, reader)
		select reader.readByte() // version
		case 0
			pName = env.readStringIDOrNullIfEmpty(reader)
			pAction = Action.withOrder(reader.readByte())
			
		else
			throw EInvalidParam.new("unsupported version")
		end
	end
	
	
	
	/** \brief Name of the trigger to set. */
	func StringID getName()
		return pName
	end
	
	/** \brief Set name of the trigger to set. */
	func void setName(StringID name)
		pName = name
	end
	
	/** \brief Action. */
	func Action getAction()
		return pAction
	end
	
	/** \brief Set action. */
	func void setAction(Action action)
		pAction = action
	end
	
	
	
	/** \brief Execute action. */
	func void execute(ConversationPlayback playback)
		if pName == null
			playback.advanceToNextAction()
			return
		end
		
		var TSTriggerTarget target
		// if run the first time get trigger target adding it if not existing yet
// 		if pTriggerTarget == null
			target = playback.getTriggerTable().getOrAddNamed(pName)
// 		end
		
		// operate the trigger
		select pAction
		case Action.fire
			target.fire()
			
		case Action.reset
			target.reset()
			
		case Action.pulse
			target.fire()
			target.reset()
			
		case Action.fullReset
			target.fullReset()
		end
		
		playback.advanceToNextAction()
	end
	
	
	
	/**
	 * \brief Name of PersistencyFactory required to load object from file.
	 * 
	 * Name of PersistencyFactory is first written to the file as string with
	 * 8-bit length. Then writeToFile() is called to write the object itself.
	 * Empty name is not allowed as is names longer than 255 characters.
	 * Name has to match a PersistencyFactory present in the Persistency
	 * instance used for loading objects. Object is read using
	 * PersistencyFactory.readObjectFromFile() of the matching PersistencyFactory.
	 */
	func String persistencyFactoryName()
		return Factory.NAME
	end
	
	/** \brief Write object to file writer. */
	func void writeObjectToFile(PersistencyEnvironment env, FileWriter writer)
		super.writeObjectToFile(env, writer)
		
		writer.writeByte(0) // version
		
		env.writeStringIDOrEmptyIfNull(writer, pName)
		writer.writeByte(pAction.order())
	end
end
