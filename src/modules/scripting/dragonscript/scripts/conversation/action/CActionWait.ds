/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.LoadSave
pin Dragengine.Scenery
pin Dragengine.Utils



/**
 * \brief Conversation action waiting for condition to become false while looping actions.
 */
class CActionWait extends ConversationAction
	/** \brief Factory for loading actor actions. */
	class Factory implements PersistencyFactory
		public static fixed var String NAME = "CActionWait"
		
		func new()
		end
		
		func Persistable readObjectFromFile(PersistencyEnvironment env, FileReader reader)
			return CActionWait.new(env, reader)
		end
		
		public static func void registerFactory(Persistency persistency)
			persistency.addFactory(NAME, Factory.new())
		end
	end
	
	
	
	var ConversationCondition pCondition
	var float pInterval
	var Array pActions
	
	
	
	/** \brief Create conversation action. */
	func new()
		pInterval = 0.2
		pActions = Array.new()
	end
	
	/** \brief Read from file. */
	func new(PersistencyEnvironment env, FileReader reader) super(env, reader)
		select reader.readByte() // version
		case 0
			pCondition = env.readObjectOrNull(reader) cast ConversationCondition
			pInterval = reader.readFloat()
			
			var int i, count = reader.readUShort()
			pActions = Array.new()
			for i = 0 to count
				pActions.add(env.readObject(reader))
			end
			
		else
			throw EInvalidParam.new("unsupported version")
		end
	end
	
	
	
	/** \brief Condition or \em null to skip action. */
	func ConversationCondition getCondition()
		return pCondition
	end
	
	/** \brief Set condition or \em null to skip action. */
	func void setCondition(ConversationCondition condition)
		pCondition = condition
	end
	
	/** \brief Interval to wait before testing the condition again to improve performance. */
	func float getInterval()
		return pInterval
	end
	
	/** \brief Set interval to wait before testing the condition again to improve performance. */
	func void setInterval(float interval)
		pInterval = DEMath.max(interval, 0.0)
	end
	
	
	
	/** \brief Visits actions. */
	func void forEachAction(Block aBlock)
		pActions.forEach(aBlock)
	end
	
	/** \brief Add action. */
	func void addAction(ConversationAction action)
		if action == null
			throw EInvalidParam.new()
		end
		pActions.add(action)
	end
	
	/** \brief Actions to loop while condition is true. */
	func ConversationActionIterator getIteratorActions()
		return ConversationActionIterator.new(pActions, 0)
	end
	
	
	
	/** \brief Execute action. */
	func void execute(ConversationPlayback playback)
		if pCondition == null
			playback.advanceToNextAction()
			return
		end
		
		var ConversationPlaybackLayer layer = ConversationPlaybackLayer.new(getIteratorActions())
		layer.setLoopCondition(pCondition)
		layer.setLoopEvalConditionInterval(pInterval)
		layer.setLooping(true)
		
		playback.pushLayer(layer)
	end
	
	
	
	/**
	 * \brief Name of PersistencyFactory required to load object from file.
	 * 
	 * Name of PersistencyFactory is first written to the file as string with
	 * 8-bit length. Then writeToFile() is called to write the object itself.
	 * Empty name is not allowed as is names longer than 255 characters.
	 * Name has to match a PersistencyFactory present in the Persistency
	 * instance used for loading objects. Object is read using
	 * PersistencyFactory.readObjectFromFile() of the matching PersistencyFactory.
	 */
	func String persistencyFactoryName()
		return Factory.NAME
	end
	
	/** \brief Write object to file writer. */
	func void writeObjectToFile(PersistencyEnvironment env, FileWriter writer)
		super.writeObjectToFile(env, writer)
		
		writer.writeByte(0) // version
		
		env.writeObjectOrNull(writer, pCondition)
		writer.writeFloat(pInterval)
		
		writer.writeUShort(pActions.getCount())
		pActions.forEach(block ConversationAction each
			env.writeObject(writer, each)
		end)
	end
end
