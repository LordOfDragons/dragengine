/* 
 * Drag[en]gine DragonScript Script Module
 *
 * Copyright (C) 2020, Roland PlÃ¼ss (roland@rptd.ch)
 * 
 * This program is free software; you can redistribute it and/or 
 * modify it under the terms of the GNU General Public License 
 * as published by the Free Software Foundation; either 
 * version 2 of the License, or (at your option) any later 
 * version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.Animators
pin Dragengine.Gui
pin Dragengine.SpeechAnimations
pin Dragengine.Scenery
pin Dragengine.LoadSave


/**
 * \brief Actor in a conversation.
 * 
 * Contains the playback information required to animate and interact properly with the actor.
 * Set all the required parameters to customize the actor. Optionally create a subclass to
 * overwrite basic behavior with your own for complex custom actors. These are the methods
 * that are typically overwritten for such complex actors:
 * 
 * - bool wait()
 * - void command(Array argList)
 * - bool conditionCommand(Array argList)
 * - DMatrix getMatrix()
 * - ConversationPlaybackGesturePose getGesturePose(StringID gestureId)
 */
class ConversationActor
	private var StringID pID
	private var StringID pAliasID
	private var UnicodeString pName
	private var ConversationPlayback pPlayback
	private var Speaker pVoiceSpeaker
	private var ParameterTable pParameterTable
	private var ConversationGesturePoseList pGesturePoses
	private var SpeechAnimationPlayback pSpeechAnimationPlayback
	private var ConversationGesturePlayback pGesturePlayback
	private var ConversationLookAtPlayback pHeadLookAtPlayback
	private var ConversationFacePosePlayback pFacePosePlayback
	private var ConversationLookAtPlayback pEyesLookAtPlayback
	private var SubTitleBoxText pSubTitleText
	private var float pRemainingSpeechTime
	
	
	
	/** \brief Creates conversation actor. */
	public func new(StringID id)
		if id == null
			throw EInvalidParam.new()
		end
		pID = id
		pName = UnicodeString.newFromUTF8("?")
	end
	
	
	
	/** \brief Unique identifier of the actor in the conversation. */
	public func StringID getID()
		return pID
	end
	
	/** \brief Optional unique alias identifier of the actor in the conversation. */
	public func StringID getAliasID()
		return pAliasID
	end
	
	/** \brief Set optional unique alias identifier of the actor in the conversation. */
	public func void setAliasID(StringID id)
		pAliasID = id
	end
	
	/** \brief Name to display for the actor in subtitle boxes. */
	public func UnicodeString getName()
		return pName
	end
	
	/** \brief Set name to display for the actor in subtitle boxes. */
	public func void setName(UnicodeString name)
		if name == null
			throw EInvalidParam.new()
		end
		pName = name
	end
	
	/** \brief Conversation playback of this actor. */
	public func ConversationPlayback getPlayback()
		return pPlayback
	end
	
	/** \brief Set conversation playback of this actor. */
	public func void setPlayback(ConversationPlayback playback)
		if playback == pPlayback
			return
		end
		
		if pPlayback != null
			leaveConversation()
		end
		
		pPlayback = playback
		
		if playback != null
			enterConversation()
		end
	end
	
	/** \brief Speaker to play the actor voice during conversation. */
	public func Speaker getVoiceSpeaker()
		return pVoiceSpeaker
	end
	
	/** \brief Set speaker to play the actor voice during conversation. */
	public func void setVoiceSpeaker(Speaker speaker)
		pVoiceSpeaker = speaker
	end
	
	/** \brief Parameter table to use or \em null. */
	public func ParameterTable getParameterTable()
		return pParameterTable
	end
	
	/** \brief Parameter table to use or \em null. */
	public func void setParameterTable(ParameterTable parameterTable)
		pParameterTable = parameterTable
	end
	
	/** \brief Gesture poses to use or \em null. */
	public func ConversationGesturePoseList getGesturePoses()
		return pGesturePoses
	end
	
	/** \brief Set gesture poses to use or \em null. */
	public func void setGesturePoses(ConversationGesturePoseList gesturePoses)
		pGesturePoses = gesturePoses
	end
	
	/** \brief Speech animation playback. */
	public func SpeechAnimationPlayback getSpeechAnimationPlayback()
		return pSpeechAnimationPlayback
	end
	
	/** \brief Set speech animation playback. */
	public func void setSpeechAnimationPlayback(SpeechAnimationPlayback playback)
		pSpeechAnimationPlayback = playback
	end
	
	/** \brief Sub title text or null if not set. */
	public func SubTitleBoxText getSubTitleText()
		return pSubTitleText
	end
	
	/** \brief Set sub title text or null to clear it. */
	public func void setSubTitleText(SubTitleBoxText text)
		pSubTitleText = text
	end
	
	/** \brief Gesture playback or null if not used. */
	public func ConversationGesturePlayback getGesturePlayback()
		return pGesturePlayback
	end
	
	/** \brief Set gesture playback or null if not used. */
	public func void setGesturePlayback(ConversationGesturePlayback playback)
		pGesturePlayback = playback
	end
	
	/** \brief Head look-at playback or null if not used. */
	public func ConversationLookAtPlayback getHeadLookAtPlayback()
		return pHeadLookAtPlayback
	end
	
	/** \brief Set head look-at playback or null if not used. */
	public func void setHeadLookAtPlayback(ConversationLookAtPlayback playback)
		pHeadLookAtPlayback = playback
	end
	
	/** \brief Face pose playback or null if not used. */
	public func ConversationFacePosePlayback getFacePosePlayback()
		return pFacePosePlayback
	end
	
	/** \brief Set face pose playback or null if not used. */
	public func void setFacePosePlayback(ConversationFacePosePlayback playback)
		pFacePosePlayback = playback
	end
	
	/** \brief Eyes look-at playback or null if not used. */
	public func ConversationLookAtPlayback getEyesLookAtPlayback()
		return pEyesLookAtPlayback
	end
	
	/** \brief Set eyes look-at playback or null if not used. */
	public func void setEyesLookAtPlayback(ConversationLookAtPlayback playback)
		pEyesLookAtPlayback = playback
	end
	
	
	
	/** \brief Remaining speech playback time in seconds. */
	public func float getRemainingSpeechTime()
		return pRemainingSpeechTime
	end
	
	/** \brief Set ramaining speech playback time in seconds. */
	public func void setRemainingSpeechTime(float remainingTime)
		pRemainingSpeechTime = DEMath.max(remainingTime, 0.0)
	end
	
	/** \brief Decrement remaining speech playback time in seconds. */
	public func void decrementRemainingSpeechTime(float elapsedTime)
		setRemainingSpeechTime(pRemainingSpeechTime - elapsedTime)
	end
	
	
	
	/**
	 * \brief Actor speech has been started by CActionActorSpeak.
	 * \version 1.9
	 * 
	 * Called after setting up actor speech and before advancing conversation playback.
	 * Allows sub class to apply additional game actions at begin of actor speech started
	 * by instance of CActionActorSpeak. This method is always called no matter if the
	 * action set up any speech parameters or not. Use the action parameter to figure out
	 * if this is the case.
	 * 
	 * \param action Action starting speech or null if called otherwise.
	 */
	public func void beginActorSpeech(CActionActorSpeak action)
	end
	
	/**
	 * \brief Update existing playback objects affecting actor locomotion.
	 * 
	 * This method is called before the actor locomotion is updated. This includes the head
	 * look-at playback if existing. This does not apply the animation yet to allow proper
	 * use of state capturing.
	 */
	public func void updateBeforeLocomotion(float elapsed)
		if pHeadLookAtPlayback != null
			pHeadLookAtPlayback.update(this, elapsed)
		end
	end
	
	/**
	 * \brief Update existing playback objects not affecting actor locomotion.
	 * 
	 * This method is called after the actor locomotion is updated. This includes all existing
	 * playback objects not updated in updatePlaybackBeforeLocomotion. This does not apply the
	 * animation yet to allow proper use of state capturing.
	 */
	public func void updateAfterLocomotion(float elapsed, AnimatorInstance sourceAnimatorInstance)
		if pGesturePlayback != null
			pGesturePlayback.update(this, sourceAnimatorInstance, elapsed)
		end
		if pFacePosePlayback != null
			pFacePosePlayback.update(elapsed)
		end
		if pEyesLookAtPlayback != null
			pEyesLookAtPlayback.update(this, elapsed)
		end
		if pSpeechAnimationPlayback != null
			pSpeechAnimationPlayback.update(elapsed)
		end
	end
	
	/**
	 * \brief Apply animation for all present playback instances.
	 * 
	 * This method is called after all animations states have been updated and the all animation
	 * states are about to be applied in the correct order.
	 */
	public func void applyAnimations()
		if pGesturePlayback != null
			pGesturePlayback.applyAnimation()
		end
		if pFacePosePlayback != null
			pFacePosePlayback.applyAnimation()
		end
		if pEyesLookAtPlayback != null
			pEyesLookAtPlayback.applyAnimation()
		end
		if pSpeechAnimationPlayback != null
			pSpeechAnimationPlayback.applyAnimation()
		end
	end
	
	/** \brief Clear all playbacks. */
	public func void clearAllPlayback()
		if pHeadLookAtPlayback != null
			pHeadLookAtPlayback.clearLookAtState()
		end
		if pGesturePlayback != null
			pGesturePlayback.removeAllGestures()
		end
		if pFacePosePlayback != null
			pFacePosePlayback.removeAllPoses()
		end
		if pEyesLookAtPlayback != null
			pEyesLookAtPlayback.clearLookAtState()
		end
		if pSpeechAnimationPlayback != null
			pSpeechAnimationPlayback.removeAllPhonemes()
		end
	end
	
	
	
	/**
	 * \brief Actor has finished speaking.
	 * 
	 * This is used by the conversation playback. Checks if the voice speaker
	 * (if existing) is playing back a sound or if the speech animation
	 * (playback (if existing) is playing back.
	 */
	public func bool isSpeaking()
		return pRemainingSpeechTime > 0.0
		//return (pVoiceSpeaker != null and pVoiceSpeaker.getPlaying()) \
		//	or (pSpeechAnimationPlayback != null and pSpeechAnimationPlayback.getSpeaking())
	end
	
	
	
	/**
	 * \brief Actor is blocking if conversation action waits for this actor.
	 * 
	 * Overload to retrieve the value form the real actor. The default
	 * implementation returns false.
	 */
	public func bool wait()
		return false
	end
	
	/**
	 * \brief Command send by the conversation script.
	 * 
	 * Overload to make the actor execute the action. The default
	 * implementation does nothing.
	 */
	public func void command(String command)
	end
	
	/**
	 * \brief Condition command send by the conversation script.
	 * 
	 * Overload to make the actor evaluate the command returning true or false as result.
	 * The default implementation returns false.
	 */
	public func bool conditionCommand(String command)
		return false
	end
	
	/**
	 * \brief Named actor parameter.
	 * 
	 * Returns value from set parameter table entry with matching name. Returns 0 if no
	 * parameter table is set, no entry matches the name or the matching entry is has
	 * no value set.
	 */
	public func int getParameter(String parameter)
		if pParameterTable != null
			var ParameterTableEntry entry = pParameterTable.getAtOrNull(parameter)
			if entry != null
				return entry.getValue(0)
			end
		end
		return 0
	end
	
	/**
	 * \brief Set named actor parameter.
	 * 
	 * Sets value in parameter table if parameter table is set.
	 */
	public func void setParameter(String parameter, int value)
		if pParameterTable != null
			pParameterTable.getAt(parameter).setValue(value)
		end
	end
	
	/**
	 * \brief Conversation matrix.
	 * 
	 * This is the matrix relative to the actor coordinate frame. In general this is
	 * the same as the position and orientation of the actor. This is also the
	 * reference frame expected by the conversation editor. Overwrite to return the
	 * actor matrix. The default implementation returns the identity matrix.
	 */
	public func DMatrix getMatrix()
		return getMatrix(null)
	end
	
	/**
	 * \brief Conversation matrix.
	 * 
	 * This is the matrix relative to the actor coordinate frame. In general this is
	 * the same as the position and orientation of the actor. This is also the
	 * reference frame expected by the conversation editor. Overwrite to return the
	 * actor matrix. The default implementation returns the identity matrix. If bone
	 * is not null the matrix has to be relative to the named bone if existing.
	 */
	public func DMatrix getMatrix(String bone)
		return DMatrix.new()
	end
	
	/** \brief Inverse conversation matrix. */
	public func DMatrix getInverseMatrix()
		return getInverseMatrix(null)
	end
	
	/** \brief Inverse conversation matrix. */
	public func DMatrix getInverseMatrix(String bone)
		return DMatrix.new()
	end
	
	/**
	 * \brief Gesture pose or null if not found.
	 * 
	 * Default implementation looks up the gesture pose in the assigned gesture poses
	 * if the gesture poses is not null.
	 */
	public func ConversationPlaybackGesturePose getGesturePose(StringID gestureId)
		if pGesturePoses != null
			return pGesturePoses.getAt(gestureId)
		end
		return null
	end
	
	
	
	/**
	 * \brief Create sub title widget for actor matching speech style.
	 * 
	 * Default implementation creates instance of SubTitleBoxTexBordert.
	 * 
	 * \param text Text to speak.
	 * \param style Identifier of style to adjust font, color or sub title in general.
	 */
	public func SubTitleBoxText createSubTitleText(UnicodeString text, StringID style)
		return SubTitleBoxTextBorder.new(text.toString(), pName.toString(), \
			null, null, null, null, SubTitleBoxTextBorder.Placement.leftBelow)
	end
	
	
	
	/**
	 * \brief Actor enters conversation.
	 * \details Overwrite to switch actor to operate in conversation.
	 */
	public func void enterConversation()
	end
	
	/**
	 * \brief Actor leaves conversation.
	 * \details Overwrite to switch actor back to operating on its own.
	 */
	public func void leaveConversation()
	end
	
	
	
	/**
	 * \brief Restore linked objects after loading.
	 * 
	 * While loading playback object is not available yet. Linked objects can not be
	 * restored during readFromFile(). Once all loading is done and the playback is
	 * present objects this call is used to restore the object links.
	 */
	public func void playbackRestoreObjectLinks()
		// pSpeechAnimationPlayback
		// pGesturePlayback
		
		if pHeadLookAtPlayback != null
			pHeadLookAtPlayback.playbackRestoreObjectLinks(pPlayback)
		end
		
		// pFacePosePlayback
		
		if pEyesLookAtPlayback != null
			pEyesLookAtPlayback.playbackRestoreObjectLinks(pPlayback)
		end
	end
	
	
	
	/** \brief Read from file. */
	public func void readFromFile(PersistencyEnvironment env, FileReader reader)
		select reader.readByte() // version
		case 0
			var PersistencyFlags flags = PersistencyFlags.new(6, reader)
			
			pAliasID = env.readStringIDOrNullIfEmpty(reader)
			
			if flags.getAt(0)
				pSpeechAnimationPlayback.readFromFile(env, reader)
			end
			if flags.getAt(1)
				pGesturePlayback.readFromFile(env, reader)
			end
			if flags.getAt(2)
				pFacePosePlayback.readFromFile(env, reader)
			end
			if flags.getAt(3)
				pHeadLookAtPlayback.readFromFile(env, reader)
			end
			if flags.getAt(4)
				pEyesLookAtPlayback.readFromFile(env, reader)
			end
			
			pSubTitleText = env.readObjectOrNull(reader) cast SubTitleBoxText
			if pSubTitleText != null
				pPlayback.getSubTitle().addSubTitleText(pSubTitleText.subtitleWidget())
			end
			
			if flags.getAt(5)
				pVoiceSpeaker.setSound(Sound.new(reader.readString16()))
				var int playFrom = reader.readInt()
				var int playTo = reader.readInt()
				pVoiceSpeaker.setPlayPosition(playFrom, playTo)
				reader.readInt() //getPlayPosition not working yet
				pVoiceSpeaker.setPlaySpeed(reader.readFloat())
				pVoiceSpeaker.setVolume(reader.readFloat())
				
				pVoiceSpeaker.play()
			end
			
			pRemainingSpeechTime = reader.readFloat()
			
		else
			throw EInvalidParam.new("unsupported version")
		end
	end
	
	/** \brief Write to file. */
	public func void writeToFile(PersistencyEnvironment env, FileWriter writer)
		writer.writeByte(0) // version
		
		var PersistencyFlags flags = PersistencyFlags.new(6)
		flags.setAt(0, pSpeechAnimationPlayback != null)
		flags.setAt(1, pGesturePlayback != null)
		flags.setAt(2, pFacePosePlayback != null)
		flags.setAt(3, pHeadLookAtPlayback != null)
		flags.setAt(4, pEyesLookAtPlayback != null)
		flags.setAt(5, pVoiceSpeaker != null and pVoiceSpeaker.getPlaying())
		flags.writeToFile(writer)
		
		env.writeStringIDOrEmptyIfNull(writer, pAliasID)
		
		if pSpeechAnimationPlayback != null
			pSpeechAnimationPlayback.writeToFile(env, writer)
		end
		if pGesturePlayback != null
			pGesturePlayback.writeToFile(env, writer)
		end
		if pFacePosePlayback != null
			pFacePosePlayback.writeToFile(env, writer)
		end
		if pHeadLookAtPlayback != null
			pHeadLookAtPlayback.writeToFile(env, writer)
		end
		if pEyesLookAtPlayback != null
			pEyesLookAtPlayback.writeToFile(env, writer)
		end
		
		if pSubTitleText castable Persistable
			env.writeObjectOrNull(writer, pSubTitleText cast Persistable)
			
		else
			env.writeObjectOrNull(writer, null)
		end
		
		if pVoiceSpeaker != null and pVoiceSpeaker.getPlaying()
			writer.writeString16(pVoiceSpeaker.getSound().getFilename())
			writer.writeInt(pVoiceSpeaker.getPlayFrom())
			writer.writeInt(pVoiceSpeaker.getPlayTo())
			writer.writeInt(0 /* pVoiceSpeaker.getPlayPosition() */) // not working yet
			writer.writeFloat(pVoiceSpeaker.getPlaySpeed())
			writer.writeFloat(pVoiceSpeaker.getVolume())
		end
		
		writer.writeFloat(pRemainingSpeechTime)
	end
end
