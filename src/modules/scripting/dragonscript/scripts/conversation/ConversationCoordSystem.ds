/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */


namespace Dragengine.ConversationSystem

pin Dragengine.LoadSave
pin Dragengine.Scenery



/**
 * \brief Coordinate system for use in conversation.
 * 
 * Returns a coordinate system matrix for conversation targets. In contrary to conversation
 * actors coordinate system objects can be added to multiple conversation playback objects
 * at the same time. Conversation coordinate systems can be static or dynamic. The game
 * scripts are responsible to update the coordinate system if it changes position or orientation
 * dynamically.
 */
class ConversationCoordSystem
	private var StringID pID
	private var StringID pAliasID
	private var DMatrix pLocalMatrix
	
	
	
	/** \brief Create conversation coordinate system. */
	public func new( StringID id ) this( id, DMatrix.new() )
	end
	
	/** \brief Create conversation coordinate system. */
	public func new( StringID id, DMatrix localMatrix )
		if id == null or localMatrix == null
			throw EInvalidParam.new()
		end
		
		this.pID = id
		this.pLocalMatrix = localMatrix
	end
	
	/** \brief Create copy of conversation coordinate system. */
	public func new( ConversationCoordSystem coordSystem )
		this.pID = coordSystem.pID
		this.pAliasID = coordSystem.pAliasID
		this.pLocalMatrix = coordSystem.pLocalMatrix
	end
	
	/** \brief Dispose of the coordinate system breaking cyclic references. */
	public func void dispose()
	end
	
	
	
	/** \brief Unique identifier of the actor in the conversation. */
	public func StringID getID()
		return this.pID
	end
	
	/** \brief Optional unique alias identifier of the coordinate system in the conversation. */
	public func StringID getAliasID()
		return this.pAliasID
	end
	
	/** \brief Set optional unique alias identifier of the coordinate system in the conversation. */
	public func void setAliasID( StringID id )
		this.pAliasID = id
	end
	
	/** \brief Local matrix to use. */
	public func DMatrix getLocalMatrix()
		return this.pLocalMatrix
	end
	
	/** \brief Set local matrix to use. */
	public func void setLocalMatrix( DMatrix matrix )
		if matrix == null
			throw EInvalidParam.new()
		end
		this.pLocalMatrix = matrix
	end
	
	
	
	/** \brief Coordinate system matrix. */
	public func DMatrix getMatrix()
		return this.pLocalMatrix
	end
	
	
	
	/**
	 * \brief Read from file.
	 * \details Used if ConversationPlayback.createConversationCoordSystem() is used.
	 */
	public func void readFromFile( PersistencyEnvironment env, FileReader reader )
		// ID has been read by ConversationPlayback
		
		select reader.readByte() // version
		case 0
			this.pAliasID = env.readStringIDOrNullIfEmpty( reader )
			this.pLocalMatrix.readFromFile( reader )
			
		else
			throw EInvalidParam.new( "unsupported version" )
		end
	end
	
	/**
	 * \brief Write to file.
	 * \details Used if ConversationPlayback.createConversationCoordSystem() is used.
	 */
	public func void writeToFile( PersistencyEnvironment env, FileWriter writer )
		writer.writeString8( this.pID.toString() )
		
		writer.writeByte( 0 ) // version
		
		env.writeStringIDOrEmptyIfNull( writer, this.pAliasID )
		this.pLocalMatrix.writeToFile( writer )
	end
end
