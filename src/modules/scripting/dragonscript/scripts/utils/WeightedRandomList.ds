/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

namespace Dragengine.Utils

pin Dragengine.Scenery
pin Dragengine.Gui


/**
 * \brief List of weighted entries for random retrieval.
 * 
 * List entries have a weight assigned representing the percentage chance of the entry to be
 * selected. If all entries have the same weight ths list behaves the same as Array.random().
 * Using different weights allows to favor or disfavor certain elements.
 */
class WeightedRandomList
	/** \brief Entry. */
	protected class Entry
		var Object pObject
		var float pWeight, pThreshold
		
		
		/** \brief Create entry. */
		func new(Object object, float weight)
			pObject = object
			pWeight = weight
			pThreshold = 0
		end
		
		/** \brief Create copy of entry. */
		func new(Entry entry)
			pObject = entry.pObject
			pWeight = entry.pWeight
			pThreshold = entry.pThreshold
		end
		
		
		/** \brief Object. */
		func Object getObject()
			return pObject
		end
		
		/** \brief Set object. */
		func void setObject(Object object)
			pObject = object
		end
		
		/** \brief Weight. */
		func float getWeight()
			return pWeight
		end
		
		/** \brief Set weight. */
		func void setWeight(float weight)
			pWeight = weight
		end
		
		/** \brief Threshold value. */
		func float getThreshold()
			return pThreshold
		end
		
		/** \brief Threshold value. */
		func void setThreshold(float threshold)
			pThreshold = threshold
		end
	end
	
	
	var Array pEntries
	var float pMaxThreshold
	var bool pDirtyThresholds
	
	
	/** \brief Create list. */
	func new()
		pEntries = Array.new()
		pMaxThreshold = 0
		pDirtyThresholds = false
	end
	
	/** \brief Create copy of list. */
	func new(WeightedRandomList list)
		pEntries = list.pEntries.map(block Entry each
			return Entry.new(each)
		end)
		pMaxThreshold = list.pMaxThreshold
		pDirtyThresholds = list.pDirtyThresholds
	end
	
	
	/** \brief Number of entries. */
	func int getCount()
		return pEntries.getCount()
	end
	
	/** \brief Object at index. */
	func Object getObjectAt(int index)
		return (pEntries.getAt(index) cast Entry).getObject()
	end
	
	/** \brief Weight at index. */
	func float getWeightAt(int index)
		return (pEntries.getAt(index) cast Entry).getWeight()
	end
	
	/** \brief Index of first object or -1 if absent. */
	func int indexOfObject(Object object)
		var Object entry = pEntries.find(block Entry each
			return each.getObject() == object
		end)
		
		if entry == null
			return -1
			
		else
			return pEntries.indexOf(entry)
		end
	end
	
	/** \brief Set object at index. */
	func void setObjectAt(int index, Object object)
		(pEntries.getAt(index) cast Entry).setObject(object)
	end
	
	/** \brief Set weight at index. */
	func void setWeightAt(int index, float weight)
		(pEntries.getAt(index) cast Entry).setWeight(weight)
		pDirtyThresholds = true
	end
	
	/** \brief Add object. */
	func void add(Object object, float weight)
		pEntries.add(Entry.new(object, weight))
		pDirtyThresholds = true
	end
	
	/** \brief Remove first occurance of object. */
	func void removeObject(Object object)
		pEntries.removeFrom(indexOfObject(object))
		pDirtyThresholds = true
	end
	
	/** \brief Remove object from index. */
	func void removeFrom(int index)
		pEntries.removeFrom(index)
		pDirtyThresholds = true
	end
	
	/** \brief Remove all objects. */
	func void removeAll()
		pEntries.removeAll()
		pMaxThreshold = 0.0
		pDirtyThresholds = false
	end
	
	/**
	 * \brief Visit entries with block.
	 * 
	 * Black is called with arguments (Object object, float weight).
	 */
	func void forEach(Block ablock)
		pEntries.forEach(block Entry each
			ablock.run(each.getObject(), each.getWeight())
		end)
	end
	
	/**
	 * \brief Find entry with block.
	 * 
	 * Black is called with arguments (Object object, float weight) and has to return
	 * \em true to select the object or \em false to continue searching.
	 */
	func Object find(Block ablock)
		var Entry entry = pEntries.find(block Entry each
			return ablock.run(each.getObject(), each.getWeight())
		end) cast Entry
		
		if entry != null
			return entry.getObject()
			
		else
			return null
		end
	end
	
	/**
	 * \brief Random object.
	 * \throws EInvalidParam List is empty.
	 */
	func Object random()
		var int upper = pEntries.getCount() - 1
		if upper == -1
			throw EInvalidParam.new("empty list")
		end
		
		if pDirtyThresholds
			updateThresholds()
		end
		
		var float randomValue = DEMath.random() * pMaxThreshold
		var int lower, middle
		var Entry entry
		
		while lower != upper
			middle = (lower + upper) / 2
			entry = pEntries.getAt(middle) cast Entry
			
			if randomValue > entry.getThreshold()
				lower = middle + 1
				
			else
				upper = middle
			end
		end
		
		return (pEntries.getAt(lower) cast Entry).getObject()
	end
	
	/**
	 * \brief New weighted random list with filtered elements.
	 * 
	 * Block is called with arguments (Object object, float Weight) and has to return
	 * \em true to include the object or \em false to exclude the object.
	 */
	func WeightedRandomList collect(Block ablock)
		var WeightedRandomList list = WeightedRandomList.new()
		pEntries.forEach(block Entry each
			if ablock.run(each.getObject(), each.getWeight()) cast bool
				list.pEntries.add(Entry.new(each))
			end
		end)
		return list
	end
	
	
	
	/** \brief Entry at index. */
	protected func Entry getEntryAt(int index)
		return pEntries.getAt(index) cast Entry
	end
	
	/** \brief Maximum threshold. */
	protected func float getMaxThreshold()
		return pMaxThreshold
	end
	
	/** \brief Threshold are dirty. */
	protected func bool getDirtyThresholds()
		return pDirtyThresholds
	end
	
	/** \brief Update thresholds. */
	protected func void updateThresholds()
		pMaxThreshold = pEntries.inject(0.0, block float result, Entry each
			each.setThreshold(result + each.getWeight())
			return each.getThreshold()
		end) cast float
		pDirtyThresholds = false
	end
end
