/*
 * MIT License
 *
 * Copyright (C) 2024, DragonDreams GmbH (info@dragondreams.ch)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

namespace Dragengine.Utils

pin Dragengine.Gui
pin Dragengine.Preloading
pin Dragengine.Scenery


/**
 * \brief List of texture replacements.
 * 
 * \see TextureReplacement
 */
class TextureReplacementList
	var Array pTextures
	
	
	/** \brief Create texture replacement list. */
	func new()
		pTextures = Array.new()
	end
	
	/** \brief Create copy of texture replacement list. */
	func new(TextureReplacementList copy)
		pTextures = copy.pTextures.map(block TextureReplacement each
			return TextureReplacement.new(each)
		end)
	end
	
	/** \brief Create texture replacement list from stub. */
	func new(StubElement stub) this()
		addFromStub(stub)
	end
	
	
	/** \brief Number of texture replacements. */
	func int getCount()
		return pTextures.getCount()
	end
	
	/** \brief Texture replacement by index. */
	func TextureReplacement getAt(int index)
		return pTextures.getAt(index) cast TextureReplacement
	end
	
	/** \brief Texture replacement by name or \em null if not found. */
	func TextureReplacement getNamed(String name)
		return pTextures.find(block TextureReplacement each
			return each.getName().equals(name)
		end) cast TextureReplacement
	end
	
	/**
	 * \brief Texture replacement is present.
	 * \version 1.29
	 */
	func bool has(TextureReplacement texture)
		return pTextures.has(texture)
	end
	
	/** \brief Named texture replacement is present. */
	func bool hasNamed(String name)
		return pTextures.find(block TextureReplacement each
			return each.getName().equals(name)
		end) != null
	end
	
	/** \brief Add texture replacement. */
	func void add(TextureReplacement texture)
		if texture == null or hasNamed(texture.getName())
			throw EInvalidParam.new()
		end
		
		pTextures.add(texture)
	end
	
	/** \brief Remove texture replacement. */
	func void remove(TextureReplacement texture)
		pTextures.remove(texture)
	end
	
	/** \brief Remove all texture replacements. */
	func void removeAll()
		pTextures.removeAll()
	end
	
	/** \brief Visit texture replacements using a block. */
	func void forEach(Block aBlock)
		pTextures.forEach(aBlock)
	end
	
	/**
	 * \brief Process value visiting texture replacements using a block.
	 * 
	 * Works like Array.inject(). Block receives as first argument Object injected value
	 * and as second argument TextureReplacement texture replacement.
	 */
	func Object inject(Object injectValue, Block aBlock)
		return pTextures.inject(injectValue, aBlock)
	end
	
	
	
	/** \brief Add texture replacements from stub. */
	func void addFromStub(StubElement stub)
		stub.forEachTexture(block StubElementTexture each
			add(TextureReplacement.new(each))
		end)
	end
	
	/** \brief Apply all texture replacements to component if possible. */
	func void applyToComponent(Component component)
		pTextures.forEach(block TextureReplacement each
			each.applyToComponent(component)
		end)
	end
end
