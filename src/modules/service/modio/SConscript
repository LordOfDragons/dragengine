from SConsCommon import *

Import('parent_env parent_targets parent_report')

#temporary until fixed by Modio team
if parent_env['platform_android'] != 'no':
	Return()

envModule = parent_env.Clone()

buildModule = envModule['build_service_modio'] != TernaryVariableNo
internalModule = envModule['build_image_png_internal']
parent_report['build Mod.io Service Module'] = (
	'yes (internal)' if internalModule else 'yes') if buildModule else 'no'

if not buildModule or 'lib_modio' not in parent_targets:
	Return()

libs = []
appendLibrary(envModule, parent_targets['dragengine'], libs, internalModule=internalModule)
appendLibrary(envModule, parent_targets['lib_modio'], libs)

# fetch values in expanded form for later use
versionString = '1.29'
if envModule['force_version']:
	versionString = envModule['force_version']

pathConfig = '{}/modules/service/modio/{}'.format(
	envModule.subst(envModule['path_de_config']), versionString)
pathModule = '{}/modules/service/modio/{}'.format(
	envModule.subst(envModule['path_de_data']), versionString)

envModule.Append(CPPFLAGS = parent_env['MODULE_CPPFLAGS'])
envModule.Append(LINKFLAGS = parent_env['MODULE_LINKFLAGS'])

# required by modio
envModule.Append(CXXFLAGS = ['-std=c++17'])

# determine the source files
sources = []
headers = []
globFiles(envModule, 'src', '*.cpp', sources)
globFiles(envModule, 'src', '*.h', headers)

# setup the builders
install = []
archive = {}

if internalModule:
	envModule.Append(CPPFLAGS = ['-DWITH_INTERNAL_MODULE'])
	envModule.Append(CPPFLAGS = ['-DMODULE_VERSION=\\"{}\\"'.format(versionString)])
	
	objects = [envModule.SharedObject(s) for s in sources]
	
	library = envModule.StaticLibrary(target='srvmodio', source=objects, LIBS=libs)
	targetBuild = parent_env.Alias('srv_modio_build', library)
	install.extend(targetBuild)
	
	parent_targets['dragengine']['add internal module'](
		envModule, library, ['lib_modio'], 'deModioRegisterInternalModule')
	
else:
	objects = [envModule.SharedObject(s) for s in sources]
	
	library = envModule.LoadableModule(target='srvmodio', source=objects, LIBS=libs)
	targetBuild = envModule.Alias('srv_modio_build', library)
	
	for l in library:
		if l.name.endswith(envModule['SHLIBSUFFIX']):
			install.append(envModule.Install(pathModule, l))
			archive[os.path.join(pathModule, l.name)] = l
	
	if envModule['OSWindows'] and envModule['CROSSCOMPILE_CLANG'] and envModule['with_install_pdb']:
		pathPdb = os.path.join(envModule.subst(envModule['path_pdb']), 'EngineModules')
		install.append(envModule.Install(pathPdb, 'srvmodio.pdb'))
		archive['{}/srvmodio.pdb'.format(pathPdb)] = envModule.File('srvmodio.pdb')
	
	manifestUpdates = [
		{'action': 'filename', 'file': library[0], 'keyword': 'FILENAME'},
		{'action': 'filesize', 'file': library[0], 'keyword': 'FILESIZE'},
		{'action': 'filehash', 'file': library[0], 'keyword': 'SHA1SUM'},
		{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]
	
	rtlibs = []
	rtlibs.extend(parent_targets['lib_modio']['runtimelibs'])
	
	for l in reversed(rtlibs):
		manifestUpdates.append({'action': 'preloadLibrary', 'library': l})
		install.append(envModule.Install(pathModule, l))
		archive[os.path.join(pathModule, l.name)] = l
	
	manifest = envModule.File('src/module.xml')
	updateManifest = envModule.UpdateModuleManifest(manifest,
		manifest.srcnode(), ManifestUpdates=manifestUpdates)
	
	install.append(envModule.Install(pathModule, updateManifest))
	archive[os.path.join(pathModule, 'module.xml')] = updateManifest[0]

targetInstall = envModule.Alias('srv_modio', install)

# source directory required for special commands
srcdir = Dir('.').srcnode().abspath

# cloc
buildClocBare = BuildCLOC(envModule, ['{}/src'.format(srcdir)], '{}/clocreport.csv'.format(srcdir), ['doc'])
buildCloc = envModule.Alias('srv_modio_cloc', buildClocBare)

# add the targets to the targets list
parent_targets['srv_modio'] = {
	'name' : 'Mod.io Service Module',
	'build' : targetBuild,
	'install' : targetInstall,
	'install-engine-runtime' : targetInstall,
	'archive-engine' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format(srcdir)}
