from SConsCommon import *

Import( 'parent_env parent_targets parent_report' )

envEditor = parent_env.Clone()

buildModule = 'deigde_shared' in parent_targets
parent_report[ 'build IGDE Project' ] = 'yes' if buildModule else 'no'
if not buildModule:
	Return()

libs = []

appendLibrary(envEditor, parent_targets['deigde_shared'], libs)
appendLibrary(envEditor, parent_targets['lib_zlib'], libs)
if envEditor[ 'OSBeOS' ]:
	libs.append( [ 'root', 'be' ] )

versionString = '1.0'
if envEditor['force_module_versions']:
	versionString = envEditor['force_module_versions']

# fetch values in expanded form for later use
pathConfig = '{}/modules/project'.format( envEditor.subst( envEditor[ 'path_igde_config' ] ) )
pathModule = '{}/modules/project'.format( envEditor.subst( envEditor[ 'path_igde_data' ] ) )
pathShared = '{}/modules/project'.format( envEditor.subst( envEditor[ 'path_igde_share' ] ) )

envEditor.Append( CPPFLAGS = parent_env[ 'MODULE_CPPFLAGS' ] )
envEditor.Append( LINKFLAGS = parent_env[ 'MODULE_LINKFLAGS' ] )

pathLauncherShare = envEditor.subst(envEditor['path_launcher_share'])
pathLauncherConfig = envEditor.subst(envEditor['path_launcher_config'])
envEditor.Append(CPPFLAGS = [
	cmdlineDefinePath(envEditor, 'LAUNCHER_SHARE_PATH', pathLauncherShare),
	cmdlineDefinePath(envEditor, 'LAUNCHER_CONFIG_PATH', pathLauncherConfig)])

# minizip special environment
envMiniZip = envEditor.Clone()
envMiniZip.Append(CPPFLAGS = ['-DUSE_FILE32API'])
envMiniZip.Append(CPPFLAGS = ['-Wno-all', '-Wno-error'])

envEditor.Append(CPPPATH = [envEditor.Dir('minizip').srcnode().abspath])

# setup the builders
sources = []
headers = []
data = []
globFiles( envEditor, 'src', '*.cpp', sources )
globFiles( envEditor, 'src', '*.h', headers )
globFiles( envEditor, 'data', '*', data )

objects = [ envEditor.SharedObject( s ) for s in sources ]
objects.append(envMiniZip.SharedObject('minizip/zip.c'))
objects.append(envMiniZip.SharedObject('minizip/unzip.c'))
objects.append(envMiniZip.SharedObject('minizip/ioapi.c'))

library = envEditor.SharedLibrary( target='project', source=objects, LIBS=libs )

# test runner binary
envTestRun = envEditor.Clone()

libsTestRun = []
appendLibrary(envTestRun, parent_targets['dragengine'], libsTestRun)
libsTestRun.extend(parent_targets['dragengine']['binlibs'])
if envEditor['OSWindows']:
	libsTestRun.extend(['kernel32', 'user32'])

sourcesTestRun = []
globFiles(envTestRun, 'testrun', '*.cpp', sourcesTestRun)

objectsTestRun = [envTestRun.StaticObject(s) for s in sourcesTestRun]

if envTestRun['OSWindows']:
	nameProgramTestRun = 'DEIGDEProjectTestRun'
else:
	nameProgramTestRun = 'deigde_project_testrun'

programTestRun = envTestRun.Program(target=nameProgramTestRun, source=objectsTestRun, LIBS=libsTestRun)

# install and package
targetBuild = envEditor.Alias('editor_project_build', [library, programTestRun])

install = []
archive = {}
for l in library:
	if l.name.endswith( envEditor[ 'SHLIBSUFFIX' ] ):
		install.append( envEditor.Install( pathModule, l ) )
		archive[ os.path.join( pathModule, l.name ) ] = l

pathBin = envEditor.subst(envEditor['path_igde_bin'])
install.append(envTestRun.Install(pathBin, programTestRun))
for p in programTestRun:
	archive[os.path.join(pathBin, p.name)] = p

manifestUpdates = [
	{ 'action': 'filename', 'name': library[ 0 ].name, 'keyword': 'FILENAME' },
	{ 'action': 'filesize', 'path': library[ 0 ].abspath, 'keyword': 'FILESIZE' },
	{ 'action': 'filehash', 'path': library[ 0 ].abspath, 'keyword': 'SHA1SUM' },
	{'action': 'text', 'value': versionString, 'keyword': 'VERSION'}]

for l in reversed( parent_targets['lib_zlib']['runtimelibs']):
	manifestUpdates.append({'action': 'preloadLibrary', 'path': l.name})
	install.append(envEditor.Install(pathModule, l))
	archive[os.path.join(pathModule, l.name)] = l

manifest = envEditor.File( 'src/module.xml' )
updateManifest = envEditor.Command( manifest, manifest.srcnode(),
	Action( UpdateModuleManifest ), ManifestUpdates=manifestUpdates )
Depends( updateManifest, library )
Depends(updateManifest, parent_targets['lib_zlib']['runtimelibs'])

install.append( envEditor.Install( pathModule, updateManifest ) )
archive[ os.path.join( pathModule, 'module.xml' ) ] = updateManifest[ 0 ]

for s in data:
	filename = os.path.join( pathShared, *(s.split(os.sep)[1:]) )
	install.append( envEditor.InstallAs( filename, s ) )
	archive[ filename ] = envEditor.File( s ).srcnode()

targetInstall = envEditor.Alias( 'editor_project', install )

# source directory required for special commands
srcdir = Dir( '.' ).srcnode().abspath

# cloc
buildClocBare = BuildCLOC( envEditor, [ '{}/src'.format( srcdir ) ], '{}/clocreport.csv'.format( srcdir ), [ 'doc' ] )
Depends( buildClocBare, sources )
Depends( buildClocBare, headers )
buildCloc = envEditor.Alias( 'editor_project_cloc', buildClocBare )

# add the targets to the targets list
parent_targets[ 'editor_project' ] = {
	'name' : 'IGDE Project',
	'build' : targetBuild,
	'install' : targetInstall,
	'archive-igde' : archive,
	'cloc' : buildCloc,
	'clocReport' : '{}/clocreport.csv'.format( srcdir ) }
